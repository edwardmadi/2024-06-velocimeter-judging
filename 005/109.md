Generous Malachite Rook

High

# disable_max_lock function incorrectly sets the maxLockIdToIndex mapping when its length is one

## Summary
disable_max_lock function incorrectly sets the maxLockIdToIndex mapping when its length is one

## Vulnerability Detail
From [VotingEscrow.sol::disable_max_lock]() code 
```solidity
  function disable_max_lock(uint _tokenId) external {
      assert(_isApprovedOrOwner(msg.sender, _tokenId));
      require(maxLockIdToIndex[_tokenId] != 0,"disabled");

      uint index =  maxLockIdToIndex[_tokenId] - 1;
      maxLockIdToIndex[_tokenId] = 0;

       // Move the last element into the place to delete
      max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];  <@
      
      // update the index 
      maxLockIdToIndex[max_locked_nfts[index]] = index + 1;
      
      // Remove the last element
      max_locked_nfts.pop();
  }
```
we can see when disable a specific tokenId , firstly , set the `maxLockIdToIndex` mapping to zero, and then move the last element into the place to delete . Finally pop the last element from `max_locked_nfts` list.

However , when there is only one element in the `max_locked_nfts` list , protocol incorrectly sets the maxLockIdToIndex to the deleted element again. Which result in this tokenId can't be enable again.
What's more ? since tokenId is still in the mapping, attacker can invoke `max_lock` to extends the lock end time
test can not be enable 
```solidity
  function testTokenIdCanNotBeEnableAgain() public {
      flowDaiPair.approve(address(escrow), TOKEN_1*2);
      uint256 lockDuration = 7 * 24 * 3600; // 1 week

      assertEq(escrow.balanceOf(address(owner)), 0);
      escrow.create_lock(TOKEN_1, lockDuration);
      escrow.create_lock(TOKEN_1, lockDuration);

      escrow.enable_max_lock(1);
      escrow.disable_max_lock(1);

      vm.expectRevert("enabled");
      escrow.enable_max_lock(1);
  }
```
out
```solidity
Ran 1 test for test/VotingEscrow.t.sol:VotingEscrowTest
[PASS] testTokenIdCanNotBeEnableAgain() (gas: 1077829)
Logs:
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 11.06ms (1.56ms CPU time)
```

test attacker extend the lock time
```solidity
    function testTokenIdCanNotBeEnableAgain() public {
        flowDaiPair.approve(address(escrow), TOKEN_1*2);
        uint256 lockDuration = 7 * 24 * 3600; // 1 week

        assertEq(escrow.balanceOf(address(owner)), 0);
        escrow.create_lock(TOKEN_1, lockDuration);
        escrow.create_lock(TOKEN_1, lockDuration);

        escrow.enable_max_lock(1);
        //owner disable max lock
        escrow.disable_max_lock(1);

        //attacker can still invoke max lock to extend the lock time.
        vm.prank(makeAddr('attacker'));
        escrow.max_lock(1);

        (,uint end) = escrow.locked(1);
        assertEq(end,52 weeks);
        console2.log(end);
    }
```
out
```shell
Ran 1 test for test/VotingEscrow.t.sol:VotingEscrowTest
[PASS] testTokenIdCanNotBeEnableAgain() (gas: 1077381)
Logs:
  31449600

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 13.52ms (3.81ms CPU time)
```


## Impact
- user can not enable his tokenId to max lock
- attacker can extend tokenId's lock end time even if it's disabled 

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L893-L908

## Tool used

Manual Review

## Recommendation
```diff
@@ -899,9 +907,11 @@ contract VotingEscrow is IERC721, IERC721Metadata, IVotes {
 
          // Move the last element into the place to delete
         max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];
-        
+
         // update the index 
-        maxLockIdToIndex[max_locked_nfts[index]] = index + 1;
+        if(max_locked_nfts[index] !=_tokenId ){
+            maxLockIdToIndex[max_locked_nfts[index]] = index + 1;
+        }
```