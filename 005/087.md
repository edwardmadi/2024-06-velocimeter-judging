Loud Inky Shark

High

# Once max lock is enabled, the last tokenId can never be disabled, which allows griefing to permanently lock user staked tokens not allowing withdrawal

## Summary
The new `veLP` design enables holders to opt for auto-relocking their `veLP` to preserve their voting power. If this feature is activated, any action involving the `veLP`, such as voting, will automatically extend your lock time to the maximum duration. Originally when you want to `disable_max_lock`, it works completely fine however, the problem lies if it was the most recent `tokenId`.

## Vulnerability Detail
This snippet will be used for variable elaboration
```solidity
    // The key is the mapping NFT token id, I used nft1 and nft 2 for simple explanation.
    mapping(uint => uint) public maxLockIdToIndex; // {nft1 => 1, nft2 => 2}
    uint[] public max_locked_nfts; // [nft1, nft2]
```
The line of code to focus on is `max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];`. If a user has 2 NFTs and wants to delete the first NFT, referred to as `nft1`, the operation proceeds as follows:

Before: `[nft1, nft2]`
After: `[nft2, nft2]`

The `index` variable is 0 because it was adjusted by this line: `uint index = maxLockIdToIndex[_tokenId] - 1;`.

The line `maxLockIdToIndex[max_locked_nfts[index]] = index + 1;` updates the index mapping:
Before: `{nft1 = 0, nft2 = 2}`
After: `{nft1 = 0, nft2 = 1}`

Finally, the last element in `max_locked_nfts` is removed, resulting in: `[nft2]`.
<details><summary>First Code Walk Through intended</summary>
<p>

```solidity
    function disable_max_lock(uint _tokenId) external {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
        // unable to call it twice due to validation
        require(maxLockIdToIndex[_tokenId] != 0,"disabled");

        // [0,1]
        // {nft 1 = 1, nft2 = 2}
        // if delete first nft1, index = 0
        uint index =  maxLockIdToIndex[_tokenId] - 1; //get the index correct
        // before {nft 1 = 1, nft2 = 2}
        // after {nft 1 = 0, nft2 = 2}
        // _tokenId = 1 
        maxLockIdToIndex[_tokenId] = 0; 

        // ok
        // [nft1,nft2]
        // Move last nft2 into nft 1, and remove nft2 duplicate
        // [nft2,nft2]
        max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];
        
        // update the index 
        // @audit-issue
        // Get the first nft [nft2,nft2], since it was updated
        // max_locked_nfts[index] == nft _tokenID
        // E.g max_locked_nfts[index] == 0 was replaced hence is nft2 _tokenID
        // before {nft 1 = 0, nft2 = 2}
        // after {nft 1 = 0, nft2 = 1} 
        //  Updates correctly
        // maxLockIdToIndex[nft2] 
        maxLockIdToIndex[max_locked_nfts[index]] = index + 1;
        
        // Remove the last element
        // [nft1, 0]
        max_locked_nfts.pop();
    }
```

</p>
</details> 

The issue arises when attempting to delete the most recent NFT, such as `[nft1, nft2]`. The problem occurs because we use the last index to update `maxLockIdToIndex`. Since we want to remove `nft2`, the last index will be 1, and the line `maxLockIdToIndex[max_locked_nfts[index]] = index + 1;` effectively sets it to 2.

Before: `{nft1 = 1, nft2 = 2}`
After: `{nft1 = 1, nft2 = 2}`

Furthermore, since `max_lock` has no access control, anyone can call it on `nft2`. This can lead to permanent griefing, resulting in the user's staked tokens, like FLOW/WETH, becoming stuck in the contract.

<details><summary>Code problem walkthrough</summary>
<p>

```solidity
    function disable_max_lock(uint _tokenId) external {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
        // unable to call it twice due to validation
        require(maxLockIdToIndex[_tokenId] != 0,"disabled");

        // [0,1]
        // {nft 1 = 1, nft2 = 2}
        // if delete second  nft2, index = 1
        uint index =  maxLockIdToIndex[_tokenId] - 1; //correct

        // before {nft 1 = 1, nft2 = 2}
        // after {nft 1 = 1, nft2 = 0}
        // _tokenId = 2 
        maxLockIdToIndex[_tokenId] = 0; 

        // [nft1,nft2]
        // Move last nft2 into nft 1, and remove nft2 duplicate if _tokenID is 1
        // [nft1,nft2]
        // But in my current scenario which _tokenId is 2
        // remains the same [nft1,nft2] @audit-issue
        max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];
        
        // update the index 
        // @audit-issue
        // Get the second nft [nft1,nft2], which was not updated
        // max_locked_nfts[index] == nft _tokenID
        // E.g is max_locked_nfts[index] == 1 was  not replaced remains as nft2 _tokenID
        // before {nft 1 = 1, nft2 = 0}
        // after {nft 1 = 1, nft2 = 2} 
        // maxLockIdToIndex[nft2]  remains the same
        // Since index was 1, it gets the adds back to 2
        maxLockIdToIndex[max_locked_nfts[index]] = index + 1;
        
        // Remove the last element
        // [nft2, 0]
        max_locked_nfts.pop();
    }

```

</p>
</details> 
Add into `VotingEscrow.t.sol` test file and run `forge test --match-test testCreateLockAndMaxLockPoC -vv`

```solidity
//PoC
     function testCreateLockAndMaxLockPoC() public {
        flowDaiPair.approve(address(escrow), TOKEN_1*2);
        uint256 lockDuration = 7 * 24 * 3600; // 1 week

        // Balance should be zero before and 1 after creating the lock
        assertEq(escrow.balanceOf(address(owner)), 0);
        escrow.create_lock(TOKEN_1, lockDuration);
        escrow.create_lock(TOKEN_1, lockDuration);
        assertEq(escrow.currentTokenId(), 2);
        assertEq(escrow.ownerOf(1), address(owner));
        assertEq(escrow.balanceOf(address(owner)), 2);
        
        escrow.max_lock(1);
        escrow.max_lock(2);

        int amount;
        uint duration;
        (amount, duration) = escrow.locked(1);
        assertEq(duration, lockDuration);

        escrow.enable_max_lock(1);
        escrow.enable_max_lock(2);

        escrow.isApprovedOrOwner(address(owner),1);

        (amount, duration) = escrow.locked(1);
        assertEq(duration, 52 * 7 * 86400);

        assertEq(escrow.maxLockIdToIndex(1),1);
        assertEq(escrow.maxLockIdToIndex(2),2);
        
        escrow.disable_max_lock(2);
        uint256 first_maxLockIdToIndex = escrow.maxLockIdToIndex(1);
        uint256 second_maxLockIdToIndex = escrow.maxLockIdToIndex(2);

        console.log("first_maxLockIdToIndex", first_maxLockIdToIndex);
        console.log("second_maxLockIdToIndex", second_maxLockIdToIndex);

        assertEq(escrow.maxLockIdToIndex(1),1);
        assertEq(escrow.maxLockIdToIndex(2),0); // Fails here, should be 0
     }
```
## Impact
The user's vested tokens will be permanently locked in the contract unable to withdraw staked LP tokens.
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L904
## Tool used

Manual Review

## Recommendation
If it is the last index do not add 1.
```diff
+        max_locked_nfts.pop();
+
+        if maxLockIdToIndex[max_locked_nfts[index]] == max_locked_nfts.length  {
+            maxLockIdToIndex[max_locked_nfts[index]] = index
+        }
+        else {
+            maxLockIdToIndex[max_locked_nfts[index]] = index + 1
+        }
         
         // Remove the last element
-        max_locked_nfts.pop();
```