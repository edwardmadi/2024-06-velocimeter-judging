Magnificent Teal Capybara

High

# The function ```disable_max_lock``` in VotingEscrow.sol doesn't handle the case when the nft being removed is the last item in the array.

## Summary
 ```disable_max_lock``` function in VotingEscrow.sol  is used to disable max_lock auto re-locking property but when the user wants disable this for the last element of the max_locked_nfts array, the mapping maxLockIdToIndex is updated incorrectly. 

## Vulnerability Detail
 ```disable_max_lock``` function handles removal of array elements as intended, the element of interest is moved to last position and ``` max_locked_nfts.pop()``` is called. The issue arises in updating the maxLockIdToIndex  mapping which is called to check if the max_lock is enabled or not. NFT id to index mapping should be 0 if the max_lock is disabled. The function works as intended for all the array elements except for the last one. Issue is in the updating the index of the last element part of the code. 
```maxLockIdToIndex[max_locked_nfts[index]] = index + 1;```. Here, the nft which was at the last position of the array is assigned the index of the one being deleted. However, if the nft being deleted is the last one, the mapping maxLockIdToIndex  is first set to 0 then again assigned to the original index by the above line. This behavior can be observed with the below code on Remix. If you add several NFT ids and remove the last one, ```getIndex``` will show the original index instead of 0. Also, when you subsequently add a new NFT id to the array, it will be assigned to the index of the one which we just deleted leading to more issues.

```solidity
pragma solidity ^0.8.13;

contract TestDisableMaxLock {

  uint[] public max_locked_nfts;

  mapping(uint => uint) public maxLockIdToIndex;

  // Function to add an NFT to the list
  function addNFT(uint tokenId) public {
    max_locked_nfts.push(tokenId);
    maxLockIdToIndex[tokenId] = max_locked_nfts.length;
  }

 function disable_max_lock(uint _tokenId) public {
        uint index =  maxLockIdToIndex[_tokenId] - 1;
        maxLockIdToIndex[_tokenId] = 0;

         // Move the last element into the place to delete
        max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];
        
        // update the index 
        maxLockIdToIndex[max_locked_nfts[index]] = index + 1;
         // Remove the last element
        max_locked_nfts.pop();
       
    }

   function getIndex(uint tokenId) public view returns (uint) {
    return maxLockIdToIndex[tokenId];
  }
  
}
```

## Impact
Funds locked forever due to the fact that ```max_lock()``` is called by ```_isApprovedOrOwner``` in every user interaction and auto re-locked for 52 weeks each time. Only way to rescue funds is to call ```maxLockToggle``` by team address, this will make max_lock property disabled irreversibly.
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L893-L909
## Tool used

Manual Review

## Recommendation
Add a condition when the index is equal to the max_locked_nfts.length, skip index updating step.