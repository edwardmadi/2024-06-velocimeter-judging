Macho Amethyst Penguin

High

# Tokens are locked forever and claiming after lock duration is permanently DOSed if maxLock is enabled

## Summary
The `max_lock` function contains a critical vulnerability where it permanently locks out users from interacting with their tokens. Once the `block.timestamp` surpasses the` _locked.end` value, the function will always revert with `"Lock expired"`. This occurs because the function checks if `_locked.end > block.timestamp` before attempting to update the lock time, and `_locked.end` is never updated within the function. Consequently, after the initial lock period expires, the function becomes permanently unusable, potentially trapping user funds indefinitely.

## Vulnerability Detail
The issue stems from the fact that `_isApprovedOrOwner` contains a call to `max_lock` and `_isApprovedOrOwner` is called from multiple functions including `withdraw`.  This is also true for claiming tokens after the initial lock period.  Critically if the user did not claim at all within the lock period and only tried to claim after the lock period then the user will have lost any ability to claim thier rewards.

## Impact
Users willl never be able to withdraw their tokens if max lock is enabled, even if they disbale max lock for the token.

## Code Snippet
### File: VotingEscrow.sol
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L955-L979?plain=1
```Solidity

    function withdraw(uint _tokenId) external nonreentrant {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
        require(attachments[_tokenId] == 0 && !voted[_tokenId], "attached");

        LockedBalance memory _locked = locked[_tokenId];
        require(block.timestamp >= _locked.end, "The lock didn't expire");
        uint value = uint(int256(_locked.amount));

        locked[_tokenId] = LockedBalance(0,0);
        uint supply_before = supply;
        supply = supply_before - value;

        // old_locked can have either expired <= timestamp or zero end
        // _locked has only 0 end
        // Both can have >= 0 amount
        _checkpoint(_tokenId, _locked, LockedBalance(0,0));

        assert(IERC20(lpToken).transfer(msg.sender, value));

        // Burn the NFT
        _burn(_tokenId);

        emit Withdraw(msg.sender, _tokenId, value, block.timestamp);
        emit Supply(supply_before, supply_before - value);
    }
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L295-L303?plain=1
```Solidity
    function _isApprovedOrOwner(address _spender, uint _tokenId) internal returns (bool) {
        max_lock(_tokenId);

        address tokenOwner = idToOwner[_tokenId];
        bool spenderIsOwner = tokenOwner == _spender;
        bool spenderIsApproved = _spender == idToApprovals[_tokenId];
        bool spenderIsApprovedForAll = (ownerToOperators[tokenOwner])[_spender];
        return spenderIsOwner || spenderIsApproved || spenderIsApprovedForAll;
    }
```
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L912-L925?plain=1
```Solidity
    function max_lock(uint _tokenId) public {
        if(maxLockIdToIndex[_tokenId] !=0 && max_lock_enabled) {
            LockedBalance memory _locked = locked[_tokenId];
            uint unlock_time = (block.timestamp + MAXTIME) / WEEK * WEEK; // Locktime is rounded down to weeks

            console2.log("unlock_time    ",unlock_time);
            console2.log("_locked.end    ",_locked.end);
            console2.log("locked_amount  ",_locked.amount);
            console2.log("block.timestamp",block.timestamp);

            if(unlock_time > _locked.end) {
                require(_locked.end > block.timestamp, 'Lock expired'); // @audit HIGH if maxlock enabled and user tries to withdrawer after the lockend this reverts and is also called when calling _isApprovedOrOwner 
                require(_locked.amount > 0, 'Nothing is locked');
                require(unlock_time <= block.timestamp + MAXTIME, 'Voting lock can be 52 weeks max');

                console2.log("max lock extended         ", unlock_time);
                console2.log("block.timestamp + MAXTIME ", block.timestamp + MAXTIME);

                _deposit_for(_tokenId, 0, unlock_time, _locked, DepositType.INCREASE_UNLOCK_TIME);
            }
        }
    }
```

## POC
Add the following test file to the test suite
```Solidity
// 1:1 with Hardhat test
pragma solidity 0.8.13;

import './BaseTest.sol';

import "contracts/RewardsDistributorV2.sol";
import "contracts/Voter.sol";
import "contracts/ExternalBribe.sol";
import "contracts/factories/GaugeFactoryV4.sol";
import "contracts/factories/BribeFactory.sol";
import "contracts/Minter.sol";

contract VotingEscrowTest is BaseTest {
    uint internal constant MAXTIME = 52 * 7 * 86400;
    int128 internal constant iMAXTIME = 52 * 7 * 86400;
    uint internal constant MULTIPLIER = 1 ether;

    RewardsDistributorV2 distributor;
    VotingEscrow escrow;
    GaugeFactoryV4 gaugeFactory;
    BribeFactory bribeFactory;
    Voter voter;
    ExternalBribe xbribe;
    Minter minter;

    function deployVoter() private {
        gaugeFactory = new GaugeFactoryV4();
        bribeFactory = new BribeFactory();

        gaugePlugin = new GaugePlugin(address(FLOW), address(WETH), owners[0]);
        voter = new Voter(
            address(escrow),
            address(factory),
            address(gaugeFactory),
            address(bribeFactory),
            address(gaugePlugin)
        );

        escrow.setVoter(address(voter));
        factory.setVoter(address(voter));

        deployPairWithOwner(address(owner));
        deployOptionTokenV4WithOwner(address(owner), address(gaugeFactory), address(voter), address(escrow));
        deployOptionTokenV4WithOwnerAndExpiry(address(owner), address(gaugeFactory), address(voter), address(escrow));

        gaugeFactory.setOFlow(address(oFlowV4));

        address[] memory tokens = new address[](2);
        tokens[0] = address(FRAX);
        tokens[1] = address(FLOW);
        distributor = new RewardsDistributorV2(address(escrow), address(DAI));
            
        minter = new Minter(
            address(voter),
            address(escrow),
            address(distributor)
        );

        voter.initialize(tokens, address(minter));

        distributor.setDepositor(address(minter));
        FLOW.setMinter(address(minter));
    }

    function setUp() public {
        deployOwners();
        deployCoins();
        mintStables();
        uint256[] memory amounts = new uint256[](3);
        amounts[0] = 1e25;
        amounts[1] = 1e25;
        amounts[2] = 1e25;
        mintFlow(owners, amounts);

        VeArtProxy artProxy = new VeArtProxy();

        deployPairFactoryAndRouter();
        deployMainPairWithOwner(address(owner));

        escrow = new VotingEscrow(address(FLOW), address(flowDaiPair),address(artProxy), owners[0]);

        deployVoter();
    }

    function testTokensLockedForeverWhenUsingMaxLockPOC() public { // @audit Valid
        address user1 = address(owner2);
        address user2 = address(owner3);

        uint256 ts = ((block.timestamp + 1 weeks) / 1 weeks) * 1 weeks;
        vm.warp(ts);

        vm.startPrank(user1);

        FLOW.approve(address(router), TOKEN_100K);
        DAI.approve(address(router), TOKEN_100K);
        router.addLiquidity(address(FLOW), address(DAI), false, TOKEN_100K, TOKEN_100K, 0, 0, user1, block.timestamp);

        flowDaiPair.approve(address(escrow), TOKEN_100K);

        uint256 tokenId = escrow.create_lock(TOKEN_100K, FIFTY_TWO_WEEKS);
        
        escrow.enable_max_lock(tokenId);
        console2.log("Max lock enable ---------^^");

        vm.stopPrank();

        for (uint256 i = 0; i < 51; i++) {
            ts = ((ts + 1 weeks) / 1 weeks) * 1 weeks;
            vm.warp(ts);
            voter.distribute();
        }

        vm.startPrank(user1);

        // Lock has not expired yet
        vm.expectRevert("The lock didn't expire");
        escrow.withdraw(tokenId);
        console2.log("First withdraw ----------^^");

        // escrow.disable_max_lock(tokenId);

        for (uint256 i = 0; i < 52; i++) {
            ts = ((ts + 1 weeks) / 1 weeks) * 1 weeks;
            vm.warp(ts);
            voter.distribute();
        }

        // Now their rewards are permanently stuck since all calls to `claim()` will revert
        vm.expectRevert();
        distributor.claim(tokenId);
        console2.log("First claim    ----------^^");

        // Lock has expired but the locked.end has been extended and will continue to be extended forever
        vm.expectRevert("Lock expired");
        escrow.withdraw(tokenId);
        console2.log("Second withdraw ----------^^");


        for (uint256 i = 0; i < 2; i++) {
            ts = ((ts + 1 weeks) / 1 weeks) * 1 weeks;
            vm.warp(ts);
            voter.distribute();
        }

        vm.expectRevert("Lock expired");
        escrow.withdraw(tokenId);
        console2.log("Third withdraw ----------^^");

        vm.stopPrank();
    }

}
```

## Log output
```Bash
Logs:
  unlock_time     32054400
  _locked.end     32054400
  locked_amount   100000000000000000000000
  block.timestamp 604800
  Max lock enable ---------^^
  unlock_time     62899200
  _locked.end     32054400
  locked_amount   100000000000000000000000
  block.timestamp 31449600
  max lock extended          62899200
  block.timestamp + MAXTIME  62899200
  First withdraw ----------^^
  unlock_time     94348800
  _locked.end     32054400
  locked_amount   100000000000000000000000
  block.timestamp 62899200
  First claim    ----------^^
  unlock_time     94348800
  _locked.end     32054400
  locked_amount   100000000000000000000000
  block.timestamp 62899200
  Second withdraw ----------^^
  unlock_time     95558400
  _locked.end     32054400
  locked_amount   100000000000000000000000
  block.timestamp 64108800
  Third withdraw ----------^^

```
## Tool used

Manual Review, forge

## Recommendation
To fix this issue, `_locked.end` would need to be updated with the new `unlock_time` before this check occurs, or the check would need to be modified to compare `unlock_time` with `block.timestamp` instead of `_locked.end`.