Gentle Green Mongoose

Medium

# The `timestamp` variable of a checkpoint is not initialized

## Summary

The `timestamp` variable of a checkpoint is not initialized anywhere in the codebase. 

## Vulnerability Detail

A checkpoint contains a `timestamp` variable which stores the block number the checkpoint is created:
```solidity
    struct Checkpoint {
        uint timestamp;
        uint[] tokenIds;
    }
```
The `VotingEscrow._findWhatCheckpointToWrite` and `VotingEscrow.getPastVotesIndex` functions rely on the `timestamp` variable of a checkpoint for computation. The following is a list of functions that calls these two affected functions:
```sh
_findWhatCheckpointToWrite -> _moveTokenDelegates -> _transferFrom
_findWhatCheckpointToWrite -> _moveTokenDelegates -> _mint
_findWhatCheckpointToWrite -> _moveTokenDelegates -> _burn
_findWhatCheckpointToWrite -> _moveAllDelegates -> _delegate -> delegate/delegateBySig

getPastVotesIndex -> getPastVotes
```
The `VotingEscrow._findWhatCheckpointToWrite` function verifies if the `timestamp` of the latest checkpoint of an account is equal to the current block number. If true, the function will return the index number of the last checkpoint.
```solidity
    function _findWhatCheckpointToWrite(address account)
        internal
        view
        returns (uint32)
    {
        uint _timestamp = block.timestamp;
        uint32 _nCheckPoints = numCheckpoints[account];

        if (
            _nCheckPoints > 0 &&
            checkpoints[account][_nCheckPoints - 1].timestamp == _timestamp
        ) {
            return _nCheckPoints - 1;
        } else {
            return _nCheckPoints;
        }
    }
```
As such, this function does not work as intended and will always return the index of a new checkpoint.

The `VotingEscrow.getPastVotesIndex` function relies on the `timestamp` of the latest checkpoint for optimization purposes. If the request block number is the most recently updated checkpoint, it will return the latest index immediately and skip the binary search. Since the `timestamp` variable is not populated, the optimization will not work.
```solidity
    function getPastVotesIndex(address account, uint timestamp) public view returns (uint32) {
        uint32 nCheckpoints = numCheckpoints[account];
        if (nCheckpoints == 0) {
            return 0;
        }
        // First check most recent balance
        if (checkpoints[account][nCheckpoints - 1].timestamp <= timestamp) {
            return (nCheckpoints - 1);
        }

        // Next check implicit zero balance
        if (checkpoints[account][0].timestamp > timestamp) {
            return 0;
        }

        uint32 lower = 0;
        uint32 upper = nCheckpoints - 1;
        while (upper > lower) {
            uint32 center = upper - (upper - lower) / 2; // ceil, avoiding overflow
            Checkpoint storage cp = checkpoints[account][center];
            if (cp.timestamp == timestamp) {
                return center;
            } else if (cp.timestamp < timestamp) {
                lower = center;
            } else {
                upper = center - 1;
            }
        }
        return lower;
    }
```

## Impact
Any function that relies on the `timestamp` of a checkpoint will break.

## Code Snippet

[VotingEscrow.sol#L1306](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1306)

[VotingEscrow.sol#L1413](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1413)

## Tool used

Manual Review

## Recommendation
Initialize the `timestamp` variable of the checkpoint in the codebase