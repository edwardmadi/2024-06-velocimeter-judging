Decent Mandarin Pangolin

High

# Voter.detachTokenFromGauge can be called by anyone and allows bypassing the attachments checks, thus allowing to execute withdrawals on locked tokens

## Summary
Due to missing access control checks in the `detachTokenFromGauge` function of the `Voter` contract, although initially during the `deposit` in the `GaugeV4`, the
```solidity
        if (tokenId > 0) {
            require(IVotingEscrow(_ve).ownerOf(tokenId) == account);
            if (tokenIds[account] == 0) {
                tokenIds[account] = tokenId;
                IVoter(voter).attachTokenToGauge(tokenId, account);
            }
            require(tokenIds[account] == tokenId);
        } else {
            tokenId = tokenIds[account];
        }
```
`Voter.attachTokenToGauge` is called, the attacker can detach the token from the gauge simply by calling `Voter.detachTokenFromGauge` shortly after.

## Vulnerability Detail
This allows the attacker inflate the `GaugeV4`'s `totalSupply` and `derivedSupply` variables, as well as this allows writing fake `checkpoints[account][...].balanceOf` values for different gauges.

An attacker can `GaugeV4.deposit` a `tokenId` as usual, but then call `Voter.detachTokenFromGauge(tokenId)`, passing in that specific `tokenId`, and finally be able to call `VotingEscrow.withdraw` or `VotingEscrow.merge` with that `tokenId`, although it is not intended by the protocol's logic.

Normally, `withdraw` would revert if the `tokenId` was attached to at least 1 gauge.

But due to missing permission checks in the `Voter.detachTokenFromGauge` function, the attacker can erase the `tokenId`'s gauge-related attachments records, and unlock the `VotingEscrow.withdraw` function.

And the `VotingEscrow` will never know that some gauges think that they still have the `tokenId` attached and reserved for them.

## Impact
Inflation of gauge rewards.

## Code Snippet
The problem lies in this function:
```solidity
    function detachTokenFromGauge(uint tokenId, address account) external {
        if (tokenId > 0) IVotingEscrow(_ve).detach(tokenId);
        emit Detach(account, msg.sender, tokenId);
    }
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/Voter.sol#L445

Unlike `attachTokenToGauge`, it is not permissioned.

Look for contrast at the `attackTokenToGauge` function:
```solidity
    function attachTokenToGauge(uint tokenId, address account) external {
        require(isGauge[msg.sender]);
        require(isAlive[msg.sender]); // killed gauges cannot attach tokens to themselves
        if (tokenId > 0) IVotingEscrow(_ve).attach(tokenId);
        emit Attach(account, msg.sender, tokenId);
    }
```

Through a call to `GaugeV4.deposit`, the `tokenId` is supposed to remain attached until the `GaugeV4.withdraw` is called:
```solidity
    function _deposit(address account, uint amount, uint tokenId) private {
        require(amount > 0);
        _updateRewardForAllTokens();

        _safeTransferFrom(stake, msg.sender, address(this), amount);
        totalSupply += amount;
        balanceOf[account] += amount;

        if (tokenId > 0) {
            require(IVotingEscrow(_ve).ownerOf(tokenId) == account);
            if (tokenIds[account] == 0) {
                tokenIds[account] = tokenId;
                IVoter(voter).attachTokenToGauge(tokenId, account); // @@ <<< take a look here, the tokenId is attached to this gauge
            }
            require(tokenIds[account] == tokenId);
        } else {
            tokenId = tokenIds[account];
        }

        uint _derivedBalance = derivedBalances[account];
        derivedSupply -= _derivedBalance;
        _derivedBalance = derivedBalance(account);
        derivedBalances[account] = _derivedBalance;
        derivedSupply += _derivedBalance;

        _writeCheckpoint(account, _derivedBalance);
        _writeSupplyCheckpoint();

        IVoter(voter).emitDeposit(tokenId, account, amount);
        emit Deposit(account, tokenId, amount);
    }
```
And the `tokenId` is only supposed to be detached during the withdrawal:
```solidity
    function withdrawToken(uint amount, uint tokenId) public lock {
        _updateRewardForAllTokens();

        uint256 totalBalance = balanceOf[msg.sender];
        uint256 lockedAmount = balanceWithLock[msg.sender];
        uint256 freeAmount = totalBalance - lockedAmount;
        // Update lock related mappings when withdraw amount greater than free amount
        if (amount > freeAmount) {
            // Check if lock has expired
            require(block.timestamp >= lockEnd[msg.sender], "The lock didn't expire");
            uint256 newLockedAmount = totalBalance - amount;
            if (newLockedAmount == 0) {
                delete lockEnd[msg.sender];
                delete balanceWithLock[msg.sender];
            } else {
                balanceWithLock[msg.sender] = newLockedAmount;
            }
        }

        totalSupply -= amount;
        balanceOf[msg.sender] -= amount;
        _safeTransfer(stake, msg.sender, amount);

        if (tokenId > 0) {
            require(tokenId == tokenIds[msg.sender]);
            tokenIds[msg.sender] = 0;
            IVoter(voter).detachTokenFromGauge(tokenId, msg.sender); // @@ <<< in reality, the token should only be detached at this stage
        } else {
            tokenId = tokenIds[msg.sender];
        }

        uint _derivedBalance = derivedBalances[msg.sender];
        derivedSupply -= _derivedBalance;
        _derivedBalance = derivedBalance(msg.sender);
        derivedBalances[msg.sender] = _derivedBalance;
        derivedSupply += _derivedBalance;

        _writeCheckpoint(msg.sender, derivedBalances[msg.sender]);
        _writeSupplyCheckpoint();

        IVoter(voter).emitWithdraw(tokenId, msg.sender, amount);
        emit Withdraw(msg.sender, tokenId, amount);
    }
```

The ability to manually and permissionlessly detach a `tokenId` from a gauge that it was attached to allows the attacker to withdraw his `tokenId` from the `VotingEscrow` and bypass its checks related to the attachments:
```solidity
    function withdraw(uint _tokenId) external nonreentrant {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
        require(attachments[_tokenId] == 0 && !voted[_tokenId], "attached"); // @@ <<< the attachments check can be bypassed

        LockedBalance memory _locked = locked[_tokenId];
        require(block.timestamp >= _locked.end, "The lock didn't expire");
        uint value = uint(int256(_locked.amount));

        locked[_tokenId] = LockedBalance(0,0);
        uint supply_before = supply;
        supply = supply_before - value;

        // old_locked can have either expired <= timestamp or zero end
        // _locked has only 0 end
        // Both can have >= 0 amount
        _checkpoint(_tokenId, _locked, LockedBalance(0,0));

        assert(IERC20(lpToken).transfer(msg.sender, value));

        // Burn the NFT
        _burn(_tokenId);

        emit Withdraw(msg.sender, _tokenId, value, block.timestamp);
        emit Supply(supply_before, supply_before - value);
    }
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L956

## Tool used
Manual human review.

## Recommendation
Add the following access control assertions:
```diff
    function detachTokenFromGauge(uint tokenId, address account) external {
+      require(isGauge[msg.sender]);
+      require(isAlive[msg.sender]); // killed gauges cannot attach tokens to themselves
        if (tokenId > 0) IVotingEscrow(_ve).detach(tokenId);
        emit Detach(account, msg.sender, tokenId);
    }
```