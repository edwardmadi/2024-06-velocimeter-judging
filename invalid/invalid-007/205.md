Wonderful Coconut Corgi

High

# Excessive access control in _burn() function disrupts merge and withdraw functionality.

## Summary
- The `_burn()` function of `votingEscrow.sol` requires excessive access control, expecting `msg.sender` to have approval for all token IDs owned by the owner rather than just the specific token ID involved. Consequently, the `withdraw()` and `merge()` transactions, which depend on the internal `_burn()` function, will fail despite `msg.sender` having approval for the relevant token ID.

## Vulnerability Detail
- The `_burn(_tokenId)` function is responsible for clearing all pending approvals for the token IDs before removing them from the accounting.
- To achieve this, it makes an internal call to `approve(address(0), _tokenId)`.
```solidity
      function _burn(uint _tokenId) internal {
          require(_isApprovedOrOwner(msg.sender, _tokenId), "caller is not owner nor approved");
  
          address tokenOwner = ownerOf(_tokenId);
  
          // Clear approval
==>       approve(address(0), _tokenId);
          // checkpoint for gov
          _moveTokenDelegates(delegates(tokenOwner), address(0), _tokenId);
          // Remove token
          _removeTokenFrom(tokenOwner, _tokenId);
          emit Transfer(tokenOwner, address(0), _tokenId);
      }
```
- The issue is that `approve(address(0), _tokenId)` expects `msg.sender` to have approval for all tokens owned by the owner of `_tokenId`, rather than just the `_tokenId` itself.
```solidity
      function approve(address _approved, uint _tokenId) public {
          address tokenOwner = idToOwner[_tokenId];
          // Throws if `_tokenId` is not a valid NFT
          require(tokenOwner != address(0));
          // Throws if `_approved` is the current owner
          require(_approved != tokenOwner);
          // Check requirements
          bool senderIsOwner = (idToOwner[_tokenId] == msg.sender);
==>       bool senderIsApprovedForAll = (ownerToOperators[tokenOwner])[msg.sender];
==>       require(senderIsOwner || senderIsApprovedForAll);
          // Set the approval
          idToApprovals[_tokenId] = _approved;
          emit Approval(tokenOwner, _approved, _tokenId);
      }
```
- As `_burn(_tokenId)` is invoked by the `withdraw()` and `merge()` functions,
```solidity
    function withdraw(uint _tokenId) external nonreentrant {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
        require(attachments[_tokenId] == 0 && !voted[_tokenId], "attached");
        .
        .
        .

        // Burn the NFT
 ==>   _burn(_tokenId);

        emit Withdraw(msg.sender, _tokenId, value, block.timestamp);
        emit Supply(supply_before, supply_before - value);
    }
```
```solidity
    function merge(uint _from, uint _to) external {
        require(attachments[_from] == 0 && !voted[_from], "attached");
        require(_from != _to);
        .
        .
        .

 ==>     _burn(_from);
        _deposit_for(_to, value0, end, _locked1, DepositType.MERGE_TYPE);
    }
```
- As a result, even if users have the necessary approval for the specific `_tokenId`, their `withdraw()` and `merge()` transactions will fail.

## Impact
- Users attempting to consolidate voting power through the merge() function (before vote) are denied despite having the necessary approval to do so.
- Users trying to withdraw() tokens for specific tokens ID wont be able to do so.
- This indirectly affects users operations like merging to vote and withdrawing to create new locks, despite having the NFTs approval.

## Code Snippet
- https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L537
- https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L251
- https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L975
- https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1208

## POC
- The following test demonstrates that a spenderâ€™s withdraw() transaction is reverted despite having approval for the token ID. 
- Place the test in the VotingEscrow.t.sol file of the test suite and run it using the command: "forge test --mt testSpenderTxFailDespiteApproval -vvv"

```solidity

    function testSpenderTxFailDespiteApproval() public {

        //Creating a lock
        flowDaiPair.approve(address(escrow), TOKEN_1);
        uint tokenId = escrow.create_lock(TOKEN_1, 1 weeks);

        // withdraw after the time has expired
        vm.warp(block.timestamp + 1 weeks);
        vm.roll(block.number + 1); // mine the next block

        //Owner approves the spender.
        address spender = address(0x123);
        escrow.approve(spender,tokenId);
        
        vm.prank(spender);
        vm.expectRevert();
        escrow.withdraw(tokenId);

        escrow.setApprovalForAll(spender,true);

        vm.prank(spender);
        escrow.withdraw(tokenId);
    }

```

## Tool used
Manual Review and foundry

## Recommendation
- Use internal function `_isApprovedOrOwner(address _spender, uint _tokenId)` instead of senderIsApprovedForAll mapping, when checking for users approval in approve() function.

```diff
function approve(address _approved, uint _tokenId) public {
       address tokenOwner = idToOwner[_tokenId];
       // Throws if `_tokenId` is not a valid NFT
       require(tokenOwner != address(0));
       // Throws if `_approved` is the current owner
       require(_approved != tokenOwner);
       // Check requirements
       bool senderIsOwner = (idToOwner[_tokenId] == msg.sender);
---    bool senderIsApprovedForAll = (ownerToOperators[tokenOwner])[msg.sender];
---    require(senderIsOwner || senderIsApprovedForAll);
+++    bool senderIsApprovedForTokenId = isApprovedOrOwner(msg.sender,_tokenId);
+++    require(senderIsOwner || senderIsApprovedForTokenId);
       // Set the approval
       idToApprovals[_tokenId] = _approved;
       emit Approval(tokenOwner, _approved, _tokenId);
  }
```
- Alternatively, you can make the spender(user) first transfer the token to themselves and then submit the transaction for `withdraw()` or `split()`, but that will be inconvenient and not so gas-efficient.
