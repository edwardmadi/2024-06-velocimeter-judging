Silly Cider Pig

Medium

# Approved user of veNFT can't withdraw it.

## Summary
Approved user of veNFT can't withdraw it.

## Vulnerability Detail
When user calls the `VotingEscrow.sol#withdraw` function, the execution path is `withdraw()` -> `_burn()` -> `approve()`, where `withdraw` and `_burn` functions requires that the sender is `_isApprovedOrOwner()` (owner or operator or approved) but the `approve` function requires that the sender is owner or operator.
```solidity
    function _burn(uint _tokenId) internal {
@>      require(_isApprovedOrOwner(msg.sender, _tokenId), "caller is not owner nor approved");

        address tokenOwner = ownerOf(_tokenId);

        // Clear approval
@>      approve(address(0), _tokenId);
        // checkpoint for gov
        _moveTokenDelegates(delegates(tokenOwner), address(0), _tokenId);
        // Remove token
        _removeTokenFrom(tokenOwner, _tokenId);
        emit Transfer(tokenOwner, address(0), _tokenId);
    }

    function approve(address _approved, uint _tokenId) public {
        address tokenOwner = idToOwner[_tokenId];
        // Throws if `_tokenId` is not a valid NFT
        require(tokenOwner != address(0));
        // Throws if `_approved` is the current owner
        require(_approved != tokenOwner);
        // Check requirements
        bool senderIsOwner = (idToOwner[_tokenId] == msg.sender);
        bool senderIsApprovedForAll = (ownerToOperators[tokenOwner])[msg.sender];
@>      require(senderIsOwner || senderIsApprovedForAll);
        // Set the approval
        idToApprovals[_tokenId] = _approved;
        emit Approval(tokenOwner, _approved, _tokenId);
    }
```
That is, since the requirement for sender does not match, the approved user can't withdraw the NFT token.

PoC:
Add the following test code to the `VotingEscrow.t.sol`.
```solidity
    function testWithdrawByApproved() public {
        flowDaiPair.approve(address(escrow), TOKEN_1);
        uint256 lockDuration = 7 * 24 * 3600; // 1 week

        // create NFT and approve to owner2
        uint tokenId = escrow.create_lock(TOKEN_1, lockDuration);
        escrow.approve(address(owner2), tokenId);

        // withdraw by approved owner2 fail
        vm.prank(address(owner2));
        vm.expectRevert();
        escrow.withdraw(tokenId);

        // but transferFrom to owner2 succeed
        escrow.transferFrom(address(owner), address(owner2), tokenId);
    }
```

## Impact
Approved user of veNFT can't `withdraw()` it.
By the same reason, approved user can't `merge()` the token with others.

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/VotingEscrow.sol#L543

## Tool used

Manual Review

## Recommendation
Modify the `VotingEscrow.sol#_burn` function as follows.
```solidity
    function _burn(uint _tokenId) internal {
        require(_isApprovedOrOwner(msg.sender, _tokenId), "caller is not owner nor approved");

        address tokenOwner = ownerOf(_tokenId);

        // Clear approval
--      approve(address(0), _tokenId);
++      idToApprovals[_tokenId] = address(0);
        // checkpoint for gov
        _moveTokenDelegates(delegates(tokenOwner), address(0), _tokenId);
        // Remove token
        _removeTokenFrom(tokenOwner, _tokenId);
        emit Transfer(tokenOwner, address(0), _tokenId);
    }
```