Fierce Obsidian Locust

Medium

# Incorrect `DOMAIN_TYPEHASH` used in `VotingEscrow.sol`

## Summary
Incorrect `DOMAIN_TYPEHASH` used in `VotingEscrow.sol`

## Vulnerability Detail
`DOMAIN_TYPEHASH` is used in `VotingEscrow.sol` as a part of `delegateBySig()` function.

```solidity
    function delegateBySig(
        address delegatee,
        uint nonce,
        uint expiry,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) public {
        bytes32 domainSeparator = keccak256(
            abi.encode(
@>                DOMAIN_TYPEHASH,
                keccak256(bytes(name)),
@>                keccak256(bytes(version)),
                block.chainid,
                address(this)
            )
        );
        bytes32 structHash = keccak256(
            abi.encode(DELEGATION_TYPEHASH, delegatee, nonce, expiry)
        );
        bytes32 digest = keccak256(
            abi.encodePacked("\x19\x01", domainSeparator, structHash)
        );
        address signatory = ecrecover(digest, v, r, s);
        require(
            signatory != address(0),
            "VotingEscrow::delegateBySig: invalid signature"
        );
        require(
            nonce == nonces[signatory]++,
            "VotingEscrow::delegateBySig: invalid nonce"
        );
        require(
            block.timestamp <= expiry,
            "VotingEscrow::delegateBySig: signature expired"
        );
        return _delegate(signatory, delegatee);
    }
```

Now, see the line of code from above function:

```solidity
        bytes32 domainSeparator = keccak256(
            abi.encode(
                DOMAIN_TYPEHASH,
                keccak256(bytes(name)),
                keccak256(bytes(version)),
                block.chainid,
                address(this)
            )
        );
```
While calculating the `domainSeparator`, the function has used `DOMAIN_TYPEHASH`, `name`, `version`, `block.chainid`, `address(this)` (verifying address) and this is 100% as per [EIP712]([EIP 712](https://eips.ethereum.org/EIPS/eip-712)).

However, `DOMAIN_TYPEHASH` used actually broken the defination of DOMAIN_TYPEHASH:

```solidity
    /// @notice The EIP-712 typehash for the contract's domain
    bytes32 public constant DOMAIN_TYPEHASH = keccak256("EIP712Domain(string name,uint256 chainId,address verifyingContract)");
```

In the build of the `DOMAIN TYPEHASH` the string `version` is MISSING, but the `delegateBySig()` function is using and calculating the `domainSeparator` with the `version`.

## Impact
Some contract or dapp/backend could building the `DOMAIN_TYPEHASH` with "correct" struct(i.e including the `version`) and try to use the `delegateBySig()` function but this function will revert with `"VotingEscrow::delegateBySig: invalid signature"`  because the expected `DOMAIN_TYPEHASH` in the VotingEscrow.sol contract was built with the "wrong" struct(i.e it didn't include version).


## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1259

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1515-L1522

## Tool used
Manual Review

## Recommendation
Add `version` as part of DOMAIN_TYPEHASH. It should be noted that `DOMAIN_TYPEHASH` is claimed to follow EIP712 as per Natspec.

consider below changes:

```diff
    /// @notice The EIP-712 typehash for the contract's domain
-    bytes32 public constant DOMAIN_TYPEHASH = keccak256("EIP712Domain(string name,uint256 chainId,address verifyingContract)");
+    bytes32 public constant DOMAIN_TYPEHASH = keccak256("EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)");
```