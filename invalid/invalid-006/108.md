Silly Cider Pig

High

# `VotingEscrow.sol#_moveTokenDelegates` function has errors.

## Summary
`VotingEscrow.sol#_moveTokenDelegates` function doesn't record timestamp of checkpoints.
Therefore, the `numCheckpoints` increases even in the same block.

## Vulnerability Detail
`VotingEscrow.sol#_moveTokenDelegates` function is the following.
```solidity
    function _moveTokenDelegates(
        address srcRep,
        address dstRep,
        uint _tokenId
    ) internal {
        if (srcRep != dstRep && _tokenId > 0) {
            if (srcRep != address(0)) {
                uint32 srcRepNum = numCheckpoints[srcRep];
                uint[] storage srcRepOld = srcRepNum > 0
                    ? checkpoints[srcRep][srcRepNum - 1].tokenIds
                    : checkpoints[srcRep][0].tokenIds;
                uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep);
                uint[] storage srcRepNew = checkpoints[srcRep][
                    nextSrcRepNum
                ].tokenIds;
                // All the same except _tokenId
                for (uint i = 0; i < srcRepOld.length; i++) {
                    uint tId = srcRepOld[i];
                    if (tId != _tokenId) {
                        srcRepNew.push(tId);
                    }
                }

                numCheckpoints[srcRep] = srcRepNum + 1;
            }

            ......
        }
    }
```
As can be seen, the function doesn't record timestamp of checkpoints.

PoC:

At first, we add the following getter function to the `VotingEscrow.sol` contract.
```solidity
    function getCheckPoint(address account, uint32 index) public view returns (Checkpoint memory) {
        return checkpoints[account][index];
    }
```
Next, we add the following test function to the `VotingEscrow.t.sol` contract.
```solidity
    function printCheckpoints() private {
        emit log_named_uint("block.number", block.number);
        emit log_named_uint("block.timestamp", block.timestamp);
        emit log("checkpoints for owner");
        uint32 numCheckpoints = escrow.numCheckpoints(address(owner));
        emit log_named_uint("  numCheckpoints", numCheckpoints);
        for (uint32 i = 0; i < numCheckpoints; i++) {
            VotingEscrow.Checkpoint memory checkpoint = escrow.getCheckpoint(address(owner), i);
            emit log_named_uint("  index", i);
            emit log_named_uint("    checkpoint.timestamp", checkpoint.timestamp);
            emit log_named_array("    checkpoint.tokenIds", checkpoint.tokenIds);
        }
    }

    function testMoveTokenDelegate() public {
        flowDaiPair.approve(address(escrow), TOKEN_10);
        uint256 lockDuration = 7 * 24 * 3600;

        emit log("--- owner has two tokens ---");

        escrow.create_lock(TOKEN_1, lockDuration);
        escrow.create_lock(TOKEN_1, lockDuration);
        printCheckpoints();

        emit log("--- owner transfered one token within the same block ---");

        escrow.transferFrom(address(owner), address(owner2), 1);
        printCheckpoints();

        emit log("--- owner transfered one token after 1 block (10 seconds) ---");

        vm.warp(block.timestamp + 10);
        vm.roll(block.number + 1);

        escrow.transferFrom(address(owner), address(owner2), 2);
        printCheckpoints();
    }
```
Then we run the following command.
```sh
$ forge test -vvv --mt testMoveTokenDelegate
```
Finally, we obtain the following result.
```sh
Ran 1 test for test/VotingEscrow.t.sol:VotingEscrowTest
[PASS] testMoveTokenDelegate() (gas: 1291375)
Logs:
  --- owner has two tokens ---
  block.number: 1
  block.timestamp: 1
  checkpoints for owner
    numCheckpoints: 2
    index: 0
      checkpoint.timestamp: 0
      checkpoint.tokenIds: [1]
    index: 1
      checkpoint.timestamp: 0
      checkpoint.tokenIds: [1, 2]
  --- owner transfered one token within the same block ---
  block.number: 1
  block.timestamp: 1
  checkpoints for owner
    numCheckpoints: 3
    index: 0
      checkpoint.timestamp: 0
      checkpoint.tokenIds: [1]
    index: 1
      checkpoint.timestamp: 0
      checkpoint.tokenIds: [1, 2]
    index: 2
      checkpoint.timestamp: 0
      checkpoint.tokenIds: [2]
  --- owner transfered one token after 1 block (10 seconds) ---
  block.number: 2
  block.timestamp: 11
  checkpoints for owner
    numCheckpoints: 4
    index: 0
      checkpoint.timestamp: 0
      checkpoint.tokenIds: [1]
    index: 1
      checkpoint.timestamp: 0
      checkpoint.tokenIds: [1, 2]
    index: 2
      checkpoint.timestamp: 0
      checkpoint.tokenIds: [2]
    index: 3
      checkpoint.timestamp: 0
      checkpoint.tokenIds: []

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.63ms (1.65ms CPU time)

Ran 1 test suite in 26.72ms (7.63ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```
As can be seen, the function increases the `numCheckpoints` within the same block and the timstamp of all checkpoints are 0.

## Impact
The `checkpoints` are used in `VotingEscrow.sol#getPastVotes`, so the issue causes serious problems to the governance voting.

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/VotingEscrow.sol#L1362-L1411

## Tool used

Manual Review

## Recommendation
Modify `VotingEscrow.sol#_moveTokenDelegates` function as follows.
```solidity
    function _moveTokenDelegates(
        address srcRep,
        address dstRep,
        uint _tokenId
    ) internal {
        if (srcRep != dstRep && _tokenId > 0) {
            if (srcRep != address(0)) {
                uint32 srcRepNum = numCheckpoints[srcRep];
--              uint[] storage srcRepOld = srcRepNum > 0
++              uint[] memory srcRepOld = srcRepNum > 0
                    ? checkpoints[srcRep][srcRepNum - 1].tokenIds
                    : checkpoints[srcRep][0].tokenIds;
                uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep);
                uint[] storage srcRepNew = checkpoints[srcRep][
                    nextSrcRepNum
                ].tokenIds;
                // All the same except _tokenId
++              for (int i = int(srcRepNew.length) - 1; i >= 0; i--) {
++                  srcRepNew.pop();
++              }
                for (uint i = 0; i < srcRepOld.length; i++) {
                    uint tId = srcRepOld[i];
                    if (tId != _tokenId) {
                        srcRepNew.push(tId);
                    }
                }

++              checkpoints[srcRep][nextSrcRepNum].timestamp = block.timestamp;
--              numCheckpoints[srcRep] = srcRepNum + 1;
++              numCheckpoints[srcRep] = nextSrcRepNum + 1;
            }

            if (dstRep != address(0)) {
                uint32 dstRepNum = numCheckpoints[dstRep];
--              uint[] storage dstRepOld = dstRepNum > 0
++              uint[] memory dstRepOld = dstRepNum > 0
                    ? checkpoints[dstRep][dstRepNum - 1].tokenIds
                    : checkpoints[dstRep][0].tokenIds;
                uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
                uint[] storage dstRepNew = checkpoints[dstRep][
                    nextDstRepNum
                ].tokenIds;
                // All the same plus _tokenId
                require(
                    dstRepOld.length + 1 <= MAX_DELEGATES,
                    "dstRep would have too many tokenIds"
                );
++              for (int i = int(dstRepNew.length) - 1; i >= 0; i--) {
++                  dstRepNew.pop();
++              }
                for (uint i = 0; i < dstRepOld.length; i++) {
                    uint tId = dstRepOld[i];
                    dstRepNew.push(tId);
                }
                dstRepNew.push(_tokenId);

++              checkpoints[dstRep][nextDstRepNum].timestamp = block.timestamp;
--              numCheckpoints[dstRep] = dstRepNum + 1;
++              numCheckpoints[dstRep] = nextDstRepNum + 1;
            }
         }
    }
```
The test result is the following.
```sh
Ran 1 test for test/VotingEscrow.t.sol:VotingEscrowTest
[PASS] testMoveTokenDelegate() (gas: 1217957)
Logs:
  --- owner has two tokens ---
  block.number: 1
  block.timestamp: 1
  checkpoints for owner
    numCheckpoints: 1
    index: 0
      checkpoint.timestamp: 1
      checkpoint.tokenIds: [1, 2]
  --- owner transfered one token within the same block ---
  block.number: 1
  block.timestamp: 1
  checkpoints for owner
    numCheckpoints: 1
    index: 0
      checkpoint.timestamp: 1
      checkpoint.tokenIds: [2]
  --- owner transfered one token after 1 block (10 seconds) ---
  block.number: 2
  block.timestamp: 11
  checkpoints for owner
    numCheckpoints: 2
    index: 0
      checkpoint.timestamp: 1
      checkpoint.tokenIds: [2]
    index: 1
      checkpoint.timestamp: 11
      checkpoint.tokenIds: []

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.11ms (1.09ms CPU time)

Ran 1 test suite in 33.42ms (9.11ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```