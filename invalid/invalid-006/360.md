Salty Pine Panda

Medium

# Users can inflate their voting balance

## Summary
Under certain circumstances, a checkpoint can contain duplicate tokenIds, which can be used to inflate their voting balance.

## Vulnerability Detail

When a user calls the `transferFrom` or `safeTransferFrom` functions, the internal `_transferFrom` function is called and it handles the redelegation of the token from the sender to the receiver. This is done in the `_moveTokenDelegates` function.
```solidity
    function _transferFrom(
        address _from,
        address _to,
        uint _tokenId,
        address _sender
    ) internal {
//...
        // auto re-delegate
        _moveTokenDelegates(delegates(_from), delegates(_to), _tokenId);
//...
        // Log the transfer
        emit Transfer(_from, _to, _tokenId);
    }
```
In the `_moveTokenDelegates` function, all the `_from`'s tokenIds are fetched and set as `srcRepOld`. A new array `srcRepNew` is created into which all the `_from`'s tokenids are copied, expect from the tokenId being transferred. The same process if then repeated for the `_to`, its initial tokenIds are fetched and set as `dstRepOld` and a new array `dstRepNew` is created. The `_to`'s entire tokenIds is then copied into the `dstRepNew` array plus the tokenId being transferred. The logic works fine as long as a new empty array (srcRepNew OR dstRepNew) is created every single time. The code relies on the `_findWhatCheckpointToWrite` function to return the index of a new checkpoint.

```solidity
    function _moveTokenDelegates(
        address srcRep,
        address dstRep,
        uint _tokenId
    ) internal {
        if (srcRep != dstRep && _tokenId > 0) {
            if (srcRep != address(0)) {
                uint32 srcRepNum = numCheckpoints[srcRep];
                uint[] storage srcRepOld = srcRepNum > 0
                    ? checkpoints[srcRep][srcRepNum - 1].tokenIds
                    : checkpoints[srcRep][0].tokenIds;
                uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep);
                uint[] storage srcRepNew = checkpoints[srcRep][
                    nextSrcRepNum
                ].tokenIds;
                // All the same except _tokenId
                for (uint i = 0; i < srcRepOld.length; i++) {
                    uint tId = srcRepOld[i];
                    if (tId != _tokenId) {
                        srcRepNew.push(tId);
                    }
                }

                numCheckpoints[srcRep] = srcRepNum + 1;
            }

            if (dstRep != address(0)) {
                uint32 dstRepNum = numCheckpoints[dstRep];
                uint[] storage dstRepOld = dstRepNum > 0
                    ? checkpoints[dstRep][dstRepNum - 1].tokenIds
                    : checkpoints[dstRep][0].tokenIds;
                uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
                uint[] storage dstRepNew = checkpoints[dstRep][
                    nextDstRepNum
                ].tokenIds;
                // All the same plus _tokenId
                require(
                    dstRepOld.length + 1 <= MAX_DELEGATES,
                    "dstRep would have too many tokenIds"
                );
                for (uint i = 0; i < dstRepOld.length; i++) {
                    uint tId = dstRepOld[i];
                    dstRepNew.push(tId);
                }
                dstRepNew.push(_tokenId);

                numCheckpoints[dstRep] = dstRepNum + 1;
            }
        }
    }
```

The `_findWhatCheckpointToWrite` however, does not always return the index of a new checkpoint. It will return the last checkpoint if it has already been written once with the same timestamp. This ideally would not be a problem on a chain like ethereum, but the protocol plans to deploy on EVM compatible chains, and as a note, on L2 blockchains, it is quite common for multiple L2 Blocks to have the same `block.timestamp`. So a malicious user can trigger the `_moveTokenDelegates` functions more than once, within the same block, or across the blocks with the same `block.timestamp`, for instance, transferring tokens more than once to the same person,  the `_findWhatCheckpointToWrite` function will return a new checkpoint in the first transfer but will return the last/previous checkpoint in the subsequent transfers.  As a result, the `srcRepNew` and `dstRepNew` array will reference back to the old checkpoint from the first transfer. This will cause the move token delegate logic to be off during the subsequent transfers.

```solidity
    function _findWhatCheckpointToWrite(address account)
        internal
        view
        returns (uint32)
    {
        uint _timestamp = block.timestamp;
        uint32 _nCheckPoints = numCheckpoints[account];

        if (
            _nCheckPoints > 0 &&
            checkpoints[account][_nCheckPoints - 1].timestamp == _timestamp
        ) {
            return _nCheckPoints - 1;
        } else {
            return _nCheckPoints;
        }
    }
```

Consequently, the `numCheckpoints` will be incorrect, the tokenid transferred the second time will be added to the `_to` but not removed from the `_from` and the `_to`'s existing tokenids (including the one from the first transfer) will be duplicated.  

This can also occur if the `delegate`/`delegateBySig` function is called directly, due to a similar logic in the `_moveAllDelegates` function.

## Impact

Since the token IDs within the checkpoint will be used to determine the voting power, the voting power will be inflated in this case as there will be a double count of certain tokens. 

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L331

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1362

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1413

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1431

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1488


## Tool used
Manual Code Review

## Recommendation

Update the move token delegate logic within the affected functions to ensure that the latest checkpoint is overwritten correctly when the functions are triggered more than once within a single block.