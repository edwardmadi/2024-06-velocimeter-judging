Polite Butter Gazelle

Medium

# `RewardsDistributorV2` contract can never distribute rewards

## Summary

Users can lock their tokens in `VotingEscrow` to receive a `veNFT`, which allows them to receive rewards via `RewardsDistributorV2`.

In [Velocimeter V3](https://github.com/Velocimeter/v3-contracts/blob/master/contracts/Minter.sol#L154), the `growth` of weekly emissions of `FLOW` tokens were sent from `Minter` to `RewardsDistributor`, which was then used to distribute rewards to `veNFT` holders.

However, in `Velocimeter V4` this is no longer the case. The `Minter` contract no longer sends rewards to the `Rewards distributor` contract, and it was not found anywhere in the codebase where rewards are sent to `RewardsDistributorV2`.

This will render the `RewardsDistributorV2` contract, which is in-scope for this contest, completely useless.

## Vulnerability Detail

`veNFT` holders can claim rewards each epoch via `RewardsDistributorV2::claim`:

[RewardsDistributorV2.sol#L282-L294](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/RewardsDistributorV2.sol#L282-L294)
```javascript
    function claim(uint _tokenId) external returns (uint) {
        require(IVotingEscrow(voting_escrow).isApprovedOrOwner(msg.sender, _tokenId));

        if (block.timestamp >= time_cursor) _checkpoint_total_supply();
        uint _last_token_time = last_token_time;
        _last_token_time = _last_token_time / WEEK * WEEK;
        uint amount = _claim(_tokenId, voting_escrow, _last_token_time);
        if (amount != 0) {
@>          _safeTransfer(msg.sender,amount);
            token_last_balance -= amount;
        }
        return amount;
    }
```

However, the `Minter` contract no longer sends rewards to `Rewards distributor`:

[Minter.sol#L112-L137](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Minter.sol#L112-L137)
```javascript
    function update_period() external returns (uint) {
        uint _period = active_period;
        if (block.timestamp >= _period + WEEK && initializer == address(0)) { // only trigger if new week
            _period = (block.timestamp / WEEK) * WEEK;
            active_period = _period;
            uint256 weekly = weekly_emission();

            uint _teamEmissions = (teamRate * weekly) /
                (PRECISION - teamRate);
            uint _required =  weekly + _teamEmissions;
            uint _balanceOf = _flow.balanceOf(address(this));
            if (_balanceOf < _required) {
                _flow.mint(address(this), _required - _balanceOf);
            }

            require(_flow.transfer(teamEmissions, _teamEmissions));

            _checkpointRewardsDistributors();

            _flow.approve(address(_voter), weekly);
            _voter.notifyRewardAmount(weekly);

            emit Mint(msg.sender, weekly, circulating_supply());
        }
        return _period;
    }
```

Unlike `Minter` in `Velocimeter V3`:

[Minter.sol#L137-L164](https://github.com/Velocimeter/v3-contracts/blob/master/contracts/Minter.sol#L137-L164)
```javascript
    function update_period() external returns (uint) {
        uint _period = active_period;
        if (block.timestamp >= _period + WEEK && initializer == address(0)) { // only trigger if new week
            _period = (block.timestamp / WEEK) * WEEK;
            active_period = _period;
            weekly = weekly_emission();

            uint _growth = calculate_growth(weekly);
            uint _teamEmissions = (teamRate * (_growth + weekly)) /
                (PRECISION - teamRate);
            uint _required = _growth + weekly + _teamEmissions;
            uint _balanceOf = _flow.balanceOf(address(this));
            if (_balanceOf < _required) {
                _flow.mint(address(this), _required - _balanceOf);
            }

            require(_flow.transfer(team, _teamEmissions));
@>          require(_flow.transfer(address(_rewards_distributor), _growth));
            _rewards_distributor.checkpoint_token(); // checkpoint token balance that was just minted in rewards distributor
            _rewards_distributor.checkpoint_total_supply(); // checkpoint supply

            _flow.approve(address(_voter), weekly);
            _voter.notifyRewardAmount(weekly);

            emit Mint(msg.sender, weekly, circulating_supply(), circulating_emission());
        }
        return _period;
    }
```

Now, the rewards are only sent to the specified `team` and `voter`. It was not found anywhere in the codebase where `RewardsDistibutorV2` receives rewards, which will DoS any calls to `claim` and render the contract useless.

## Impact

`RewardsDistributorV2` contract is rendered useless, `veNFT` holders will lose out on possible rewards.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/RewardsDistributorV2.sol#L282-L294

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Minter.sol#L112-L137

## Tool used

Manual Review

## Recommendation

Consider sending rewards from `Minter` to `RewardsDistributorV2`.