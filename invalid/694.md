Active Lace Hippo

Low/Info

# Missing Access Controls On `GaugeFactory::createGauge`

## Summary

Accounts may errantly create [`Gauge`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Gauge.sol)s for [`Pair`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol)s which do not possess whitelisted tokens, and even for non-Velocimeter pairs.

## Vulnerability Detail

Velocimeter defines an [**invariant**](https://github.com/sherlock-audit/2024-06-velocimeter/tree/main?tab=readme-ov-file#q-if-you-are-integrating-tokens-are-you-allowing-only-whitelisted-tokens-to-work-with-the-codebase-or-any-complying-with-the-standard-are-they-assumed-to-have-certain-properties-eg-be-non-reentrant-are-there-any-types-of-weird-tokens-you-want-to-integrate) that [`Gauge`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Gauge.sol)s must not be created for [`Pair`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol)s which do not possess whitelisted tokens:

> Any users can create gauge for pools with whitelisted tokens on one side. (Permissionless gauge creation)
> Otherwise, gauge for pools without whitelisted tokens **can only be created by protocol**.

These controls are intended to be imposed at the [`Voter`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol):

```solidity
if (msg.sender != governor && msg.sender != emergencyCouncil) { // gov can create for any pool, even non-Velocimeter pairs
    require(isPair, "!_pool"); /// @audit must_be_a_velocimeter_native_pair
    require(IGaugePlugin(gaugePlugin).checkGaugeCreationAllowance(msg.sender, tokenA, tokenB), "!whitelistedForGaugeCreation"); /// @audit must_use_whitelisted_tokens
}

// ...

address _gauge = IGaugeFactory(_gaugeFactory).createGauge(_pool, _external_bribe, _ve, isPair, allowedRewards);
```

However, these restrictions do not prevent an account from manually invoking [`createGauge`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/factories/GaugeFactoryV4.sol#L20C5-L26C6) directly on the [`GaugeFactoryV4`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/factories/GaugeFactoryV4.sol):

```solidity
function createGauge(address _pool, address _external_bribe, address _ve, bool isPair, address[] memory allowedRewards) external returns (address) {
    last_gauge = address(new GaugeV4(_pool, _external_bribe, _ve, msg.sender, oFlow, address(this), isPair, allowedRewards));
    if (oFlow != address(0)) {
        IAccessControl(oFlow).grantRole(MINTER_ROLE, last_gauge); /// @audit is_integrated_with_protocol
    }
    return last_gauge;
}
```

Due to the lack of access controls, accounts are able to bypass the intended validation rules.

It can be demonstrated that all variables can be specified arbitrarily, resulting in the creation of [`Gauge`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Gauge.sol)s not just for pools which not only fail to contain non-whitelisted tokens, but even for arbitrary smart contract dependencies which conform in a superficial manner to the demands of a [`Gauge`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Gauge.sol).

> [!IMPORTANT]
> It should be emphasized that this isn't as trivial as merely deploying an arbitrary instance of a [`Gauge`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Gauge.sol) that is not associated to the protocol in a meaningful way, as any permissionless actor with access to the source is free to do.
> 
> We can see that the created [`Gauge`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Gauge.sol) can be assigned the `MINTER_ROLE` by the [`GaugeFactoryV4`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/factories/GaugeFactoryV4.sol), and able to perform meaningful liquidity incentivisation within the protocol, even when referring to an unsupported (i.e. third party) pool.
>
> Additionally, we can see the [`ProxyGaugeFactory`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/factories/ProxyGaugeFactory.sol#L19C102-L19C111) _does_ impose the correct access controls:
> ```solidity
> function deployGauge(address _notifyAddress,address _pairAddress,string memory _symbol) external onlyOwner returns (address) {
>     address last_gauge = address(new ProxyGauge(flow,_notifyAddress,_symbol));
>     if(_pairAddress == address(0)) {
>         _pairAddress = last_gauge;
>     }
>     isWhitelisted[_pairAddress] = last_gauge;
>     return last_gauge;
> } 
> ```

## Impact

Medium, the documented protocol invariant is undermined.

## Code Snippet

```solidity
function createGauge(address _pool, address _external_bribe, address _ve, bool isPair, address[] memory allowedRewards) external returns (address) {
    last_gauge = address(new GaugeV4(_pool, _external_bribe, _ve, msg.sender, oFlow, address(this), isPair, allowedRewards));
    if (oFlow != address(0)) {
        IAccessControl(oFlow).grantRole(MINTER_ROLE, last_gauge);
    }
    return last_gauge;
}
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/factories/GaugeFactoryV4.sol#L20C5-L26C6

## Tool used

Manual Review

## Recommendation

Add access controls to the [`GaugeFactoryV4`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/factories/GaugeFactoryV4.sol) to avoid errant gauge creation:

```diff
- function createGauge(address _pool, address _external_bribe, address _ve, bool isPair, address[] memory allowedRewards) external returns (address) {
+ function createGauge(address _pool, address _external_bribe, address _ve, bool isPair, address[] memory allowedRewards) external onlyVoter returns (address) {
```


