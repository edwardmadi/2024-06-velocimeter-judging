Polite Butter Gazelle

Medium

# `OptionTokenV4::expire` will change the 1:1 ratio of the supply of options token to underlying token, rendering some options tokens useless

## Summary

For each `option token` that is minted, an address with `MINTER_ROLE` or `ADMIN_ROLE` must transfer the same amount of underlying tokens (i.e, FLOW tokens) to the `OptionTokenV4` contract. For example, to mint `10e18 oFLOW` tokens, the minter or admin must transfer exactly `10e18 FLOW` tokens to the `OptionTokenV4` contract. This ensures that for each `oFLOW` token in circulation, there is a `FLOW` token that the contract has, so that when users exercise `x` amount of `oFLOW` tokens, there are also `x` amount of `FLOW` tokens in the `OptionTokenV4` contract to exercise these options.

The `OptionTokenV4` contract also has an `expire()` functionality. As described by the `Velocimeter` protocol developers on discord, this `expire()` functionality burns the entire supply of underlying (i.e., `FLOW`) token from the `OptionTokenV4` contract to ensure that options can no longer be exercised, which can be useful for airdrops.

However, recall that for every `option token` minted, the protocol must receive the exact amount of `underlying token`, creating a 1:1 ratio with the amount of `option tokens` minted to the amount of `underlying token` in the contract.

Since the `expire()` functionality burns all of the `underlying token` in the contract, there will be some `option tokens` in circulation that will be completely useless.

## Vulnerability Detail

Let's assume that `option token = oFLOW` and `underyling token = FLOW`.

[OptionTokenV4.sol#L516-L533](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L516-L533)
```javascript
    /// @notice Called by the admin to mint options tokens. Admin must grant token approval.
    /// @param _to The address that will receive the minted options tokens
    /// @param _amount The amount of options tokens that will be minted
    function mint(address _to, uint256 _amount) external onlyMinter {
        // transfer underlying tokens from the caller
        _safeTransferFrom(underlyingToken, msg.sender, address(this), _amount);
        // mint options tokens
        _mint(_to, _amount);
    }

    /// @notice Called by the admin to burn options tokens and transfer underlying tokens to the caller.
    /// @param _amount The amount of options tokens that will be burned and underlying tokens transferred to the caller
    function burn(uint256 _amount) external onlyAdmin {
        // transfer underlying tokens to the caller
        _safeTransfer(underlyingToken, msg.sender, _amount);
        // burn option tokens
        _burn(msg.sender, _amount);
    }
```

As we can see here, for each `oFLOW` token minted, the sender must transfer the same amount of `FLOW` tokens to the `OptionTokenV4` contract.

Conversely, when burning `oFLOW`, the same amount of `FLOW` tokens are transferred to the caller.

This ensures that the amount of `oFLOW` in circulation is equivalent to the amount of `FLOW` in the contract.

Users have three ways to exercise options: `exercise()`, `exerciseVe()`, `exerciseLp()`. Each function call will burn the specified `amount` of `oFLOW` from the caller and proceed to transfer `amount` of `FLOW` to either the user or to the respective liquidity pool (i.e., FLOW/DAI). In other words, when users exercise options, the amount of `oFLOW` burned will be equal to the amount of `FLOW` transferred out of the `OptionsTokenV4` contract (i.e., user burns 1e18 oFLOW, then contract will transfer 1e18 FLOW to user or pool). The amount of `oFLOW` in circulation will still be 1:1 with the amount of `FLOW` in the contract.

[OptionTokenV4.sol#L587-L588](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L587-L588)
```javascript
    // send underlying tokens to recipient
    _safeTransfer(underlyingToken, _recipient, _amount); //@audit caller will receive the same amount of FLOW for the oFLOW burned
```

[OptionTokenV4.sol#L685-L695](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L685-L695)
```javascript
    (, , lpAmount) = IRouter(router).addLiquidity(
        underlyingToken,
        paymentToken,
        false,
@>      _amount, //@audit liquidity pool will receive exact amount of FLOW for the oFLOW burned
        paymentAmountToAddLiquidity,
        1,
        1,
        address(this),
        block.timestamp
    );
```

There is one case where the 1:1 ratio of amount of `oFLOW` in circulation to the amount of `FLOW` in the `OptionsTokenV4` contract will change:

[OptionTokenV4.sol#L535-L547](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L535-L547)
```javascript
    function startExpire() external onlyAdmin {
        require(expiryCooldownTime != 0,"no expiry token");
        require(expiryTime == 0,"expiry started");

        expiryTime = block.timestamp + expiryCooldownTime;
    }

    function expire() external onlyAdmin {
        require(expiryTime != 0,"expiry not started");
        require(expiryTime < block.timestamp,"no expiry time");

@>      IERC20(underlyingToken).burn(IERC20(underlyingToken).balanceOf(address(this))); //@audit entire supply of FLOW will be burned
    }
```

This functionality burns all of the `FLOW` tokens in the contract to ensure that users can no longer exercise options. Let's look at the following scenario (these values allow for simple math):

1. Alice and Bob stake their LP tokens in a gauge and earn rewards. `100e18 FLOW` is transferred to `OptionTokenV4` from the `Gauge`, minting `100e18 oFLOW` and transferring them to Alice and Bob.
2. Alice and Bob now have access to the `100e18 oFLOW` that was minted, assume they have `50e18 oFLOW` each.
3. Alice: `50e18 oFLOW`, Bob: `50e18 oFLOW`, OptionTokenV4: `100e18 FLOW`
4. Admin calls `startExpire()`, which is set to burn the entire supply of `FLOW` tokens within the `OptionTokenV4` contract after `expiryCooldownTime` passes.
5. Bob exercises his entire supply of option tokens by calling `exercise()` and specifying `50e18 oFLOW` to burn. He receives `50e18 FLOW` from the `OptionTokenV4` contract.
6. Alice: `50e18 oFLOW`, Bob: `50e18 FLOW`, OptionTokenV4: `50e18 FLOW`.
7. `expiryCooldownTime` passes and admin calls `expire()`, burning the entire supply of `50e18 FLOW` in the `OptionTokenV4` contract.
8. Alice attempts to exercise her `oFLOW` tokens, but the calls revert because the `OptionTokenV4` contract does not have enough `FLOW` tokens to transfer.
9. Alice now has `50e18 oFLOW` that can never be exercised. These tokens will remain in circulation and will never be able to exercise permanently.

The only possible way for Alice to exercise her tokens in this example is to directly transfer `50e18 FLOW` tokens to the `OptionTokenV4` contract, which will cause a huge loss of funds for her.

> <strong style = "margin-left: 4px">Note:</strong>
>
>A solution for this is *not* that Alice will simply use her `oFLOW` on a separate `OptionTokenV4` contract. The point being made is that there will be `oFLOW` tokens in circulation that can never be exercised since the `OptionTokenV4` contract contains only as many `FLOW` tokens as `oFLOW` tokens minted. Therefore, if another `OptionTokenV4` contract exists that has `100e18 FLOW` tokens, that means `100e18 oFLOW` tokens were also minted. If Alice chooses to exercise her `50e18 oFLOW` tokens on this contract, the contract will then have `50e18 FLOW` tokens, but `100e18 oFLOW` will remain in circulation. Thus, there will still be `50e18 oFLOW` tokens in circulation that can never be exercised.

## Proof of Concept

<details>
<summary>Coded PoC</summary>
<br>

To run this test, first make the following changes to `test/BaseTest.sol` to set the expiry in the `OptionTokenV4` contract constructor

```diff
     function deployOptionTokenV4WithOwner(
        address _owner,
        address _gaugeFactory,
        address _voter,
        address _escrow
    ) public {
        oFlowV4 = new OptionTokenV4(
            "Option to buy FLOW",
            "oFLOW",
            _owner,
            address(FLOW),
            _owner,
            _voter,
            address(router),
            false,
            false,
            false,
-           0
+           ONE_DAY
        );

        oFlowV4.setPairAndPaymentToken(flowDaiPair, address(DAI));
        oFlowV4.grantRole(oFlowV4.ADMIN_ROLE(), _gaugeFactory);
    }
```

Now, add the following to `test/OptionTokenV4.t.sol` and run `forge test --mt testExpiryRatioChange -vv`

```javascript
    function testExpiryRatioChange() public {
        vm.startPrank(address(owner));
        FLOW.approve(address(oFlowV4), TOKEN_1);
        // mint Option token to owner 2
        oFlowV4.mint(address(owner2), TOKEN_1);
        oFlowV4.addTreasury(OptionTokenV4.TreasuryConfig(address(owner3),5,false));
        washTrades();
        vm.stopPrank();

        FLOW.approve(address(oFlowV4), 100e18);
        oFlowV4.mint(address(owner2), 100e18);
        address bob = vm.addr(1);
        address alice = vm.addr(2);
        DAI.mint(bob, TOKEN_100K);
        DAI.mint(alice, TOKEN_100K);

        vm.startPrank(address(owner2));
        oFlowV4.transfer(address(bob), 50e18); // for simplicity simply transfer the tokens to them, however in real scenario they will receive this by staking in gauge
        oFlowV4.transfer(address(alice), 50e18);
        vm.stopPrank();
        

        uint256 flowBalanceBefore = FLOW.balanceOf(address(oFlowV4));
        uint256 bobOflowBefore = oFlowV4.balanceOf(address(bob));
        uint256 aliceOflowBefore = oFlowV4.balanceOf(address(alice));

        console.log("OptionToken contract FLOW balance before expiry: ", flowBalanceBefore);
        console.log("Bob's oFLOW balance before expiry: ", bobOflowBefore);
        console.log("Alice's oFLOW balance before expiry: ", aliceOflowBefore);
        

        oFlowV4.startExpire(); //start expiry

        
        // Bob exercises oTokens
        vm.startPrank(address(bob));    
        DAI.approve(address(oFlowV4), TOKEN_100K);
        oFlowV4.exercise(50e18, 50e18, address(bob));
        vm.stopPrank();

        uint256 bobOflowAfterExercise = oFlowV4.balanceOf(address(bob));
        uint256 bobFlowAfterExercise = FLOW.balanceOf(address(bob));

        console.log("Bob's oFLOW balance after exercise and before expiry: ", bobOflowAfterExercise);
        console.log("Bob's FLOW balance after exercise and before expiry: ", bobFlowAfterExercise);

        vm.warp(block.timestamp + 10);

        vm.warp(block.timestamp + 86400);

        oFlowV4.expire();

        uint256 flowBalanceAfter = FLOW.balanceOf(address(oFlowV4));
        uint256 bobOflowAfterExpiry = oFlowV4.balanceOf(address(bob));
        uint256 bobFlowAfterExpiry = FLOW.balanceOf(address(bob));
        uint256 aliceOflowAfter = oFlowV4.balanceOf(address(alice));

        console.log("OptionToken contract FLOW balance after expiry: ", flowBalanceAfter);
        console.log("Bob's oFLOW balance after expiry: ", bobOflowAfterExpiry);
        console.log("Bob's FLOW balance after expiry: ", bobFlowAfterExpiry);
        console.log("Alice's oFLOW balance after expiry: ", aliceOflowAfter);


        // Alice attempts to exercises oTokens
        vm.startPrank(address(alice));
        DAI.approve(address(oFlowV4), TOKEN_100K);
        vm.expectRevert();
        oFlowV4.exercise(50e18, 50e18, address(alice));
        vm.stopPrank();

    }
```
</details>

### Console Output

```text
Running 1 test for test/OptionTokenV4.t.sol:OptionTokenV4Test
[PASS] testExpiryRatioChange() (gas: 3252258)
Logs:
  OptionToken contract FLOW balance before expiry:  101000000000000000000
  Bob's oFLOW balance before expiry:  50000000000000000000
  Alice's oFLOW balance before expiry:  50000000000000000000
  Bob's oFLOW balance after exercise and before expiry:  0
  Bob's FLOW balance after exercise and before expiry:  50000000000000000000
  OptionToken contract FLOW balance after expiry:  0
  Bob's oFLOW balance after expiry:  0
  Bob's FLOW balance after expiry:  50000000000000000000
  Alice's oFLOW balance after expiry:  50000000000000000000

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 16.93ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

We can see from this PoC that Alice is left with `50e18` oFLOW that will remain in circulation but can never be used.

## Impact

Loss of funds, `oFLOW` tokens in circulation that cannot be exercised, rendering them useless.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L587-L588

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L685-L695

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L535-L547

## Tool used

Manual Review

## Recommendation

Acknowledge this issue or create a way for users to safely execute an operation that will utilize their extra `oTokens` for some benefit. Another option is to remove the `expire()` functionality.