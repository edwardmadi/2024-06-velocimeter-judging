Polite Butter Gazelle

Medium

# Rewards are sent to `msg.sender` instead of `veNFT` owner

## Summary

`RewardsDistributorV2::claim` and `RewardsDistributorV2::claim_many` allows `veNFT` owners and approved addresses to claim their weekly rewards. This has [changed](https://ftmscan.com/address/0xE0B708753aab9d2Cc142ea52Cd7CD0c923A3389A#code) from `Velicometer V3`, where the claimed funds were directly deposited and locked back into `VotingEscrow`. Now, it is sent to `msg.sender` of the respective `claim` call.

However, transferring the claimed funds to `msg.sender` instead of the `veNFT` owner raises an issue. This allows approved addresses to take the rewards for themselves, rather than to the rightful owner of the `token id`.

## Vulnerability Detail

[RewardsDistributorV2.sol#L282-L318](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/RewardsDistributorV2.sol#L282-L318)
```javascript
    function claim(uint _tokenId) external returns (uint) {
@>      require(IVotingEscrow(voting_escrow).isApprovedOrOwner(msg.sender, _tokenId));

        if (block.timestamp >= time_cursor) _checkpoint_total_supply();
        uint _last_token_time = last_token_time;
        _last_token_time = _last_token_time / WEEK * WEEK;
        uint amount = _claim(_tokenId, voting_escrow, _last_token_time);
        if (amount != 0) {
@>          _safeTransfer(msg.sender,amount);
            token_last_balance -= amount;
        }
        return amount;
    }

    function claim_many(uint[] memory _tokenIds) external returns (bool) {
        if (block.timestamp >= time_cursor) _checkpoint_total_supply();
        uint _last_token_time = last_token_time;
        _last_token_time = _last_token_time / WEEK * WEEK;
        address _voting_escrow = voting_escrow;
        uint total = 0;

        for (uint i = 0; i < _tokenIds.length; i++) {
            uint _tokenId = _tokenIds[i];
@>          require(IVotingEscrow(voting_escrow).isApprovedOrOwner(msg.sender, _tokenId));
            if (_tokenId == 0) break;
            uint amount = _claim(_tokenId, _voting_escrow, _last_token_time);
            if (amount != 0) {
@>              _safeTransfer(msg.sender,amount);
                total += amount;
            }
        }
        if (total != 0) {
            token_last_balance -= total;
        }

        return true;
    }
```

The `require` checks for both functions ensures that only the owner or approved addresses of the `tokenId` can call `claim rewards` functions. However, we can see that the amount to claim is transferred to `msg.sender` instead of the `_tokenId owner`. This allows approved addresses to take rewards that should go to the rightful owner.

## Impact

Approved addresses can take rewards that should be sent to the rightful `veNFT` owner, causing a loss of funds for them.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/RewardsDistributorV2.sol#L282-L318

## Tool used

Manual Review

## Recommendation

`Velodrome V2` [handles this case](https://github.com/velodrome-finance/contracts/blob/main/contracts/RewardsDistributor.sol#L149-L150) by ensuring the rewards are sent to the actual owner by utilizing `ownerOf` from the `VestingEscrow` contract. Consider implementing the same changes:

```diff
    function claim(uint _tokenId) external returns (uint) {
        require(IVotingEscrow(voting_escrow).isApprovedOrOwner(msg.sender, _tokenId));

        if (block.timestamp >= time_cursor) _checkpoint_total_supply();
        uint _last_token_time = last_token_time;
        _last_token_time = _last_token_time / WEEK * WEEK;
        uint amount = _claim(_tokenId, voting_escrow, _last_token_time);
        if (amount != 0) {
+               address _owner = IVotingEscrow(voting_escrow).ownerOf(_tokenId);
+               _safeTransfer(_owner,amount);
-               _safeTransfer(msg.sender,amount);
            token_last_balance -= amount;
        }
        return amount;
    }

    function claim_many(uint[] memory _tokenIds) external returns (bool) {
        if (block.timestamp >= time_cursor) _checkpoint_total_supply();
        uint _last_token_time = last_token_time;
        _last_token_time = _last_token_time / WEEK * WEEK;
        address _voting_escrow = voting_escrow;
        uint total = 0;

        for (uint i = 0; i < _tokenIds.length; i++) {
            uint _tokenId = _tokenIds[i];
            require(IVotingEscrow(voting_escrow).isApprovedOrOwner(msg.sender, _tokenId));
            if (_tokenId == 0) break;
            uint amount = _claim(_tokenId, _voting_escrow, _last_token_time);
            if (amount != 0) {
+               address _owner = IVotingEscrow(voting_escrow).ownerOf(_tokenId);
+               _safeTransfer(_owner,amount);
-               _safeTransfer(msg.sender,amount);
                total += amount;
            }
        }
        if (total != 0) {
            token_last_balance -= total;
        }

        return true;
    }

```