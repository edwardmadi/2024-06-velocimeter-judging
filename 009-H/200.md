Sweet Lemonade Lynx

High

# Improper Reset in `disable_max_lock` Leads to Permanent Token Lock For Last element in max_locked_nfts


## Summary

The `disable_max_lock` function in `VotingEscrow.sol` contains a flaw that prevents the proper resetting of `maxLockIdToIndex` when the last element in the array is disabled. This issue can lead to a denial of service (DoS) by prohibiting users from withdrawing their tokens even after the lock period has expired. Additionally, an attacker can exploit the `max_lock` function to extend the lock time indefinitely, preventing users from disabling max lock again and permanently locking their tokens.

## Vulnerability Detail

In the `disable_max_lock` function, if a user is the last person to disable max lock in the `max_locked_nfts` array, the `maxLockIdToIndex` is not reset to 0 correctly. This leads to a situation where the user cannot withdraw their tokens even after the lock period has expired, due to a failing assertion in the `withdraw` function.

Additionally, the `max_lock` function is a public function with no access control, allowing anyone to extend the lock time for any token ID enabled for maxLock. An attacker can exploit this by continuously extending the lock time for a token ID that has been disabled from the max lock last.

Additionally This vulnerability prevents the user from re-enabling max lock due to the check in the `enable_max_lock` function:

[Line 885](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L885)

```solidity
require(maxLockIdToIndex[_tokenId] == 0, "enabled");
```

Which would be false and as a result, the user may never be able to withdraw their tokens for that token ID permanently.

## Impact

The impact of this vulnerability is twofold:

1. **Denial of Service**: Users may be unable to withdraw their tokens even after their lock period has expired due to the improper reset of `maxLockIdToIndex`.
2. **Permanent Lock**: An attacker can indefinitely extend the lock period for any token ID that has been disabled from max lock last.

## Code Snippet

[Line 893](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L893)

```solidity
function disable_max_lock(uint _tokenId) external {
    assert(_isApprovedOrOwner(msg.sender, _tokenId));
    require(maxLockIdToIndex[_tokenId] != 0, "disabled");

    uint index = maxLockIdToIndex[_tokenId] - 1;
    maxLockIdToIndex[_tokenId] = 0;

    // Move the last element into the place to delete
    max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];

    // update the index
    maxLockIdToIndex[max_locked_nfts[index]] = index + 1;

    // Remove the last element
    max_locked_nfts.pop();
}
```

**Proof of Concept:** To be added to the VotingEscrow.t.sol:

```solidity
     function testCreateLockAndMaxLockAndWithdrawDOS() public {
        flowDaiPair.approve(address(escrow), TOKEN_1*2);
        uint256 lockDuration = 7 * 24 * 3600; // 1 week

        // Balance should be zero before and 1 after creating the lock
        assertEq(escrow.balanceOf(address(owner)), 0);
        escrow.create_lock(TOKEN_1, lockDuration);
        escrow.create_lock(TOKEN_1, lockDuration);
        assertEq(escrow.currentTokenId(), 2);
        assertEq(escrow.ownerOf(1), address(owner));
        assertEq(escrow.balanceOf(address(owner)), 2);

        escrow.max_lock(1);
        escrow.max_lock(2);

        int amount;
        uint duration;
        (amount, duration) = escrow.locked(1);
        assertEq(duration, lockDuration);

        escrow.enable_max_lock(1);
        escrow.enable_max_lock(2);

        escrow.isApprovedOrOwner(address(owner),1);

        (amount, duration) = escrow.locked(1);
        assertEq(duration, 52 * 7 * 86400);

       (,uint duration2) = escrow.locked(2);

        assertEq(escrow.maxLockIdToIndex(1),1);
        assertEq(escrow.maxLockIdToIndex(2),2);

        escrow.disable_max_lock(1);
        assertEq(escrow.maxLockIdToIndex(1),0);

        escrow.disable_max_lock(2);
        // after disabling for last id in the array should be 0 but its 1
        assertEq(escrow.maxLockIdToIndex(2),1); // 1 <---- change to 0 to see it fail..

        // to test that a random attacker can extend maxLock time of the tokenId 2 uncomment the block below
        // vm.warp(block.timestamp + duration2 - 86400 ); // <--- assumes 1 day left for lock
        // vm.roll(block.number + 1);
        // escrow.max_lock(2);
        // comment out the next block to run the above properly

        // warp time to make user withdraw
        vm.warp(block.timestamp + duration2 + 1000);
        vm.roll(block.number + 1);
        vm.expectRevert("Lock expired"); // <--- would revert cause max_lock can get called
        escrow.withdraw(2);

     }
```

## Tool Used

Manual Review & Foundry

## Recommendation

**Fix the Index Reset**: Ensure that `maxLockIdToIndex` is properly reset to 0 for the last element in the `max_locked_nfts` array.
