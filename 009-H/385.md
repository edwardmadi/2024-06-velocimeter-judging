Spicy Maroon Sloth

Medium

# disable_max_lock() may not be disabled

## Summary
When executing `disable_max_lock()`, `maxLockIdToIndex[_tokenId]` may be incorrectly overwritten, resulting in `not equal to `0`, and not successfully `disable`.

## Vulnerability Detail
The `disable_max_lock()` code is implemented as follows:
```solidity
    function disable_max_lock(uint _tokenId) external {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
        require(maxLockIdToIndex[_tokenId] != 0,"disabled");

        uint index =  maxLockIdToIndex[_tokenId] - 1;
@>      maxLockIdToIndex[_tokenId] = 0;

         // Move the last element into the place to delete
        max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];
        
        // update the index 
@>      maxLockIdToIndex[max_locked_nfts[index]] = index + 1;
        
        // Remove the last element
        max_locked_nfts.pop();
    }
```

The above code first puts `maxLockIdToIndex[_tokenId] = 0`, after which it executes `maxLockIdToIndex[max_locked_nfts[index]] = index + 1; `
If `tokenId` is on the last one, then `maxLockIdToIndex[_tokenId]` will be reassigned to the old one.
Example:
maxLockIdToIndex[NFT_1] = 1   
maxLockIdToIndex[NFT_2] = 2
max_locked_nfts[0]=NFT_1
max_locked_nfts[1]=NFT_2

when execute `disable_max_lock(NFT_2)`

1. index =  maxLockIdToIndex[_tokenId] - 1 = 1;
2. maxLockIdToIndex[NFT_2]= 0
3. max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1] 
     - max_locked_nfts[1]= max_locked_nfts[1] = NFT_2
4. maxLockIdToIndex[max_locked_nfts[index]] = index + 1;
     - maxLockIdToIndex[NFT_2] = 2          ---------> **set back old index**
5. max_locked_nfts.pop()


## POC

```solidity
     function testErrorIndex() public {
        flowDaiPair.approve(address(escrow), TOKEN_1*2);
        uint256 lockDuration = 7 * 24 * 3600; // 1 week

        // Balance should be zero before and 1 after creating the lock
        assertEq(escrow.balanceOf(address(owner)), 0);
        escrow.create_lock(TOKEN_1, lockDuration);
        escrow.create_lock(TOKEN_1, lockDuration);
        assertEq(escrow.currentTokenId(), 2);
        assertEq(escrow.ownerOf(1), address(owner));
        assertEq(escrow.balanceOf(address(owner)), 2);
        
        escrow.max_lock(1);
        escrow.max_lock(2);

        int amount;
        uint duration;
        (amount, duration) = escrow.locked(1);
        assertEq(duration, lockDuration);

        escrow.enable_max_lock(1);
        escrow.enable_max_lock(2);

        escrow.isApprovedOrOwner(address(owner),1);

        (amount, duration) = escrow.locked(1);
        assertEq(duration, 52 * 7 * 86400);

        assertEq(escrow.maxLockIdToIndex(1),1);
        assertEq(escrow.maxLockIdToIndex(2),2);
        
        console2.log("(before)NFT_2 index:",escrow.maxLockIdToIndex(2));
        console2.log("execute disable_max_lock(2)");
        escrow.disable_max_lock(2);
        console2.log("(after)NFT_2 index:",escrow.maxLockIdToIndex(2));
     }

```

```console
$ forge test -vvv --match-test testErrorIndex

[PASS] testErrorIndex() (gas: 1282229)
Logs:
  (before)NFT_2 index: 2
  execute disable_max_lock(2)
  (after)NFT_2 index: 2
```

## Impact

`maxLockIdToIndex` cannot be cleared properly, causing `max_lock()` to assume that it is not `disable`.

Or a malicious re-execution of `disable_max_lock()` modifies someone else's configuration with the wrong index.

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L898
## Tool used

Manual Review

## Recommendation

```diff
    function disable_max_lock(uint _tokenId) external {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
        require(maxLockIdToIndex[_tokenId] != 0,"disabled");

        uint index =  maxLockIdToIndex[_tokenId] - 1;
-       maxLockIdToIndex[_tokenId] = 0;

         // Move the last element into the place to delete
        max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];
        
        // update the index 
        maxLockIdToIndex[max_locked_nfts[index]] = index + 1;
        
+       maxLockIdToIndex[_tokenId] = 0;
        // Remove the last element
        max_locked_nfts.pop();
    }
```