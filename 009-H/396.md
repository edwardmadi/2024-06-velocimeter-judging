Fresh Azure Copperhead

High

# The last item of `max_locked_nfts` in `VotingEscrow` cannot be marked as max_lock disabled by calling `VotingEscrow#disable_max_lock()`

## Summary
There is no way to disable the max_lock feature of `_tokenId` if it is the last item of `max_locked_nfts`.

## Vulnerability Detail
A veNFT can be marked as max_lock enabled by calling `enable_max_lock()`:
```solidity
    function enable_max_lock(uint _tokenId) external {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
        require(maxLockIdToIndex[_tokenId] == 0,"enabled");

        max_locked_nfts.push(_tokenId);
@>      maxLockIdToIndex[_tokenId] = max_locked_nfts.length;//@audit-info non-zero value means enabled

        max_lock(_tokenId);
    }
```
Once enable, anyone can call `max_lock()` to extend the locking time of veNFT to maximum the owners profit:
```solidity
    function max_lock(uint _tokenId) public {
 @>     if(maxLockIdToIndex[_tokenId] !=0 && max_lock_enabled) {//@audit-info only max_lock enabled token(maxLockIdToIndex[_tokenId] != 0) can extend their locking time to MAXTIME
            LockedBalance memory _locked = locked[_tokenId];
            uint unlock_time = (block.timestamp + MAXTIME) / WEEK * WEEK; // Locktime is rounded down to weeks

            if(unlock_time > _locked.end) {
                require(_locked.end > block.timestamp, 'Lock expired');
                require(_locked.amount > 0, 'Nothing is locked');
                require(unlock_time <= block.timestamp + MAXTIME, 'Voting lock can be 52 weeks max');

                _deposit_for(_tokenId, 0, unlock_time, _locked, DepositType.INCREASE_UNLOCK_TIME);
            }
        }
    }
```
The owner/operator of the veNFT can mark it as max_lock disabled by calling `disable_max_lock()`:
```solidity
    function disable_max_lock(uint _tokenId) external {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
        require(maxLockIdToIndex[_tokenId] != 0,"disabled");

        uint index =  maxLockIdToIndex[_tokenId] - 1;
        maxLockIdToIndex[_tokenId] = 0;

         // Move the last element into the place to delete
        max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];
        
        // update the index 
@>      maxLockIdToIndex[max_locked_nfts[index]] = index + 1;//@audit maxLockIdToIndex[_tokenId] is marked as non-zero again if _tokenId is the last item in max_locked_nfts
        
        // Remove the last element
        max_locked_nfts.pop();
    }
```
Once the locking time is expired, the owner/operator of the veNFT can withdraw its locked lpToken and burn the veNFT.

When `disable_max_lock(_tokenId)` is called, `maxLockIdToIndex[_tokenId]` should be set to `0`. However, if `_tokenId` is the last item in `max_locked_nfts`, `maxLockIdToIndex[_tokenId]` will be updated to non-zero again, and its locking time can be extended by anyone.

Copy below codes to [VotingEscrow.t.sol](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/test/VotingEscrow.t.sol) and run forge test --match-test testMaxLockAfterDisabled
```solidity
    function testMaxLockAfterDisabled() public {
        flowDaiPair.approve(address(escrow), TOKEN_1);
        uint256 lockDuration = 7 * 24 * 3600; // 1 week

        // Balance should be zero before and 1 after creating the lock
        
        escrow.create_lock(TOKEN_1, lockDuration);
        (, uint lockingEnd) = escrow.locked(1);
        //@audit-info locking should be ended in 1 week
        assertLe(lockingEnd, block.timestamp + 1 weeks);
        //@audit-info the index of lockId(1) is 0 before max_lock enabled
        uint index = escrow.maxLockIdToIndex(1);
        assertEq(index, 0);

        //@audit-info max_lock is enabled
        escrow.enable_max_lock(1);
        (, lockingEnd) = escrow.locked(1);
        //@audit-info once max_lock enabled, locking time can be extended to no less than weeks.
        assertGe(lockingEnd, block.timestamp + 51 weeks);
        //@audit-info max_lock is disabled
        escrow.disable_max_lock(1);
        //@audit-info the index was not reset to 0
        index = escrow.maxLockIdToIndex(1);
        assertEq(index, 1);

        vm.warp(51 weeks);
        (, lockingEnd) = escrow.locked(1);
        //@audit-info locking is supposed to end in 1 week
        assertLe(lockingEnd, block.timestamp + 1 weeks);
        //@audit-info anyone can call max_lock() to extend the locking time
        vm.prank(makeAddr("anyone"));
        escrow.max_lock(1);
        (, lockingEnd) = escrow.locked(1);
        //@audit-info the locking time is extended to no less than 51 weeks.
        assertGe(lockingEnd, block.timestamp + 51 weeks);
    }
```
As we can see, the locking time can be extended by anyone after `maxLockIdToIndex()` is called. There is no way to withdraw the locked lpToken as long as the locking time is not expired.
## Impact
A `veNFT` might be locked forever, resulting in its lpToken being unable to be withdrawn.
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L904
## Tool used

Manual Review

## Recommendation
Check if `_tokenId` is the last item of `max_locked_nfts`:
```diff
    function disable_max_lock(uint _tokenId) external {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
        require(maxLockIdToIndex[_tokenId] != 0,"disabled");

        uint index =  maxLockIdToIndex[_tokenId] - 1;
        maxLockIdToIndex[_tokenId] = 0;

         // Move the last element into the place to delete
-       max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];
        
        // update the index 
-       maxLockIdToIndex[max_locked_nfts[index]] = index + 1;
+       if (index != max_locked_nfts.length - 1) {
+           max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];            
+           maxLockIdToIndex[max_locked_nfts[index]] = index + 1;
+       }        
        // Remove the last element
        max_locked_nfts.pop();
    }
```