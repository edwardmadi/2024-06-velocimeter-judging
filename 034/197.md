Delightful Lavender Goose

Medium

# Merging expired locks may prevent reward claims.

## Summary
Users might loose rewards when allowed to merge expired locks.

## Vulnerability Detail
The `merge` function updates `locked[_to].end` to the end time of `_from` if `_from` has a later end time. However, it doesn't handle cases where either of the two tokenIds has already expired. This allows merging an expired `nftId` (with a past end time) with a non-expired `nftId`, potentially preventing users from claiming rewards for the merged `nftId` in certain scenarios. For example:

- A user's lock expires, and they don't immediately claim their accumulated rewards (leaving at least one week unclaimed).
- During this time, the total voting power (`ve_supply`) drops to zero due to no active locks.
- The user creates a new lock and merges it with the expired `nftId` (`_to`). The `merge` function updates the end time of the expired lock to that of the new lock, reactivating the expired lock.

```solidity
f    function merge(uint _from, uint _to) external {
        ...
        LockedBalance memory _locked0 = locked[_from];
        LockedBalance memory _locked1 = locked[_to];
        uint value0 = uint(int256(_locked0.amount));
        uint end = _locked0.end >= _locked1.end ? _locked0.end : _locked1.end;

        ...
}
```

Between the expired lock's end time and merging the new lock, `ve_supply` was zero, recorded in the `RewardsDistributorV2.sol` contract:

```solidity
    function _checkpoint_total_supply() internal {
        ...

        for (uint i = 0; i < 20; i++) {
            ...
                ve_supply[t] = Math.max(uint(int256(pt.bias - pt.slope * dt)), 0); // this will be zero when ever not token locked in locker contract. (ignore the unsafe casting for now )
            }
           ..
    }
```

When the user tries to claim rewards through the `RewardsDistributorV2.sol` contract, the claim function starts the reward calculation from the last unclaimed epoch of the expired lock. The calculation encounters the period with zero `ve_supply`, and since rewards are proportional to the user's share of `ve_supply`, it attempts to divide by zero, causing a revert. As a result, the user cannot claim any rewards, and all accumulated rewards for the merged `nftId` become unclaimable.

Additionally, allowing the merge of expired locks enables users to exceed the maximum lock duration. For instance, a user with one NFT locked for the maximum period could later lock a new NFT for the maximum period and merge it with the first, resulting in a lock duration exceeding the maximum period. However, this does not grant additional voting power or rewards, as the elapsed time is not restored.


## Impact
Allowing expired locks to merge can cause issues in the system. It might disrupt how rewards are distributed and stop users from getting their rightful rewards, as explained earlier.

## POC
```solidity
    function testMergeExpired() public {
        initializeVotingEscrow();

        address alice = address(0x1337);
        FLOW.transfer(address(flowDaiPair), TOKEN_100K);
        DAI.transfer(address(flowDaiPair), TOKEN_100K);

        flowDaiPair.mint(alice);
        flowDaiPair.balanceOf(alice);

        vm.startPrank(alice);
        flowDaiPair.approve(address(escrow), TOKEN_100K);
        uint tokenId_1 = escrow.create_lock(1 ether, 3 weeks);
        vm.stopPrank();

        for (uint a; a < 5; a++) {
          DAI.transfer(address(distributor), TOKEN_1);
          _elapseOneWeek();
          minter.update_period();
        }

        vm.startPrank(alice);
        uint tokenId_2 = escrow.create_lock(1 ether, 10 weeks);
        escrow.merge(tokenId_2, tokenId_1);

        vm.expectRevert();
        distributor.claim(tokenId_1);
    }
```

## Code Snippet
```solidity
    function merge(uint _from, uint _to) external {
        require(attachments[_from] == 0 && !voted[_from], "attached");
        require(_from != _to);
        require(_isApprovedOrOwner(msg.sender, _from));
        require(_isApprovedOrOwner(msg.sender, _to));

        LockedBalance memory _locked0 = locked[_from];
        LockedBalance memory _locked1 = locked[_to];
        uint value0 = uint(int256(_locked0.amount));
@>>     uint end = _locked0.end >= _locked1.end ? _locked0.end : _locked1.end;

        locked[_from] = LockedBalance(0, 0);
        _checkpoint(_from, _locked0, LockedBalance(0, 0));
        _burn(_from);
        _deposit_for(_to, value0, end, _locked1, DepositType.MERGE_TYPE);
    }
```
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1204
## Tool used

Manual Review

## Recommendation
```diff
    function merge(uint _from, uint _to) external {
        require(attachments[_from] == 0 && !voted[_from], "attached");
        require(_from != _to);
        require(_isApprovedOrOwner(msg.sender, _from));
        require(_isApprovedOrOwner(msg.sender, _to));

        LockedBalance memory _locked0 = locked[_from];
        LockedBalance memory _locked1 = locked[_to];
+       require(_locked1.end > block.timestamp, "Lock Expired");
        uint value0 = uint(int256(_locked0.amount));
        uint end = _locked0.end >= _locked1.end ? _locked0.end : _locked1.end;

        ...
    }
```
