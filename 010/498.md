Square Arctic Chicken

Medium

# `checkpoint_total_supply()` can prematurely update the `veSupply[t]` and `time_cursor` leading to wrong reward calculations

## Summary
The `RewardsDistributorV2::checkpoint_total_supply()` function is permissionlessly callable by anyone. The `_checkpoint_total_supply(...)` function has an incorrect comparison of `>` instead of `>=`, and this can lead to a checkpoint updating the `ve_supply[t]` prematurely leading to unpredictable results and breaking accounting for the protocol.

## Vulnerability Detail

```solidity
    function _checkpoint_total_supply() internal {
        address ve = voting_escrow;
        uint t = time_cursor; // the current location of time
        uint rounded_timestamp = block.timestamp / WEEK * WEEK; // this can be >= time_cursor depending on when _checkpoint_total_supply() is called
        IVotingEscrow(ve).checkpoint();

        for (uint i = 0; i < 20; i++) {
            if (t > rounded_timestamp) {
                break;
            } else {
                uint epoch = _find_timestamp_epoch(ve, t); // epoch prior to or at t
                IVotingEscrow.Point memory pt = IVotingEscrow(ve).point_history(epoch);
                int128 dt = 0;
                if (t > pt.ts) {
                    dt = int128(int256(t - pt.ts)); // change in time 
                }
  ->            ve_supply[t] = Math.max(uint(int256(pt.bias - pt.slope * dt)), 0);
            }
            t += WEEK;
        }
        time_cursor = t;
    }
```
Notice that whenever `t <= rounded_timestamp`, the `ve_supply[t]` is updated and also notice the `t += WEEK` increment happens immediately after (to ensure the update are done for all epochs up until the current week), and this permanently progresses the cursor so that `veSupply[t]` is not updated again until the `time_cursor` is in the  current epoch. Also, notice that the `ve_supply[t]` update is only skipped if `t > rounded_timestamp`.

The is not correct because `t == rounded_timestamp` is the first second of an epoch,  and as such the `ve_supply[t]` can be updated before the current epoch ends or before the last second in the current epoch is even complete considering that `rounded_timestamp` is the first second in the current epoch.

```solidity

File: RewardsDistributorV2.sol
282:     function claim(uint _tokenId) external returns (uint) {
...
284: 
285:   >>    if (block.timestamp >= time_cursor) _checkpoint_total_supply(); // update time cursor
....
288:   >>    uint amount = _claim(_tokenId, voting_escrow, _last_token_time);
...
294:     }


169:     function _claim(uint _tokenId, address ve, uint _last_token_time) internal returns (uint) {
170:         uint user_epoch = 0;
...
194: 
195:         for (uint i = 0; i < 50; i++) {
196:             if (week_cursor >= _last_token_time) break; // he has been claiming up to date
197: 
198:             if (week_cursor >= user_point.ts && user_epoch <= max_user_epoch) {
...
206:    @>>>     } else {
207:                 int128 dt = int128(int256(week_cursor - old_user_point.ts)); // week_cursor - 0
208:                 uint balance_of = Math.max(uint(int256(old_user_point.bias - dt * old_user_point.slope)), 0); // overflow
209:                 if (balance_of == 0 && user_epoch > max_user_epoch) break;
210:                 if (balance_of != 0) {
211:   @>>>              to_distribute += balance_of * tokens_per_week[week_cursor] / ve_supply[week_cursor]; 
212:                 }
213:                 week_cursor += WEEK;
214:             }
215:         }
......
223:         return to_distribute;
224:     }

```

A problem could arise when claim is called, because `ve_supply[t]` is updated in on L285 within `_checkpoint_total_supply()` with epochs ahead of time, `to_distribute` on L211 may accrue the wrong reward amount (in worse case of zero value, a revert can occur) for some weeks depending on the time `claim(...)` is called .

```solidity
210:                 if (balance_of != 0) {
211:   @>>>              to_distribute += balance_of * tokens_per_week[week_cursor] / ve_supply[week_cursor]; 
212:                 }
```

Need I also mention that the `time_cursor` is wrongly updated because the cursor can be advance into an incomplete or worse yet entirely new epoch.


## Impact
The `ve_supply[t]` can be updated with values in the wrong epoch and this
- breaks accounting and core protocol functionality
- can lead to wrong estimation of rewards in some instances

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/RewardsDistributor.sol#L150-L162
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/RewardsDistributor.sol#L287
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/RewardsDistributor.sol#L211-L213

## Tool used

Manual Review

## Recommendation
Modify the `_checkpoint_total_supply(...)` function as shown below

```diff
    function _checkpoint_total_supply() internal {
        address ve = voting_escrow;
        uint t = time_cursor; // the current location of time
        uint rounded_timestamp = block.timestamp / WEEK * WEEK; // this can be >= time_cursor depending on when _checkpoint_total_supply() is called
        IVotingEscrow(ve).checkpoint();

        for (uint i = 0; i < 20; i++) {
-           if (t > rounded_timestamp) {
+           if (t >= rounded_timestamp) {
                break;
            } else {
                uint epoch = _find_timestamp_epoch(ve, t); // epoch prior to or at t
                IVotingEscrow.Point memory pt = IVotingEscrow(ve).point_history(epoch);
                int128 dt = 0;
                if (t > pt.ts) {
                    dt = int128(int256(t - pt.ts)); // change in time 
                }
                ve_supply[t] = Math.max(uint(int256(pt.bias - pt.slope * dt)), 0);
            }
            t += WEEK;
        }
        time_cursor = t;
    }
```