Decent Mandarin Pangolin

High

# Permanent freezing of user's funds as a consequence of _checkpoint_total_supply using > instead of >= in the RewardsDistributorV2

## Summary
As a consequence of `RewardsDistributorV2.checkpoint_total_supply()` utilizing an incorrect `>` operator instead of `>=`, the total supply of the `VotingEscrow` will be incorrectly accounted.

This will lead to permanently improperly calculating the `to_distribute` variable.

And, **in the worst-case scenario**, **PERMANENTLY** DoS'ing the `RewardsDistributorV2`'s `claim` function for the first user of the `VotingEscrow`.

This means that the `_claim` will always revert on division by `0` (because in
```solidity
to_distribute += balance_of * tokens_per_week[week_cursor] / ve_supply[week_cursor];
```
, `ve_supply[week_cursor]` will always be `0`.

And the `_tokenId`'s week cursor will never be increased and moved to a new week:
```solidity
        user_epoch_of[_tokenId] = user_epoch;
        time_cursor_of[_tokenId] = week_cursor;
```

Because the week cursor for a `_tokenId` is supposed to be updated here:
```solidity
                if (balance_of == 0 && user_epoch > max_user_epoch) break;
                if (balance_of != 0) {
                    to_distribute += balance_of * tokens_per_week[week_cursor] / ve_supply[week_cursor];
                }
                week_cursor += WEEK; // @@ <<< this should move the cursor later on
            }
        }

        user_epoch = Math.min(max_user_epoch, user_epoch - 1);
        user_epoch_of[_tokenId] = user_epoch;
        time_cursor_of[_tokenId] = week_cursor;
```

## Vulnerability Detail
- Notice that the `RewardsDistributorV2.checkpoint_total_supply` function is permissionless and publicly exposed, so it can be called to maliciously trigger a DoS for the first user.
- The `checkpoint_total_supply` function checkpoints the total supply before a timestamp is complete, thus leading to miscalculated total supply accounting in the state.

## Impact
**Permanently DoS'ing `claim` for a `tokenId`.**

## Code Snippet
The problem lies in this internal function:
```solidity
    function _checkpoint_total_supply() internal {
        address ve = voting_escrow;
        uint t = time_cursor;
        uint rounded_timestamp = block.timestamp / WEEK * WEEK;
        IVotingEscrow(ve).checkpoint();

        for (uint i = 0; i < 20; i++) {
            if (t > rounded_timestamp) { // @@ <<< it should be t >= rounded_timestamp
                break;
                
            } else {
                uint epoch = _find_timestamp_epoch(ve, t);
                IVotingEscrow.Point memory pt = IVotingEscrow(ve).point_history(epoch);
                int128 dt = 0;
                if (t > pt.ts) {
                    dt = int128(int256(t - pt.ts));
                }
                ve_supply[t] = Math.max(uint(int256(pt.bias - pt.slope * dt)), 0);
            }
            t += WEEK;
        }
        time_cursor = t;
    }

    function checkpoint_total_supply() external {
        _checkpoint_total_supply();
    }
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/RewardsDistributorV2.sol#L149

As each time the `_checkpoint_total_supply`'s loop doesn't `break;`, the `time_cursor` is moved by one more `WEEK`, there's no way to get back and re-checkpoint for the previous `WEEK` cursor:
```solidity
          t += WEEK;
        }
        time_cursor = t;
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/RewardsDistributorV2.sol#L160-L162

## Tool used
Manual human review.

## Recommendation
Consider using `>=` instead of `>` for the check in the `_checkpoint_total_supply` function to properly account for all the currently supplied assets in the `VotingEscrow`:
```diff
    function _checkpoint_total_supply() internal {
        address ve = voting_escrow;
        uint t = time_cursor;
        uint rounded_timestamp = block.timestamp / WEEK * WEEK;
        IVotingEscrow(ve).checkpoint();

        for (uint i = 0; i < 20; i++) {
-           if (t > rounded_timestamp) {
+           if (t >= rounded_timestamp) {
                break;
            } else {
                uint epoch = _find_timestamp_epoch(ve, t);
                IVotingEscrow.Point memory pt = IVotingEscrow(ve).point_history(epoch);
                int128 dt = 0;
                if (t > pt.ts) {
                    dt = int128(int256(t - pt.ts));
                }
                ve_supply[t] = Math.max(uint(int256(pt.bias - pt.slope * dt)), 0);
            }
            t += WEEK;
        }
        time_cursor = t;
    }
```