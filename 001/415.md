Silly Cider Pig

High

# The claimable amount of Flow token for paused gauge will be locked forever.

## Summary
If administrator pause a gauge in `Voter.sol`, the claimable amount of Flow token for the paused gauge will be locked forever.

## Vulnerability Detail
`Voter.sol#pauseGauge` function is the following.
```solidity
    function pauseGauge(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugePauseAllowance(msg.sender, _gauge)
            , "Pause gauge not allowed");
        }
        require(isAlive[_gauge], "gauge already dead");
        isAlive[_gauge] = false;
@>      claimable[_gauge] = 0;
        address _pair = IGauge(_gauge).stake(); // TODO: add test cases
        try IPair(_pair).setHasGauge(false) {} catch {}
        emit GaugePaused(_gauge);
    }
```
As can be seen, the above function only set `claimable[_gauge]` to zero, so the amount of Flow token will be locked in the `Voter.sol`.
And there is no way of refunding or burning the locked Flow tokens.

PoC:
Add the following test code into the `Voter.t.sol`.
```solidity
    function testPauseGauge() public {

        vm.warp(block.timestamp + ONE_WEEK);
        vm.roll(block.number + 1);

        // create ve NFTs
        flowDaiPair.approve(address(escrow), 10e18);
        uint tokenId1 = escrow.create_lock(1e18, FIFTY_TWO_WEEKS);
        uint tokenId2 = escrow.create_lock(1e18, FIFTY_TWO_WEEKS);

        // create gauges
        address gauge1 = voter.createGauge(address(pair2), 0);
        address gauge2 = voter.createGauge(address(pair3), 0);

        // create votes
        address[] memory pools = new address[](1);
        pools[0] = address(pair2);
        uint256[] memory weights = new uint256[](1);
        weights[0] = 5000;

        voter.vote(tokenId1, pools, weights);

        pools[0] = address(pair3);
        voter.vote(tokenId2, pools, weights);

        // distribute first week emission
        Flow flow = Flow(escrow.baseToken());
        flow.approve(address(voter), 10e18);

        voter.notifyRewardAmount(1e18);
        voter.updateAll();

        // pause second gauge
        voter.pauseGauge(gauge1);

        emit log("--- first week ---");
        uint claimable1 = voter.claimable(gauge1);
        uint claimable2 = voter.claimable(gauge2);
        emit log_named_uint("claimable of gauge1", claimable1);
        emit log_named_uint("claimable of gauge2", claimable2);
        emit log_named_uint("locked amount", flow.balanceOf(address(voter)) - claimable1 - claimable2);

        
        vm.warp(block.timestamp + ONE_WEEK);
        vm.roll(block.number + 1);

        // distribute second week emission        
        voter.notifyRewardAmount(1e18);
        voter.updateAll();

        emit log("--- second week ---");
        claimable1 = voter.claimable(gauge1);
        claimable2 = voter.claimable(gauge2);
        emit log_named_uint("claimable of gauge1", claimable1);
        emit log_named_uint("claimable of gauge2", claimable2);
        emit log_named_uint("locked amount", flow.balanceOf(address(voter)) - claimable1 - claimable2);
    }
```
And the following is the result of test code.
```sh
Ran 1 test for test/Voter.t.sol:VoterTest
[PASS] testPauseGauge() (gas: 11943149)
Logs:
  --- first week ---
  claimable of gauge1: 0
  claimable of gauge2: 499999999999999999
  locked amount: 500000000000000001
  --- second week ---
  claimable of gauge1: 0
  claimable of gauge2: 999999999999999998
  locked amount: 1000000000000000002

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 15.56ms (2.42ms CPU time)

Ran 1 test suite in 23.68ms (15.56ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```
As can be seen, the claimable tokens for `gauge1` are locked in the `Voter.sol` when the admin pause `gauge1` (first week).
And the amount of locked tokens are increased by time if the user doesn't withdraw voting for the paused gauge (second week).

## Impact
The claimable amount of Flow token for the paused gauge are locked forever.
And the amount of locked tokens are increased by time if the user doesn't withdraw voting for the paused gauge.
However, the locked Flow tokens will be included in the return value of `Minter.sol#circulating_supply`.

## Code Snippet
- [v4-contracts/contracts/Voter.sol#L380-L392](https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/Voter.sol#L380-L392)
- [v4-contracts/contracts/Voter.sol#L549-L562](https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/Voter.sol#L549-L562)

## Tool used

Manual Review

## Recommendation
At first, it is recommended to remove the `claimable[_gauge] = 0;` from `Voter.sol#pauseGauge` function as follows.
```solidity
    function pauseGauge(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugePauseAllowance(msg.sender, _gauge)
            , "Pause gauge not allowed");
        }
        require(isAlive[_gauge], "gauge already dead");
        isAlive[_gauge] = false;
--      claimable[_gauge] = 0;
        address _pair = IGauge(_gauge).stake(); // TODO: add test cases
        try IPair(_pair).setHasGauge(false) {} catch {}
        emit GaugePaused(_gauge);
    }
```
And then burn the claimable amount of Flow token for paused gauge in `Voter.sol#distribute` function as follows.
```solidity
    function distribute(address _gauge) public lock {
        IMinter(minter).update_period();
        _updateFor(_gauge); // should set claimable to 0 if killed
        uint _claimable = claimable[_gauge];
        if (_claimable > IGauge(_gauge).left(base) && _claimable / DURATION > 0) {
            claimable[_gauge] = 0;
            if((_claimable * 1e18) / currentEpochRewardAmount > minShareForActiveGauge) {
                activeGaugeNumber += 1;
            }

++          if (!isAlive[_gauge]) 
++              IERC20(base).burn(_claimable);
++          else 
                IGauge(_gauge).notifyRewardAmount(base, _claimable);
            emit DistributeReward(msg.sender, _gauge, _claimable);
        }
    }
```
Or it is also possible to distribute the claimable amount of Flow token for paused gauge to other active gauges proportionaly.

