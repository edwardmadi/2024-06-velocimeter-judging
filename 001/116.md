Digital Daisy Skunk

High

# RestartGauge Can Be Called With Killed Gauge

## Summary
The `Voter` contract contains a vulnerability in the `restartGauge` function, which does not check if the gauge was completely killed using the `killGaugeTotally` function. This allows a malicious user to restart a gauge that was previously killed, leading to stale and inconsistent data.

## Vulnerability Detail
The `Voter` contract has three key functions for managing gauges: `pauseGauge`, `restartGauge` and `killGaugeTotally`. The `restartGauge` function sets `isAlive[gauge]` to `true` without checking if the gauge was killed using `killGaugeTotally`. When `killGaugeTotally` is called, it not only sets `isAlive[gauge]` to `false`, but also deletes several other mappings and state variables related to the gauge, effectively removing all relevant data for the gauge.

If a gauge is restarted after being killed, the system ends up with stale data for the gauge, causing functions like `Voter::updateGauge` and `Voter::distribute` to operate incorrectly.

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L394-L429

## Impact
Restarting a gauge that was killed can lead to stale and inconsistent data, causing malfunction in various parts of the system. Specifically, operations relying on accurate gauge data, such as reward distribution, will fail or behave unexpectedly. For example, `Voter::distribute` may incorrectly set `claimable` rewards for a gauge, leading to incorrect reward distribution.

## Code Snippet
```solidity
function restartGauge(address _gauge) external {
    if (msg.sender != emergencyCouncil) {
        require(
            IGaugePlugin(gaugePlugin).checkGaugeRestartAllowance(msg.sender, _gauge),
            "Restart gauge not allowed"
        );
    }
    require(!isAlive[_gauge], "gauge already alive");
    isAlive[_gauge] = true;
    address _pair = IGauge(_gauge).stake(); // TODO: add test cases
    try IPair(_pair).setHasGauge(true) {} catch {}
    emit GaugeRestarted(_gauge);
}

function killGaugeTotally(address _gauge) external {
    if (msg.sender != emergencyCouncil) {
        require(
            IGaugePlugin(gaugePlugin).checkGaugeKillAllowance(msg.sender, _gauge),
            "Restart gauge not allowed"
        );
    }
    require(isAlive[_gauge], "gauge already dead");

    address _pool = poolForGauge[_gauge];

    delete isAlive[_gauge];
    delete external_bribes[_gauge];
    delete poolForGauge[_gauge];
    delete isGauge[_gauge];
    delete claimable[_gauge];
    delete supplyIndex[_gauge];
    delete gauges[_pool];
    try IPair(_pool).setHasGauge(false) {} catch {}

    killedGauges.push(_gauge);

    emit GaugeKilledTotally(_gauge);
}

function distribute(address _gauge) public lock {
    IMinter(minter).update_period();
    _updateFor(_gauge); // should set claimable to 0 if killed
    uint _claimable = claimable[_gauge];
    if (_claimable > IGauge(_gauge).left(base) && _claimable / DURATION > 0) {
        claimable[_gauge] = 0;
        if((_claimable * 1e18) / currentEpochRewardAmount > minShareForActiveGauge) {
            activeGaugeNumber += 1;
        }

        IGauge(_gauge).notifyRewardAmount(base, _claimable);
        emit DistributeReward(msg.sender, _gauge, _claimable);
    }
}
```

## Tool used
Manual Review

## Recommendation
To prevent restarting a gauge that has been killed, add a check in the `restartGauge` function to ensure the gauge has not been completely killed. One way to achieve this is by maintaining a separate state to track gauges that have been killed and checking this state in `restartGauge`.

```diff
function restartGauge(address _gauge) external {
+    require(!killedGauges.contains(_gauge), "gauge has been killed");

    if (msg.sender != emergencyCouncil) {
        require(
            IGaugePlugin(gaugePlugin).checkGaugeRestartAllowance(msg.sender, _gauge),
            "Restart gauge not allowed"
        );
    }
    require(!isAlive[_gauge], "gauge already alive");
    isAlive[_gauge] = true;
    address _pair = IGauge(_gauge).stake();
    try IPair(_pair).setHasGauge(true) {} catch {}
    emit GaugeRestarted(_gauge);
}
```
