Lone Frost Porcupine

High

# Calling `killGaugeTotally` can lock claimable rewards for that gauge

## Summary
If `killGaugeTotally` or `pauseGauge` is called on a gauge that has `claimable` rewards yet to be distributed, then those rewards will be permanently locked inside `Voter`.

## Vulnerability Detail
The `claimable` mapping stores the total amount of rewards per gauge ready to be distributed. The `distribute` function can be called by anyone and will send the rewards to the specified gauge and set `claimable[gauge]` to `0`. The problem is that both `killGaugeTotally`  and `pauseGauge` functions reset the `claimable` mapping of the specified gauge to `0` without checking first if those rewards were in fact distributed.
```solidity
    function killGaugeTotally(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugeKillAllowance(msg.sender, _gauge)
            , "Restart gauge not allowed");
        }
        require(isAlive[_gauge], "gauge already dead");

        address _pool = poolForGauge[_gauge];

        delete isAlive[_gauge];
        delete external_bribes[_gauge];
        delete poolForGauge[_gauge];
        delete isGauge[_gauge];
        delete claimable[_gauge]; //@audit
        delete supplyIndex[_gauge];
        delete gauges[_pool];
        try IPair(_pool).setHasGauge(false) {} catch {}

        killedGauges.push(_gauge);

        emit GaugeKilledTotally(_gauge);
    }
```
```solidity
    function pauseGauge(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugePauseAllowance(msg.sender, _gauge)
            , "Pause gauge not allowed");
        }
        require(isAlive[_gauge], "gauge already dead");
        isAlive[_gauge] = false; 
        claimable[_gauge] = 0; //@audit
        address _pair = IGauge(_gauge).stake();
        try IPair(_pair).setHasGauge(false) {} catch {}
        emit GaugePaused(_gauge);
    }
```
Now the corresponding amount of `flow` already present in the contract is locked and unable to be recovered.
Bellow is the coded PoC for the bug. Paste it [here](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/test/KillGauges.t.sol):
```solidity
  function test_cid_lockedClaimable() public {
      address Bob = makeAddr("Bob");

      //Bob locks some tokens and receives NFT
      deal(address(flowDaiPair), Bob, 100 * TOKEN_1);
      vm.startPrank(Bob);
      flowDaiPair.approve(address(escrow), 100 * TOKEN_1);
      escrow.create_lock(100 * TOKEN_1, FIFTY_TWO_WEEKS);
      vm.roll(block.number + 1); 
      assertEq(Bob, escrow.ownerOf(2)); //Bob is the owner of tokenId 2

      //Bob votes in some gauge
      vm.warp(block.timestamp + 1 weeks); // fwd one epoch
      address[] memory pools = new address[](2);
      pools[0] = address(pair);
      pools[1] = address(pair2);
      uint256[] memory weights = new uint256[](2);
      weights[0] = 10000;
      weights[1] = 10000;      
      voter.vote(2, pools, weights);
      vm.stopPrank();

      vm.startPrank(address(minter));
      uint256 rewardToNotify = 1000 ether;
      deal(address(FLOW), address(minter), rewardToNotify);
      FLOW.approve(address(voter), rewardToNotify);
      voter.notifyRewardAmount(rewardToNotify);
      vm.stopPrank();
      voter.updateAll();

      //gauge is killed
      voter.killGaugeTotally(address(gauge));
      assertEq(voter.claimable(address(gauge)), 0); //@elcid gauge has lost its claimable rewards

      uint256 gauge2RewardsBefore = FLOW.balanceOf(address(gauge2));
      voter.distribute(address(gauge2));
      assertApproxEqAbs(FLOW.balanceOf(address(gauge2)) - gauge2RewardsBefore, rewardToNotify/2, 1000); //@elcid gauge 2 received FLOW

      uint256 gaugeRewardsBefore = FLOW.balanceOf(address(gauge));
      voter.distribute(address(gauge));
      assertEq(FLOW.balanceOf(address(gauge)) - gaugeRewardsBefore, 0); //@elcid no rewards were sent to gauge 0

      voter.createGauge(address(pair), 0);
      voter.updateAll();
      assertEq(voter.claimable(address(gauge)), 0);
      console.log(FLOW.balanceOf(address(voter)));
  }       
```
## Impact
Loss of funds

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L421
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L388

## Tool used

Manual Review

## Recommendation
