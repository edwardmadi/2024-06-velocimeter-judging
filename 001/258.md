Delightful Lavender Goose

High

# User can be locked from voting

## Summary
The Voter contract fails to properly clear votes when a gauge is destroyed, causing reward withdrawal issues.

## Vulnerability Detail
In the `Voter` contract, users vote to get rewards from gauges. Rewards are reset every epoch, and users need to vote again by calling the `poke` or `vote` functions. If a user doesn't vote in an epoch, they won't receive rewards because votes don't carry over from one epoch to the next.

When a user votes, the `_reset` function is called first to clear the previous vote allocation of the `tokenId` to the gauges. During this reset, external bribes from the votes are cleared by calling the `withdraw` function of the `Bribes` contract.

However, if a gauge is destroyed using the `killGaugeTotally` function, the external bribe address will be cleared but the `poolVote` mapping still shows votes for pools linked to these non-existent gauges. This causes the `withdraw` calls to fail for any voter escrow token that still has uncleared votes for a gauge killed with `killGaugeTotally`.

```solidity
    function killGaugeTotally(address _gauge) external {
        ...

        delete isAlive[_gauge];
@>>     delete external_bribes[_gauge];
        ...

        killedGauges.push(_gauge);

        emit GaugeKilledTotally(_gauge);
    }

    function _reset(uint _tokenId) internal {
        address[] storage _poolVote = poolVote[_tokenId];
        uint _poolVoteCnt = _poolVote.length;
        uint256 _totalWeight = 0;

        for (uint i = 0; i < _poolVoteCnt; i ++) {
            address _pool = _poolVote[i];
            uint256 _votes = votes[_tokenId][_pool];

            if (_votes != 0) {
                _updateFor(gauges[_pool]);
                weights[_pool] -= _votes;
                votes[_tokenId][_pool] -= _votes;
                if (_votes > 0) {
@>>                 IBribe(external_bribes[gauges[_pool]])._withdraw(uint256(_votes), _tokenId);
                    _totalWeight += _votes;
                } else {
                    _totalWeight -= _votes;
                }
                emit Abstained(_tokenId, _votes);
            }
        }
        totalWeight -= uint256(_totalWeight);
        usedWeights[_tokenId] = 0;
        delete poolVote[_tokenId];
    }
```    
## Impact
User will be locked from voting.

## POC
```solidity
    function skipToNextEpoch(uint256 offset) public {
        uint256 ts = block.timestamp;
        uint256 nextEpoch = ts - (ts % (1 weeks)) + (1 weeks);
        vm.warp(nextEpoch + offset);
        vm.roll(block.number + 1);
    }

    function testCanSetExternalBribeAndClaimFeesFromNewBribe() public {
        createLock();
        vm.warp(block.timestamp + 1 weeks);

        voter.createGauge(address(pair), 0);
        address gaugeAddress = voter.gauges(address(pair));

        address[] memory rewards = new address[](2);
        rewards[0] = address(USDC);
        rewards[1] = address(FRAX);
        ExternalBribe newExternalBribe = new ExternalBribe(
            address(voter),
            rewards
        );
        vm.expectEmit(true, true, false, true);
        emit ExternalBribeSet(address(this), gaugeAddress, address(newExternalBribe));
        voter.setExternalBribeFor(gaugeAddress, address(newExternalBribe));

        Router.route[] memory routes = new Router.route[](1);
        routes[0] = Router.route(address(USDC), address(FRAX), true);

        assertEq(
            router.getAmountsOut(USDC_1, routes)[1],
            pair.getAmountOut(USDC_1, address(USDC))
        );

        uint256[] memory assertedOutput = router.getAmountsOut(USDC_1, routes);
        USDC.approve(address(router), USDC_1);
        router.swapExactTokensForTokens(
            USDC_1,
            assertedOutput[1],
            routes,
            address(owner),
            block.timestamp
        );

        address[] memory pools = new address[](1);
        pools[0] = address(pair);
        uint256[] memory weights = new uint256[](1);
        weights[0] = 5000;
        // Voter votes to pair pool.
        voter.vote(1, pools, weights);

        vm.warp(block.timestamp + 1 weeks);

        // The guage corresponding to pair is killed.
        vm.prank(voter.emergencyCouncil());
        voter.killGaugeTotally(gaugeAddress);

        // Let pass the epoch
        skipToNextEpoch(1 hours + 1);

        // Reset reverts because externalbribe is removed in `killGaugeTotally`
        // And external bribe withdraw will fail.
        vm.expectRevert();
        voter.reset(1);

        // Voter now voting to new pool because the previous one is killed.
        pools[0] = address(pair2);

        // Voter can't vote because the _reset function will revert.
        vm.expectRevert();
        voter.vote(1, pools, weights);
    }

```
## Code Snippet
- https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L418
```solidity
    function killGaugeTotally(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugeKillAllowance(msg.sender, _gauge)
            , "Restart gauge not allowed");
        }
        require(isAlive[_gauge], "gauge already dead");

        address _pool = poolForGauge[_gauge];

        delete isAlive[_gauge];
@>>     delete external_bribes[_gauge];
        delete poolForGauge[_gauge];
        delete isGauge[_gauge];
        delete claimable[_gauge];
        delete supplyIndex[_gauge];
        delete gauges[_pool];
        try IPair(_pool).setHasGauge(false) {} catch {}

        killedGauges.push(_gauge);

        emit GaugeKilledTotally(_gauge);
    }
```
- https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L221
```solidity
    function _reset(uint _tokenId) internal {
        address[] storage _poolVote = poolVote[_tokenId];
        uint _poolVoteCnt = _poolVote.length;
        uint256 _totalWeight = 0;

        for (uint i = 0; i < _poolVoteCnt; i ++) {
            address _pool = _poolVote[i];
            uint256 _votes = votes[_tokenId][_pool];

            if (_votes != 0) {
                _updateFor(gauges[_pool]);
                weights[_pool] -= _votes;
                votes[_tokenId][_pool] -= _votes;
                if (_votes > 0) {
@>>                 IBribe(external_bribes[gauges[_pool]])._withdraw(uint256(_votes), _tokenId);
                    _totalWeight += _votes;
                } else {
                    _totalWeight -= _votes;
                }
                emit Abstained(_tokenId, _votes);
            }
        }
        totalWeight -= uint256(_totalWeight);
        usedWeights[_tokenId] = 0;
        delete poolVote[_tokenId];
    }
```
## Tool used

Manual Review

## Recommendation
```diff
    function killGaugeTotally(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugeKillAllowance(msg.sender, _gauge)
            , "Restart gauge not allowed");
        }
        require(isAlive[_gauge], "gauge already dead");

        address _pool = poolForGauge[_gauge];

        delete isAlive[_gauge];
-       delete external_bribes[_gauge];
        delete poolForGauge[_gauge];
        delete isGauge[_gauge];
        delete claimable[_gauge];
        delete supplyIndex[_gauge];
        delete gauges[_pool];
        try IPair(_pool).setHasGauge(false) {} catch {}

        killedGauges.push(_gauge);

        emit GaugeKilledTotally(_gauge);
    }
```

