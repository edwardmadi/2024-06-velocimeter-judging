Wonderful Rosewood Pheasant

Medium

# Claimable gauge distributions are locked when `killGaugeTotally` is called

## Summary
When a gauge is terminated using the `killGaugeTotally` function, the claimable amount associated with the gauge is deleted and permanently locked within the contract. This happens because the `claimable[_gauge]` key value is cleared without redistributing or returning the accumulated rewards. The rewards are therefore irretrievable, leading to a loss of assets.

## Vulnerability Detail
When a gauge is killed, the `claimable[_gauge]` key value is cleared. Because any rewards received by the Voter contract are indexed and distributed in proportion to each pool's weight, this claimable amount is permanently locked within the contract.
```solidity
 function killGaugeTotally(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugeKillAllowance(msg.sender, _gauge)
            , "Restart gauge not allowed");
        }
        require(isAlive[_gauge], "gauge already dead");

        address _pool = poolForGauge[_gauge];

        delete isAlive[_gauge];
        delete external_bribes[_gauge];
        delete poolForGauge[_gauge];
        delete isGauge[_gauge];
        delete claimable[_gauge];
        delete supplyIndex[_gauge];
        delete gauges[_pool];
        try IPair(_pool).setHasGauge(false) {} catch {}

        killedGauges.push(_gauge);

        emit GaugeKilledTotally(_gauge);
    }
```

## Impact
Permanent loss of rewards that were accrued for a gauge prior to its termination. Since the rewards are not redistributed or returned, they remain locked within the Voter contract.

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/Voter.sol#L407-L429

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/Voter.sol#L517-L534

## Tool used

Manual Review

## Recommendation
Consider modifying the `killGaugeTotally` function to return the claimable amount to the Minter contract instead of deleting it. This will ensure that the accrued rewards are not lost but are instead properly redistributed. The updated function can include the following changes:

```diff
    function killGaugeTotally(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugeKillAllowance(msg.sender, _gauge)
            , "Restart gauge not allowed");
        }
        require(isAlive[_gauge], "gauge already dead");

        address _pool = poolForGauge[_gauge];
           // Return claimable back to minter
+        uint256 _claimable = claimable[_gauge];
+        if (_claimable > 0) {
+             IERC20(base).safeTransfer(minter, _claimable);
+             delete claimable[_gauge];
+         }

         delete isAlive[_gauge];
         delete external_bribes[_gauge];
         delete poolForGauge[_gauge];
         delete isGauge[_gauge];
-        delete claimable[_gauge];
         delete supplyIndex[_gauge];
         delete gauges[_pool];
         try IPair(_pool).setHasGauge(false) {} catch {}

         killedGauges.push(_gauge);

         emit GaugeKilledTotally(_gauge);
    }
```
Additionally, to ensure that residual votes (_supplied) are properly handled, consider modifying the _updateFor function to return any claimable amount to the Minter contract if the gauge is no longer active:

Something like this:
```diff
    function _updateFor(address _gauge) internal {
        address _pool = poolForGauge[_gauge];
        uint256 _supplied = weights[_pool];
        if (_supplied > 0) {
            uint _supplyIndex = supplyIndex[_gauge];
            uint _index = index; // get global index0 for accumulated distro
            supplyIndex[_gauge] = _index; // update _gauge current position to global position
            uint _delta = _index - _supplyIndex; // see if there is any difference that need to be accrued
            if (_delta > 0) {
                uint _share = uint(_supplied) * _delta / 1e18; // add accrued difference for each supplied token
                if (isAlive[_gauge]) {
                    claimable[_gauge] += _share;
+               }  else {
+                   IERC20(base).safeTransfer(minter, _share);
+               }
            }
        } else {
            supplyIndex[_gauge] = index; // new users are set to the default global state
        }
    }
 ```
These changes will ensure that any rewards associated with a terminated gauge are properly handled and not permanently locked within the contract.
