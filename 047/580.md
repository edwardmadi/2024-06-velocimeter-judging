Delightful Lavender Goose

Medium

# Fee Claims Valid for NFTs Transferred to Zero Address

## Summary
`_transferFrom` allows transfers to `address(0)`, which will still accrue a portion of rewards, contrary to the protocol's intention. This will also cause other voters to receive fewer rewards and the remaining rewards to be stuck in the contract.

## Vulnerability Detail

The comment in the `_transferFrom` function says that it does not allow transferring to the zero address, but this is not true. Here's a simplified breakdown of the issues:

1. **Incorrect Comment**: 
    - The comment falsely claims that transfers to the zero address are not allowed.

2. **Actual Behavior**:
    - Tokens can be transferred to the zero address (`address(0)`).
    - After such a transfer, `address(0)` becomes the owner of the token.
    - The token will appear in the list of tokens owned by `address(0)`.

3. **Effect Similar to Burning**:
    - Transferring a token to `address(0)` is similar to burning it.
    - The previous owner loses access to voting power and the deposited asset.



## Impact
- **Voting Power**: The voting power of tokens held by `address(0)` can be used by anyone because functions like `votingPowerOf` and `_balanceOfNFT` do not check if the owner is `address(0)`. These tokens will retain substantial voting power until the lock expires.
-  **No Checkpoint**: No checkpoint is created for this kind of "burn". The voting power (bias and slope values) will still be considered in global calculations until the lock expires.
-  **Incorrect Total Supply**: The total supply value does not decrease as it should in a burn. This causes incorrect accounting in external voting contracts (e.g., incorrect quorum calculations), which cannot be corrected even after the lock expires.
-  **Assets Stuck**: The underlying assets will be stuck forever in the zero address contract.
    
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L312
```solidity
    /// @dev Exeute transfer of a NFT.
    ///      Throws unless `msg.sender` is the current owner, an authorized operator, or the approved
    ///      address for this NFT. (NOTE: `msg.sender` not allowed in internal function so pass `_sender`.)
@>> ///      Throws if `_to` is the zero address.
    ///      Throws if `_from` is not the current owner.
    ///      Throws if `_tokenId` is not a valid NFT.
    function _transferFrom(
        address _from,
        address _to,
        uint _tokenId,
        address _sender
    ) internal {
        require(attachments[_tokenId] == 0 && !voted[_tokenId], "attached");
        // Check requirements
        require(_isApprovedOrOwner(_sender, _tokenId));
        require(_from != _to,"from == to");
        
        // Clear approval. Throws if `_from` is not the current owner
        _clearApproval(_from, _tokenId);
        // Remove NFT. Throws if `_tokenId` is not a valid NFT
        _removeTokenFrom(_from, _tokenId);
        // auto re-delegate
        _moveTokenDelegates(delegates(_from), delegates(_to), _tokenId);
        // Add NFT
        _addTokenTo(_to, _tokenId);
        // Set the block of ownership transfer (for Flash NFT protection)
        ownership_change[_tokenId] = block.number;

        //unlock split
        blockedSplit[_tokenId] = false;

        // Log the transfer
        emit Transfer(_from, _to, _tokenId);
    }

```
## Tool used

Manual Review

## Recommendation
```diff
    /// @dev Exeute transfer of a NFT.
    ///      Throws unless `msg.sender` is the current owner, an authorized operator, or the approved
    ///      address for this NFT. (NOTE: `msg.sender` not allowed in internal function so pass `_sender`.)
   ///      Throws if `_to` is the zero address.
    ///      Throws if `_from` is not the current owner.
    ///      Throws if `_tokenId` is not a valid NFT.
    function _transferFrom(
        address _from,
        address _to,
        uint _tokenId,
        address _sender
    ) internal {
        require(attachments[_tokenId] == 0 && !voted[_tokenId], "attached");
        // Check requirements
        require(_isApprovedOrOwner(_sender, _tokenId));
        require(_from != _to,"from == to");
+       require(_to != address(0) ,"_to == address(0)");
        
        // Clear approval. Throws if `_from` is not the current owner
        _clearApproval(_from, _tokenId);
        // Remove NFT. Throws if `_tokenId` is not a valid NFT
        _removeTokenFrom(_from, _tokenId);
        // auto re-delegate
        _moveTokenDelegates(delegates(_from), delegates(_to), _tokenId);
        // Add NFT
        _addTokenTo(_to, _tokenId);
        // Set the block of ownership transfer (for Flash NFT protection)
        ownership_change[_tokenId] = block.number;

        //unlock split
        blockedSplit[_tokenId] = false;

        // Log the transfer
        emit Transfer(_from, _to, _tokenId);
    }

```