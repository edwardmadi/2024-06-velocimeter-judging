Fit Burgundy Narwhal

High

# Overstated weekly emissions will be minted during distribution due to stale `activeGaugeNumber`

## Summary
When a FLOW token reward is `distribute`d to a Gauge, the `activeGaugeNumber` state variable counter is incremented by 1 (`+= 1`). Each next time the Voter's `distribute()` function is called, it makes the Voter contract calculate the total weekly emissions of FLOW token based on the `activeGaugeNumber`, but if a gauge had lost its votes since the last distribution, the Minter will overestimate the amount of tokens to mint.

Users can change their pool weights votes each Thursday, meaning if on Wednesday there were 10 pools that had enough voting power to them in order to receive rewards from the Voter's `distribute()`, the `activeGaugeNumber` will be `10`. Now, if users decide they want to vote for only half of these pools, in the next distribution cycle there'll be only 5 gauges that'll have an adequate `claimable` amount that is above the minimum share required. The Minter contract however will use the stale  `activeGaugeNumber` (which is `10`) when calculating how much FLOW tokens it should emit for the current distribution cycle, overstating them by 100% and thus distributing double the amount to each gauge in the current cycle.
## Vulnerability Detail
During the current distribution cycle in the Voter it will increment the `activeGaugeNumber` counter for every gauge that has enough votes to it to receive rewards

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L549-L562
```solidity
    function distribute(address _gauge) public lock {
        IMinter(minter).update_period();
        _updateFor(_gauge); // should set claimable to 0 if killed
        uint _claimable = claimable[_gauge];
        if (_claimable > IGauge(_gauge).left(base) && _claimable / DURATION > 0) {
            claimable[_gauge] = 0;
            if((_claimable * 1e18) / currentEpochRewardAmount > minShareForActiveGauge) {
→               activeGaugeNumber += 1;
            }

            IGauge(_gauge).notifyRewardAmount(base, _claimable);
            emit DistributeReward(msg.sender, _gauge, _claimable);
        }
    }
```

The problem arises when between distributions the votes on gauges change in a way where less gauges are now eligible for receiving reward (`(_claimable * 1e18) / currentEpochRewardAmount <= minShareForActiveGauge`). When Minter's `update_period()` is called, it'll calculate the weekly emissions using the Voter's `activeGaugeNumber`.

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Minter.sol#L112-L137
```solidity
function update_period() external returns (uint) {
        uint _period = active_period;
        if (block.timestamp >= _period + WEEK && initializer == address(0)) { // only trigger if new week
            _period = (block.timestamp / WEEK) * WEEK;
            active_period = _period;
→           uint256 weekly = weekly_emission();

            uint _teamEmissions = (teamRate * weekly) /
                (PRECISION - teamRate);
            uint _required =  weekly + _teamEmissions;
            uint _balanceOf = _flow.balanceOf(address(this));
            if (_balanceOf < _required) {
                _flow.mint(address(this), _required - _balanceOf);
            }

            require(_flow.transfer(teamEmissions, _teamEmissions));

            _checkpointRewardsDistributors();

            _flow.approve(address(_voter), weekly);
            _voter.notifyRewardAmount(weekly);

            emit Mint(msg.sender, weekly, circulating_supply());
        }
        return _period;
    }
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Minter.sol#L98-L104
```solidity
    function weekly_emission() public view returns (uint) {
        uint256 numberOfGauges = _voter.activeGaugeNumber();
        if(numberOfGauges == 0) { 
            return weeklyPerGauge;
        }
        return weeklyPerGauge * numberOfGauges;
    }
```

And due to `activeGaugeNumber` being stale as it describes the gauges that had enough claimable amount in the **last** distribution cycle the Minter will emit rewards for 10 gauges but the reward will be split between less than 10 gauges. This way every time the number of eligible gauges between distributions drops, the remaining gauges will receive more rewards.
## Impact
Eligible gauges will receive more rewards than they should due to the usage of a stale `activeGaugeNumber` when the number of active gauges between distributions goes down causing an unfair distribution of rewards.
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Minter.sol#L98-L104 
## Tool used
Manual Review
## Recommendation
`activeGaugeNumber` should be a function that dynamically calculates the active gauges based on their claimable amount after `_updateFor(gauge)` has been called.
