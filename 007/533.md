Young Mango Tiger

High

# Unsafe casting throughout entire RewardsDistributor

## Summary
Unsafe casting of negative bias from int256 -> uint256 will cause a silent underflow and assign a very large number instead of setting it to 0 as expected.
## Vulnerability Detail
```solidity
    function ve_for_at(uint _tokenId, uint _timestamp) external view returns (uint) {
        address ve = voting_escrow;
        uint max_user_epoch = IVotingEscrow(ve).user_point_epoch(_tokenId);
        uint epoch = _find_timestamp_user_epoch(ve, _tokenId, _timestamp, max_user_epoch);
        IVotingEscrow.Point memory pt = IVotingEscrow(ve).user_point_history(_tokenId, epoch);
        return Math.max(uint(int256(pt.bias - pt.slope * (int128(int256(_timestamp - pt.ts))))), 0); 
           // @audit in cases of negative bias, casting it to uint will silently underflow to a large number
    }
```
In cases where one's vote weight is checked in the future, past the lock's expiration, return values are expected to be 0. However in the example above the negative number is first cast to uint and then the Math.max operation is performed which will return a large number instead of 0. This flawed logic is observed multiple times across the `RewardsDistributorV2.sol` in [_checkpoint_total_supply](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/RewardsDistributorV2.sol#L158), [_claim](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/RewardsDistributorV2.sol#L208) and [_claimable](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/RewardsDistributorV2.sol#L265).
## Impact
 
In instances of unclaimed rewards up till a lock's expiration, invoking `claim` past expiration will lead to transaction revert due to overflow when calculating  
```solidity
            } else {
                int128 dt = int128(int256(week_cursor - old_user_point.ts));
                uint balance_of = Math.max(uint(int256(old_user_point.bias - dt * old_user_point.slope)), 0);
              // balance_of underflows silently to a large number in the line above
                if (balance_of == 0 && user_epoch > max_user_epoch) break;
                if (balance_of != 0) {
                    to_distribute += balance_of * tokens_per_week[week_cursor] / ve_supply[week_cursor];
             // to_distribute overflows due to multiplying by balance_of, reverting the transaction
                }
```
Unclaimed funds post-expiration are permanently locked.

## Code Snippet
```solidity
        return Math.max(uint(int256(pt.bias - pt.slope * (int128(int256(_timestamp - pt.ts))))), 0);
```
## Tool used

Manual Review

## Recommendation
First compare the potentially negative bias against 0 and cast it to uint after.