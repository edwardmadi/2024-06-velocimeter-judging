Polite Butter Gazelle

Medium

# Inadequate slippage protection for `OptionTokenV4::exerciseVe` and `OptionTokenV4::exerciseLp`

## Summary

`OptionTokenV4::exerciseVe` and `OptionTokenV4::exerciseLp` functions allow users to burn their option tokens and transfer `paymentToken` (i.e, DAI) which will be used for fees, adding liquidity to the respective liquidity pool (i.e, FLOW/DAI) for lp tokens, and for sending rewards to the gauge. The sender must transfer `paymentAmount + paymentAmountToAddLiquidity` amount of `paymentToken`.

`paymentAmount` is used for treasury fees + gauge reward and `paymentAmountToAddLiquidity` is used for adding liquidity for lp tokens.

The problem is that the slippage protection for `OptionTokenV4::exerciseVe` and `OptionTokenV4::exerciseLp` only checks if `paymentAmount > _maxPaymentAmount`. There are no slippage checks for `paymentAmountToAddLiquidity`. This is erroneous, as the current pool reserves are used to calculate `paymentAmountToAddLiquidity`, which can change while the transaction is in the mempool. Users may have to pay more than expected, causing a loss of funds.

## Vulnerability Detail

[OptionTokenV4.sol#L593-L621)](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L593-L621)
```javascript
    function _exerciseVe(
        uint256 _amount,
@>      uint256 _maxPaymentAmount,
        uint256 _discount,
        address _recipient
    ) internal returns (uint256 paymentAmount, uint256 nftId,uint256 lpAmount) {
        if (isPaused) revert OptionToken_Paused();
        if (isExerciseVePaused) revert OptionToken_Paused();

        if (_discount > minLPDiscount || _discount < maxLPDiscount)
            revert OptionToken_InvalidDiscount();
            
        // burn callers tokens
        _burn(msg.sender, _amount);
@>      (uint256 paymentAmount,uint256 paymentAmountToAddLiquidity) =  getPaymentTokenAmountForExerciseLp(_amount,_discount); // TODO decide if we want to have the curve or just always maxlock
@>      if (paymentAmount > _maxPaymentAmount) //@audit-issue inadequate slippage, as the user must pay paymentAmount + paymentAmountToAddLiquidity
            revert OptionToken_SlippageTooHigh();
          
        // Take team fee
@>      uint256 paymentGaugeRewardAmount = _discount == 0 ? 0 : _takeFees( //@audit take fees (which caller must pay)
            paymentToken,
            paymentAmount
        );
        _safeTransferFrom(
            paymentToken,
            msg.sender,
            address(this),
@>          paymentGaugeRewardAmount + paymentAmountToAddLiquidity
        );
```

Users can specify `_maxPaymentAmount` for the slippage check if `(paymentAmount > _maxPaymentAmount)`.

Fees are taken from the amount via `_takeFees`, which directly transfers the fee amount the user must pay via `safeTransferFrom`. The fees are deducted from `paymentAmount` and `paymentGaugeRewardAmount` is returned.

The protocol proceeds to transfer `paymentGaugeRewardAmount + paymentAmountToAddLiquidity` from the caller. We can see now that the full amount of `paymentToken` the user must pay is `paymentAmount + paymentAmountToAddLiquidity`.

Therefore the slippage check should be if `paymentAmount + paymentAmountToAddLiquidity` exceeds the user specified max payment amount.

In addition, the `paymentAmountToAddLiquidity` amount is calculated based off the *current* reserves of the pool:

[OptionTokenV4.sol#L350-L356](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L350-L356)
```javascript
    function getPaymentTokenAmountForExerciseLp(uint256 _amount,uint256 _discount) public view returns (uint256 paymentAmount, uint256 paymentAmountToAddLiquidity)
    {
       
        paymentAmount = _discount == 0 ? 0 : getLpDiscountedPrice(_amount, _discount);
@>      (uint256 underlyingReserve, uint256 paymentReserve) = IRouter(router).getReserves(underlyingToken, paymentToken, false);
        paymentAmountToAddLiquidity = (_amount * paymentReserve) / underlyingReserve;
    }
```

This can change prior to function execution (i.e, front-running, large amount of pool swaps, etc), which may cause the user to pay more `paymentAmountToAddLiquidity` than expected, causing a loss of funds.

## Proof of Concept

<details>
<summary>Coded PoC</summary>
<br>

Add the following to `test/OptionTokenV4.t.sol` and run `forge test --mt testInsufficientSlippage -vv`

```javascript
    function testInsufficientSlippage() public {
        vm.startPrank(address(owner));
        FLOW.approve(address(oFlowV4), TOKEN_1);
        // mint Option token to owner 2
        oFlowV4.mint(address(owner2), TOKEN_1);

        washTrades();
        vm.stopPrank();

        uint256 bobOflowBalanceBefore = oFlowV4.balanceOf(address(owner2));
        uint256 bobDaiBalanceBefore = DAI.balanceOf(address(owner2));
        uint256 maxAmount = 1e10;

        console.log("Bob's oFLOW balance before exercising options: ", bobOflowBalanceBefore);
        console.log("Bob's DAI balance before exercising options: ", bobDaiBalanceBefore);
        console.log("Bob's maxAmount of DAI to spend: ", maxAmount);

        vm.startPrank(address(owner2));
        DAI.approve(address(oFlowV4), TOKEN_100K);
 
        (, uint256 nftId, ) = oFlowV4.exerciseVe(
            1e10,
            maxAmount,
            address(owner2),
            80,
            block.timestamp
        );
        vm.stopPrank();

        uint256 bobOflowBalanceAfter = oFlowV4.balanceOf(address(owner2));
        uint256 bobDaiBalanceAfter = DAI.balanceOf(address(owner2));
        uint256 bobDaiBalanceSpent = bobDaiBalanceBefore - bobDaiBalanceAfter;

        console.log("Bob's oFLOW balance after exercising options: ", bobOflowBalanceAfter);
        console.log("Bob's DAI balance after exercising options: ", bobDaiBalanceAfter);
        console.log("Amount of DAI Bob spent: ", bobDaiBalanceSpent);
        assert(bobDaiBalanceSpent > maxAmount);
    }
```
</details>

### Console Output

```text
Running 1 test for test/OptionTokenV4.t.sol:OptionTokenV4Test
[PASS] testInsufficientSlippage() (gas: 3511876)
Logs:
  Bob's oFLOW balance before exercising options:  1000000000000000000
  Bob's DAI balance before exercising options:  1000000000000000000000000000000
  Bob's maxAmount of DAI to spend:  10000000000
  Bob's oFLOW balance after exercising options:  999999990000000000
  Bob's DAI balance after exercising options:  999999999999999999981999999970
  Amount of DAI Bob spent:  18000000030

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 17.34ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

We can see from this PoC that the amount of `paymentToken` (DAI) Bob paid far exceeded the max amount of `paymentToken` Bob specified to spend.

## Impact

Loss of funds for users due to inadequate slippage protection.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L593-L621

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L350-L356

## Tool used

Manual Review

## Recommendation

Consider making the following changes to `OptionTokenV4::exerciseVe` and `OptionTokenV4::exerciseLp`

```diff
-   if (paymentAmount > _maxPaymentAmount)
+    if (paymentAmount + paymentAmountToAddLiquidity > _maxPaymentAmount)
        revert OptionToken_SlippageTooHigh();
```