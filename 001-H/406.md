Fresh Azure Copperhead

Medium

# Undistributed gauge reward might be locked forever when `pauseGauge()` is called to kill gauge

## Summary
It doesn't check if there is undistributed gauge reward when [`pauseGauge()`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L380-L392) is called to kill gauge.
## Vulnerability Detail
[`Voter#pauseGauge()`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L380-L392) can be called to kill gauge. However it doesn't check if there is any undistributed gauge reward.  `claimable[_gauge]` is set to `0` immediately, resulting undistributed gauge reward being locked forever.

Copy below codes to [KillGauges.t.sol](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/test/KillGauges.t.sol) and run forge test --match-test testKilledGaugeLockUndistributedReward
```solidity
  function testKilledGaugeLockUndistributedReward() public {
    minter.startActivePeriod();
    vm.warp(block.timestamp + ONE_WEEK * 2);
    vm.roll(block.number + 1);
    address[] memory pools = new address[](1);
    pools[0] = address(pair);
    uint256[] memory weights = new uint256[](1);
    weights[0] = 10000;
    voter.vote(1, pools, weights);
    //@audit-info FLOW balance in voter is 0 before update_period()
    assertEq(FLOW.balanceOf(address(voter)), 0);
    vm.warp(block.timestamp + ONE_WEEK * 3);
    minter.update_period();
    voter.updateGauge(address(gauge));
    uint balanceBefore = FLOW.balanceOf(address(voter));
    //@audit-info minter minted FLOW to voter
    assertTrue(balanceBefore != 0);
    //@audit-info gauge has undistributed reward
    assertTrue(voter.claimable(address(gauge)) != 0);
    //@audit-info kill gague
    voter.pauseGauge(address(gauge));
    //@audit-info reward is set to 0 
    assertTrue(voter.claimable(address(gauge)) == 0);
    //@audit-info FLOW balance in voter keep unchanged without any distribution.
    assertEq(balanceBefore, FLOW.balanceOf(address(voter)));
  }  
```
## Impact
Undistributed gauge reward might be locked forever when `pauseGauge()` is called to kill gauge
### Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L380-L392
## Tool used

Manual Review

## Recommendation
Undistributed gauge reward should be distributed before it is reset to `0`:
```diff
    function pauseGauge(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugePauseAllowance(msg.sender, _gauge)
            , "Pause gauge not allowed");
        }
        require(isAlive[_gauge], "gauge already dead");
+       distribute(_gauge);
        isAlive[_gauge] = false;
        claimable[_gauge] = 0;
        address _pair = IGauge(_gauge).stake(); // TODO: add test cases
        try IPair(_pair).setHasGauge(false) {} catch {}
        emit GaugePaused(_gauge);
    }
```