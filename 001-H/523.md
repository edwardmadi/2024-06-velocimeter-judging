Polite Butter Gazelle

Medium

# Rewards are locked if gauge with claimable rewards is killed

## Summary

`Minter::update_period` sends `weekly emissions` of `FLOW` tokens to the `Voter` contract, which is distributed to gauges based off their respective weights (determined by votes). 

If the gauge has any `claimable` rewards, which is determined based off their respective weights (determined by votes), they will receive a portion of the `weekly emissions` via `Voter::distribute`.

The problem is, if a gauge is killed, the `claimable` rewards for that gauge is set to 0 to ensure that the killed gauge can no longer receive rewards, even if the gauge had claimable rewards for that epoch. Since the rewards can no longer be claimed by anyone, this will cause the rewards to be permanently locked in the `Voter` contract.

## Vulnerability Detail

`Voter::_updateFor` calculates the `claimable` rewards for gauges:

[Voter.sol#L517-L529](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L517-L529)
```javascript
    function _updateFor(address _gauge) internal {
        address _pool = poolForGauge[_gauge];
        uint256 _supplied = weights[_pool];
        if (_supplied > 0) {
            uint _supplyIndex = supplyIndex[_gauge];
            uint _index = index; // get global index0 for accumulated distro
            supplyIndex[_gauge] = _index; // update _gauge current position to global position
            uint _delta = _index - _supplyIndex; // see if there is any difference that need to be accrued
            if (_delta > 0) {
                uint _share = uint(_supplied) * _delta / 1e18; // add accrued difference for each supplied token
                if (isAlive[_gauge]) {
@>                  claimable[_gauge] += _share;
                }
```

The amount of `claimable` rewards for the gauge is then sent to the gauge:

[Voter.sol#L549-L562](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L549-L562)
```javascript
    function distribute(address _gauge) public lock {
        IMinter(minter).update_period();
        _updateFor(_gauge); // should set claimable to 0 if killed
@>      uint _claimable = claimable[_gauge];
        if (_claimable > IGauge(_gauge).left(base) && _claimable / DURATION > 0) {
            claimable[_gauge] = 0;
            if((_claimable * 1e18) / currentEpochRewardAmount > minShareForActiveGauge) {
                activeGaugeNumber += 1;
            }

@>          IGauge(_gauge).notifyRewardAmount(base, _claimable);
            emit DistributeReward(msg.sender, _gauge, _claimable);
        }
    }
```

However, when a gauge is killed, the claimable rewards are set to 0:

[Voter.sol#L407-L429](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L407-L429)
```javascript
    function killGaugeTotally(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugeKillAllowance(msg.sender, _gauge)
            , "Restart gauge not allowed");
        }
        require(isAlive[_gauge], "gauge already dead");

        address _pool = poolForGauge[_gauge];

        delete isAlive[_gauge];
        delete external_bribes[_gauge];
        delete poolForGauge[_gauge];
        delete isGauge[_gauge];
@>      delete claimable[_gauge]; //@audit dead gauge can't claim rewards
        delete supplyIndex[_gauge];
        delete gauges[_pool];
        try IPair(_pool).setHasGauge(false) {} catch {}

        killedGauges.push(_gauge);

        emit GaugeKilledTotally(_gauge);
    }
```

This is to ensure that the killed gauge can no longer claim rewards. The problem is that if there were any claimable rewards for the killed gauge, it can no longer be claimed by anyone, therefore it will be permanently locked in the `Voter` contract.

## Proof of Concept

<details>
<summary>Coded PoC</summary>
<br>

Add the following to `test/Minter.t.sol` and run: `forge test --mt testKilledGaugeRewardsLocked -vv`

```javascript
    function testClaimableRewardsLocked() public {
        initializeVotingEscrow();

        FLOW.approve(address(router), TOKEN_1);
        FRAX.approve(address(router), TOKEN_1);
        router.addLiquidity(address(FRAX), address(FLOW), false, TOKEN_1, TOKEN_1, 0, 0, address(owner), block.timestamp);
        address pair1 = router.pairFor(address(FRAX), address(FLOW), false);
        address pair2 = router.pairFor(address(DAI), address(FLOW), false);

        address gauge = voter.gauges(pair1);
      
        address[] memory pools = new address[](2);
        pools[0] = pair1;

        uint256[] memory weights = new uint256[](2);
        weights[0] = 300;

        _elapseOneWeek();

        voter.vote(1, pools, weights);
        minter.update_period(); // give rewards to Voter contract

        voter.updateGauge(address(gauge)); 

        console.log("Claimable rewards of gauge before killing gauge: ", voter.claimable(address(gauge)));
        console.log("Voter contract balance before killing gauge: ", FLOW.balanceOf(address(voter)));
        voter.killGaugeTotally(address(gauge));
        console.log("Claimable rewards of gauge after killing gauge: ", voter.claimable(address(gauge)));
        console.log("Voter contract balance after killing gauge: ", FLOW.balanceOf(address(voter)));

    }
```
</details>

### Console Output

```text
Running 1 test for test/Minter.t.sol:MinterTest
[PASS] testClaimableRewardsLocked() (gas: 58840817)
Logs:
  Claimable rewards of gauge before killing gauge:  1999999999999999999999
  Voter contract balance before killing gauge:  2000000000000000000000
  Claimable rewards of gauge after killing gauge:  0
  Voter contract balance after killing gauge:  2000000000000000000000

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.39ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact

Permanently locked rewards in the `Voter` contract, `FLOW` tokens minted that can never be recovered, loss of funds for the protocol.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L555-L557

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Minter.sol#L98-L104

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L407-L429

## Tool used

Manual Review

## Recommendation

`Velodrome Finance` [fixed this issue](https://github.com/velodrome-finance/contracts/commit/e4b230ffcffa6e6998b897a595dbfc40ebf3f7c2) by sending the claimable rewards back to the `Minter` contract when a gauge is killed. Consider implementing the same changes.