Polite Butter Gazelle

Medium

# Pausing and unpausing gauges will cause the gauge to lose all claimable rewards

## Summary

Gauges earn weekly emissions, which is distributed based off `weights` of a pool that corresponds to the amount of votes to the pool by `veNFT` holders. Voters earn these rewards by calling `getReward` on the gauge.

LPs of the pool incentivize votes by locking their LP tokens in the gauge, setting external bribes, etc. The more funds provided by LPs to incentivize votes, the more votes they are likely to get.

The `Voter` contract allows for gauges to be created, killed, and paused/unpaused. Currently, only the `emergency council` can pause/unpause gauges.

However, there is a problem with pausing/unpausing gauges that will cause all claimable rewards for that epoch to be lost, causing loss of funds for LPs and rewards lost for `veNFT` holders who voted on the pool of the gauge.

## Vulnerability Detail

Let's look at how rewards are distributed to gauges.

[Voter.sol#L485-L495](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L485-L495)
```javascript
    function notifyRewardAmount(uint amount) external {
        require(msg.sender == minter,"not a minter");
        activeGaugeNumber = 0;
        currentEpochRewardAmount = amount;
        _safeTransferFrom(base, msg.sender, address(this), amount); // transfer the distro in
        uint256 _ratio = amount * 1e18 / totalWeight; // 1e18 adjustment is removed during claim
        if (_ratio > 0) {
            index += _ratio;
        }
        emit NotifyReward(msg.sender, base, amount);
    }
```

Every week this function is called by the `Minter` contract to send weekly rewards to the `Voter` contract, which go to gauges.

The following function will update the rewards for the specific gauge, by incrementing `claimable` mapping for that gauge.

[Voter.sol#L513-L534](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L513-L534)
```javascript
    function updateGauge(address _gauge) external {
        _updateFor(_gauge);
    }

    function _updateFor(address _gauge) internal {
        address _pool = poolForGauge[_gauge];
        uint256 _supplied = weights[_pool];
        if (_supplied > 0) {
            uint _supplyIndex = supplyIndex[_gauge];
            uint _index = index; // get global index0 for accumulated distro
            supplyIndex[_gauge] = _index; // update _gauge current position to global position
            uint _delta = _index - _supplyIndex; // see if there is any difference that need to be accrued
            if (_delta > 0) {
                uint _share = uint(_supplied) * _delta / 1e18; // add accrued difference for each supplied token
                if (isAlive[_gauge]) {
@>                  claimable[_gauge] += _share;
                }
            }
        } else {
            supplyIndex[_gauge] = index; // new users are set to the default global state
        }
    }
```

Finally, the following will actually send the `claimable` amount of rewards to the gauge:

[Voter.sol#L549-L562](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L549-L562)
```javascript
    function distribute(address _gauge) public lock {
        IMinter(minter).update_period();
        _updateFor(_gauge); // should set claimable to 0 if killed
@>      uint _claimable = claimable[_gauge];
        if (_claimable > IGauge(_gauge).left(base) && _claimable / DURATION > 0) {
            claimable[_gauge] = 0;
            if((_claimable * 1e18) / currentEpochRewardAmount > minShareForActiveGauge) {
                activeGaugeNumber += 1;
            }

@>          IGauge(_gauge).notifyRewardAmount(base, _claimable);
            emit DistributeReward(msg.sender, _gauge, _claimable);
        }
    }
```

Let's look at what happens when gauges are paused by the `emergency council`:

[Voter.sol#L380-L405](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L380-L405)
```javascript
    function pauseGauge(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugePauseAllowance(msg.sender, _gauge)
            , "Pause gauge not allowed");
        }
        require(isAlive[_gauge], "gauge already dead");
        isAlive[_gauge] = false;
@>      claimable[_gauge] = 0; //@audit-issue resets claimable rewards to 0
        address _pair = IGauge(_gauge).stake(); // TODO: add test cases
        try IPair(_pair).setHasGauge(false) {} catch {}
        emit GaugePaused(_gauge);
    }

    function restartGauge(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugeRestartAllowance(msg.sender, _gauge)
            , "Restart gauge not allowed");
        }
        require(!isAlive[_gauge], "gauge already alive");
        isAlive[_gauge] = true;
        address _pair = IGauge(_gauge).stake(); // TODO: add test cases
        try IPair(_pair).setHasGauge(true) {} catch {}
        emit GaugeRestarted(_gauge);
    }
```

We can see here that `claimable` amount is set to 0. This is erroneous as this will cause the gauge to lose all rewards which cannot be recovered after unpausing. `veNFT` voters can only vote for one gauge per epoch, which will cause the vote to be wasted, since they will not receive any rewards. The LPs will also not be receiving rewards and will lose funds from the amount invested into incentivizing voters due to lost rewards.

The correct solution would be to distribute any claimable rewards to the gauge prior to pausing it, allowing voters and LP to receive their rewards.

## Proof of Concept

<details>
<summary>Coded PoC</summary>
<br>

For this test, there are two test functions. The first test displays the problem, the second test displays the solution.

Add the following to `test/Minter.t.sol` and run: `forge test --mt testPauseRewardsLost -vv`

```javascript
    function testPauseRewardsLost() public {
        initializeVotingEscrow();

        FLOW.approve(address(router), TOKEN_1);
        FRAX.approve(address(router), TOKEN_1);
        router.addLiquidity(address(FRAX), address(FLOW), false, TOKEN_1, TOKEN_1, 0, 0, address(owner), block.timestamp);
        address pair1 = router.pairFor(address(FRAX), address(FLOW), false);
        address pair2 = router.pairFor(address(DAI), address(FLOW), false);

        voter.createGauge(pair2, 0);
        address gauge = voter.gauges(pair2);
      
        address[] memory pools = new address[](2);
        pools[0] = pair1;
        pools[1] = pair2;
        uint256[] memory weights = new uint256[](2);
        weights[0] = 9899;
        weights[1] = 101;

        _elapseOneWeek();

        voter.vote(1, pools, weights);
        minter.update_period(); // give rewards to Voter contract
        voter.updateGauge(address(gauge)); // update rewards for gauge

        // expect distribution
        assertGt(voter.claimable(address(gauge)), 0); // verify there are claimable rewards
        console.log("Claimable rewards before pausing: ", voter.claimable(address(gauge)));
        console.log("Gauge balance before pause: ", FLOW.balanceOf(gauge));

        _elapseOneWeek();

        voter.pauseGauge(address(gauge));
        vm.warp(block.timestamp + 1 days); // admin decides to unpause next day
        voter.restartGauge(address(gauge));

        assertEq(voter.claimable(address(gauge)), 0); 
        console.log("Claimable rewards after unpausing next day: ", voter.claimable(address(gauge)));
        console.log("Gauge balance after unpause: ", FLOW.balanceOf(gauge));
    }
```

Add the following to `test/Minter.t.sol` and run: `forge test --mt testPauseDistributeRewards -vv`

```javascript
    function testPauseDistributeRewards() public {
        initializeVotingEscrow();

        FLOW.approve(address(router), TOKEN_1);
        FRAX.approve(address(router), TOKEN_1);
        router.addLiquidity(address(FRAX), address(FLOW), false, TOKEN_1, TOKEN_1, 0, 0, address(owner), block.timestamp);
        address pair1 = router.pairFor(address(FRAX), address(FLOW), false);
        address pair2 = router.pairFor(address(DAI), address(FLOW), false);

        voter.createGauge(pair2, 0);
        address gauge = voter.gauges(pair2);

        address[] memory pools = new address[](2);
        pools[0] = pair1;
        pools[1] = pair2;
        uint256[] memory weights = new uint256[](2);
        weights[0] = 9899;
        weights[1] = 101;

        _elapseOneWeek();

        voter.vote(1, pools, weights);
        minter.update_period(); // give rewards to Voter contract
        voter.updateGauge(address(gauge)); // update rewards for gauge

        // expect distribution
        assertGt(voter.claimable(address(gauge)), 0);
        console.log("Claimable rewards before pausing: ", voter.claimable(address(gauge)));
        console.log("Gauge balance before pause: ", FLOW.balanceOf(gauge));

        _elapseOneWeek();

        voter.distribute(gauge); // distribute rewards when pausing
        voter.pauseGauge(address(gauge));
        vm.warp(block.timestamp + 1 days); // admin decides to unpause next day
        voter.restartGauge(address(gauge));

        assertEq(voter.claimable(address(gauge)), 0);
        console.log("Claimable rewards after unpausing next day: ", voter.claimable(address(gauge)));
        console.log("Gauge balance after unpause: ", FLOW.balanceOf(gauge));
    }
```
</details>

### Console Output

This test displays the problem, with all rewards being lost when pausing/unpausing gauges:

```text
Running 1 test for test/Minter.t.sol:MinterTest
[PASS] testPauseRewardsLost() (gas: 64083334)
Logs:
  Claimable rewards before pausing:  20199999999999999122
  Gauge balance before pause:  0
  Claimable rewards after unpausing next day:  0
  Gauge balance after unpause:  0

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 26.14ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

This test displays the solution to distribute current claimable rewards when pausing:

```text
Running 1 test for test/Minter.t.sol:MinterTest
[PASS] testPauseDistributeRewards() (gas: 64446467)
Logs:
  Claimable rewards before pausing:  20199999999999999122
  Gauge balance before pause:  0
  Claimable rewards after unpausing next day:  0
  Gauge balance after unpause:  40399999999999998244

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 10.61ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```


## Impact

Rewards lost for gauges, causing a loss for voters and LPs. In addition, wasted votes for `veNFT` holders since they can only vote once per week, and loss of funds for LPs.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L485-L495

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L513-L534

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L549-L562

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L380-L405

## Tool used

Manual Review

## Recommendation

Consider distributing any claimable rewards to the gauge when pausing:

```diff
    function pauseGauge(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugePauseAllowance(msg.sender, _gauge)
            , "Pause gauge not allowed");
        }
        require(isAlive[_gauge], "gauge already dead");
        isAlive[_gauge] = false;
-       claimable[_gauge] = 0;
+       distribute(_gauge);
        address _pair = IGauge(_gauge).stake(); // TODO: add test cases
        try IPair(_pair).setHasGauge(false) {} catch {}
        emit GaugePaused(_gauge);
    }
```