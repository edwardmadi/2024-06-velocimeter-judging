Gorgeous Snowy Skunk

High

# Off-by-One error in the `RewardsDistributorV2._checkpoint_total_supply()` leading to incorrect reward calculation.

### Summary

An off-by-one error in the `RewardsDistributorV2._checkpoint_total_supply()` function can result in incorrect reward calculations. This issue arises because the `totalSupply` of the VotingEscrow contract may be cached prematurely in the `ve_supply[]`.

When `_checkpoint_total_supply()` is invoked exactly at the epoch boundary (i.e., `block.timestamp == block.timestamp / WEEK * WEEK`), it causes `totalSupply` to be cached even though it can still be updated in the VotingEscrow with the current `block.timestamp`.

### Vulnerability Details

The `ve_supply[]` should not be stored if values in the VotingEscrow can still be updated at the current `block.timestamp`, especially when a new block is produced at an epoch boundary (`block.timestamp` equals `block.timestamp / WEEK * WEEK`).

This issue arises because there can be multiple epochs in the VotingEscrow with the same timestamp. When calling `_find_timestamp_epoch()`, it is crucial to ensure that the checked timestamp is in the past.

```solidity
function _checkpoint_total_supply() internal {
    address ve = voting_escrow;
    uint t = time_cursor;
    uint rounded_timestamp = block.timestamp / WEEK * WEEK;
    IVotingEscrow(ve).checkpoint();

    for (uint i = 0; i < 20; i++) {
        if (t > rounded_timestamp) {
            break;
        } else {
            uint epoch = _find_timestamp_epoch(ve, t); // <== there can be multiple epochs with the same timestamp. If we update prematurely, we will have the wrong value in case t == block.timestamp.
            ...
```

#### Preconditions:

1. A block is produced with `block.timestamp == block.timestamp / WEEK * WEEK` (every Thursday at 00:00:00).
2. `ve_supply[]` for `t == block.timestamp == block.timestamp / WEEK * WEEK` has already been stored in this block.

Such an action, whether intentional or not, can lead to invalid rewards calculations:

- If the `totalSupply` of the VotingEscrow was reduced in that block, the `ve_supply[]` used in calculations will be larger than the `totalSupply` in the VotingEscrow, diluting the rewards for all users in the current epoch.
- In case of intentional exploitation, an attacker could manipulate `ve_supply[]` to inflate the rewards. By depositing a large amount after the `ve_supply[]` update, the attacker can multiply the rewards for the current epoch, potentially stealing all funds in the RewardsDistributorV2 contract.

### Impact

The incorrect reward calculation can result in diluted rewards for users or, in the worst case, the theft of all funds from the RewardsDistributorV2 contract.

### Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/RewardsDistributorV2.sol#L165
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/RewardsDistributorV2.sol#L152
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/RewardsDistributorV2.sol#L158

### Tool used

Manual Review

### Recommendation

When looking for the proper epoch, use a timestamp that is 1 second before `t`:

```diff
function _checkpoint_total_supply() internal {
    address ve = voting_escrow;
    uint t = time_cursor;
    uint rounded_timestamp = block.timestamp / WEEK * WEEK;
    IVotingEscrow(ve).checkpoint();

    for (uint i = 0; i < 20; i++) {
        if (t > rounded_timestamp) {
            break;
        } else {
-           uint epoch = _find_timestamp_epoch(ve, t);
+           uint epoch = _find_timestamp_epoch(ve, t - 1); // -1 sec to get the correct end of epoch
            IVotingEscrow.Point memory pt = IVotingEscrow(ve).point_history(epoch);
            int128 dt = 0;
            if (t > pt.ts) {
                dt = int128(int256(t - pt.ts));
            }
            ve_supply[t] = Math.max(uint(int256(pt.bias - pt.slope * dt)), 0);
        }
        t += WEEK;
    }
    time_cursor = t;
}
```