Main Golden Griffin

Medium

# Vulnerability in `OptionTokenV4::exerciseLp` Function Enabling Malicious Lock Manipulation

## Summary
[OptionTokenV4::exerciseLp](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/OptionTokenV4.sol#L305) function is for exercising options tokens to create LP and stake in gauges with lock.

However attackers can maliciously increase the lock duration of a victim's LP position. This prevents the victim from being able to withdraw their locked balance for the extended lock period.

## Vulnerability Detail

The [GaugeV4::depositWithLock](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L443) function is designed to be callable only by privileged accounts, as it has the ability to increase the lock duration of user deposits.

The access control check is done at [L444](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L444)

```solidity

File: GaugeV4.sol
443: function depositWithLock(address account, uint256 amount, uint256 _lockDuration) external lock {
444:         require(msg.sender == account || isOToken[msg.sender],"Not allowed to deposit with lock");
445:         _deposit(account, amount, 0);
446: 
447:         if(block.timestamp >= lockEnd[account]) { // if the current lock is expired relased the tokens from that lock before loking again
448:             delete lockEnd[account];
449:             delete balanceWithLock[account];
450:         }
451: 
452:         balanceWithLock[account] += amount;
453:         uint256 currentLockEnd = lockEnd[account];
454:         uint256 newLockEnd = block.timestamp + _lockDuration ;
455:         if (currentLockEnd > newLockEnd) {
456:             revert("The current lock end > new lock end");
457:         } 
458:         lockEnd[account] = newLockEnd;
459:     }

```

If an attacker calls the [OptionTokenV4::exerciseLp](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/OptionTokenV4.sol#L305) function and sets the `_recipient` parameter to the victim's address, they can maliciously increase the victim's lock duration. The attacker can achieve this by passing a dust amount

```solidity
File: OptionTokenV4.sol
305: function exerciseLp(
306:         uint256 _amount,
307:         uint256 _maxPaymentAmount,
308:         address _recipient,
309:         uint256 _discount,
310:         uint256 _deadline
311:     ) external returns (uint256, uint256) {
312:         if (block.timestamp > _deadline) revert OptionToken_PastDeadline();
313:         return _exerciseLp(_amount, _maxPaymentAmount, _recipient, _discount);
314:     }


OptionTokenV4.sol
652: function _exerciseLp(
653:         uint256 _amount,   // the oTOKEN amount the user wants to redeem with
654:         uint256 _maxPaymentAmount, // the 
655:         address _recipient,
656:         uint256 _discount
657:     ) internal returns (uint256 paymentAmount, uint256 lpAmount) {
            [...]         

700:         IGaugeV4(_gauge).depositWithLock( // @audit this will increase the lock duration of the _recipient
701:             _recipient,
702:             lpAmount,
703:             getLockDurationForLpDiscount(_discount)
704:         );
705: 
706:         // notify gauge reward with payment token
707:         _transferRewardToGauge();
708: 
709:         emit ExerciseLp(
710:             msg.sender,
711:             _recipient,
712:             _amount,
713:             paymentAmount,
714:             lpAmount
715:         );
716:     }

```



## Impact

This vulnerability enables attackers to arbitrarily extend the lock period for a victim's LP position, preventing the victim from withdrawing their staked funds for the extended duration. This undermines the intended purpose of the protocol's locking mechanism and exposes users to potential lock manipulation attacks.

## Code Snippet


https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/OptionTokenV4.sol#L305

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L443



## Tool used

Manual Review

## Recommendation

It is recommended to add check in the `OptionTokenV4::exerciseLp` as follows:

```diff

function exerciseLp(
        uint256 _amount,
        uint256 _maxPaymentAmount,
        address _recipient,
        uint256 _discount,
        uint256 _deadline
    ) external returns (uint256, uint256) {
        if (block.timestamp > _deadline) revert OptionToken_PastDeadline();
+       require(msg.sender == _recipient, "Not allowed");        
        return _exerciseLp(_amount, _maxPaymentAmount, _recipient, _discount);
    }

```