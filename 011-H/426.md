Dizzy Fuzzy Nuthatch

High

# Users funds will get stuck, if they have voted for a gauge that got killed after they voted

## Summary

If a user has voted for a gauge and that gauge got killed before the user got a chance to reset his vote, he will not be able to vote/poke/reset. This happens, because the user is trying to call `external_bribes[gauges[pool]]`, but after that gauge got killed this address gets deleted.

This issue leads to other issues in the `VotingEscrow`, since some actions  require that the NFT, should not have voted.

## Vulnerability Detail

In the `Voter` contract users vote in order to receive rewards from gauges and bribes. Any user is able to vote by calling `Voter::vote` function, which calls the internal function `_vote` which handles the voting.

```solidity
    function vote(
        uint tokenId,
        address[] calldata _poolVote,
        uint256[] calldata _weights
    ) external onlyNewEpoch(tokenId) {
        require(IVotingEscrow(_ve).isApprovedOrOwner(msg.sender, tokenId));
        require(_poolVote.length == _weights.length);
        lastVoted[tokenId] = block.timestamp;
        _vote(tokenId, _poolVote, _weights);
    }
```

Note that the function has a modifier, which checks if the user has already voted for the current epoch.

```solidity
    function _vote(
        uint _tokenId,
        address[] memory _poolVote,
        uint256[] memory _weights
    ) internal {
        _reset(_tokenId);
        uint _poolCnt = _poolVote.length;
        uint256 _weight = IVotingEscrow(_ve).balanceOfNFT(_tokenId);
        uint256 _totalVoteWeight = 0;
        uint256 _totalWeight = 0;
        uint256 _usedWeight = 0;

        for (uint i = 0; i < _poolCnt; i++) {
            _totalVoteWeight += _weights[i];
        }

        for (uint i = 0; i < _poolCnt; i++) {
            address _pool = _poolVote[i];
            address _gauge = gauges[_pool];

            if (isGauge[_gauge]) {
                require(isAlive[_gauge], "gauge already dead");
                uint256 _poolWeight = (_weights[i] * _weight) /
                    _totalVoteWeight;
                require(votes[_tokenId][_pool] == 0);
                require(_poolWeight != 0);
                _updateFor(_gauge);

                poolVote[_tokenId].push(_pool);

                weights[_pool] += _poolWeight;
                votes[_tokenId][_pool] += _poolWeight;
                IBribe(external_bribes[_gauge])._deposit(
                    uint256(_poolWeight),
                    _tokenId
                );
                _usedWeight += _poolWeight;
                _totalWeight += _poolWeight;
                emit Voted(msg.sender, _tokenId, _poolWeight);
            }
        }
        if (_usedWeight > 0) IVotingEscrow(_ve).voting(_tokenId);
        totalWeight += uint256(_totalWeight);
        usedWeights[_tokenId] = uint256(_usedWeight);
    }
```

As we can see, right in the beggining the function calls `Voter::_reset` to clear the previous applied votes from the tokenId, and then handle the voting logic with clean state.

Also right the end, we are calling [VotingEscrow::voting](https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/VotingEscrow.sol#L1175-L1178) function, which sets `voted[tokenId] = true`.

```solidity
function _reset(uint _tokenId) internal {
        address[] storage _poolVote = poolVote[_tokenId];
        uint _poolVoteCnt = _poolVote.length;
        uint256 _totalWeight = 0;

        for (uint i = 0; i < _poolVoteCnt; i++) {
            address _pool = _poolVote[i];
            uint256 _votes = votes[_tokenId][_pool];

            if (_votes != 0) {
                _updateFor(gauges[_pool]);
                weights[_pool] -= _votes;
                votes[_tokenId][_pool] -= _votes;
                if (_votes > 0) {
                    // @review - > After killing a gauge this will be address(0)
                    IBribe(external_bribes[gauges[_pool]])._withdraw(
                        uint256(_votes),
                        _tokenId
                    );
                    _totalWeight += _votes;
                } else {
                    _totalWeight -= _votes;
                }
                emit Abstained(_tokenId, _votes);
            }
        }
        totalWeight -= uint256(_totalWeight);
        usedWeights[_tokenId] = 0;
        delete poolVote[_tokenId];
    }
```

The action to call for killing a gauge is `Voter::killGaugeTotally`:

```solidity
function killGaugeTotally(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugeKillAllowance(
                    msg.sender,
                    _gauge
                ),
                "Restart gauge not allowed"
            );
        }

        require(isAlive[_gauge], "gauge already dead");

        address _pool = poolForGauge[_gauge];

        delete isAlive[_gauge];
        delete external_bribes[_gauge]; //@review - We deleting the bribes for that particular gauge
        delete poolForGauge[_gauge];
        delete isGauge[_gauge];
        delete claimable[_gauge];
        delete supplyIndex[_gauge];
        delete gauges[_pool];
        try IPair(_pool).setHasGauge(false) {} catch {}

        killedGauges.push(_gauge);

        emit GaugeKilledTotally(_gauge);
    }
```
However, as we can see from above we are wiping out the `external_bribes` for that gauge when killing it. Now since `poolVotes[tokenId]` is not cleared, if the user tries to vote/poke or reset his call will revert due to trying to call a function on address(0).


## Proof Of Concept

1. User votes for gauge1 at epoch 2.
2. The user stop interacting with the contract until epoch 3.
3. On epoch 2 however gauge1 gets killed.
4. The user wants to vote for gauge2, but since gauge1 got killed every voting action will revert since all of them internally call `_reset`
5. The user tries to withdraw his nft, by calling `VotingEscrow::withdraw`, but due to [this check](https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/VotingEscrow.sol#L957), the  function will always revert, unless the user removes his vote.
6. User's funds got locked with no way of withdrawing them.

Paste test function inside of `Voter.t.sol`:

```solidity
     function testUsersFundsGetsLockedIfABribeHeVotedForPreviouslyGetsKilled() public {
        createLock();
        vm.warp(block.timestamp + 1 weeks);

        voter.createGauge(address(pair), 0);
        address gaugeAddress = voter.gauges(address(pair));

        address[] memory rewards = new address[](2);
        rewards[0] = address(USDC);
        rewards[1] = address(FRAX);
        ExternalBribe newExternalBribe = new ExternalBribe(
            address(voter),
            rewards
        );
        vm.expectEmit(true, true, false, true);
        emit ExternalBribeSet(address(this), gaugeAddress, address(newExternalBribe));
        voter.setExternalBribeFor(gaugeAddress, address(newExternalBribe));

        Router.route[] memory routes = new Router.route[](1);
        routes[0] = Router.route(address(USDC), address(FRAX), true);

        assertEq(
            router.getAmountsOut(USDC_1, routes)[1],
            pair.getAmountOut(USDC_1, address(USDC))
        );

        uint256[] memory assertedOutput = router.getAmountsOut(USDC_1, routes);
        USDC.approve(address(router), USDC_1);
        router.swapExactTokensForTokens(
            USDC_1,
            assertedOutput[1],
            routes,
            address(owner),
            block.timestamp
        );

        address[] memory pools = new address[](1);
        pools[0] = address(pair);
        uint256[] memory weights = new uint256[](1);
        weights[0] = 5000;


        //@note - User votes in epoch 1
        voter.vote(1, pools, weights);


        vm.warp(block.timestamp + 3 days);

        //@note - 3 days after the user voted the gauge got killed
        voter.killGaugeTotally(voter.gauges(address(pair)));

        vm.warp(block.timestamp + 4 days);

        //@note - Now that the epoch is over the user decides to vote again, but since the external_bribe is wiped it will revert
        vm.expectRevert();
        voter.reset(1);

        //@note - Same here
        vm.expectRevert();
        voter.vote(1, pools, weights);

        //@note - Same here
        vm.expectRevert();
        voter.poke(1);


        //@note - The user tries to withdraw the nft, since it is malfucitoning, but is unable to since it has already voted for a gauge
        vm.expectRevert("attached");
        escrow.withdraw(1);

        //@note - For the same reason he is not able to split it
                vm.expectRevert("attached");
        escrow.split(1, 2e17);
        
        //@note - For the same reason, he is not able to merge it 
                vm.expectRevert("attached");
        escrow.transferFrom(address(this), address(312443), 1);

        //Complete loss of funds for the user
    }
```

## Impact

User will not be able to remove his vote or vote for a different gauge.

Due to the user not being able to remove his vote, this leads to many other issues in `VotingEscrow`.

The actions `VotingEscrow::transferFrom`, `VotingEscrow::withdraw`, `VotingEscrow::split` share the same check to ensure that the NFT is not attached or has not voted.

```solidity
 require(attachments[_tokenId] == 0 && !voted[_tokenId], "attached");
```

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/VotingEscrow.sol#L321
https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/VotingEscrow.sol#L957
https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/VotingEscrow.sol#L1220
https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/Voter.sol#L201-L205
https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/Voter.sol#L207-L232
https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/Voter.sol#L234-L247
https://github.com/sherlock-audit/2024-06-velocimeter/tree/main/v4-contracts/contracts/Voter.sol#L249-L285

## Recommendation

In order to avoid locking users out of voting, consider adding logic to avoid reverts during `_reset` calls to tokens which have outstanding votes to totally killed gauges.
