Vast Vermilion Aardvark

Medium

# Malicious users could grief other users by creating dust locks in vote escrow

## Summary
Checkpoints, in vote escrow, is a data structure to keep track of owned nfts for each account. Whenever an nft is created, transfer to, or burn from the account, new checkpoint, which has `timestamp` (when action happens) and `tokenIds` array (nfts owned - new version after update), will be created.

Since that anyone can create lock for the other in create_lock_for() and that there is no minimum amount requirement to do that, malicious users can just create dust locks for normal users. Doing so will fill up `tokenIds` array in new checkpoints created, blocking users from creating new locks,
## Vulnerability Detail
checkpoints is a mapping to track each account's owned nfts
```solidity
/// @notice A record of delegated token checkpoints for each account, by index
    mapping(address => mapping(uint32 => Checkpoint)) public checkpoints;
```
An user can only have up to `MAX_DELEGATES` (1024) nfts in their account. If the limit is reached, moving any nft to their account will cause a revert.
```solidity
 uint public constant MAX_DELEGATES = 1024;

if (dstRep != address(0)) {
                uint32 dstRepNum = numCheckpoints[dstRep];
        >>>        uint[] storage dstRepOld = dstRepNum > 0
                    ? checkpoints[dstRep][dstRepNum - 1].tokenIds
                    : checkpoints[dstRep][0].tokenIds;
                uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
                uint[] storage dstRepNew = checkpoints[dstRep][
                    nextDstRepNum
                ].tokenIds;
                // All the same plus _tokenId
                require(
               >>>     dstRepOld.length + 1 <= MAX_DELEGATES,
                    "dstRep would have too many tokenIds"
                );
                for (uint i = 0; i < dstRepOld.length; i++) {
                    uint tId = dstRepOld[i];
                    dstRepNew.push(tId);
                }
                dstRepNew.push(_tokenId);

                numCheckpoints[dstRep] = dstRepNum + 1;
            }
```


The problem is that, any other user can create dust locks for this user via creat_lock_for(), filling up tokenIds array in checkpoint.

```solidity
function create_lock_for(uint _value, uint _lock_duration, address _to) external nonreentrant returns (uint) {
        return _create_lock(_value, _lock_duration, _to);
    }
function _create_lock(uint _value, uint _lock_duration, address _to) internal returns (uint) {
        uint unlock_time = (block.timestamp + _lock_duration) / WEEK * WEEK; // Locktime is rounded down to weeks

        require(_value > 0); // dev: need non-zero value
        require(unlock_time > block.timestamp, 'Can only lock until time in the future');
        require(unlock_time <= block.timestamp + MAXTIME, 'Voting lock can be 52 weeks max');

        ++tokenId;
        uint _tokenId = tokenId;
        _mint(_to, _tokenId);

        _deposit_for(_tokenId, _value, unlock_time, locked[_tokenId], DepositType.CREATE_LOCK_TYPE);
        return _tokenId;
    }
```

## POC
Please add this test into VotingEscrow.t.sol

```solidity
    function testCreateDustFillupNftArray() public {
        flowDaiPair.approve(address(escrow), TOKEN_1);
        uint256 lockDuration = 7 * 24 * 3600; // 1 week
        uint256 tokenId = escrow.create_lock(40000, lockDuration );
        
        for (uint256 i = 0; i < 1024 ; i++) {            
            escrow.create_lock_for(1, lockDuration, address(owner2));
        }
        //vm.stopPrank();
        vm.startPrank(address(owner2));
        flowDaiPair.approve(address(escrow), TOKEN_1);
        vm.expectRevert();
        escrow.create_lock(1, lockDuration);
    }
```

## Impact
Grief users. Innocent users who have their `tokenIds` array filled up will be unable to create new lock unless they merge/transfer dust locks to somewhere else. Even when they do so, attackers can just create new locks to fill up again.
The viable scenario for this attack is that attackers want to delay/block some users who are competing to them in voting power. 
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1269
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1266
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1388-L1409
## Tool used

Manual Review

## Recommendation
Consider adding restriction for creating locks for other users. For example, adding minimum amount required for a new lock to be created.