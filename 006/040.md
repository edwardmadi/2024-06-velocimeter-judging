Active Lace Hippo

Medium

# Accounts Can Receive `Gauge` Incentives Without Locking Up `VestingEscrow` Tokens

## Summary

The [`GaugeV4`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol) contract is designed to provide token rewards to accounts that lock up their tokens in the [`VotingEscrow`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol) for a minimum time period. However, it erroneously treats unallocated [`tokenId`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L469C58-L469C65)s as valid escrow tokens.

This allows users to subvert the risk of locking up their tokens for a tangible delay whilst still receiving incentivization bonuses.

## Vulnerability Detail

Notice that when making a deposit to the [`GaugeV4`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol), callers may either:

1. Specify a non-zero[`tokenId`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L469C58-L469C65) to attach the escrow against.
2. Alternatively, supply a value of zero to lock against the [`account`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L469C58-L469C65)'s most recently attached token as defined in the [`tokenIds`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L41C37-L41C45) mapping.

These cases are described below:

```solidity
function deposit(uint amount, uint tokenId) public lock { /// @audit can_specify_tokenid_0
    _deposit(msg.sender, amount, tokenId);
}

function depositFor(address account, uint amount) public lock {
    _deposit(account, amount, 0); /// @audit for_this_case_defaults_to_tokenid_0
}

function _deposit(address account, uint amount, uint tokenId) private {
    require(amount > 0);
    _updateRewardForAllTokens();

    _safeTransferFrom(stake, msg.sender, address(this), amount);
    totalSupply += amount;
    balanceOf[account] += amount;

    if (tokenId > 0) /* @audit case_1 */ {
        require(IVotingEscrow(_ve).ownerOf(tokenId) == account);
        if (tokenIds[account] == 0) {
@>          tokenIds[account] = tokenId; /// @audit valid_tokens_get_latched
@>          IVoter(voter).attachTokenToGauge(tokenId, account); /// @audit valid_tokens_get_attached
        }
        require(tokenIds[account] == tokenId);
    } else /* @audit case_2 */ {
@>      tokenId = tokenIds[account]; /// @audit else_specify_preexisting_tokenId
    }

    uint _derivedBalance = derivedBalances[account];
    derivedSupply -= _derivedBalance;
    _derivedBalance = derivedBalance(account);
    derivedBalances[account] = _derivedBalance;
    derivedSupply += _derivedBalance;

    _writeCheckpoint(account, _derivedBalance);
    _writeSupplyCheckpoint();

    IVoter(voter).emitDeposit(tokenId, account, amount);
    emit Deposit(account, tokenId, amount);
}
```

For the case where [`tokenId`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L469C58-L469C65) is zero and a pre-existing attached token does not exist (i.e. a new depositor), a call to deposit will consequently make a **deposit to an imaginary token**, since the existence of the dynamically selected token is not verified.

> [!NOTE]
> It should noted that it is not possible for a [`VotingEscrow`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol) to compute a zero `tokenId`, as it is explicitly pre-incremented:
>
> ```solidity
> ++tokenId;
> uint _tokenId = tokenId;
> _mint(_to, _tokenId);
> ```
>
> https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L835C5-L852C6

Consequently, accounts may stake and receive rewards whilst circumventing the lockup:

### KillGauges.t.sol

```solidity
/// @notice Accounts can deposit to non-existent escrow tokens.
function testSherlockDepositToImaginaryToken() external {
    address token = address(pair);
    address external_bribe = voter.external_bribes(address(gauge));

    MockERC20 baseToken = MockERC20(VotingEscrow(voter._ve()).baseToken());

    MockERC20 token0 = MockERC20(Pair(token).token0());
    MockERC20 token1 = MockERC20(Pair(token).token1());

    assertTrue(external_bribe != address(0));
    assertTrue(voter.isGauge(address(gauge)));

    vm.prank(minter.team()) /* start_minting */;
    minter.startActivePeriod();

    address c0ffee = address(0xc0ffee);
    uint256 amountToMint = 1 ether;
    Pair pair = Pair(token);
    token0.mint(address(pair), amountToMint);
    token1.mint(address(pair), amountToMint);
    pair.mint(c0ffee);

    vm.startPrank(c0ffee);
        pair.approve(address(gauge), type(uint256).max);
        gauge.deposit(pair.balanceOf(c0ffee), 0 /* null_tokenid */);
        assertEq(escrow.ownerOf(0), address(0)) /* not_a_real_token */;
    vm.stopPrank();
}
```

## Impact

Account holders may accrue rewards even though they do not respect (3,3), thereby reducing the locking incentive.

## Code Snippet

```solidity
if (tokenId > 0) {
    require(IVotingEscrow(_ve).ownerOf(tokenId) == account);
    if (tokenIds[account] == 0) {
        tokenIds[account] = tokenId;
        IVoter(voter).attachTokenToGauge(tokenId, account);
    }
    require(tokenIds[account] == tokenId);
} else {
    tokenId = tokenIds[account];
}
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L477C9-L486C10

## Tool used

Manual Review

## Recommendation

When dynamically determining a pre-deposited token reference via the use of a zero `tokenId`, validate the existence of the evaluated `tokenId` before attempting to interact with it:

###  GaugeV4.sol:

```diff
if (tokenId > 0) {
    require(IVotingEscrow(_ve).ownerOf(tokenId) == account);
    if (tokenIds[account] == 0) {
        tokenIds[account] = tokenId;
        IVoter(voter).attachTokenToGauge(tokenId, account);
    }
    require(tokenIds[account] == tokenId);
} else {
    tokenId = tokenIds[account];
+   require(IVotingEscrow(_ve).ownerOf(tokenId) == account);
}
```

```diff
if (tokenId > 0) {
    require(tokenId == tokenIds[msg.sender]);
    tokenIds[msg.sender] = 0;
    IVoter(voter).detachTokenFromGauge(tokenId, msg.sender);
} else {
    tokenId = tokenIds[msg.sender];
+   require(IVotingEscrow(_ve).ownerOf(tokenId) == msg.sender);
}
```