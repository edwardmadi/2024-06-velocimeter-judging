Salty Azure Eagle

High

# User loses unclaimed rewards when merging tokens.

## Summary

When merging tokens, any unclaimed rewards from the token that is burned are lost.

## Vulnerability Detail

When merging tokens, only the locked lp balance is merged, not the accrued rewards from exercised oFlow and weekly flow emissions. As a result hereof, any unclaimed rewards from the token passed as `_from` to `VotingEscrow.merge()` are lost.

## Impact

User will permanently lose any unclaimed rewards when merging tokens.

## Proof of Concept

Copy this to a new file anywhere within `v4-contracts/test` and run it with `forge test --match-contract "LoseRewardsOnMerge"`

<details>
<summary>Coded PoC</summary>

```solidity
pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import "solmate/tokens/WETH.sol";
import "contracts/Flow.sol";
import "contracts/factories/PairFactory.sol";
import "contracts/factories/GaugeFactoryV4.sol";
import "contracts/factories/ProxyGaugeFactory.sol";
import "contracts/factories/BribeFactory.sol";
import "contracts/Pair.sol";
import "contracts/Router.sol";
import "contracts/Voter.sol";
import "contracts/VotingEscrow.sol";
import "contracts/Minter.sol";
import "contracts/RewardsDistributorV2.sol";

contract LoseRewardsOnMergeTest is Test {
    address DEPLOYER = address(uint160(uint(keccak256("DEPLOYER"))));
    address ALICE = address(uint160(uint(keccak256("ALICE"))));
    address BOB = address(uint160(uint(keccak256("BOB"))));

    WETH weth;
    Flow flow;

    VotingEscrow votingEscrow;
    Voter voter;
    Router router;
    RewardsDistributorV2 rewardsDistributorWETH;
    RewardsDistributorV2 rewardsDistributorFlow;

    Pair flowWethPair;

    function setUp() public {
        vm.deal(DEPLOYER, 100 ether);
        vm.deal(ALICE, 100 ether);
        vm.deal(BOB, 100 ether);

        weth = new WETH();

        vm.startPrank(DEPLOYER);

        flow = new Flow(DEPLOYER, 1e21);

        PairFactory pairFactory = new PairFactory();
        GaugeFactoryV4 gaugeFactory = new GaugeFactoryV4();
        ProxyGaugeFactory proxyFactory = new ProxyGaugeFactory(address(flow));
        router = new Router(address(pairFactory), address(weth));

        _addLiquidityFlowWeth(2e18, DEPLOYER);

        flowWethPair = Pair(
            pairFactory.getPair(address(flow), address(weth), false)
        );

        votingEscrow = new VotingEscrow(
            address(flow),
            address(flowWethPair),
            address(0),
            DEPLOYER
        );

        voter = new Voter(
            address(votingEscrow),
            address(pairFactory),
            address(gaugeFactory),
            address(new BribeFactory()),
            address(0)
        );

        votingEscrow.setVoter(address(voter));

        voter.addFactory(address(pairFactory), address(proxyFactory));

        rewardsDistributorWETH = new RewardsDistributorV2(
            address(votingEscrow),
            address(weth)
        );

        rewardsDistributorFlow = new RewardsDistributorV2(
            address(votingEscrow),
            address(flow)
        );

        Minter minter = new Minter(
            address(voter),
            address(votingEscrow),
            address(rewardsDistributorFlow)
        );
        minter.addRewardsDistributor(address(rewardsDistributorWETH));

        rewardsDistributorWETH.setDepositor(address(minter));
        rewardsDistributorFlow.setDepositor(address(minter));

        minter.startActivePeriod();

        flow.setMinter(address(minter));

        pairFactory.setVoter(address(voter));
        flowWethPair.setVoter();

        address[] memory whitelistedTokens = new address[](2);

        whitelistedTokens[0] = address(flow);
        whitelistedTokens[1] = address(weth);

        voter.initialize(whitelistedTokens, address(minter));

        proxyFactory.deployGauge(
            address(rewardsDistributorFlow),
            address(flowWethPair),
            "veNFT"
        );

        voter.createGauge(address(flowWethPair), 1);

        // we need some votes, otherwise voter.distribute() -> ... -> voter.notifyRewardAmount() will revert due to divison by zero
        address[] memory pools = new address[](1);
        pools[0] = address(flowWethPair);
        uint[] memory weights = new uint[](1);
        weights[0] = 1;
        _createLockMaxDuration(1e18);
        vm.warp(block.timestamp + 1 weeks);
        voter.vote(1, pools, weights);

        weth.deposit{value: 10e18}();

        flow.transfer(ALICE, 1e20);
        flow.transfer(BOB, 1e20);

        vm.stopPrank();
    }

    function _addLiquidityFlowWeth(uint amount, address to) internal {
        flow.approve(address(router), amount);
        router.addLiquidityETH{value: amount}(
            address(flow),
            false,
            amount,
            0,
            0,
            to,
            block.timestamp
        );
    }

    function _createLockMaxDuration(uint amount) internal {
        flowWethPair.approve(address(votingEscrow), amount);
        votingEscrow.create_lock(amount, 52 weeks);
    }

    function testLoseRewardsOnMerge() public {
        // Alice and Bob both create 2 tokens with the same amounts and lock duration.
        vm.startPrank(ALICE);
        _addLiquidityFlowWeth(1e18, ALICE);
        _createLockMaxDuration(5e17);
        _createLockMaxDuration(5e17);
        vm.stopPrank();

        vm.startPrank(BOB);
        _addLiquidityFlowWeth(1e18, BOB);
        _createLockMaxDuration(5e17);
        _createLockMaxDuration(5e17);
        vm.stopPrank();

        // Alice owns tokens 2 and 3
        assertEq(votingEscrow.ownerOf(2), ALICE);
        assertEq(votingEscrow.ownerOf(3), ALICE);
        // Bob owns tokens 4 and 5.
        assertEq(votingEscrow.ownerOf(4), BOB);
        assertEq(votingEscrow.ownerOf(5), BOB);
        // All 4 tokens have the same balance.
        assertEq(votingEscrow.balanceOfNFT(2), votingEscrow.balanceOfNFT(3));
        assertEq(votingEscrow.balanceOfNFT(3), votingEscrow.balanceOfNFT(4));
        assertEq(votingEscrow.balanceOfNFT(4), votingEscrow.balanceOfNFT(5));

        vm.warp(block.timestamp + 1 weeks);
        voter.distribute();

        vm.startPrank(DEPLOYER);
        flow.approve(address(rewardsDistributorFlow), 1e18);
        // simulate rewards from emissions
        rewardsDistributorFlow.notifyRewardAmount(1e18);
        weth.approve(address(rewardsDistributorWETH), 1e18);
        // simulate rewards from oFlow exercise revenue
        rewardsDistributorWETH.notifyRewardAmount(1e18);
        vm.stopPrank();

        vm.warp(block.timestamp + 1 weeks);
        voter.distribute();

        uint aliceWethBefore = weth.balanceOf(ALICE);
        uint aliceFlowBefore = flow.balanceOf(ALICE);
        uint bobWethBefore = weth.balanceOf(BOB);
        uint bobFlowBefore = flow.balanceOf(BOB);

        vm.startPrank(ALICE);
        rewardsDistributorWETH.claim(2);
        rewardsDistributorFlow.claim(2);
        rewardsDistributorWETH.claim(3);
        rewardsDistributorFlow.claim(3);
        vm.stopPrank();

        vm.startPrank(BOB);
        // Bob merges token 4 into token 5 and can not claim rewards on token 4
        votingEscrow.merge(4, 5);
        vm.expectRevert();
        rewardsDistributorWETH.claim(4);
        vm.expectRevert();
        rewardsDistributorFlow.claim(4);
        rewardsDistributorWETH.claim(5);
        rewardsDistributorFlow.claim(5);
        vm.stopPrank();

        uint aliceWethAfter = weth.balanceOf(ALICE);
        uint aliceWethGained = aliceWethAfter - aliceWethBefore;
        uint aliceFlowAfter = flow.balanceOf(ALICE);
        uint aliceFlowGained = aliceFlowAfter - aliceFlowBefore;
        uint bobWethAfter = weth.balanceOf(BOB);
        uint bobWethGained = bobWethAfter - bobWethBefore;
        uint bobFlowAfter = flow.balanceOf(BOB);
        uint bobFlowGained = bobFlowAfter - bobFlowBefore;

        // Bob receives only half the rewards of Alice, as
        // the rewards from the token that was merged are lost
        assertEq(aliceWethGained / 2, bobWethGained);
        assertEq(aliceFlowGained / 2, bobFlowGained);
    }
}

```
</details>

## Code Snippet

`VotingEscrow.merge()` calls `VotingEscrow._burn`.

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1195-L1210

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L537-L549

After the token has been burned, `require(IVotingEscrow(voting_escrow).isApprovedOrOwner(msg.sender, _tokenId))` in `RewardsDistributorV2.claim` will fail, and as the accounting for the accrued rewards has not been merged, the rewards cannot be claimed.

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/RewardsDistributorV2.sol#L282-L294

## Tool used

Manual Review

## Recommendation

Either

1. Merge accounting of accrued rewards when merging tokens.
or
2. Claim rewards of `_from` token when merging/burning. This is probably the easiest and could be done by adding a `RewardsDistributorV2.claim_for(uint _tokenId)` that has `require(msg.sender == voting_escrow)`, and have `VotingEscrow._burn` call this for every `RewardsDistributorV2` in `Minter._rewards_distributors`.