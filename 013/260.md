Striped Concrete Hare

Medium

# Improperly created `DOMAIN_TYPEHASH` will cause issues when delegating by signature

## Summary

The `DOMAIN_TYPEHASH` is improperly encoded, as a result the created `domainSeperator` will not be EIP-712 complaint, which will lead to issues when attempting to delegate using signature.
 
## Vulnerability Detail
Delegations can be performed with signatures using the `delegateBySig` function. This is done through a signature which presumably will be generated by common generating tools. The declared `DOMAIN_TYPEHASH` however is not properly constructed, and doesn't match with the expected hashing of the singature and as result, will cause the created `domainSeparator` to be incorrect. 
The `DOMAIN_TYPEHASH` is declared like this - with the name, chainId and verifying contract.

```solidity
    bytes32 public constant DOMAIN_TYPEHASH = keccak256("EIP712Domain(string name,uint256 chainId,address verifyingContract)");
```

However, when creating the `domainSeparator`, the `version` parameter is added, causing the created digest will yield a different signatory when compared with the user generated signatures.

```solidity
    function delegateBySig(
        address delegatee,
        uint nonce,
        uint expiry,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) public {
        bytes32 domainSeparator = keccak256(
            abi.encode(
                DOMAIN_TYPEHASH,
                keccak256(bytes(name)),
                keccak256(bytes(version)),
                block.chainid,
                address(this)
            )
        );
        bytes32 structHash = keccak256(
            abi.encode(DELEGATION_TYPEHASH, delegatee, nonce, expiry)
        );
        bytes32 digest = keccak256(
            abi.encodePacked("\x19\x01", domainSeparator, structHash)
        );
        address signatory = ecrecover(digest, v, r, s);
...
}
```

## Impact
This will cause issues with signing and delegating by signature especially when the generated signature is from a common signature generated tool which is EIP-712 compliant. Therefore users may not be able to use the `delegateBySig` function.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1259

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1515

## Tool used
Manual Code Review

## Recommendation
Introduce the version parameter into the `DOMAIN_TYPEHASH`, like is done [here](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/Pair.sol#L438) when caculating `DOMAIN_SEPARATOR`
```solidity
-    bytes32 public constant DOMAIN_TYPEHASH = keccak256("EIP712Domain(string name,uint256 chainId,address verifyingContract)");
+    bytes32 public constant DOMAIN_TYPEHASH = keccak256("EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)");

```