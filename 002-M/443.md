Fierce Obsidian Locust

High

# In `Pair.sol`, First liquidity provider of a stable pair can DOS the pool

## Summary
In `Pair.sol`, First liquidity provider of a stable pair can DOS the pool

## Vulnerability Detail
Velocimeter's `Pair.sol` have implemented `stable pairs` which is used to denote stable or volatile pair in `Pair.sol`

```solidity
    bool public immutable stable;
```

The invariant k of a stable pool is calculated as follows which can be checked [here](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L403-L413)

```solidity
    function _k(uint x, uint y) internal view returns (uint) {
        if (stable) {
            uint _x = x * 1e18 / decimals0;
            uint _y = y * 1e18 / decimals1;
            uint _a = (_x * _y) / 1e18;
            uint _b = ((_x * _x) / 1e18 + (_y * _y) / 1e18);
@>          return _a * _b / 1e18;  // x3y+y3x >= k
        } else {
            return x * y; // xy >= k
        }
    }
```

The value of `_a = ( x * y ) / 1e18 = 0` due to rounding error when `x * y < 1e18`. The rounding error can lead to the invariant `k` of stable pools equals `zero`, and the trader can steal whatever is left in the pool.

The first liquidity provider can DOS the pair by :

1) mint a small amount of liquidity to the pool,
2) Steal whatever is left in the pool,
3) Repeat Step 1, and Step 2 until the overflow of the total supply.

To prevent the issue of rounding error, the reserve of a pool should never go too small. The mint function which was borrowed from uniswapV2 has a minimum liquidity check of `sqrt(a * b) > MINIMUM_LIQUIDITY`; This, however, isn't safe enough to protect the invariant formula of stable pools. mint() function can be checked [here](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L250-L269)

```solidity
    uint internal constant MINIMUM_LIQUIDITY = 10**3;
    
    . . . some code . . .
    
    function mint(address to) external lock returns (uint liquidity) {
        (uint _reserve0, uint _reserve1) = (reserve0, reserve1);
        uint _balance0 = IERC20(token0).balanceOf(address(this));
        uint _balance1 = IERC20(token1).balanceOf(address(this));
        uint _amount0 = _balance0 - _reserve0;
        uint _amount1 = _balance1 - _reserve1;

        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
            liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
            _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
        } else {
            liquidity = Math.min(_amount0 * _totalSupply / _reserve0, _amount1 * _totalSupply / _reserve1);
        }
        require(liquidity > 0, 'ILM'); // Pair: INSUFFICIENT_LIQUIDITY_MINTED
        _mint(to, liquidity);

        _update(_balance0, _balance1, _reserve0, _reserve1);
        emit Mint(msg.sender, _amount0, _amount1);
    }
```

## Impact
The liquidity provider of a stable pair can DOS the pool by mint a small amount of liquidity to the pool and can steal whatever is left in the pool, Therefore, a high severity issue.

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L250-L269

## Tool used
Manual Review

## Recommendation
Recommend to add two restrictions on the first lp of stable pools:

1) only allows equal amounts of liquidity.
2) invariant _k should be larger than the MINIMUM_K

Please check [this](https://github.com/velodrome-finance/contracts/commit/59f9c135ccf7685af81f021918c879b21c2c5f04) commit by velodrome finance to get the correct mitigation in contract.

For example, Velodrome has mitigated the issue like below and i think Velocimeter would follow the same as its a fork of Velodrome:

```diff
    uint internal constant MINIMUM_LIQUIDITY = 10**3;
+    uint256 internal constant MINIMUM_K = 10**10;

    
    . . . some code . . .
    
    
    
    function mint(address to) external lock returns (uint liquidity) {
        (uint _reserve0, uint _reserve1) = (reserve0, reserve1);
        uint _balance0 = IERC20(token0).balanceOf(address(this));
        uint _balance1 = IERC20(token1).balanceOf(address(this));
        uint _amount0 = _balance0 - _reserve0;
        uint _amount1 = _balance1 - _reserve1;

        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
            liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
            _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
+           if (stable) {
+                require((_amount0 * 1e18) / decimals0 == (_amount1 * 1e18) / decimals1, "Pair: stable deposits must be equal");
+                require(_k(_amount0, _amount1) > MINIMUM_K, "Pair: stable deposits must be above minimum k");
+            }
        } else {
            liquidity = Math.min(_amount0 * _totalSupply / _reserve0, _amount1 * _totalSupply / _reserve1);
        }
        require(liquidity > 0, 'ILM'); // Pair: INSUFFICIENT_LIQUIDITY_MINTED
        _mint(to, liquidity);

        _update(_balance0, _balance1, _reserve0, _reserve1);
        emit Mint(msg.sender, _amount0, _amount1);
    }
```

## References:
This issue is directly referenced from [this](https://solodit.xyz/issues/first-liquidity-provider-of-a-stable-pair-can-dos-the-pool-spearbit-none-velodrome-finance-pdf) issue found in Velodrome audit at spearbit.