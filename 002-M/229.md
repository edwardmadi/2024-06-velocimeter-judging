Atomic Citron Fly

High

# First liquidity provider of a stable pair can DOS the pool

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L250

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Pair.sol#L403

## Summary
The first liquidity provider of a stable pair could DOS the pool

## Vulnerability Detail
The invariant k of a stable pool is calculated as follows

```javascript
function _k(uint x, uint y) internal view returns (uint) {
        if (stable) {
            uint _x = x * 1e18 / decimals0;
            uint _y = y * 1e18 / decimals1;
            uint _a = (_x * _y) / 1e18;
            uint _b = ((_x * _x) / 1e18 + (_y * _y) / 1e18);
            return _a * _b / 1e18;  // x3y+y3x >= k
        } else {
            return x * y; // xy >= k
        }
    }
```

The value of `_a = (_x * _y) / 1e18` = 0 due to rounding error when `_x * _y < 1e18`. The rounding error can lead to
the invariant k of stable pools equals zero, and the trader can steal whatever is left in the pool.

The first liquidity provider can DOS the pair by:

- mint a small amount of liquidity to the pool,
- Steal whatever is left in the pool,
- Repeat step 1, and step 2 until the overflow of the total supply.

To prevent the issue of rounding error, the reserve of a pool should never go too small. The mint function which was
borrowed from uniswapV2 has a minimum liquidity check of sqrt(a \* b) > MINIMUM_LIQUIDITY; This, however,
isn't safe enough to protect the invariant formula of stable pools.

```javascript

    function mint(address to) external lock returns (uint liquidity) {
        (uint _reserve0, uint _reserve1) = (reserve0, reserve1);
        uint _balance0 = IERC20(token0).balanceOf(address(this));
        uint _balance1 = IERC20(token1).balanceOf(address(this));
        uint _amount0 = _balance0 - _reserve0;
        uint _amount1 = _balance1 - _reserve1;

        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
            liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
            _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
        } else {
            liquidity = Math.min(_amount0 * _totalSupply / _reserve0, _amount1 * _totalSupply / _reserve1);
        }
        require(liquidity > 0, 'ILM'); // Pair: INSUFFICIENT_LIQUIDITY_MINTED
        _mint(to, liquidity);

        _update(_balance0, _balance1, _reserve0, _reserve1);
        emit Mint(msg.sender, _amount0, _amount1);
    }
```

## Code Snippet

```javascript
function _k(uint x, uint y) internal view returns (uint) {
        if (stable) {
            uint _x = x * 1e18 / decimals0;
            uint _y = y * 1e18 / decimals1;
            uint _a = (_x * _y) / 1e18;
            uint _b = ((_x * _x) / 1e18 + (_y * _y) / 1e18);
            return _a * _b / 1e18;  // x3y+y3x >= k
        } else {
            return x * y; // xy >= k
        }
    }
```

## Tool used

Manual Review

## Recommendation
Add two restrictions on the first lp of stable pools:

1. only allows equal amounts of liquidity.
2. invariant _k should be larger than the MINIMUM_K

```javascript
    function mint(address to) external lock returns (uint liquidity) {
        (uint _reserve0, uint _reserve1) = (reserve0, reserve1);
        uint _balance0 = IERC20(token0).balanceOf(address(this));
        uint _balance1 = IERC20(token1).balanceOf(address(this));
        uint _amount0 = _balance0 - _reserve0;
        uint _amount1 = _balance1 - _reserve1;

        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
            liquidity = Math.sqrt(_amount0 * _amount1) - MINIMUM_LIQUIDITY;
            _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
+      if (stable) {
+          require(_amount0 * 1e18 / decimals0 == _amount1 * 1e18 / decimals1, "Pair: Stable pair,! must be equal amounts");
+
+          require(_k(_amount0, _amount1) > MINIMUM_K, "Pair: Stable pair must be above minimum,! k");
+
+      }
// ...
    }
    }

```
