Steep Sapphire Gorilla

Medium

# Approved users wouldn't be able to merge tokens

## Summary

approve system in `VotingEscrow` contract not compatible for `merge()` function.

## Vulnerability Detail

if alice approve to bob for merging NFT tokens, bob will always revert in `merge` function.

because the `merge()` function calls `_burn()` and this also calls `approve()` but it will revert because `approve()` function requires msg.sender to be owner.

```solidity
function approve(address _approved, uint _tokenId) public {
        address tokenOwner = idToOwner[_tokenId];
        // Throws if `_tokenId` is not a valid NFT
        require(tokenOwner != address(0));
        // Throws if `_approved` is the current owner
        require(_approved != tokenOwner);
        // Check requirements
        bool senderIsOwner = (idToOwner[_tokenId] == msg.sender);
        bool senderIsApprovedForAll = (ownerToOperators[tokenOwner])[msg.sender];
        require(senderIsOwner || senderIsApprovedForAll);
        // Set the approval
        idToApprovals[_tokenId] = _approved;
        emit Approval(tokenOwner, _approved, _tokenId);
    }

```

please run this POC:

- add this function in to `VotingEscrow.t.sol` contract
- run with: `forge test --mt testMergeApproveIssue`

```solidity
function testMergeApproveIssue() public {
        address user = makeAddr("user");
        flowDaiPair.approve(address(escrow), TOKEN_1 + TOKEN_1);

        uint256 lockDuration = 7 * 24 * 3600 * 3; // 1 week
        deal(address(flowDaiPair), address(this), TOKEN_1 + TOKEN_1);
      
        uint tokenId1 = escrow.create_lock(TOKEN_1, lockDuration);
        uint tokenId2 = escrow.create_lock(TOKEN_1, lockDuration);

        escrow.approve(user, tokenId1);
        escrow.approve(user, tokenId2);

        vm.prank(user);
        vm.expectRevert();
        escrow.merge(tokenId1, tokenId2);
    }
```

```solidity
[PASS] testMergeApproveIssue() (gas: 1100467)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 10.14ms (4.25ms CPU time)
```

## Impact

`approve()` core function is useless for meging tokens.

## Code Snippet

```solidity
function approve(address _approved, uint _tokenId) public {
        address tokenOwner = idToOwner[_tokenId];
        // Throws if `_tokenId` is not a valid NFT
        require(tokenOwner != address(0));
        // Throws if `_approved` is the current owner
        require(_approved != tokenOwner);
        // Check requirements
        bool senderIsOwner = (idToOwner[_tokenId] == msg.sender);
        bool senderIsApprovedForAll = (ownerToOperators[tokenOwner])[msg.sender];
        require(senderIsOwner || senderIsApprovedForAll);
        // Set the approval
        idToApprovals[_tokenId] = _approved;
        emit Approval(tokenOwner, _approved, _tokenId);
    }
```

```solidity
function _burn(uint _tokenId) internal {
        require(_isApprovedOrOwner(msg.sender, _tokenId), "caller is not owner nor approved");

        address tokenOwner = ownerOf(_tokenId);

        // Clear approval
        approve(address(0), _tokenId);
        // checkpoint for gov
        _moveTokenDelegates(delegates(tokenOwner), address(0), _tokenId);
        // Remove token
        _removeTokenFrom(tokenOwner, _tokenId);
        emit Transfer(tokenOwner, address(0), _tokenId);
    }
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L251-L264

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L537-L549

## Tool used

Manual Review

## Recommendation

```diff
function _burn(uint _tokenId) internal {
        require(_isApprovedOrOwner(msg.sender, _tokenId), "caller is not owner nor approved");

        address tokenOwner = ownerOf(_tokenId);

        // Clear approval
-        approve(address(0), _tokenId);
+	  idToApprovals[_tokenId] = address(0);

        // checkpoint for gov
        _moveTokenDelegates(delegates(tokenOwner), address(0), _tokenId);
        // Remove token
        _removeTokenFrom(tokenOwner, _tokenId);
        emit Transfer(tokenOwner, address(0), _tokenId);
    }
```