Fast Black Eel

Medium

# Access Control Flaw in _burn Function Leads to Operational Disruption

## Summary
The vulnerability in the VotingEscrow contract arises due to an access control flaw within the `_burn` function, which inappropriately handles approval resetting, requiring broader permissions than necessary. This flaw disrupts operations such as token merging and withdrawals, as transactions initiated by users who are only approved for specific token IDs (and not globally) revert unexpectedly. This could lead to operational disruptions, preventing users from consolidating voting power or accessing their funds post-lock period, thereby undermining user trust and the functionality of the contract. Such issues could significantly impact user engagement and the platform's overall reliability.
## Vulnerability Detail
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol
```solidity
function _burn(uint _tokenId) internal {
        require(_isApprovedOrOwner(msg.sender, _tokenId), "caller is not owner nor approved");

        address tokenOwner = ownerOf(_tokenId);

        // Clear approval
@>        approve(address(0), _tokenId);
        // checkpoint for gov
        _moveTokenDelegates(delegates(tokenOwner), address(0), _tokenId);
        // Remove token
        _removeTokenFrom(tokenOwner, _tokenId);
        emit Transfer(tokenOwner, address(0), _tokenId);
    }

    function merge(uint _from, uint _to) external {
        require(attachments[_from] == 0 && !voted[_from], "attached");
        require(_from != _to);
        require(_isApprovedOrOwner(msg.sender, _from));
        require(_isApprovedOrOwner(msg.sender, _to));

       //...//
        _burn(_from);
        //...//
    }
   function withdraw(uint _tokenId) external nonreentrant {
        assert(_isApprovedOrOwner(msg.sender, _tokenId));
       //...//
        // Burn the NFT
        _burn(_tokenId);

        emit Withdraw(msg.sender, _tokenId, value, block.timestamp);
        emit Supply(supply_before, supply_before - value);
    }
```
However, two of these functions are in question in this report; `merge` and `withdraw`, since the `_burn` function is used.

The `_burn` function within the `VotingEscrow` contract is designed to remove tokens from circulation by clearing approvals and performing other state updates. However, due to the way access control is implemented, only the token owner or an entity approved for all user tokens can successfully execute this function without causing a revert. This is because the function internally calls `approve(address(0), _tokenId)`, which checks for broader permissions than those granted to entities approved for a single token.

## Impact
This results in operational disruptions for users who are legitimately authorized to perform actions (like merge and withdraw) that rely on _burn. They may encounter transaction reverts, leading to:

  * Inability to merge tokens for voting power consolidation or management.
  * Failed attempts to withdraw tokens post-lock period, leading to user dissatisfaction and potential disruption in planned economic activities within the platform.

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L537-L549
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1195-L1210
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L955-L979

## Proof of Concept
add this test in `test/VotingEscrow.t.sol` and run with `forge test --mt testPOCWithdrawFailForApprovedAddress  -vvvv`
```solidity
function testPOCWithdrawFailForApprovedAddress() public {
        address admin = address(0xadadadad);
        flowDaiPair.approve(address(escrow), TOKEN_1);
        uint256 lockDuration = 7 * 24 * 3600; // 1 week

        // Balance should be zero before and 1 after creating the lock
        assertEq(escrow.balanceOf(address(owner)), 0);
        uint tokenId = escrow.create_lock(TOKEN_1, lockDuration);

        vm.warp(block.timestamp + lockDuration);
        vm.roll(block.number + 1);

        // Now the token is withdrawable
        // owner  approve admin address on `tokenId`
        escrow.approve(address(admin), tokenId);

        vm.startPrank(address(admin));
        assertTrue(escrow.isApprovedOrOwner(address(admin),tokenId));

        // the call will revert because of approve(address(0),_tokenId)
        vm.expectRevert();
        escrow.withdraw(tokenId);      
    }
```

## Tool used

Manual Review

## Recommendation
  * Consider refactoring approval logic in `_burn`, Modify the internal logic of the `_burn` function to handle cases where the caller is only approved for the specific token ID being burned. This could involve bypassing the approval reset or adjusting the approval check to recognize and allow this scenario.

  * Consider standardizing the approval reset process to use `_clearApproval`, especially in contexts where specific token approval is sufficient. This would align the behavior across different contract functions and improve the reliability of operations involving token transfers, burns, or modifications.

```diff
function _burn(uint _tokenId) internal {
        require(_isApprovedOrOwner(msg.sender, _tokenId), "caller is not owner nor approved");

        address tokenOwner = ownerOf(_tokenId);

        // Clear approval
-        approve(address(0), _tokenId);
+       delete idToApprovals[_tokenId];
        // checkpoint for gov
        _moveTokenDelegates(delegates(tokenOwner), address(0), _tokenId);
        // Remove token
        _removeTokenFrom(tokenOwner, _tokenId);
        emit Transfer(tokenOwner, address(0), _tokenId);
    }
```