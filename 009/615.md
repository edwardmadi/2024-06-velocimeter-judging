Macho Amethyst Penguin

High

# User approved for a single token are not able to Merge or Withdraw

## Summary
The `approve` function incorrectly restricts token-specific approvals, allowing only full operator approval. This prevents approved users from executing withdraw or merge functions for individual tokens, forcing users to grant full approval for all their tokens instead of token-specific permissions.

This classified as a high severity due to the fact we have a logical flaw in the system, meaning it does not work as intended.

## Vulnerability Detail
As you can see from the comments of the `approve` function `Throws unless msg.sender is the current NFT owner, or an authorized operator of the current owner.` however this is not the case, the function will throw when a user is approved for a single token due to the require statement `require(senderIsOwner || senderIsApprovedForAll)` being false in this case. `approve` is called by the `burn` function, which in turn is called by `merge` and `withdraw`.

## Impact
Approved user will not be able to carry out `withdraw` or `merge` functions for the delegatee. Users need to give permission for all their tokens to another user when they want that user to carry `withdraw` or `merge` operations for them.

## Code Snippet
### File: VotingEscrow.sol
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L537-L549?plain=1
```Solidity
    function _burn(uint _tokenId) internal {
        require(_isApprovedOrOwner(msg.sender, _tokenId), "caller is not owner nor approved");

        address tokenOwner = ownerOf(_tokenId);

        // Clear approval
        approve(address(0), _tokenId); // @audit this reverts
        // checkpoint for gov
        _moveTokenDelegates(delegates(tokenOwner), address(0), _tokenId);
        // Remove token
        _removeTokenFrom(tokenOwner, _tokenId);
        emit Transfer(tokenOwner, address(0), _tokenId);
    }
```
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L251-L264?plain=1
```Solidity
    /// @dev Set or reaffirm the approved address for an NFT. The zero address indicates there is no approved address.
    ///      Throws unless `msg.sender` is the current NFT owner, or an authorized operator of the current owner.
    ///      Throws if `_tokenId` is not a valid NFT. (NOTE: This is not written the EIP)
    ///      Throws if `_approved` is the current owner. (NOTE: This is not written the EIP)
    /// @param _approved Address to be approved for the given NFT ID.
    /// @param _tokenId ID of the token to be approved.
    function approve(address _approved, uint _tokenId) public {
        address tokenOwner = idToOwner[_tokenId];
        // Throws if `_tokenId` is not a valid NFT
        require(tokenOwner != address(0));
        // Throws if `_approved` is the current owner
        require(_approved != tokenOwner);
        // Check requirements
        bool senderIsOwner = (idToOwner[_tokenId] == msg.sender);
        bool senderIsApprovedForAll = (ownerToOperators[tokenOwner])[msg.sender];

        require(senderIsOwner || senderIsApprovedForAll, "send not owner or approved");
        // Set the approval
        idToApprovals[_tokenId] = _approved;
        emit Approval(tokenOwner, _approved, _tokenId);
    }
```

## POC
Add the following test file to the test suite
```Solidity
// 1:1 with Hardhat test
pragma solidity 0.8.13;

import './BaseTest.sol';

import "contracts/RewardsDistributorV2.sol";
import "contracts/Voter.sol";
import "contracts/ExternalBribe.sol";
import "contracts/factories/GaugeFactoryV4.sol";
import "contracts/factories/BribeFactory.sol";
import "contracts/Minter.sol";

contract VotingEscrowTest is BaseTest {
    uint internal constant MAXTIME = 52 * 7 * 86400;
    int128 internal constant iMAXTIME = 52 * 7 * 86400;
    uint internal constant MULTIPLIER = 1 ether;

    RewardsDistributorV2 distributor;
    VotingEscrow escrow;
    GaugeFactoryV4 gaugeFactory;
    BribeFactory bribeFactory;
    Voter voter;
    ExternalBribe xbribe;
    Minter minter;

    function deployVoter() private {
        gaugeFactory = new GaugeFactoryV4();
        bribeFactory = new BribeFactory();

        gaugePlugin = new GaugePlugin(address(FLOW), address(WETH), owners[0]);
        voter = new Voter(
            address(escrow),
            address(factory),
            address(gaugeFactory),
            address(bribeFactory),
            address(gaugePlugin)
        );

        escrow.setVoter(address(voter));
        factory.setVoter(address(voter));

        deployPairWithOwner(address(owner));
        deployOptionTokenV4WithOwner(address(owner), address(gaugeFactory), address(voter), address(escrow));
        deployOptionTokenV4WithOwnerAndExpiry(address(owner), address(gaugeFactory), address(voter), address(escrow));

        gaugeFactory.setOFlow(address(oFlowV4));

        address[] memory tokens = new address[](2);
        tokens[0] = address(FRAX);
        tokens[1] = address(FLOW);
        distributor = new RewardsDistributorV2(address(escrow), address(DAI));
            
        minter = new Minter(
            address(voter),
            address(escrow),
            address(distributor)
        );

        voter.initialize(tokens, address(minter));

        distributor.setDepositor(address(minter));
        FLOW.setMinter(address(minter));
    }

    function setUp() public {
        deployOwners();
        deployCoins();
        mintStables();
        uint256[] memory amounts = new uint256[](3);
        amounts[0] = 1e25;
        amounts[1] = 1e25;
        amounts[2] = 1e25;
        mintFlow(owners, amounts);

        VeArtProxy artProxy = new VeArtProxy();

        deployPairFactoryAndRouter();
        deployMainPairWithOwner(address(owner));

        escrow = new VotingEscrow(address(FLOW), address(flowDaiPair),address(artProxy), owners[0]);

        deployVoter();
    }
    function testWithdrawRevertsForTokenApprovedAddressPOC() public {
        // Create address for Alice
        address admin = address(owner);
        address alice = address(owner3);

        uint256 ts = ((block.timestamp + 1 weeks) / 1 weeks) * 1 weeks;
        vm.warp(ts);

        // alice start interacions
        vm.startPrank(alice);

        FLOW.approve(address(router), TOKEN_1);
        DAI.approve(address(router), TOKEN_1);
        router.addLiquidity(address(FLOW), address(DAI), false, TOKEN_1, TOKEN_1, 0, 0, alice, block.timestamp);

        flowDaiPair.approve(address(escrow), TOKEN_1);

        // Create new token for Alice
        uint256 tokenId = escrow.create_lock(TOKEN_1, ONE_WEEK);

        // Reset the token status 
        voter.reset(tokenId);
        // Finish the epoch
        vm.warp(block.timestamp + 1 weeks);
        voter.distribute();

        // Go to the next epoch
        vm.warp(block.timestamp + 1 weeks);

        // Now the token is withdrawable
        // Alice approve admin address on `tokenId`
        escrow.approve(admin, tokenId);

        vm.stopPrank();

        vm.startPrank(admin);
        // Make sure admin is approved for alice token
        assertTrue(escrow.isApprovedOrOwner(admin, tokenId));

        // Now if Admin try to withdraw, 
        // the call will revert with message that suggest the caller is not owner or approved.
        // While Admin is approved for the specific tokenId.
        vm.expectRevert();
        escrow.withdraw(tokenId);

        // This happen because the underline function `_burn` call `approve` with zero address to clear specific token approvals,
        // which revert if the caller is not owner nor approved for all.
        vm.expectRevert();
        escrow.approve(address(0), tokenId);

        // However, Admin can use transferFrom function and then withdraw 
        escrow.transferFrom(alice, admin, tokenId);
        escrow.withdraw(tokenId);

        vm.stopPrank();
    }
}
```
## Tool used

Manual Review, Forge

## Recommendation

Use the `_clearApproval(owner, _tokenId)` function to clear the approvals in the `_burn` function.