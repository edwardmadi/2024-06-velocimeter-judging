Polite Butter Gazelle

Medium

# Killing gauges doesn't decrease number of active gauges, causing locked rewards

## Summary

`Minter::update_period` sends `weekly emissions` of `FLOW` tokens to the `Voter` contract, which is distributed to gauges based off their respective weights (determined by votes). 

The `weekly emissions` are calculated based off the number of `active gauges`. For example if `active gauges = 7`, then `weekly emissions` is calculated by multiplying the base weekly emissions by 7.

The problem is, if a gauge is killed, the number of `active gauges` remain. If the killed gauge was an active gauge, the `Minter` contract will proceed to send weekly emissions for a gauge that can never be distributed to from the `Voter` contract. Therefore, the rewards will be permanently locked in the `Voter` contract.

## Vulnerability Detail

`activeGaugeNumber` is increased if the claimable rewards for that gauge exceeds the minimum shares based off the total rewards for that epoch:

[Voter.sol#L555-L557](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L555-L557)
```javascript
    if((_claimable * 1e18) / currentEpochRewardAmount > minShareForActiveGauge) {
        activeGaugeNumber += 1;
    }
```

In the next epoch, when weekly emissions are sent to the `Voter` contract by `Minter::update_period`, the following calculates the number of emissions to send:

[Minter.sol#L98-L104](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Minter.sol#L98-L104)
```javascript
    function weekly_emission() public view returns (uint) {
@>      uint256 numberOfGauges = _voter.activeGaugeNumber(); //@audit pulls the `activeGaugeNumber`
        if(numberOfGauges == 0) { 
            return weeklyPerGauge;
        }
@>      return weeklyPerGauge * numberOfGauges;
    }
```

We can see that the number of emissions is multiplied by the number of active gauges that was calculated during reward distribution for the previous epoch.

When a gauge is killed, the claimable rewards are set to 0:

[Voter.sol#L407-L429](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L407-L429)
```javascript
    function killGaugeTotally(address _gauge) external {
        if (msg.sender != emergencyCouncil) {
            require(
                IGaugePlugin(gaugePlugin).checkGaugeKillAllowance(msg.sender, _gauge)
            , "Restart gauge not allowed");
        }
        require(isAlive[_gauge], "gauge already dead");

        address _pool = poolForGauge[_gauge];

        delete isAlive[_gauge];
        delete external_bribes[_gauge];
        delete poolForGauge[_gauge];
        delete isGauge[_gauge];
@>      delete claimable[_gauge]; //@audit dead gauge can't claim rewards
        delete supplyIndex[_gauge];
        delete gauges[_pool];
        try IPair(_pool).setHasGauge(false) {} catch {}

        killedGauges.push(_gauge);

        emit GaugeKilledTotally(_gauge);
    }
```

This is to ensure that the killed gauge can no longer claim rewards. The problem is that these extra rewards are still sent to the `Voter` contract since that gauge may still be considered active, but can no longer be claimed by anyone, therefore will be permanently locked in the `Voter` contract.

## Proof of Concept

<details>
<summary>Coded PoC</summary>
<br>

Add the following to `test/Minter.t.sol` and run: `forge test --mt testKilledGaugeRewardsLocked -vv`

```javascript
    function testKilledGaugeRewardsLocked() public {
        initializeVotingEscrow();

        FLOW.approve(address(router), TOKEN_1);
        FRAX.approve(address(router), TOKEN_1);
        router.addLiquidity(address(FRAX), address(FLOW), false, TOKEN_1, TOKEN_1, 0, 0, address(owner), block.timestamp);
        address pair1 = router.pairFor(address(FRAX), address(FLOW), false);
        address pair2 = router.pairFor(address(DAI), address(FLOW), false);

        address gauge1 = voter.gauges(pair1);

        voter.createGauge(pair2, 0);
        address gauge2 = voter.gauges(pair2);
        
        uint totalGauges = voter.length(); // this returns number of pools with gauges, which have 1:1 ratio, so this will give total gauges
        console.log("Total gauges: ", totalGauges);
      
        address[] memory pools = new address[](2);
        pools[0] = pair1;
        pools[1] = pair2;
        uint256[] memory weights = new uint256[](2);
        weights[0] = 9899;
        weights[1] = 101;

        _elapseOneWeek();

        voter.vote(1, pools, weights);
        minter.update_period(); // give rewards to Voter contract
        voter.distribute(address(gauge1)); // give rewards to gauges
        voter.distribute(address(gauge2));
        
        uint activeGauges = IVoter(voter).activeGaugeNumber();
        console.log("Active gauges considered for next epoch: ", activeGauges);

        _elapseOneWeek(); // next epoch

        voter.vote(1, pools, weights); // vote for gauges

        voter.killGaugeTotally(address(gauge1));

        activeGauges = IVoter(voter).activeGaugeNumber();
        console.log("Active gauges considered for current epoch after gauge1 is killed: ", activeGauges);

        uint voterBalanceBefore = FLOW.balanceOf(address(voter));
        minter.update_period(); // give rewards to Voter contract
        uint voterBalanceAfter = FLOW.balanceOf(address(voter));
        uint totalEmissionsSent = voterBalanceAfter - voterBalanceBefore;

        console.log("Total emissions sent from minter: ", totalEmissionsSent);

        voterBalanceBefore = FLOW.balanceOf(address(voter));
        voter.distribute(address(gauge2));
        voterBalanceAfter = FLOW.balanceOf(address(voter));

        uint256 totalEmissionsClaimed = voterBalanceBefore - voterBalanceAfter;

        console.log("Total rewards locked in contract after gauge2 claimed: ", totalEmissionsSent - totalEmissionsClaimed);
        
    }
```
</details>

### Console Output

```text
Running 1 test for test/Minter.t.sol:MinterTest
[PASS] testKilledGaugeRewardsLocked() (gas: 64777258)
Logs:
  Total gauges:  2
  Active gauges considered for next epoch:  2
  Active gauges considered for current epoch after gauge1 is killed:  2
  Total emissions sent from minter:  4000000000000000000000
  Total rewards locked in contract after gauge2 claimed:  3959600000000000001458

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 10.30ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

The test displayed how two gauges are considered active despite one of them being killed, causing the `Minter` contract to send extra rewards, which is permanently locked in the `Voter` contract. We can also see the problem is amplified if the weights for the killed gauge was relatively high.

## Impact

Permanently locked rewards in the `Voter` contract, `FLOW` tokens minted that are lost permanently, loss of funds for the protocol.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L555-L557

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Minter.sol#L98-L104

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/Voter.sol#L407-L429

## Tool used

Manual Review

## Recommendation

Consider creating a mapping to check which gauges are active, and proceed to check if the killed gauge was an active gauge. If true, proceed to decrement the number of active gauges. Ensure to delete this mapping afterwards. It's important to note that this issue may also apply to paused gauges (where it still may be considered active for the next weekly emissions), so consider implementing the solution to paused gauges as well.