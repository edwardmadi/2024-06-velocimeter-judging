Fit Burgundy Narwhal

High

# `_moveTokenDelegates()` will wipe all token ids of a user or can double them when called more than once within a block

## Summary
There are two problems with the VotingEscrow's `_moveTokenDelegates()` function:
1. When called more than twice in a single block, it'll wipe all `tokenIds` of the source address.
2. When called more than once in a single block, it'll double the existing `tokenIds` of the destination address.
Both of these issues stem from the way new and old checkpoints are fetched.
## Vulnerability Detail
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1362-L1411
```solidity
    function _moveTokenDelegates(
        address srcRep,
        address dstRep,
        uint _tokenId
    ) internal {
        if (srcRep != dstRep && _tokenId > 0) {
            if (srcRep != address(0)) {
                uint32 srcRepNum = numCheckpoints[srcRep];
                uint[] storage srcRepOld = srcRepNum > 0
                    ? checkpoints[srcRep][srcRepNum - 1].tokenIds
                    : checkpoints[srcRep][0].tokenIds;
                uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep);
                uint[] storage srcRepNew = checkpoints[srcRep][
                    nextSrcRepNum
                ].tokenIds;
                // All the same except _tokenId
                for (uint i = 0; i < srcRepOld.length; i++) {
                    uint tId = srcRepOld[i];
                    if (tId != _tokenId) {
                        srcRepNew.push(tId);
                    }
                }

                numCheckpoints[srcRep] = srcRepNum + 1;
            }

            // ... 
        }
    }
```

Let's first examine the first problem with the function: When called more than twice in a single block, it'll wipe all `tokenIds` of the source address. Before continuing with the PoC examples, we need to clarify what `_findWhatCheckpointToWrite(srcRep)` returns:

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1413-L1429
```solidity
    function _findWhatCheckpointToWrite(address account)
        internal
        view
        returns (uint32)
    {
        uint _timestamp = block.timestamp;
        uint32 _nCheckPoints = numCheckpoints[account];

        if (
            _nCheckPoints > 0 &&
            checkpoints[account][_nCheckPoints - 1].timestamp == _timestamp
        ) {
            return _nCheckPoints - 1;
        } else {
            return _nCheckPoints;
        }
    }
```

If the account has any checkpoints (its counter is > 0) and the last checkpoint has the same timestamp as the current block's â€“ return the index of the last checkpoint. Otherwise return the index of the new checkpoint to be recorded.

Now continuing with calling `_moveTokenDelegates()` for the same `srcRep` more than twice within a block. Let's say `numCheckpoints[srcRep]` is 5 and the source's last checkpoint holds `[1, 2, 3, 4]` as a value for `tokenIds`. 

1. First call within single block, removing `tokenId` 6 from `srcRep`
`numCheckpoints[srcRep]` = 5
`srcRepOld` = checkpoints\[srcRep]\[4].tokenIds (\[1, 2, 3, 4, 5, 6])
`srcRepNew` = checkpoints\[srcRep]\[5].tokenIds (\[1, 2, 3, 4, 5])
`numCheckpoints[srcRep]` += 1

1. Second call within single block, removing `tokenId` 5 from `srcRep`
`numCheckpoints[srcRep]` = 6
`srcRepOld` = checkpoints\[srcRep]\[5].tokenIds (\[1, 2, 3, 4, 5])
`srcRepNew` = checkpoints\[srcRep]\[5].tokenIds (\[1, 2, 3, 4])
`numCheckpoints[srcRep]` += 1

3. Third call within single block, removing `tokenId` 4 from `srcRep`
`numCheckpoints[srcRep]` = 7
`srcRepOld` = checkpoints\[src]\[6].tokenIds (\[])
`srcRepNew` = checkpoints\[src]\[6].tokenIds (\[])
`numCheckpoints[srcRep]` += 1

Now the user effectively lost the other 3 of their tokens because `numCheckpoints[srcRep]` was always incremented by 1 even though in step 2 both the old and the new checkpoint were the exact same.

Now looking at the second half of the function `_moveTokenDelegates()` where the destination address' checkpoints are handled.

```solidity
    function _moveTokenDelegates(
        address srcRep,
        address dstRep,
        uint _tokenId
    ) internal {
        if (srcRep != dstRep && _tokenId > 0) {
            // ...
            
            if (dstRep != address(0)) {
                uint32 dstRepNum = numCheckpoints[dstRep];
                uint[] storage dstRepOld = dstRepNum > 0
                    ? checkpoints[dstRep][dstRepNum - 1].tokenIds
                    : checkpoints[dstRep][0].tokenIds;
                uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
                uint[] storage dstRepNew = checkpoints[dstRep][
                    nextDstRepNum
                ].tokenIds;
                // All the same plus _tokenId
                require(
                    dstRepOld.length + 1 <= MAX_DELEGATES,
                    "dstRep would have too many tokenIds"
                );
                for (uint i = 0; i < dstRepOld.length; i++) {
                    uint tId = dstRepOld[i];
                    dstRepNew.push(tId);
                }
                dstRepNew.push(_tokenId);

                numCheckpoints[dstRep] = dstRepNum + 1;
            }
        }
    }
```

Let's say `srcRep` from the example above was moving `tokenId`s 6 and 5 to `dstRep`. And let's say `dstRep` also has `numCheckpoints[dstRep]` at 5 and in their last checkpoint their've got `tokenIds` `[7, 8, 9]`.

1. First call within single block, adding `tokenId` 6 to `dstRep`
`numCheckpoints[dstRep]` = 5
`dstRepOld` = checkpoints\[dstRep]\[4].tokenIds (\[7, 8 ,9])
`dstReRNew` = checkpoints\[dstRep]\[5].tokenIds (\[7, 8, 9, 6])
`numCheckpoints[dstRep]` += 1

2. Second call within single block, adding `tokenId` 5 to `dstRep`
`numCheckpoints[dstRep]` = 6
`dstRepOld` = checkpoints\[dstRep]\[5].tokenIds (\[7, 8, 9, 6])
`dstRepNew` = checkpoints\[dstRep]\[5].tokenIds (\[7, 8, 9, 6, 7, 8, 9, 6, 5])
`numCheckpoints[dstRep]` += 1
## Impact
An account's voting power is directly dependent on the token ids stored across checkpoints.

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1292-L1304
```solidity
    function getVotes(address account) external view returns (uint) {
        uint32 nCheckpoints = numCheckpoints[account];
        if (nCheckpoints == 0) {
            return 0;
        }
        uint[] storage _tokenIds = checkpoints[account][nCheckpoints - 1].tokenIds;
        uint votes = 0;
        for (uint i = 0; i < _tokenIds.length; i++) {
            uint tId = _tokenIds[i];
            votes = votes + _balanceOfNFT(tId, block.timestamp);
        }
        return votes;
    }

```

So in an essence, a user can loose all their voting power by transferring more than 2 of their token ids within a single block or abuse the system by doubling their voting power without any preconditions needing to be met.
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1369-L1385
## Tool used
Manual Review
## Recommendation
Increment `numCheckpoints` only if the indexes of the new and old checkpoints of both `srcRep` and `dstRep` are different. Also to fix the second issue where the token ids are doubled it'd probably be the easiest to use a Set, however that'll require a more serious refactoring.
