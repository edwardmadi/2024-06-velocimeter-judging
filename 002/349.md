Wonderful Rosewood Pheasant

Medium

# The `timestamp` variable of a checkpoint is not initialized

## Summary
 The `VotingEscrow` contract where the `timestamp` variable within the `Checkpoint` struct is not initialized anywhere in the codebase. This oversight leads to malfunctioning of functions that rely on this `timestamp`, specifically `VotingEscrow._findWhatCheckpointToWrite` and `VotingEscrow.getPastVotesIndex`.

## Vulnerability Detail
A checkpoint contains a `timestamp` variable which stores the `block.timestamp` of when the checkpoint is created.
```solidity
   struct Checkpoint {
        uint256 timestamp;
        uint256[] tokenIds;
    }
```
However, it was found that the `timestamp` variable of a checkpoint was not initialized anywhere in the codebase. Therefore, any function that relies on the `timestamp` of a checkpoint will break.

The [VotingEscrow._findWhatCheckpointToWrite](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1413-L1429) and [VotingEscrow.getPastVotesIndex](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1306-L1335) functions were found to rely on the `timestamp` variable of a checkpoint for computation.

Instance 1 - `VotingEscrow._findWhatCheckpointToWrite` function:

The `VotingEscrow._findWhatCheckpointToWrite` function verifies if the `timestamp` of the latest checkpoint of an account is equal to the current `block.timestamp`. If true, the function will return the index number of the last checkpoint.
```solidity
   function _findWhatCheckpointToWrite(address account) internal view returns (uint32) {
        uint256 _timestamp = block.timestamp;
        uint32 _nCheckPoints = numCheckpoints[account];

        if (_nCheckPoints > 0 && checkpoints[account][_nCheckPoints - 1].timestamp == _timestamp) {
            return _nCheckPoints - 1;
        } else {
            return _nCheckPoints;
        }
    }
```
As such, this function does not work as intended and will always return the index of a new checkpoint.

Instance 2 - `VotingEscrow.getPastVotesIndex` function:

The `VotingEscrow.getPastVotesIndex` function relies on the `timestamp` of the latest checkpoint for optimization purposes. If the request `block.timestamp` is the most recently updated checkpoint, it will return the latest index immediately and skip the binary search. Since the `timestamp` variable is not populated, the optimization will not work.
```solidity
  
    function getPastVotesIndex(address account, uint256 timestamp) public view returns (uint32) {
        uint32 nCheckpoints = numCheckpoints[account];
        if (nCheckpoints == 0) {
            return 0;
        }
        // First check most recent balance
        if (checkpoints[account][nCheckpoints - 1].timestamp <= timestamp) {
            return (nCheckpoints - 1);
        }

        // Next check implicit zero balance
        if (checkpoints[account][0].timestamp > timestamp) {
            return 0;
        }

        uint32 lower = 0;
        uint32 upper = nCheckpoints - 1;
        while (upper > lower) {
            uint32 center = upper - (upper - lower) / 2; // ceil, avoiding overflow
            Checkpoint storage cp = checkpoints[account][center];
            if (cp.timestamp == timestamp) {
                return center;
            } else if (cp.timestamp < timestamp) {
                lower = center;
            } else {
                upper = center - 1;
            }
        }
        return lower;
    }
```

## Impact
The missing initialization of the `timestamp` variable in checkpoints causes critical functions in the `VotingEscrow` contract to malfunction. Specifically, the `_findWhatCheckpointToWrite` function always returns the index of a new checkpoint instead of the last one, and the `getPastVotesIndex` function's optimization to skip the binary search does not work. This can lead to incorrect data retrieval and potential vulnerabilities in vote recording and balance checking mechanisms.

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1413-L1429

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1306-L1335

## Tool used
Manual Review

## Recommendation
Initialize the `timestamp` variable of the checkpoint in the codebase to ensure all functions relying on this variable work correctly.
