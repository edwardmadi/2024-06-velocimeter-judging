Square Arctic Chicken

High

# Users `checkpoints` timestamp is not initialised and never updated causing voting power to be over estimated

## Summary

The `Checkpoint` struct holds a record of delegated token `checkpoints` for each account containing record of a user tokens and timestamps at which the checkpoint was updated. However, whenever a checkpoint is created, its timestamp is not updated and as such any function that relies on the `timestamp` of a user's `checkpoint` such as the `getPastVotesIndex(...)` and `getPastVotes(...)` will return wrong results as shown below in the coded POC.

## Vulnerability Detail

```solidity
File: VotingEscrow.sol
44:     struct Checkpoint {
45:         uint timestamp;
46:         uint[] tokenIds;
47:     }
...
1269:    mapping(address => mapping(uint32 => Checkpoint)) public File: VotingEscrow.sol
44:     struct Checkpoint {
45:         uint timestamp;
46:         uint[] tokenIds;
47:     }
;
```
The `_findWhatCheckpointToWrite(...)` function is called for an account to check what checkpoints to write to during minting, burning, transfers and delegation. However, after each checkpoint write/update operation the  `timestamp` of the `checkpoint` is not updated. This will cause function that rely on a user's  `checkpoint` to return underestimated or over estimated results at any given time because the `timestamp` of all users checkpoint will always be zero

```solidity
File: VotingEscrow.sol
1413:     function _findWhatCheckpointToWrite(address account)
1414:         internal
1415:         view
1416:         returns (uint32)
1417:     {
1418:         uint _timestamp = block.timestamp;
1419:         uint32 _nCheckPoints = numCheckpoints[account];
1420: 
1421:         if (
1422:             _nCheckPoints > 0 &&
1423:             checkpoints[account][_nCheckPoints - 1].timestamp == _timestamp
1424:         ) {
1425:             return _nCheckPoints - 1;
1426:         } else {
1427:             return _nCheckPoints;
1428:         }
1429:     }

```

As shown below, the `checkpoint` is updated in the `_moveTokenDelegates(...)` and `_moveAllDelegates(...)` functions respectively, but also notice that the timestamps are not written to them at the time the time that the `tokenIds` are updated. This posses a problem for any function that reads from a users `checkpoint` as it is no a reliable source of users checkpoint information

```solidity
File: VotingEscrow.sol
1362:     function _moveTokenDelegates( 
...
1373:                 uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep);
1374:  ->             uint[] storage srcRepNew = checkpoints[srcRep][ 
1375:                     nextSrcRepNum
1376:                 ].tokenIds;
.....
1386:             }
.....
1393:                 uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
1394:  ->             uint[] storage dstRepNew = checkpoints[dstRep][
1395:                     nextDstRepNum
1396:                 ].tokenIds;
......
1410:         }
1411:     }

1431:     function _moveAllDelegates( 
....
1443:                 uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep); 
1444:  ->             uint[] storage srcRepNew = checkpoints[srcRep][
1445:                     nextSrcRepNum
1446:                 ].tokenIds;
....
1463:                 uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
1464:   ->            uint[] storage dstRepNew = checkpoints[dstRep][
1465:                     nextDstRepNum
1466:                 ].tokenIds;
....
1486:     }


```

<details>
<summary>Some functions that read from and write to a user's `checkpoint` are included here</summary>

```solidity
READ/WRITE
_findWhatCheckpointToWrite -> _moveTokenDelegates    <<->> _transferFrom
_findWhatCheckpointToWrite -> _moveTokenDelegates    <<->> _mint
_findWhatCheckpointToWrite -> _moveTokenDelegates    <<->> _burn
_findWhatCheckpointToWrite -> _moveAllDelegates          <<->> _delegate/ delegate/delegateBySig

READ
getPastVotesIndex -> getTokenIdsAt
getPastVotesIndex -> getPastVotes -> GovernorSimpleVotes._getVotes
```
</details>

Consider Alice at time t
- at t = 0, alice minta a tId1 and her checkpoint token is `[tId1]`
    - `numCheckpoints` = 1 and `timestamp` = 0
- at t = 100, she creates another lock tId2 and her checkpoint token is `[tId1, tId2]`
    - `numCheckpoints` = 2 and `timestamp` = 0
- at t = 200, she creates another lock tId3 and her checkpoint token is `[tId1, tId2, tId3]`
    - `numCheckpoints` = 3 and `timestamp` = 0
-  at t = 500, alice calls `getPastVotes(alice, t = 0)` and it evaluates the output using `[tId1, tId2, tId3]` which is the last checpoint


I am reporting this as a high because in the reverse case when the user starts from 3 tokens and transfers out 2 leaving 1 behind, his voting power when he had 3 will still be the power when he had just one.

As shown below, with the current flawed implementation it will return a higher voting power even if the it has decayed over time and this can be used for exploits that can lead to loss of funds (e.g governance voting).

**CODED POC**

add the test case below to the `VotingEscrow.t.sol` and run `forge test --mt testFindCheckPointIsWrong -vvv`

<details>
<summary>testFindCheckPointIsWrong case</summary>

```solidity
    function testFindCheckPointIsWrong() public {
        flowDaiPair.approve(address(escrow), TOKEN_1);
        uint256 lockDuration = 7 * 24 * 3600; // 1 week

        vm.warp(((block.timestamp / ONE_WEEK )* ONE_WEEK) + ONE_WEEK);


        // Balance should be zero before and 1 after creating the lock
        assertEq(escrow.balanceOf(address(owner)), 0);
        escrow.create_lock(TOKEN_1, lockDuration);
        assertEq(escrow.currentTokenId(), 1);
        assertEq(escrow.ownerOf(1), address(owner));
        assertEq(escrow.balanceOf(address(owner)), 1);

        // for demonstration purposes, give Alice lpTokens to create lock
        vm.prank(address(owner));
        flowDaiPair.transferFrom(address(owner), alice, 100e18);

        uint t1; uint tId1;
        uint t2; uint tId2;
        uint t3; uint tId3;
        uint t4; uint tId4;

        vm.warp(((block.timestamp / ONE_WEEK )* ONE_WEEK) + 1);

        // alice lock lp in ve
        vm.startPrank(address(alice));
        flowDaiPair.approve(address(escrow), 100e18);
        for (uint i; i < 4; i++) {
            
            if (i == 0) {
                t1 = block.timestamp;
                tId1 = escrow.create_lock(25e18, 10 * ONE_WEEK);
            }
            if (i == 1) {
                t2 = block.timestamp;
                tId2 = escrow.create_lock(25e18, 10 * ONE_WEEK);
            }
            if (i == 2) {
                t3 = block.timestamp;
                tId3 = escrow.create_lock(25e18, 10 * ONE_WEEK);
            }
            if (i == 3) {
                t4 = block.timestamp;
                tId4 = escrow.create_lock(25e18, 10 * ONE_WEEK);
            }

            vm.warp(((block.timestamp / ONE_WEEK )* ONE_WEEK) + ONE_WEEK);
        }

        emit log_named_uint("t1", t1);
        emit log_named_uint("tId1", tId1);
        emit log_named_uint("t2", t2);
        emit log_named_uint("tId2", tId2);
        emit log_named_uint("t3", t3);
        emit log_named_uint("tId3", tId3);
        emit log_named_uint("t4", t4);
        emit log_named_uint("tId4", tId4);

        emit log_named_uint("Current numchecpoint index", escrow.numCheckpoints(alice));

        emit log_named_uint("votes Index at t1", escrow.getPastVotesIndex(alice, t1));
        emit log_named_uint("votes Index at t2", escrow.getPastVotesIndex(alice, t2));
        emit log_named_uint("votes Index at t3", escrow.getPastVotesIndex(alice, t3));
        emit log_named_uint("votes Index at t4", escrow.getPastVotesIndex(alice, t4));
        emit log_named_decimal_uint("votes at t1", escrow.getPastVotes(alice, t1), 18);
        emit log_named_decimal_uint("votes at t2", escrow.getPastVotes(alice, t2), 18);
        emit log_named_decimal_uint("votes at t3", escrow.getPastVotes(alice, t3), 18);
        emit log_named_decimal_uint("votes at t4", escrow.getPastVotes(alice, t4), 18);

    }
```
</details>


Current out put returns the last index where alice has minted all NFTS irrespective of the time (t1, t2, t3 or t4).
```solidity
  votes Index at t1: 3
  votes Index at t2: 3
  votes Index at t3: 3
  votes Index at t4: 3
  votes at t1: 22.115381435675297912
  votes at t2: 20.192307692290675200
  votes at t3: 18.269230769215372800
  votes at t4: 16.346153846140070400
```

with recommendations applied
```solidity
  votes Index at t1: 0
  votes Index at t2: 1
  votes Index at t3: 2
  votes Index at t4: 3
  votes at t1: 4.807691512765586078
  votes at t2: 9.134615384607686400
  votes at t3: 12.980769230758291200
  votes at t4: 16.346153846140070400
```

## Impact
This can lead to over/understated votes as shown in the coded POC and can be be used for exploits in different parts of the protocols and protocols who integrate from VM



## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1373-L1376

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1393-L1396

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1443-L1446

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1393-L1396


## Tool used

Foundry test

## Recommendation

Modify the functions as shown below

```diff
File: VotingEscrow.sol
1362:     function _moveTokenDelegates( 
1363:         address srcRep, // srcRep / srcRep current delegatee
1364:         address dstRep, // dstRep / dstRep current delegatee
1365:         uint _tokenId
1366:     ) internal {
...
1373:                 uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep);
1374:  ->             uint[] storage srcRepNew = checkpoints[srcRep][ 
1375:                     nextSrcRepNum
1376:                 ].tokenIds;
+                       checkpoints[srcRep][nextSrcRepNum].timestamp = block.timestamp
.....
1386:             }

.....
1393:                 uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
1394:  ->             uint[] storage dstRepNew = checkpoints[dstRep][
1395:                     nextDstRepNum
1396:                 ].tokenIds;
+                       checkpoints[dstRep][nextDstRepNum].timestamp = block.timestamp
......
1410:         }
1411:     }



1431:     function _moveAllDelegates( 
1432:         address tokenOwner, // delegator
1433:         address srcRep, // currentDelegate
1434:         address dstRep // delegatee
1435:     ) internal {
....
1443:                 uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep); 
1444:  ->             uint[] storage srcRepNew = checkpoints[srcRep][
1445:                     nextSrcRepNum
1446:                 ].tokenIds;
+                       checkpoints[srcRep][nextSrcRepNum].timestamp = block.timestamp
....
1463:                 uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
1464:   ->            uint[] storage dstRepNew = checkpoints[dstRep][
1465:                     nextDstRepNum
1466:                 ].tokenIds;
+                       checkpoints[dstRep][nextDstRepNum].timestamp = block.timestamp
....

1486:     }


```
