Delightful Lavender Goose

High

# Voting Power inflation due to duplicate tokenIds in Checkpoint.

## Summary
A checkpoint can contain duplicated tokenIDs if `_moveTokenDelegates` function triggered in same txn leading to double counting of voting power.

## Vulnerability Detail
The function `_findWhatCheckpointToWrite` does not always return the index of a new checkpoint. It will return the last checkpoint if it has already been written once within the same timestamp.
```solidity
    function _findWhatCheckpointToWrite(address account)
        internal
        view
        returns (uint32)
    {
        uint _timestamp = block.timestamp;
        uint32 _nCheckPoints = numCheckpoints[account];

        if (
            _nCheckPoints > 0 &&
@>>         checkpoints[account][_nCheckPoints - 1].timestamp == _timestamp
        ) {
@>>         return _nCheckPoints - 1;
        } else {
@>>         return _nCheckPoints;
        }
    }
```
The existing logic works fine as long as a new checkpoint is created as it a new empty array (`srcRepNew` OR `dstRepNew`) is created every single time. 

But if last/previous checkpoint is returned then **_srcRepNew will contain tokens from previous checkpoint_** . In the next line old tokens (`srcRepOld`) will be merged with new array with tokens (`srcRepNew`) this creates duplicates tokens.
```solidity
    function _moveTokenDelegates(
        address srcRep,
        address dstRep,
        uint _tokenId
    ) internal {
        if (srcRep != dstRep && _tokenId > 0) {
            if (srcRep != address(0)) {
                ...
@>          uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep);
@>              uint[] storage srcRepNew = checkpoints[srcRep][
@>                  nextSrcRepNum
                ].tokenIds;
                // All the same except _tokenId
@>              for (uint i = 0; i < srcRepOld.length; i++) {
@>                  uint tId = srcRepOld[i];
@>                  if (tId != _tokenId) {
@>                      srcRepNew.push(tId);
@>                  }
                }
            }

            if (dstRep != address(0)) {
                ..
@>           uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
@>                 uint[] storage dstRepNew = checkpoints[dstRep][
@>                     nextDstRepNum
@>                 ].tokenIds;
                // All the same plus _tokenId
                require(
                    dstRepOld.length + 1 <= MAX_DELEGATES,
                    "dstRep would have too many tokenIds"
                );
@>               for (uint i = 0; i < dstRepOld.length; i++) {
@>                   uint tId = dstRepOld[i];
@>                   dstRepNew.push(tId);
@>               }
@>               dstRepNew.push(_tokenId);
            }
        }
    }
```

## POC
Initial State:
```solidity
Alice:
numCheckPoint[alice] = 1
checkpoints[alice][0] = [t1, t2] 

Bob:
numCheckPoint[bob] = 1
checkpoints[bob][0] = [t3]
```
Make two transactions at a time,
-> Alice transfers `t2` to Bob.
```solidity
Alice:
numCheckPoint[alice] = 2
checkpoints[alice][0] = [t1, t2] 
checkpoints[alice][1] = [t1] 

Bob:
numCheckPoint[bob] = 2
checkpoints[bob][0] = [t3]
checkpoints[bob][1] = [t3, t2]

```
-> Alice transfers `t1` to Bob in same txn.
```solidity
Alice:
numCheckPoint[alice] = 3
checkpoints[alice][0] = [t1, t2] 
checkpoints[alice][1] = [t1] 
checkpoints[alice][2] = [t1] <====== Even if the token is transferred, Token is still not removed from tokenIds

Bob:
numCheckPoint[bob] = 3
checkpoints[bob][0] = [t3]
checkpoints[bob][1] = [t3, t2]
checkpoints[bob][2] = [t3, t2, t3, t2, t1]  <====== Duplicated TokenIds
```


## Impact
Malicious users could exploit this vulnerability to inflate the voting balance of their accounts and
participate in governance voting, potentially tipping the scales in favor of harmful proposals and disrupting fair governance.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1421-L1425

```solidity
    function _moveTokenDelegates(
        address srcRep,
        address dstRep,
        uint _tokenId
    ) internal {
        if (srcRep != dstRep && _tokenId > 0) {
            if (srcRep != address(0)) {
                uint32 srcRepNum = numCheckpoints[srcRep];
                uint[] storage srcRepOld = srcRepNum > 0
                    ? checkpoints[srcRep][srcRepNum - 1].tokenIds
                    : checkpoints[srcRep][0].tokenIds;
@>          uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep);
                uint[] storage srcRepNew = checkpoints[srcRep][
                    nextSrcRepNum
                ].tokenIds;
                // All the same except _tokenId
                for (uint i = 0; i < srcRepOld.length; i++) {
                    uint tId = srcRepOld[i];
                    if (tId != _tokenId) {
                        srcRepNew.push(tId);
                    }
                }

@>          numCheckpoints[srcRep] = srcRepNum + 1;
            }

            if (dstRep != address(0)) {
                uint32 dstRepNum = numCheckpoints[dstRep];
                uint[] storage dstRepOld = dstRepNum > 0
                    ? checkpoints[dstRep][dstRepNum - 1].tokenIds
                    : checkpoints[dstRep][0].tokenIds;
@>           uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
                uint[] storage dstRepNew = checkpoints[dstRep][
                    nextDstRepNum
                ].tokenIds;
                // All the same plus _tokenId
                require(
                    dstRepOld.length + 1 <= MAX_DELEGATES,
                    "dstRep would have too many tokenIds"
                );
                for (uint i = 0; i < dstRepOld.length; i++) {
                    uint tId = dstRepOld[i];
                    dstRepNew.push(tId);
                }
                dstRepNew.push(_tokenId);

@>          numCheckpoints[dstRep] = dstRepNum + 1;
            }
        }
    }

    function _findWhatCheckpointToWrite(address account)
        internal
        view
        returns (uint32)
    {
        uint _timestamp = block.timestamp;
        uint32 _nCheckPoints = numCheckpoints[account];

        if (
            _nCheckPoints > 0 &&
@>>         checkpoints[account][_nCheckPoints - 1].timestamp == _timestamp
        ) {
            return _nCheckPoints - 1;
        } else {
            return _nCheckPoints;
        }
    }
```
## Tool used

Manual Review

## Recommendation
Ensure that the following invariants hold in the functions `_moveTokenDelegates` & `_moveAllDelegates`:
• No duplicated veNFTs (tokenIDs) within a checkpoint
• When moving a tokenID, it must be deleted from the source tokenIds list and added to the destination
tokenIds list
• numCheckpoints should only increase for new timestamp.