Sweet Pecan Wasp

High

# VotingEscrow never initialises Checkpoint timestamps, which leads to getPastVotesIndex() returning the wrong Checkpoint index

## Summary

`VotingEscrow.sol` Checkpoints consist of the checkpoint timestamp and the account tokenIds at the timestamp. The checkpoint timestamps are never initialised anywhere within the code, meaning they will always equal 0. This leads to the function `VotingEscrow::getPastVotesIndex` to always return the most recent checkpoint index, which will cause past votes to be incorrectly counted.

## Vulnerability Detail

When looking through the `VotingEscrow.sol` contract, the Checkpoint timestamp is never initialised, meaning any comparisions with the Checkpoint timestamp will be incorrect and lead to invalid results.

[VotingEscrow::getPastVotesIndex()](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1306-L1335)
```solidity
    function getPastVotesIndex(address account, uint timestamp) public view returns (uint32) {
        uint32 nCheckpoints = numCheckpoints[account];
        if (nCheckpoints == 0) {
            return 0;
        }
        // First check most recent balance
>       if (checkpoints[account][nCheckpoints - 1].timestamp <= timestamp) {
            return (nCheckpoints - 1);
        }

        // Next check implicit zero balance
        if (checkpoints[account][0].timestamp > timestamp) {
            return 0;
        }

        uint32 lower = 0;
        uint32 upper = nCheckpoints - 1;
        while (upper > lower) {
            uint32 center = upper - (upper - lower) / 2; // ceil, avoiding overflow
            Checkpoint storage cp = checkpoints[account][center];
            if (cp.timestamp == timestamp) {
                return center;
            } else if (cp.timestamp < timestamp) {
                lower = center;
            } else {
                upper = center - 1;
            }
        }
        return lower;
    }
```
The first if statement within the function (highlighted) will always be true, as `0` will always be less than or equal to the provided timestamp. Meaning no matter what timestamp is being searched for, the latest account Checkpoint index will be returned. 

This will lead to [getPastVotes]() to return the wrong amount of past votes for the provided timestamp:
```solidity
    function getPastVotes(address account, uint timestamp)
        public
        view
        returns (uint)
    {
>       uint32 _checkIndex = getPastVotesIndex(account, timestamp);
        // Sum votes
        uint[] storage _tokenIds = checkpoints[account][_checkIndex].tokenIds;
        uint votes = 0;
        for (uint i = 0; i < _tokenIds.length; i++) {
            uint tId = _tokenIds[i];
            // Use the provided input timestamp here to get the right decay
            votes = votes + _balanceOfNFT(tId, timestamp);
        }
        return votes;
    }
```

`VotingEscrow::getPastVotes` is utilised within:
`GovernorVotes::_getVotes` -> `Governor::_castVote()`

Due to the incorrect tokenIds being returned, the `Governor::_castVote` function will incorrectly cast votes for proposals using the most recent Checkpoint tokenIds. When a proposal is created, a snapshot is created which determins at which timestamp Checkpoints are meant to be read and utilised for casting votes. 

## Impact

Due to the uninitialised Checkpoint timestamps, `VotingEscrow::getPastVotes` will always return the most recent account Checkpoint, rather than the Checkpoint from the provided timestamp. This will lead to `Governor::_castVote()` utilising the most recent user tokenId votes, rather than the tokens owned at the snapshot set when creating a proposal. This breaks the intended voting functionality and allows users to increase their voting power whilst a proposal is active to have larger influence, which can be used to vote on proposals that benefit the user such as directing rewards to gauges where the user has a vested interest, which can lead to a loss of rewards for other users.

Another issue is that the function [VotingEscrow::_findWhatCheckpointToWrite()](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1413-L1429) will always return a new checkpoint, as the if statement within the function will never succeed as the Checkpoint timestamp will always be 0.

## Code Snippet

[VotingEscrow::_findWhatCheckpointToWrite()](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1413-L1429)

## Tool used

Manual Review

## Recommendation

Ensure to set the Checkpoint's timestamp when creating a new Checkpoint.

Once the Checkpoint timestamps are fixed, an issue with `VotingEscrow::_moveTokenDelegates()` will be surfaced, which is outlined in a seperate report.
