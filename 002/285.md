Fit Burgundy Narwhal

Medium

# Checkpoints' `timestamp` is never initialized or set in the VotingEscrow

## Summary
Nowhere in the VotingEscrow contract are either new or old checkpoints' `timestamp`s set or initialized. This will break functionalities relying on checkpoints' timestamps.
## Vulnerability Detail
The `Checkpoint` struct contains a `timestamp` and `tokenIds` field.
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L43-L47
```solidity
    /// @notice A checkpoint for marking delegated tokenIds from a given timestamp
    struct Checkpoint {
        uint timestamp;
        uint[] tokenIds;
    }
```

Nowhere in the VotingEscrow contract, however, is a Checkpoint's `timestamp` set or updated. This breaks multiple functionalities relying on the timestamps.
## Impact
1. `_findWhatCheckpointToWrite()` will always return the index of the new checkpoint to be written:
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1413-L1429
```solidity
    function _findWhatCheckpointToWrite(address account)
        internal
        view
        returns (uint32)
    {
        uint _timestamp = block.timestamp;
        uint32 _nCheckPoints = numCheckpoints[account];

        if (
            _nCheckPoints > 0 &&
→           checkpoints[account][_nCheckPoints - 1].timestamp == _timestamp
        ) {
            return _nCheckPoints - 1;
        } else {
            return _nCheckPoints;
        }
    }
```

As `checkpoints[account][_nCheckpoints - 1].timestamp` will always be 0 and never equal to `block.timestamp`, the function will always return `_nCheckpoints` - the index of the new checkpoint to be recorded.

2. `getPastVotesIndex()` will always return the last checkpoint of the account.
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1306-L1335
```solidity
    function getPastVotesIndex(address account, uint timestamp) public view returns (uint32) {
        uint32 nCheckpoints = numCheckpoints[account];
        if (nCheckpoints == 0) {
            return 0;
        }
        // First check most recent balance
→       if (checkpoints[account][nCheckpoints - 1].timestamp <= timestamp) {
            return (nCheckpoints - 1);
        }

        // Next check implicit zero balance
        if (checkpoints[account][0].timestamp > timestamp) {
            return 0;
        }

        uint32 lower = 0;
        uint32 upper = nCheckpoints - 1;
        while (upper > lower) {
            uint32 center = upper - (upper - lower) / 2; // ceil, avoiding overflow
            Checkpoint storage cp = checkpoints[account][center];
            if (cp.timestamp == timestamp) {
                return center;
            } else if (cp.timestamp < timestamp) {
                lower = center;
            } else {
                upper = center - 1;
            }
        }
        return lower;
    }
```

Here, any time `getPastVotesIndex` is called for any `account` and `timestamp`, `checkpoints[account][nCheckpoints - 1].timestamp` will be 0 and always <= to `timestamp`, thus skipping the binary search and always returning the index of the last checkpoint of the account.

3. Stemming from `getPastVotesIndex()`, `getPastVotes()` will always return the voting power of an account at its last checkpoint, no matter for which `timestamp` the function queries the checkpoints history.
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1337-L1352
```solidity
    function getPastVotes(address account, uint timestamp)
        public
        view
        returns (uint)
    {
→       uint32 _checkIndex = getPastVotesIndex(account, timestamp);
        // Sum votes
        uint[] storage _tokenIds = checkpoints[account][_checkIndex].tokenIds;
        uint votes = 0;
        for (uint i = 0; i < _tokenIds.length; i++) {
            uint tId = _tokenIds[i];
            // Use the provided input timestamp here to get the right decay
            votes = votes + _balanceOfNFT(tId, timestamp);
        }
        return votes;
    }
```
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1413-L1429
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1306-L1335
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L1337-L1352
## Tool used
Manual Review
## Recommendation
Whenever writing to a new checkpoint, set its `timestamp` to `block.timestmap`.