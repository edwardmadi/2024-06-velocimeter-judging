Main Golden Griffin

High

# User can duplicate veNFT tokens in a checkpoint variable

## Summary

If user calls the `VotingEscrow._moveTokenDelegates` function twice in the same block, veNFT tokens can be pushed twice in a checkpoint variable.
Malicious users could exploit this vulnerability to increase the voting balance of their accounts.

## Vulnerability Detail

If the `block.timestamp` is same as the timestamp of the user's last checkpoints, the `_findWhatCheckpointToWrite` function returns last checkpoint index from L1425.
If it isn't, it returns new checkpoint index from L1427.

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1413

```solidity
            function _findWhatCheckpointToWrite(address account)
                internal
                view
                returns (uint32)
            {
                uint _timestamp = block.timestamp;
                uint32 _nCheckPoints = numCheckpoints[account];

                if (
                    _nCheckPoints > 0 &&
                    checkpoints[account][_nCheckPoints - 1].timestamp == _timestamp
                ) {
L1425:              return _nCheckPoints - 1;
                } else {
L1427:              return _nCheckPoints;
                }
            }
```

In the `VotingEscrow._moveTokenDelegates` function, it pushes the `tokenID` of last index checkpoint into checkpoint of returned index.
If the `block.timestamp` is same as the timestamp of the user's last checkpoints, this function pushes into last index checkpoint.
As a result, it is available to duplicate the veNFT tokens into a checkpoint.

Let's consider the following scenario:
- numCheckpoints[`Alice`] = 2, numCheckpoints[`Bob`] = 2 and `Alice` owned the [1, 2, 3, 4, 5] ID veNFT tokens and `Bob` owned the [6] ID: checkpoints[`Alice`][1].tokenIds = [1,2,3,4,5], checkpoints[`Bob`][1].tokenIds = [6]
- `Alice` transfers tokenID = 4 to `Bob` and `Alice` transfers tokenID = 5 to `Bob` again in one transaction.
By transferring tokenID = 4, checkpoints[`Alice`][2].tokenIds = [1,2,3,5] and checkpoints[`Bob`][2].tokenIds = [6,4]
By transferring tokenID = 5 in the same transaction, checkpoints[`Alice`][2].tokenIds = [1,2,3,5,1,2,3] and checkpoints[`Bob`][2].tokenIds = [6,4,6,4,5].
As a result, checkpoints[`Alice`][2].tokenIds becomes [1,2,3,5,1,2,3] instead of [1,2,3] and checkpoints[`Bob`][2].tokenIds becomes [6,4,6,4,5] instead of [6,4,5]
This makes the `Alice` and `Bob`'s voting balance increase than their actual balance.

## Impact

Malicious users can increase the voting balance of their accounts.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/VotingEscrow.sol#L1362

## Tool used

Manual Review

## Recommendation

It is recommended to change the code as following:

```diff
            function _moveTokenDelegates(
                address srcRep,
                address dstRep,
                uint _tokenId
            ) internal {
                if (srcRep != dstRep && _tokenId > 0) {
                    if (srcRep != address(0)) {
                        uint32 srcRepNum = numCheckpoints[srcRep];
-                       uint[] storage srcRepOld = srcRepNum > 0
+                       uint[] memory srcRepOld = srcRepNum > 0
                            ? checkpoints[srcRep][srcRepNum - 1].tokenIds
                            : checkpoints[srcRep][0].tokenIds;
                        uint32 nextSrcRepNum = _findWhatCheckpointToWrite(srcRep);
                        uint[] storage srcRepNew = checkpoints[srcRep][
                            nextSrcRepNum
                        ].tokenIds;
+                       delete srcRepNew;
                        // All the same except _tokenId
                        for (uint i = 0; i < srcRepOld.length; i++) {
                            uint tId = srcRepOld[i];
                            if (tId != _tokenId) {
                                srcRepNew.push(tId);
                            }
                        }

                        numCheckpoints[srcRep] = srcRepNum + 1;
                    }

                    if (dstRep != address(0)) {
                        uint32 dstRepNum = numCheckpoints[dstRep];
-                       uint[] storage dstRepOld = dstRepNum > 0
+                       uint[] memory dstRepOld = dstRepNum > 0
                            ? checkpoints[dstRep][dstRepNum - 1].tokenIds
                            : checkpoints[dstRep][0].tokenIds;
                        uint32 nextDstRepNum = _findWhatCheckpointToWrite(dstRep);
                        uint[] storage dstRepNew = checkpoints[dstRep][
                            nextDstRepNum
                        ].tokenIds;
+                       delete dstRepNew;
                        // All the same plus _tokenId
                        require(
                            dstRepOld.length + 1 <= MAX_DELEGATES,
                            "dstRep would have too many tokenIds"
                        );
                        for (uint i = 0; i < dstRepOld.length; i++) {
                            uint tId = dstRepOld[i];
                            dstRepNew.push(tId);
                        }
                        dstRepNew.push(_tokenId);

                        numCheckpoints[dstRep] = dstRepNum + 1;
                    }
                }
            }
```
