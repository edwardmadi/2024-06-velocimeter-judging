Delightful Lavender Goose

Medium

# Incorrect accounting of `supply` variable.

## Summary
The merge function in the `VotingEscrow` contract incorrectly inflates the total locked tokens, misrepresenting the actual `supply`.

## Vulnerability Detail
In the VotingEscrow contract, the `merge` function is intended to combine the balances of two NFTs representing locked tokens. It calls the` _deposit_for` function, which updates the `supply` variable that tracks the total locked tokens.
However in case of `merge` , `_deposit_for` incorrectly increases the `supply` with the amount from the merged NFT, even though this amount is already included in the total. This leads to an inflated supply that misrepresents the actual amount of locked tokens.

## Impact
Likelihood is high. It will certainly happen. This might be with regular user interactions or a malicious user might intentionally merge multiple positions to arbitrarily increase the supply.

The impact is low in the current codebase since the supply is not used in important calculations. However, it creates a broken invariant, emits Supply event with incorrect values.

## Code Snippet
```solidity
    function _deposit_for(
        uint _tokenId,
        uint _value,
        uint unlock_time,
        LockedBalance memory locked_balance,
        DepositType deposit_type
    ) internal {
        LockedBalance memory _locked = locked_balance;
        uint supply_before = supply;

@>>     supply = supply_before + _value;
        LockedBalance memory old_locked;
        (old_locked.amount, old_locked.end) = (_locked.amount, _locked.end);
        // Adding to existing lock, or if a lock is expired - creating a new one
        _locked.amount += int128(int256(_value));
        if (unlock_time != 0) {
            _locked.end = unlock_time;
        }
        locked[_tokenId] = _locked;

        // Possibilities:
        // Both old_locked.end could be current or expired (>/< block.timestamp)
        // value == 0 (extend lock) or value > 0 (add to lock or extend lock)
        // _locked.end > block.timestamp (always)
        _checkpoint(_tokenId, old_locked, _locked);

        address from = msg.sender;
@>>     if (_value != 0 && deposit_type != DepositType.MERGE_TYPE && deposit_type != DepositType.SPLIT_TYPE) {
@>>         assert(IERC20(lpToken).transferFrom(from, address(this), _value));
@>>     }

        emit Deposit(from, _tokenId, _value, _locked.end, deposit_type, block.timestamp);
        emit Supply(supply_before, supply_before + _value);
    }
```
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L787
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L804-L806

## Tool used

Manual Review

## Recommendation
```diff
    function merge(uint _from, uint _to) external {
        require(attachments[_from] == 0 && !voted[_from], "attached");
        require(_from != _to);
        require(_isApprovedOrOwner(msg.sender, _from));
        require(_isApprovedOrOwner(msg.sender, _to));

        LockedBalance memory _locked0 = locked[_from];
        LockedBalance memory _locked1 = locked[_to];
        uint value0 = uint(int256(_locked0.amount));
        uint end = _locked0.end >= _locked1.end ? _locked0.end : _locked1.end;

+       supply -= value0;
        locked[_from] = LockedBalance(0, 0);
        _checkpoint(_from, _locked0, LockedBalance(0, 0));
        _burn(_from);
        _deposit_for(_to, value0, end, _locked1, DepositType.MERGE_TYPE);
    }
```