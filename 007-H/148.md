Active Lace Hippo

High

# `OptionTokenV4` Enables Holding Victim `GaugeV4::balanceWithLock` Hostage

## Summary

An oversight in the [`GaugeV4`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol) contract enables malicious actors to extend the lock up period of an arbitrary account's locked funds indefinitely through the exploitation of the [`OptionTokenV4`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol)'s trusted relationship, preventing victim accounts from accessing their [`balanceWithLock`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L45C37-L45C52) at negligible cost to an attacker.

## Vulnerability Detail

The [`GaugeV4`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol) exposes  the [`depositWithLock(address,uint256,uint256)`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L443C14-L443C85) operation, a function which enables liquidity provision to take place whilst locking the liquidity for a fixed period. In Velocimeter's ve(3,3) implementation, this mechanism is used to ensure a discount on future value redemption in exchange for sticky liquidity provision that helps incentivise deep liquidity:

```solidity
function depositWithLock(address account, uint256 amount, uint256 _lockDuration) external lock {
@>  require(msg.sender == account || isOToken[msg.sender],"Not allowed to deposit with lock");
    _deposit(account, amount, 0);

    if(block.timestamp >= lockEnd[account]) { // if the current lock is expired relased the tokens from that lock before loking again
        delete lockEnd[account];
        delete balanceWithLock[account]; /// @custom:hunter loss_of_deposit
    }

    balanceWithLock[account] += amount;
    uint256 currentLockEnd = lockEnd[account];
@>  uint256 newLockEnd = block.timestamp + _lockDuration;
    if (currentLockEnd > newLockEnd) {
        revert("The current lock end > new lock end");
    } 
    lockEnd[account] = newLockEnd;
}
```

Notice here that if the [`lockEnd`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L46C37-L46C44) for an [`account`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L443C38-L443C45) is going to be increased, it must either be instigated by the holder of the affected [`account`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L443C38-L443C45) (since this is expected to be opt-in functionality as the action prolongs the duration until they can regain access their stake)  or by an `OptionToken` (i.e. [`OptionTokenV4`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol) is an instance of an `oToken`, which has the ability to create locked positions).

Now, looking at the implementation of [`OptionTokenV4`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol), we can see that [`depositWithLock(address,uint256,uint256)`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L443C14-L443C85) can be invoked by specifying a [`_recipient`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/OptionTokenV4.sol#L701C13-L701C23) who is **not** the `msg.sender` during a call to [`exerciseLp(uint256,uint256,address,uint256,uint256)`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/OptionTokenV4.sol#L305C14-L311C6):

```solidity
IGaugeV4(_gauge).depositWithLock(
@>  _recipient, /// @audit increase_lock_duration_for_specified_recipient
    lpAmount,
@>  getLockDurationForLpDiscount(_discount)
);
```

This is because the exposed staking interface enables created positions to be donated to caller-defined recipient addresses.

Consequently, an attacker may errantly increase the [`lockEnd`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L46C37-L46C44) of a victim through token donation, since they exploit the [`OptionTokenV4`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol) as a trusted proxy, allowing an attacker to errantly gatekeep a victim from access to their funds.

This could potentially be used as a way of extorting victims by holding their [`balanceWithLock`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L45C37-L45C52) hostage by simply donating worthless positions at the highest lock up time to the victim account to prevent access to their due balance.

## Impact

Malicious actors can ensure locked liquidity remains locked indefinitely at negligible expense or attack complexity.

## Code Snippet

```solidity
function _exerciseLp(
    uint256 _amount,   // the oTOKEN amount the user wants to redeem with
    uint256 _maxPaymentAmount, // the 
    address _recipient,
    uint256 _discount
) internal returns (uint256 paymentAmount, uint256 lpAmount) {
    if (isPaused) revert OptionToken_Paused();
    if (isExerciseLpPaused) revert OptionToken_Paused();

    if (_discount > minLPDiscount || _discount < maxLPDiscount)
        revert OptionToken_InvalidDiscount();

    // burn callers tokens
    _burn(msg.sender, _amount);
    (uint256 paymentAmount,uint256 paymentAmountToAddLiquidity) =  getPaymentTokenAmountForExerciseLp(_amount,_discount);
    if (paymentAmount > _maxPaymentAmount)
        revert OptionToken_SlippageTooHigh();
      
    // Take team fee
    uint256 paymentGaugeRewardAmount = _discount == 0 ? 0 : _takeFees(
        paymentToken,
        paymentAmount
    );
    _safeTransferFrom(
        paymentToken,
        msg.sender,
        address(this),
        paymentGaugeRewardAmount + paymentAmountToAddLiquidity
    );

    // Create Lp for users
    _safeApprove(underlyingToken, router, _amount); /// @custom:hunter underlyingToken_cant_be_paymentToken
    _safeApprove(paymentToken, router, paymentAmountToAddLiquidity);
    (, , lpAmount) = IRouter(router).addLiquidity(
        underlyingToken,
        paymentToken,
        false,
        _amount,
        paymentAmountToAddLiquidity,
        1,
        1,
        address(this),
        block.timestamp
    );

    // Stake the LP in the gauge with lock
    address _gauge = gauge;
    _safeApprove(address(pair), _gauge, lpAmount);

    IGaugeV4(_gauge).depositWithLock(
        _recipient,
        lpAmount,
        getLockDurationForLpDiscount(_discount)
    );

    // notify gauge reward with payment token
    _transferRewardToGauge();

    emit ExerciseLp(
        msg.sender,
        _recipient,
        _amount,
        paymentAmount,
        lpAmount
    );
}
```

https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/OptionTokenV4.sol#L652C5-L716C6

## Tool used

Manual Review

## Recommendation

Do not permit the [`lockEnd`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/GaugeV4.sol#L46C37-L46C44) to be errantly increased by a foreign account without express approval from the affected [`_recipient`](https://github.com/sherlock-audit/2024-06-velocimeter/blob/63818925987a5115a80eff4bd12578146a844cfd/v4-contracts/contracts/OptionTokenV4.sol#L701C13-L701C23) (i.e. a signature or verified invocation).
