Energetic Macaroon Albatross

High

# Attacker could increase users lock time by exercising small amount with `exerciseLp`

## Summary

 malicious actor can exploit the `exerciseLp` function to increase the lock time of other users' funds by exercising a small amount of oToken on their behalf. This forces those users' funds to remain locked for a longer period than expected, potentially indefinitely.

## Vulnerability Detail

The `exerciseLp` function allows any user to exercise a certain amount of oToken at a specified discount on behalf of another user without enforcing conditions on the caller or recipient:

```solidity
function exerciseLp(
    uint256 _amount,
    uint256 _maxPaymentAmount,
    address _recipient,
    uint256 _discount,
    uint256 _deadline
) external returns (uint256, uint256) {
    if (block.timestamp > _deadline) revert OptionToken_PastDeadline();
    return _exerciseLp(_amount, _maxPaymentAmount, _recipient, _discount);
}
```

Under the hood, `_exerciseLp` is called:

```solidity
function _exerciseLp(
    uint256 _amount, // the oTOKEN amount the user wants to redeem with
    uint256 _maxPaymentAmount, // the
    address _recipient,
    uint256 _discount
) internal returns (uint256 paymentAmount, uint256 lpAmount) {
    if (isPaused) revert OptionToken_Paused();
    if (isExerciseLpPaused) revert OptionToken_Paused();

    if (_discount > minLPDiscount || _discount < maxLPDiscount)
        revert OptionToken_InvalidDiscount();

    // burn callers tokens
    _burn(msg.sender, _amount);
    (
        uint256 paymentAmount,
        uint256 paymentAmountToAddLiquidity
    ) = getPaymentTokenAmountForExerciseLp(_amount, _discount);
    if (paymentAmount > _maxPaymentAmount)
        revert OptionToken_SlippageTooHigh();

    // Take team fee
    uint256 paymentGaugeRewardAmount = _discount == 0
        ? 0
        : _takeFees(paymentToken, paymentAmount);
    _safeTransferFrom(
        paymentToken,
        msg.sender,
        address(this),
        paymentGaugeRewardAmount + paymentAmountToAddLiquidity
    );

    // Create Lp for users
    _safeApprove(underlyingToken, router, _amount);
    _safeApprove(paymentToken, router, paymentAmountToAddLiquidity);
    (, , lpAmount) = IRouter(router).addLiquidity(
        underlyingToken,
        paymentToken,
        false,
        _amount,
        paymentAmountToAddLiquidity,
        1,
        1,
        address(this),
        block.timestamp
    );

    // Stake the LP in the gauge with lock
    address _gauge = gauge;
    _safeApprove(address(pair), _gauge, lpAmount);
    //@audit malicious actor could use this to increase lock duration of user by locking small amount with high discount
    IGaugeV4(_gauge).depositWithLock(
        _recipient,
        lpAmount,
        getLockDurationForLpDiscount(_discount)
    );

    // notify gauge reward with payment token
    _transferRewardToGauge();

    emit ExerciseLp(
        msg.sender,
        _recipient,
        _amount,
        paymentAmount,
        lpAmount
    );
}
```

From the above code, it is clear that any user can use their own tokens (oToken and paymentToken) to exercise for another recipient address. The function will add liquidity using the provided tokens and stake the received LP in the gauge with a lock by invoking `depositWithLock`, the later accepts three parameters: the recipient of the locked LP, the LP amount and the new lock duration.

It's important to note that the new lock duration is dependant on the discount provided by the caller calculated by `getLockDurationForLpDiscount`, so if the caller provide the higher possible discount the maximum possible lock duration will be set for the new lock:

```solidity
function getLockDurationForLpDiscount(
    uint256 _discount
) public view returns (uint256 duration) {
    (int256 slope, int256 intercept) = getSlopeInterceptForLpDiscount();
    duration = _discount == 0
        ? lockDurationForMaxLpDiscount
        : SignedMath.abs(slope * int256(_discount) + intercept);
}
```

On the other hand, the `depositWithLock` function shown below, will always increase the lock end time by the new duration calculated previously through `getLockDurationForLpDiscount` (assuming the user previous lock hasn't expired):

```solidity
function depositWithLock(
    address account,
    uint256 amount,
    uint256 _lockDuration
) external lock {
    require(
        msg.sender == account || isOToken[msg.sender],
        "Not allowed to deposit with lock"
    );
    _deposit(account, amount, 0);

    if (block.timestamp >= lockEnd[account]) {
        // if the current lock is expired relased the tokens from that lock before loking again
        delete lockEnd[account];
        delete balanceWithLock[account];
    }

    balanceWithLock[account] += amount;
    uint256 currentLockEnd = lockEnd[account];
    uint256 newLockEnd = block.timestamp + _lockDuration;
    if (currentLockEnd > newLockEnd) {
        revert("The current lock end > new lock end");
    }
    lockEnd[account] = newLockEnd;
}
```

With all this informations mentioned, there is an attack scenario that can be exploited by a malicious actor to force other user to remain locked against their will for a longer period than expected if not forever. This scenario is simple to execute, requires minimal funds, and only necessitates that the recipient user already has an active lock in the gauge. 

Let's highlight this scenario with an example:

1. Bob (honest user) has LP locked in the Gauge, current lock is set to expire in 4 weeks.

2. Alice (malicious actor) wants to force Bob to remain locked longer

3. Alice executes the attack:
   - Alice calls `exerciseLp` with:
     - `_amount`: 10 oTokens (could be any small amount)
     - `_maxPaymentAmount`: Sufficient to cover the payment
     - `_recipient`: Bob's address
     - `_discount`: 20 (maximum discount, which results in maximum lock duration)
     - `_deadline`: any deadline in the future

4. `exerciseLp` execution:
   - Alice's 10 oTokens are burned.
   - Payment amount is calculated with 20% discount.
   - Liquidity is added, resulting in 20 new LP tokens (just an example small number, not real calculation as the actual LP amount doesn't matter in this attack).

5. `depositWithLock` is called internally:
   - `account`: Bob's address
   - `amount`: 20 LP tokens
   - `_lockDuration`: 52 weeks (FULL_LOCK, due to maximum discount)

6. `depositWithLock` processes:
   - Bob's `balanceWithLock` increases by 20 LP tokens.
   - Bob's `lockEnd` is updated and is now set to expire in 52 weeks from current date.

7. Result:
   - Bob's lock, originally set to expire in 4 weeks, is now extended to a full year.
   - Bob's locked balance only increased by a small amount (negligible), but his lock duration increased by 48 weeks.

8. Repeated attacks:
   - Alice repeats this weekly, each time extending Bob's lock by another 52 weeks.
   - After 10 weeks: Bob's lock extends could be extended almost 2 years from the original end date.

9. Impact on Bob:
   - Bob cannot withdraw his LP tokens when expected.
   - Bob's funds are locked for years beyond his intended timeframe.
   - If Alice continues, Bob might never regain access to his funds.

## Impact

A malicious actor can force other users' funds to remain locked for an extended period, disrupting their ability to access their assets. This can be particularly harmful if users need liquidity or have planned their investments based on specific lock durations.

## Code Snippet

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L305-L314

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L652-L716

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol#L443-L459

## Tool used

Manual Review

## Recommendation

To mitigate this issue, implement checks to ensure that only the account owner or authorized entities can call `exerciseLp` on behalf of another user.
