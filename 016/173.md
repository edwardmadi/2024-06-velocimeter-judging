Silly Cider Pig

High

# `VotingEscrow.sol#_checkpoint` function has error in calculating block number of epochs.

## Summary
`VotingEscrow.sol#_checkpoint` function has error in calculating block number of epochs.
This causes serious problems to the protocol.

## Vulnerability Detail
`VotingEscrow.sol#_checkpoint` function is the following.
```solidity
    function _checkpoint(
        uint _tokenId,
        LockedBalance memory old_locked,
        LockedBalance memory new_locked
    ) internal {
        --- SKIP ---
        // initial_last_point is used for extrapolation to calculate block number
        // (approximately, for *At methods) and save them
        // as we cannot figure that out exactly from inside the contract
642:    Point memory initial_last_point = last_point;
        uint block_slope = 0; // dblock/dt
        if (block.timestamp > last_point.ts) {
645:        block_slope = (MULTIPLIER * (block.number - last_point.blk)) / (block.timestamp - last_point.ts);
        }
        // If last point is already recorded in this block, slope=0
        // But that's ok b/c we know the block in such case

        // Go over weeks to fill history and calculate what the current point is
        {
            uint t_i = (last_checkpoint / WEEK) * WEEK;
            for (uint i = 0; i < 255; ++i) {
                // Hopefully it won't happen that this won't get used in 5 years!
                // If it does, users will be able to withdraw but vote weight will be broken
                t_i += WEEK;
                int128 d_slope = 0;
                if (t_i > block.timestamp) {
                    t_i = block.timestamp;
                } else {
                    d_slope = slope_changes[t_i];
                }
                last_point.bias -= last_point.slope * int128(int256(t_i - last_checkpoint));
                last_point.slope += d_slope;
                if (last_point.bias < 0) {
                    // This can happen
                    last_point.bias = 0;
                }
                if (last_point.slope < 0) {
                    // This cannot happen - just in case
                    last_point.slope = 0;
                }
                last_checkpoint = t_i;
                last_point.ts = t_i;
675:            last_point.blk = initial_last_point.blk + (block_slope * (t_i - initial_last_point.ts)) / MULTIPLIER;
                _epoch += 1;
                if (t_i == block.timestamp) {
                    last_point.blk = block.number;
                    break;
                } else {
                    point_history[_epoch] = last_point;
                }
            }
        }

        --- SKIP ---
    }
```
As can be seen, the memory variable `initial_last_point` is initialized directly by memory variable `last_point` in `L642`.
This means that `initial_last_point` is the reference variable to the `last_point` variable.
Therefore, after that, whenever the `last_point` changes, the `initial_last_point` variable changes too and this is not intended and so causes error in calculating block number of epochs.

In more detail, the `initial_last_point.blk` and `initial_last_point.ts` of `L675` is updated every time in the loop due to the update of `last_point` variable.
This problem, combined with the rounding error of `L645` and `L675`, prevents the `last_point.blk` of `L675` from being updated.

PoC:
Add the following test code to the `VotingEscrow.t.sol`.
```solidity
    function testBlockSlope() public {
        uint SECONDS_PER_BLOCK = 13;
        uint WEEKS = 3;
        
        flowDaiPair.approve(address(escrow), TOKEN_10);

        uint tokenId = escrow.create_lock(TOKEN_1, FIFTY_TWO_WEEKS);

        emit log_named_uint("first block.number", block.number);

        vm.warp(block.timestamp + WEEKS * ONE_WEEK);
        vm.roll(block.number + WEEKS * ONE_WEEK / SECONDS_PER_BLOCK);

        emit log_named_uint("last block.number", block.number);

        escrow.checkpoint();

        uint epoch = escrow.epoch();

        for (uint i = 1; i <= epoch; i++) {
            (int128 bias, int128 slope, uint ts, uint blk) = escrow.point_history(i);
            emit log_named_uint("-------- epoch", i);
            emit log_named_int("    bias", bias);
            emit log_named_int("    slope", slope);
            emit log_named_uint("    ts", ts);
            emit log_named_uint("    blk", blk);
        }
    }
```
And this is the result of the test code.
```sh
Ran 1 test for test/VotingEscrow.t.sol:VotingEscrowTest
[PASS] testBlockSlope() (gas: 860814)
Logs:
  first block.number: 1
  last block.number: 139570
  -------- epoch: 1
      bias: 999999968174574804
      slope: 31796906796
      ts: 1
      blk: 1
  -------- epoch: 2
      bias: 980769230741260800
      slope: 31796906796
      ts: 604800
      blk: 1
  -------- epoch: 3
      bias: 961538461511040000
      slope: 31796906796
      ts: 1209600
      blk: 1
  -------- epoch: 4
      bias: 942307692280819200
      slope: 31796906796
      ts: 1814400
      blk: 1
  -------- epoch: 5
      bias: 942307660483912404
      slope: 31796906796
      ts: 1814401
      blk: 139570

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.98ms (625.30µs CPU time)

Ran 1 test suite in 28.83ms (9.98ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```
As can be seen, the block number for epoch 2, 3 and 4 is the same value `1` and not updated at all.

## Impact
The `point_history` is used to get balance of NFT tokens and the totalSupply of VotingEscrow.
Therefore, the error in calculating block numbers causes the serious damage to the protocol.

## Code Snippet
https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L642

## Tool used

Manual Review

## Recommendation
Modify the `VotingEscrow.sol#_checkpoint` function as follows.
```solidity
    function _checkpoint(
        uint _tokenId,
        LockedBalance memory old_locked,
        LockedBalance memory new_locked
    ) internal {
        --- SKIP ---
        // initial_last_point is used for extrapolation to calculate block number
        // (approximately, for *At methods) and save them
        // as we cannot figure that out exactly from inside the contract
--      Point memory initial_last_point = last_point;
++      Point memory initial_last_point = Point(last_point.bias, last_point.slope, last_point.ts, last_point.blk);
        uint block_slope = 0; // dblock/dt
        if (block.timestamp > last_point.ts) {
            block_slope = (MULTIPLIER * (block.number - last_point.blk)) / (block.timestamp - last_point.ts);
        }
        --- SKIP ---
    }
```
And the following is the result of the above test code for modified function and you can see the correct block numbers for all epochs.
```sh
Ran 1 test for test/VotingEscrow.t.sol:VotingEscrowTest
[PASS] testBlockSlope() (gas: 861146)
Logs:
  first block.number: 1
  last block.number: 139570
  -------- epoch: 1
      bias: 999999968174574804
      slope: 31796906796
      ts: 1
      blk: 1
  -------- epoch: 2
      bias: 980769230741260800
      slope: 31796906796
      ts: 604800
      blk: 46523
  -------- epoch: 3
      bias: 961538461511040000
      slope: 31796906796
      ts: 1209600
      blk: 93046
  -------- epoch: 4
      bias: 942307692280819200
      slope: 31796906796
      ts: 1814400
      blk: 139569
  -------- epoch: 5
      bias: 942307660483912404
      slope: 31796906796
      ts: 1814401
      blk: 139570

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 6.22ms (586.20µs CPU time)

Ran 1 test suite in 20.51ms (6.22ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```
