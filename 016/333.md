Atomic Citron Fly

High

# Flow in User Point History Recording in `_checkpoint` Function

https://github.com/sherlock-audit/2024-06-velocimeter/blob/main/v4-contracts/contracts/VotingEscrow.sol#L598

## Summary
The current implementation of the `_checkpoint` function does not handle multiple updates within the same block efficiently. This leads to redundant entries in the user point history, resulting in unnecessary data storage and potential inefficiencies in data retrieval. Additionally, the global point history must be handled correctly to avoid redundant entries and ensure proper epoch and last_point settings.

## Vulnerability Detail
The current implementation of the `_checkpoint` function does not handle multiple updates within the same block efficiently. Currently the function unconditionally increments the user point epoch and creates new history entries for each update, even if they occur within the same block.  This can lead to redundant entries in the user point history, causing unnecessary data storage and potential inefficiencies in data retrieval. This redundancy could bloat the blockchain storage and increase the cost of interactions with the contract over time which could lead to DOS.

Proof of Concept:
In the _checkpoint function, when recording the user point history, the function does not check if an entry for the current block already exists.

```javascript
function _checkpoint(
        uint _tokenId,
        LockedBalance memory old_locked,
        LockedBalance memory new_locked
    ) internal {
        Point memory u_old;
        Point memory u_new;
        int128 old_dslope = 0;
        int128 new_dslope = 0;
        uint _epoch = epoch;

        if (_tokenId != 0) {
            // Calculate slopes and biases
            // Kept at zero when they have to
            if (old_locked.end > block.timestamp && old_locked.amount > 0) {
                u_old.slope = old_locked.amount / iMAXTIME;
                u_old.bias =
                    u_old.slope *
                    int128(int256(old_locked.end - block.timestamp));
            }
            if (new_locked.end > block.timestamp && new_locked.amount > 0) {
                u_new.slope = new_locked.amount / iMAXTIME;
                u_new.bias =
                    u_new.slope *
                    int128(int256(new_locked.end - block.timestamp));
            }
            ...MORE_CODE...

            // Now handle user history
            uint user_epoch = user_point_epoch[_tokenId] + 1;

            user_point_epoch[_tokenId] = user_epoch; 
            u_new.ts = block.timestamp;
            u_new.blk = block.number;
            user_point_history[_tokenId][user_epoch] = u_new;
```

As we can see in this part where the user history is being handled we dont check if we already have existing entries

```javascript
// Now handle user history
            uint user_epoch = user_point_epoch[_tokenId] + 1;

            user_point_epoch[_tokenId] = user_epoch; 
            u_new.ts = block.timestamp;
            u_new.blk = block.number;
            user_point_history[_tokenId][user_epoch] = u_new;
```

This implementation will create a new entry for every update within the same block, leading to multiple entries for the same block timestamp. We should ensure that if a user point already exists for the current block, it is updated instead of creating a new entry. 

see example implementation below
https://github.com/velodrome-finance/contracts/blob/main/contracts/VotingEscrow.sol#L739C6-L751C10

Additionally, ensure the function correctly handles global point history to avoid redundant entries while setting the epoch and last_point

an example implementation

```javascript
// If timestamp of last global point is the same, overwrite the last global point
// Else record the new global point into history
// Exclude epoch 0 (note: _epoch is always >= 1, see above)
// Two possible outcomes:
// Missing global checkpoints in prior weeks. In this case, _epoch = epoch + x, where x > 1
// No missing global checkpoints, but timestamp != block.timestamp. Create new checkpoint.
// No missing global checkpoints, but timestamp == block.timestamp. Overwrite last checkpoint.
if (_epoch != 1 && _pointHistory[_epoch - 1].ts == block.timestamp) {
    // _epoch = epoch + 1, so we do not increment epoch
    _pointHistory[_epoch - 1] = lastPoint;
} else {
    // more than one global point may have been written, so we update epoch
    epoch = _epoch;
    _pointHistory[_epoch] = lastPoint;
}
```

see the link below to see example implementation

https://github.com/velodrome-finance/contracts/blob/main/contracts/VotingEscrow.sol#L702C1-L716C10



## Impact
Creating new history entries for each update within the same block results in redundant entries, which bloat blockchain storage and increase costs. The increased storage and processing requirements could lead to a denial of service (DoS) attacks.

## Code Snippet
```javascript
 function _checkpoint(
        uint _tokenId,
        LockedBalance memory old_locked,
        LockedBalance memory new_locked
    ) internal {
        Point memory u_old;
        Point memory u_new;
        int128 old_dslope = 0;
        int128 new_dslope = 0;
        uint _epoch = epoch;

        if (_tokenId != 0) {
            // Calculate slopes and biases
            // Kept at zero when they have to
            if (old_locked.end > block.timestamp && old_locked.amount > 0) {
                u_old.slope = old_locked.amount / iMAXTIME;
                u_old.bias = u_old.slope * int128(int256(old_locked.end - block.timestamp));
            }
            if (new_locked.end > block.timestamp && new_locked.amount > 0) {
                u_new.slope = new_locked.amount / iMAXTIME;
                u_new.bias = u_new.slope * int128(int256(new_locked.end - block.timestamp));
            }

            // Read values of scheduled changes in the slope
            // old_locked.end can be in the past and in the future
            // new_locked.end can ONLY by in the FUTURE unless everything expired: than zeros
            old_dslope = slope_changes[old_locked.end];
            if (new_locked.end != 0) {
                if (new_locked.end == old_locked.end) {
                    new_dslope = old_dslope;
                } else {
                    new_dslope = slope_changes[new_locked.end];
                }
            }
        }

        Point memory last_point = Point({bias: 0, slope: 0, ts: block.timestamp, blk: block.number});
        if (_epoch > 0) {
            last_point = point_history[_epoch];
        }
        uint last_checkpoint = last_point.ts;
        // initial_last_point is used for extrapolation to calculate block number
        // (approximately, for *At methods) and save them
        // as we cannot figure that out exactly from inside the contract
        Point memory initial_last_point = last_point;
        uint block_slope = 0; // dblock/dt
        if (block.timestamp > last_point.ts) {
            block_slope = (MULTIPLIER * (block.number - last_point.blk)) / (block.timestamp - last_point.ts);
        }
        // If last point is already recorded in this block, slope=0
        // But that's ok b/c we know the block in such case

        // Go over weeks to fill history and calculate what the current point is
        {
            uint t_i = (last_checkpoint / WEEK) * WEEK;
            for (uint i = 0; i < 255; ++i) {
                // Hopefully it won't happen that this won't get used in 5 years!
                // If it does, users will be able to withdraw but vote weight will be broken
                t_i += WEEK;
                int128 d_slope = 0;
                if (t_i > block.timestamp) {
                    t_i = block.timestamp;
                } else {
                    d_slope = slope_changes[t_i];
                }
                last_point.bias -= last_point.slope * int128(int256(t_i - last_checkpoint));
                last_point.slope += d_slope;
                if (last_point.bias < 0) {
                    // This can happen
                    last_point.bias = 0;
                }
                if (last_point.slope < 0) {
                    // This cannot happen - just in case
                    last_point.slope = 0;
                }
                last_checkpoint = t_i;
                last_point.ts = t_i;
                last_point.blk = initial_last_point.blk + (block_slope * (t_i - initial_last_point.ts)) / MULTIPLIER;
                _epoch += 1;
                if (t_i == block.timestamp) {
                    last_point.blk = block.number;
                    break;
                } else {
                    point_history[_epoch] = last_point;
                }
            }
        }

        epoch = _epoch;
        // Now point_history is filled until t=now

        if (_tokenId != 0) {
            // If last point was in this block, the slope change has been applied already
            // But in such case we have 0 slope(s)
            last_point.slope += (u_new.slope - u_old.slope);
            last_point.bias += (u_new.bias - u_old.bias);
            if (last_point.slope < 0) {
                last_point.slope = 0;
            }
            if (last_point.bias < 0) {
                last_point.bias = 0;
            }
        }

        // Record the changed point into history
        point_history[_epoch] = last_point;

        if (_tokenId != 0) {
            // Schedule the slope changes (slope is going down)
            // We subtract new_user_slope from [new_locked.end]
            // and add old_user_slope to [old_locked.end]
            if (old_locked.end > block.timestamp) {
                // old_dslope was <something> - u_old.slope, so we cancel that
                old_dslope += u_old.slope;
                if (new_locked.end == old_locked.end) {
                    old_dslope -= u_new.slope; // It was a new deposit, not extension
                }
                slope_changes[old_locked.end] = old_dslope;
            }

            if (new_locked.end > block.timestamp) {
                if (new_locked.end > old_locked.end) {
                    new_dslope -= u_new.slope; // old slope disappeared at this point
                    slope_changes[new_locked.end] = new_dslope;
                }
                // else: we recorded it already in old_dslope
            }
            // Now handle user history
            uint user_epoch = user_point_epoch[_tokenId] + 1;

            user_point_epoch[_tokenId] = user_epoch;
            u_new.ts = block.timestamp;
            u_new.blk = block.number;
            user_point_history[_tokenId][user_epoch] = u_new;
        }
    }
```

## Tool used

Manual Review

## Recommendation
Update the `_checkpoint` function to include a check for existing entries in the current block before creating a new user point history entry: below is just an example of how this could be done

```javascript

if (user_epoch != 0 && user_point_history[_tokenId][user_epoch].ts == block.timestamp) {
    user_point_history[_tokenId][user_epoch] = u_new;
} else {
    user_point_epoch[_token_id] = ++user_epoch;
    user_point_history[_token_id][user_epoch] = u_new;
}
```
Global Point History Check:
Ensure the function handles global point history correctly, below is just an example of how it could be done

```javascript
// If timestamp of last global point is the same, overwrite the last global point
// Else record the new global point into history
// Exclude epoch 0 (note: _epoch is always >= 1, see above)
// Two possible outcomes:
// Missing global checkpoints in prior weeks. In this case, _epoch = epoch + x, where x > 1
// No missing global checkpoints, but timestamp != block.timestamp. Create new checkpoint.
// No missing global checkpoints, but timestamp == block.timestamp. Overwrite last checkpoint.
if (_epoch != 1 && _pointHistory[_epoch - 1].ts == block.timestamp) {
    // _epoch = epoch + 1, so we do not increment epoch
    _pointHistory[_epoch - 1] = last_Point;
} else {
    // more than one global point may have been written, so we update epoch
    epoch = _epoch;
    _pointHistory[_epoch] = last_Point;
}
```

